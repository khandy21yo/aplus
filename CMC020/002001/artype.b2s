10	!---------------------------------------------------------------&	
	!								&
	! ARTYPE.B2S	V1.0	March 1985				&
	!								&
	! Author - Paul Cazier, Computer Management Center, Inc.	&
	!								&
	! Files Used -	ARTYPE.DAT  - ISAM	Vendor Description File	&
	!								&
	!---------------------------------------------------------------&
	! 								&
	! 								&
	!		      Copyright (c) 1984 by 			&
	!	  Computer Management Center, Idaho Falls, Idaho	&
	! 								&
	! This software is furnished under a license and may be used	&
	! and copied only in accordance with the terms of such license 	&
	! and with the inclusion of the above copyright notice.  This 	&
	! software or any other copies thereof may not be provided or 	&
	! otherwise made available to any other person.  No title to 	&
	! and ownership of the software is hereby transferred.		&
	! 								&
	! The information in this software is subject to change without &
	! notice and should not be construed as a committment by 	&
	! Computer Management Center. 					&
	! 								&
	! CMC assumes no responsibility for the use or reliability of 	&
	! its software on equipment which is not supported by CMC. 	&
	! 								&
	!---------------------------------------------------------------&

50	ON ERROR GOTO 19000 &
\	JUNK% = CTRLC &
\	JUNK$ = SYS(CHR$(6%) + CHR$(16%) + CHR$(0%) + CHR$(255%) + &
		CHR$(81%) + STRING$(5%,0%) + CHR$(255%) + &
		STRING$(14%,0%) + CHR$(255%) + CHR$(128%+26%) + &
		STRING$(6%,0%)) &
\	TEMP$ = SYS(CHR$(12%)) &
\	PRJPRG$ = "[" + NUM1$( ASCII( MID( TEMP$,6%,1%))) + "," + &
		NUM1$( ASCII( MID( TEMP$,5%,1%))) + "]" &

60	OPEN "KB:" AS FILE #1%, MODE 8%+256% &
					!    8% - Echo Control &
					!   16% - Disable hibernation, Cntl-C &
\	CH%=1%				! Keyboard channel &
\	ESC$=CHR$(155%)			! Escape code for VT100 control &
\	CLSCN$=ESC$+"[H"+ESC$+"[J"	! Clear screen &
\	CLRLIN$=ESC$+"[2K"		! Erase entire line &
\	CLREL$=ESC$+"[K"		! Cursor to end of line &
\ 	B.ON$=ESC$+"[1m"		! Bold face on &
\	CLRBOT$=ESC$+"[21;1H"+ESC$+"[J"	! Erase cursor to end of screen &
\	R.ON$=ESC$+"[7m"      		! Reverse video &
\	INSERT.LIN$=ESC$+"[1L"		! Insert a line &
\	G.OFF$=ESC$+"[m"		! Select graphic off &
\	LDS.ON$=ESC$+"(0"		! Line drawing set &
\	SCROLL.REGION$=ESC$+"[4;19r"	! Scroll region &
\	SCROLL.ON$=ESC$+"[1;24r"	! Scroll control &
\	USASCII$=ESC$+"(B"		! United States ASCII &
\	BLINK.ON$=ESC$+"[5m"		! Blinking &
\	COLM.OFF$=ESC$+"[?3l"		! 80 Column &
\	COLM.ON$=ESC$+"[?3h"		! 132 Column &

100	ARTYPE.CH%   = 2% &
\	DEVICE.CH%   = 2% &
\	CHART.CH%    = 4% &
\	MENU.CH% = 12% &
\	LOOP.DATA% = 2% &
\	DATA.LEN%  = 16% &

200	IF FNO%(DEVICE.CH%,"DEVICE.DAT","/RO","")=0% &
	THEN	ARTYPE.DEVICE$=MID(FNL$,7%,20%)+'' &
			IF FNG%(DEVICE.CH%,"ARTYPE")=0% &
\		CHART.DEVICE$=MID(FNL$,7%,20%)+'' &
			IF FNG%(DEVICE.CH%,"CHART")=0% &
\		V%=FNC%(DEVICE.CH%) &

300	V%=FNO%(ARTYPE.CH%,ARTYPE.DEVICE$+'ARTYPE.DAS','/SF/RW','') &
\	V%=FNO%(ARTYPE.CH%,ARTYPE.DEVICE$+'ARTYPE.DAS','/SF/CR:16','') &
								IF FNS%=5% &
\	IF 	FNS% &
	THEN	V$=FNMESS$(FNS%,'ARTYPE.DAS',0%,-1%) &
\		GOTO 1045 &

310	CHART.FLAG%=FNO%(CHART.CH%,CHART.DEVICE$+'CHART.DAT','/RO','') &

400	OPEN "MENU.FIL/RO" FOR INPUT AS FILE MENU.CH% &
\	DIM #12%, A0$(1%)=64% &
\	COMPANY$=CVT$$(RIGHT(A0$(1%),2%),128%) &
\	CLOSE MENU.CH% &

450	OPEN "NL:" AS FILE 11%, RECORDSIZE 32% &
\	FIELD #11%,	01% AS ARTYPE.TYPE$, &
			08% AS ARTYPE.ACCNUM$	&
\	FIELD #11%, 16% AS ARTYPE.BUF$ &

500	FRAME$=FRAME$+FNP$(NUM1$(I%)+';1')+" "+FNP$(NUM1$(I%)+';80')+" " FOR I%=2% TO 19% &
\	FRAME1$=FRAME1$+FNP$(NUM1$(I%)+';1')+" "+FNP$(NUM1$(I%)+';132')+" " FOR I%=2% TO 19% &
\	COMMAND$  = " " &
\	COMMAND$  = COMMAND$+RIGHT(NUM1$(I%+100%),2%) + " " &
		FOR I%=1% TO LOOP.DATA% &

1000	!--------------------------------------------PROGRAM RESTART POINT &
	IF FNG%(ARTYPE.CH%,"")=0% &
	THEN	1010 &
		ELSE	PRINT #CH%, G.OFF$; &
\			GOSUB 1060 &
\			GOTO 1030 &

1005	IF FNN%(ARTYPE.CH%) &
	THEN	18910 &
		ELSE	1015 &

1010	PRINT #CH%, COLM.OFF$;SCROLL.ON$;G.OFF$; &
\	GOSUB 1060 &

1015	KEY.POINTER%=FNR(ARTYPE.CH%) &
\	GOSUB 6000 &

1030	CHNG.FLAG%=0% &
\	PRINT #CH%, CLRBOT$;G.OFF$; &
		FNP$('21;1');"COMMAND: Add Erase Change Blank Initialize "; &
		"Default Find Next Restore View";FNP$('22;10'); &
		"Quit "; &
\	OPT$=CVT$$(FNINP$(CH%,128%," ",1%,1%),32%) &
\	OPT$="A" IF OPT$="" &
\	OPT$="" IF KEY.POINTER%=0% AND INSTR(1%,"AQD",OPT$)=0% &

1040	IF OPT$='Q' &
	THEN	PRINT #CH%, CLSCN$;SCROLL.ON$;FNP$('1;75'); &
			B.ON$;BLINK.ON$;R.ON$;'WAIT';G.OFF$;FNP$('24;1'); &
			FNX%('',0%,''); &

1050	GOTO 2000 IF OPT$="A" &
\	GOTO 2400 IF OPT$="E" &
\	GOTO 2200 IF OPT$="C" &
\	GOTO 2300 IF OPT$="B" &
\	GOTO 2000 IF OPT$="F" &
\	GOTO 1005 IF OPT$="N" &
\	GOTO 17000 IF OPT$="V" &
\	GOTO 2500 IF OPT$="D" &
\	GOTO 2600 IF OPT$="I" &
\	GOTO 1000 IF OPT$="R" &
\	GOTO 1030 &

1060	TEMP$     = "A/R TYPE DEFINITION MAINTENANCE" &
\	PRINT #CH%, CLSCN$;R.ON$;LEFT(TEMP$,39%);SPACE$(40%-LEN(TEMP$)); &
			SPACE$(40%-LEN(COMPANY$));COMPANY$; &
			FNP$('20;1');SPACE$(80%);FRAME$; &
            		SPACE$(80%);FNP$('21;1');G.OFF$; &
\	PRINT #CH%,	FNP$('4;20');"(01) Type"; &
			FNP$('6;20');"(02) Account #"; &
\	RETURN &

2000	!-------------------------------------------------SEARCH FOR KEYS &
	FOR LOOP%=1% TO LOOP.DATA% &
\		INP$='' &
\		INP$=DEFAULT$(LOOP%) IF OPT$='A' &
\		GOSUB 6030 &
\	NEXT LOOP% &
\	OPTION$="ADD " &
\	OPTION$="FIND " IF OPT$="F" &
\	LOOP%=1% &
\	GOSUB 6210 &
\	SEARCH.KEY$=ARTYPE.TYPE$+'' &
\	LOOP%=2% &
\	GOSUB 6230 &
\	SEARCH.KEY$=SEARCH.KEY$+ARTYPE.ACCNUM$+'' &
\	V%=FNG%(ARTYPE.CH%,SEARCH.KEY$) &
\	IF OPT$="A" AND FNS%=0% OR OPT$='F' &
	THEN	GOTO 1015 &

2100	!-----------------------------------------------------ADD RECORD &

2110	IF FNA%(ARTYPE.CH%,ARTYPE.BUF$) &
	THEN	PRINT #CH%, FNP$('24;1');"Error";FNS%;"while adding to file"; &
			FNP$('24;55');"Hit any key to continue"; &
\		INP$=FNINP$(CH%,128%," ",1%,1%) &
\		GOTO 1000 &

2120	KEY.POINTER%=FNR(ARTYPE.CH%) &
\	GOTO 1030 &

2200	!--------------------------------------------------CHANGE RECORD &
	OPTION$ = "CHANGE " &
\	CHNG.FLAG% = -1% &
\	PRINT #CH%, CLRBOT$; "Change:"; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",2%,1%),32%) &
\	GOTO 1030 IF INP$="" &
\	LOOP%=(INSTR(1%,COMMAND$," "+INP$+" ")+2%)/3% &
\	GOTO 2200 IF LOOP%=0% &
\	GOSUB 6010 &

2210	V%=FNG%(-ARTYPE.CH%,NUM1$(KEY.POINTER%)) &
\	V%=FNU%(-ARTYPE.CH%,ARTYPE.BUF$) IF FNS%=0% &
\	IF FNS% &
	THEN	PRINT #CH%, FNP$('24;1');"Error";FNS%; &
			"while changing to file";FNP$('24;55'); &
			"Hit any key to continue"; &
\		INP$=FNINP$(CH%,128%," ",1%,1%) &
\		GOTO 1010 &

2220	GOTO 2200 &

2300	!--------------------------------------------------BLANK RECORD &
	OPTION$ = "BLANK " &
\	PRINT #CH%, CLRBOT$; "Blank:"; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",2%,1%),32%) &
\	GOTO 1030 IF INP$="" &
\	LOOP%=(INSTR(1%,COMMAND$," "+INP$+" ")+2%)/3% &
\	GOTO 2300 IF LOOP%>LOOP.DATA% &
\	INP$="" &
\	GOSUB 6030 &

2310	V%=FNG%(-ARTYPE.CH%,NUM1$(KEY.POINTER%)) &
\	V%=FNU%(-ARTYPE.CH%,ARTYPE.BUF$) IF FNS%=0% &
\	IF FNS% &
	THEN	PRINT #CH%, FNP$('24;1'); &
			"Error";FNS%;"while changing to file"; &
			FNP$('24;55');"Hit any key to continue"; &
\		INP$=FNINP$(CH%,128%," ",1%,1%) &
\		GOTO 1010 &

2320	GOTO 2300 &

2400	!-----------------------------------------------DELETE RECORD &
	PRINT #CH%, CLRBOT$;"Confirm deletion (Yes/No) "; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",1%,1%),32%) &
\	IF INP$<>"Y" &
	THEN	PRINT #CH%, FNP$('24;1');CLRLIN$;"This record has "; B.ON$; &
			BLINK.ON$;"NOT ";G.OFF$; "been deleted."; &
			FNP$('24;55');"Hit any key to continue "; &
\		INP$=FNINP$(CH%,128%," ",1%,1%) &
\		GOTO 1030 &

2420	IF FND%(ARTYPE.CH%,"")=0% &
	THEN	PRINT #CH%, FNP$('24;1');CLRLIN$; &
			"This record has been deleted."; &
			FNP$('24;55');"Hit any key to continue "; &
\		INP$=FNINP$(CH%,128%," ",1%,1%) &
\		GOTO 1005 &

2430	PRINT #CH%, FNP$('24;1');"Error";FNS%;"while deleting to file"; &
		FNP$('24;55');"Hit any key to continue"; &
\	INP$=FNINP$(CH%,128%," ",1%,1%) &
\	GOTO 1005 &

2500	!-----------------------------------------------SET DEFAULT VALUES &
	FOR LOOP%=1% TO LOOP.DATA% &
\		INP$=DEFAULT$(LOOP%) &
\		GOSUB 6030 &
\	NEXT LOOP% &

2510	OPTION$="DEFAULT " &
\	PRINT #CH%, CLRBOT$;"Default:"; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",2%,0%),32%) &
\	GOTO 1010 IF INP$="%^C" OR INP$="" &
\	LOOP%=(INSTR(1%,COMMAND$," "+INP$+" ")+2%)/3% &
\	GOTO 2510 IF LOOP%=0% &
\	GOSUB 6010 &
\	DEFAULT$(LOOP%)=INP$ &
\	GOTO 2510 &

2600	!-----------------------------------------------INITIALIZATION &
	INP$='' &
\	GOSUB 6030 FOR LOOP%=1% TO LOOP.DATA% &
\	INITIALIZE$=" " &

2610	PRINT #CH%, CLRBOT$;FNP$('22;1');INITIALIZE$; &
		FNP$('21;1');"Field to initialize:"; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",2%,1%),32%) &
\	IF INP$<>"" &
	THEN	LOOP%=(INSTR(1%,COMMAND$," "+INP$+" ")+2%)/3% &
\		INITIALIZE$=INITIALIZE$+INP$+" " IF &
			LOOP%<>0% AND (INSTR(1%,INITIALIZE$," "+INP$+" ") &
				+2%)/3%=0% &
\		GOTO 2610 &

2620	GOTO 1010 IF INITIALIZE$="" &
\	INP$='' &
\	GOSUB 6030 FOR LOOP%=1% TO LOOP.DATA% &
\	OPT$="C" &
\	OPTION$="INITIALIZE " &
\	CHNG.FLAG%=-1% &
\	GOTO 2660 &

2650	GOTO 18910 IF FNN%(ARTYPE.CH%) &
\	KEY.POINTER%=FNR(ARTYPE.CH%) &

2660	GOSUB 6000 &
\	FOR I%=1% TO LEN(INITIALIZE$)/3% &
\		LOOP%=(INSTR(1%,COMMAND$,MID(INITIALIZE$,I%*3%-2%,3%))+2%)/3% &
\		GOSUB 6010 &

2680		IF INP$<>"" &
		THEN	V%=FNG%(-ARTYPE.CH%,NUM1$(KEY.POINTER%)) &
\			V%=FNU%(-ARTYPE.CH%,ARTYPE.BUF$) IF FNS%=0% &
\			SORT.FLAG%=-1% IF LOOP%=1% AND FNS%=0% &
\			IF FNS% &
			THEN	PRINT #CH%, FNP$('24;1');"Error";FNS%; &
					"while changing to file"; &
				FNP$('24;55');"Hit any key to continue"; &
\				INP$=FNINP$(CH%,128%," ",1%,1%) &
\				GOTO 1010 &

2690	NEXT I% &
\	GOTO 2650 &

6000	!------------------------------------------------DATA ENTRY SECTION &
	RETURN IF KEY.POINTER%<1% OR FNG%(-ARTYPE.CH%,NUM1$(KEY.POINTER%)) &
\	LSET ARTYPE.BUF$=FNL$+'' &
\	FOR LOOP%=1% TO LOOP.DATA% &
\		ON LOOP%  GOSUB	6220, 6240 &
\	NEXT LOOP% &
\	RETURN &

6010	ON LOOP% GOSUB	6210, 6230 &
\	RETURN &
		! INPUT DATA &

6030	ON LOOP% GOSUB	6217, 6237 &
\	RETURN &
		! DEFAULT &

6200	!-----------------------------------------------DATA ENTRY SECTION &

6210	INP$=CVT$$(FNSIO$('','4;42',ARTYPE.TYPE$,DEFAULT$(LOOP%), &
			CHR$(CH%)+STRING$(2%,LEN(ARTYPE.TYPE$))+CHR$(1%)+ &
			CHR$(0%)+OPTION$+' ALPHA'),-1%) &
\	GOTO 6210 IF INP$='' AND OPT$='A' &
\	IF 	OPT$='C' AND INP$<>'' &
	THEN	IF 	FNG%(SERCHG.CH%,INP$+SPACE$(1%-LEN(INP$)))=0% &
		THEN	PRINT #CH%, CLRBOT$;FNP$('24;1');'Duplicate key ';INP$; &
				FNP$('21;1');'COMMAND: Re-enter Exit '; &
\			INP$=FNINP$(CH%,128%,' ',1%,0%) &
\			PRINT #CH%, FNP$('4;42');B.ON$;ARTYPE.TYPE$;G.OFF$; &
\			GOTO 6210 IF INP$='R' &
\			RETURN &

6215	IF INP$<>'' OR CHNG.FLAG%=0% &
	THEN	IF INSTR(1%,' DOSC',INP$)=0% &
		THEN	PRINT #CH%, FNP$('23;1');CLRLIN$; &
				"'S' = Sale; 'C' = Cost of sales"; &
				FNP$('24;1');CLRLIN$; &
				"Blank = not recorded; 'D' = Discount"; &
				"; 'O' = Other";FNP$('24;55'); &
				"Hit any key to continue"; &
\			INP$=FNINP$(CH%,128%," ",1%,1%) &
\			PRINT #CH%, FNP$('24;1');CLRLIN$; &
				FNP$('23;1');CLRLIN$; &
\			GOTO 6210 &

6217	LSET ARTYPE.TYPE$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6220	PRINT #CH%, FNP$('4;42');B.ON$;ARTYPE.TYPE$;G.OFF$; &
\	RETURN &

6230	INP$=FNSIO$('','6;42',ARTYPE.ACCNUM$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(ARTYPE.ACCNUM$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &
\	IF INP$<>'' AND CHART.FLAG%=0% &
	THEN	IF FNG%(CHART.CH%,SPACE$(8%-LEN(INP$))+INP$) &
		THEN	PRINT #CH%, FNP$('24;1');CLRLIN$; &
			'Undefined account number.';FNP$('24;55'); &
			'Entry anyway (Y/N) '; &
\			NW$=CVT$$(FNINP$(CH%,128%,'_',1%,1%),-1%) &
\			PRINT #CH%, FNP$('24;1');CLRLIN$; &
\			IF NW$<>'Y' &
			THEN	GOTO 6230 &

6237	RSET ARTYPE.ACCNUM$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6240	PRINT #CH%, FNP$('6;42');B.ON$;ARTYPE.ACCNUM$;G.OFF$; &
\	ACC.DESC$=SPACE$(20%) &
\	IF CHART.FLAG%=0% &
	THEN	IF FNG%(CHART.CH%,ARTYPE.ACCNUM$)=0% &
		THEN	ACC.DESC$=MID(FNL$,9%,20%)+'' &

6245	PRINT #CH%, FNP$('6;52');ACC.DESC$; &
\	RETURN &

17000	!-----------------------------------------------VIEW ROUTINE &
! Frame the scroll region &
	PRINT #CH%, R.ON$;CLSCN$;COLM.OFF$;FNP$('1;1');SPACE$(80%);FRAME$; &
	FNP$('20;1');SPACE$(80%);SCROLL.REGION$; &
! Set up title line &
\	PRINT #CH%, G.OFF$;	FNP$('2;2');"Type"; &
				FNP$('2;7');"Acct Num"; &
\	PRINT #CH%, R.ON$;LDS.ON$; &
				FNP$('2;6');CHR$(120%); &
				FNP$('2;15');CHR$(120%); &
					USASCII$;G.OFF$; &
\	PRINT #CH%, R.ON$;LDS.ON$; &
				FNP$('3;2');STRING$(80%,113%); &
				FNP$('3;6');CHR$(110%); &
				FNP$('3;15');CHR$(110%); &
					USASCII$;G.OFF$; &
\	DISPLAY.LINE%=0% &
\	PRINT.LINE%=4% &

17010	V%=FNG%(-ARTYPE.CH%,NUM1$(KEY.POINTER%)) &
\	GOTO 17105 &

17100	IF FNN%(ARTYPE.CH%) &
	THEN	17200 &

17105	I$=NUM1$(PRINT.LINE%) &
\	LSET ARTYPE.BUF$=FNL$+'' &
\	PRINT #CH%,	FNP$(I$+';2');ARTYPE.TYPE$; &
			FNP$(I$+';7');ARTYPE.ACCNUM$; &
			R.ON$;LDS.ON$; &
			FNP$(I$+';6');CHR$(120%); &
			FNP$(I$+';15');CHR$(120%); &
				USASCII$;G.OFF$; &
\	PRINT.LINE%=PRINT.LINE%+1% IF PRINT.LINE%<19% &

17110	IF DISPLAY.LINE%>14% &
	THEN	PRINT #CH%, FNP$('24;55');"Hit any key to continue "; &
\		INP$=FNINP$(CH%,128%," ",1%,1%) &
\		GOTO 17210 IF INP$="%^C" &
\		DISPLAY.LINE%=0% &
\		PRINT #CH%, CLRBOT$;FNP$(NUM1$(PRINT.LINE%)+';1'); &

17120	PRINT #CH% &
\	DISPLAY.LINE%=DISPLAY.LINE%+1% &
\	IF PRINT.LINE%=19% &
	THEN	PRINT #CH%, FNP$(NUM1$(PRINT.LINE%)+';1');R.ON$;" "; &
			FNP$(NUM1$(PRINT.LINE%)+';80');" ";G.OFF$; &

17190	GOTO 17100 &

17200	IF DISPLAY.LINE%>0% &
	THEN	PRINT #CH%, FNP$('24;1');"End of file.  "; &
			FNP$('24;55');"Hit any key to continue "; &
\		INP$=FNINP$(CH%,128%," ",1%,1%) &

17210	V%=FNG%(-ARTYPE.CH%,NUM1$(KEY.POINTER%)) &
\	PRINT #CH%, FNP$('1;1');SCROLL.ON$; &
\	GOTO 1010 &

18910	!----------------------------------------------END OF FILE &
	PRINT #CH%, CLRBOT$;FNP$('24;1');"End of file has been reached."; &
		FNP$('24;55');"Hit any key to continue "; &
\	INP$=FNINP$(CH%,128%," ",1%,1%) &
\	GOTO 1000 &

19000	!-------------------------------------------------ERROR TRAPPING &
	RESUME 30705 IF (ERR=51% OR ERR=52%) AND ERL>30700% &
\	RESUME IF ERR=51% OR ERR=52% &
\	RESUME 450 IF ERL=400% &
\	RESUME 1010 IF ERR=54% &
\	ON ERROR GOTO 0 &

30000	DEF FNINP$(CHN%,KYP%,FILLCHAR$,INPUTLEN%,TO.ERR%) &
\		PRINT #CHN%, STRING$(INPUTLEN%,ASCII(FILLCHAR$)); &
			  STRING$(INPUTLEN%,8%); &
\		PRINT #CHN%, RECORD 256%, CHR$(KYP%+INPUTLEN%)+FILLCHAR$; &
\		GET #CHN% &
\		FIELD #CHN%, RECOUNT AS BUFFER$ &
\		BUFFER$='%^C' IF INSTR(1%,BUFFER$,CHR$(3%)) &
\		FNINP$=CVT$$(BUFFER$,4%) &
\		V=SQR(-1) IF BUFFER$='%^C' AND TO.ERR% &
\	FNEND &

30050	  !------------------------------------------------------------ &
	  ! A N S I   D I R E C T   C U R S O R   A D D R E S S I N G &
	  !------------------------------------------------------------ &
	  DEF*FNP$(ROWCOL$)=ESC$+"["+ROWCOL$+"H" &

30060	DEF FND8%(D8)=D8 &

30070	DEF FND6%(D9$)=VAL(MID(D9$,3%,2%))+VAL(LEFT(D9$,2%))*32% &
		+ FND8%(VAL(RIGHT(D9$,5%)))*512% &

30080	DEF FND6$(D9%)=RIGHT(NUM1$((D9% AND 15%*32%)/32%+100%),2%) &
		+ "/"+RIGHT(NUM1$((D9% AND 31%)+100%),2%) &
		+ "/"+RIGHT(NUM1$(((SWAP%(D9%) AND 254%)/2%)+100%),2%) &

30250	!-----------------------------------------------SYSTEM MESSAGES	&
	DEF*FNMESS$(ERRNUM%,DESC$,TO.ERR%,PRINT.TEST%)			&
\		MESG$=MID(SYS(CHR$(6%)+CHR$(9%)+CHR$(ERRNUM%)),3%,30%)+	&
								DESC$	&
\		IF 	PRINT.TEST%					&
		THEN	PRINT #CH%, FNP$('24;1');MESG$;FNP$('24;55');	&
				'Hit any key to continue.';		&
\			NW$=FNINP$(CH%,128%,' ',1%,TO.ERR%)		&

30260		FNMESS$=MESG$						&
\	FNEND 								&

30700	!========================================= DATA INPUT FUNCTIONS	&
	DEF*FNSIO$(FRMAT$,ROWCOL$,VARABLE$,DEFAL$,ITEMS$)		&
\		CHN%=ASCII(MID(ITEMS$,1%,1%))				&
\		KIND%=ASCII(MID(ITEMS$,4%,1%))				&
\		DECI=ASCII(MID(ITEMS$,5%,1%))				&
\		OPT$=MID(ITEMS$,6%,1%)					&

30705		PRINT #CHN%, FNP$(ROWCOL$);R.ON$;B.ON$;			&
\		IF OPT$='C'						&
		THEN	ON KIND% GOTO 30710, 30715, 30720, 30725, 30730	&
		ELSE	PRINT #CHN%, SPACE$(ASCII(MID(ITEMS$,2%,1%)));	&
\			GOTO 30735					&

30710		PRINT #CHN%, VARABLE$;			   ! ALPHA	&
\		GOTO 30735						&

30715		PRINT #CHN% USING FRMAT$,ASCII(VARABLE$);  ! ASCII	&
\		GOTO 30735						&

30720		PRINT #CHN% USING FRMAT$, CVT$%(VARABLE$)/(10.**DECI);	&
\		GOTO 30735				   ! INTEGER	&

30725		PRINT #CHN%, FND6$(CVT$%(VARABLE$));	   ! DATE	&
\		GOTO 30735						&

30730		PRINT #CHN% USING FRMAT$,CVT$F(VARABLE$);  ! FLOAT	&

30735		PRINT #CHN%,G.OFF$;CLRBOT$;RIGHT(ITEMS$,6%)+':  ';B.ON$;&
\		FIN$ = FNINP$(CHN%,0%,'_',ASCII(MID(ITEMS$,3%,1%)),1%)	&
\		V% = VAL(FIN$) IF KIND%=3%				&
\		V  = VAL(FIN$) IF KIND%>3%				&
\		IF FIN$=''						&
		THEN	FIN$ = DEFAL$ IF INSTR(1%,'CF',OPT$)=0%		&
\			GOTO 30745					&

30740		IF KIND%=3% OR KIND%=5%					&
		THEN	TEMP = 1.					&
\			TEMP = 10.**DECI IF INSTR(1%,FIN$,'.')=0%	&
\			FIN$ = NUM1$(VAL(FIN$)/TEMP)			&
\			FIN$ = FIN$ + '.' IF INSTR(1%,FIN$,'.')=0%	&

30745		FNSIO$ = FIN$+''					&
\	FNEND								&
!\	INP$=FNSIO$('',NUM1$(ROW%)+';'+NUM1$(COL%),TAXTBL.AUTH$,'',CHR$(CH%)+ &
!		CHR$(LEN(TAXTBL.AUTH$))+CHR$(LEN(TAXTBL.AUTH$))+CHR$(0%)+OPTION$) &
!\	INP$=FNSIO$('<PRINT USING OR NULL>','<ROW>;<COL>',<FIELDED VARIABLE>, &
!			DEFAULT$(LOOP%),CHR$(<CHANNEL #>%), &
!			CHR$(<HI LIGHT LEN>%),CHR$(<FNINP$ LEN.>%), &
!			CHR$(<1-ALPHA 2-ASCII 3-INTEGER 4-DATE 5-FLOAT>%), &
!			CHR$(<# OF DECIMAL PLACES>%),<OPTION$+" ALPHA" or &
!			OPTION$+" VALUE" or OPTION$+" DATE(MMDDYY)">) &

32767	END &



