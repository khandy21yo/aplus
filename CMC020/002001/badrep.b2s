10	!---------------------------------------------------------------&
	!	ANY			-BADCHK				&
	!	BAD CKECK LIST						&
	!								&
	!	BADREP.B2S	V1.0	June 1985			&
	!								&
	! Author - Glenn C. Hillam, Computer Management Center, Inc.	&
	!								&
	! Files-PPRINT.DAT	-ISAM	Printing Info File		&
	!	BADCHK.DAT	-ISAM	File Description Name		&
	!		Other files					&
	!---------------------------------------------------------------&
	!		      Copyright (c) 1984 by 			&
	!	  Computer Management Center, Idaho Falls, Idaho	&
	! 								&
	! This software is furnished under a license and may be used	&
	! and copied only in accordance with the terms of such license 	&
	! and with the inclusion of the above copyright notice.  This 	&
	! software or any other copies therof may not be provided or 	&
	! otherwise made available to any other person.  No title to 	&
	! and ownership of the software is hereby transferred.		&
	!      The information in this software is subject to change	&
	! without notice and should not be construed as a committment 	&
	! by Computer Management Center. 				&
	!      CMC assumes no responsibility for the use or reliability &
	! of its software on equipment which is not supported by CMC. 	&
	!---------------------------------------------------------------&

20	PRINT '?Please run REPORT' &
\	M$=SYS(CHR$(9%)) &

30	ON ERROR GOTO 19000 &
\	JUNK$=SYS(CHR$(6%)+CHR$(-7%)) &
\	JUNK$=SYS(CHR$(6%)+CHR$(9%)) &
!	&
\	TEMP$=SYS(CHR$(12%)) &
\	PRJPRG$=MID(TEMP$,23%,2%)+NUM1$(ASCII(MID(TEMP$,25%,1%))) + ':'+'['+ &
		NUM1$(ASCII(MID(TEMP$,6%,1%)))+','+NUM1$(ASCII(MID(TEMP$,5%, &
		1%)))+']'+RAD$(ASCII(MID(TEMP$,7%,1%))+SWAP%(ASCII(MID(TEMP$,&
		8%,1%))))+RAD$(ASCII(MID(TEMP$,9%,1%))+SWAP%(ASCII(MID(TEMP$,&
		10%,1%))))   &

60	CH%,OUTPUT.CH%=1%			! Keybrd,Output channel &
\	OPEN 'KB:' AS FILE 1%, MODE 8%+256% 	! Echo,Disbl Hibrn,%^C 	&

70	ESC$=CHR$(155%)				! Escape code for VT100 &
\	COLM.ON$=ESC$+'[?3h'			! 132 Column mode 	&
\	COLM.OFF$=ESC$+'[?3l'			! 80 Column mode 	&
\	R.ON$=ESC$+'[7m'      			! Reverse video 	&
\	G.OFF$=ESC$+'[m'			! Select graphic off 	&
\	CLRLIN$=ESC$+'[2K'			! Erase entire line 	&
\	CLSCN$=ESC$+'[H'+ESC$+'[J'		! Clear entire screen 	&
\	ENTER.COPY$=ESC$+'[5i'			! Enter media copy 	&
\	EXIT.COPY$=ESC$+'[4i'			! Exit media copy 	&

80	!COM(THIS) DROP.DEAD.DATE$ = 8, VERSION.NO$ = 6, SERIAL.NO$ = 10 &
	DROP.DEAD.DATE$='        ' &
\	VERSION.NO$='V1.0' &
\	DATE.TIME$=SYS(CHR$(6%)+CHR$(20%)+CHR$(0%)+CHR$(0%)+CHR$(1%)+ &
		CHR$(SWAP%(1%))+STRING$(11%,0%)+CHR$(SWAP%(0%))+CHR$(1%)+ &
		CHR$(SWAP%(1%))) &
\	IF 	DROP.DEAD.DATE$<>'' &
	THEN	IF 	DROP.DEAD.DATE$<MID(DATE.TIME$,7%,8%) &
		THEN	MESG$=MID(SYS(CHR$(6%)+CHR$(9%)+CHR$(47%)),3%,30%) &
\			PRINT #CH%, MESG$;SYS(CHR$(9%)) &

100	DEVICE.CH%,PPRINT.CH%	= 02% &
\	BADCHK.CH% 			= 02% &
\	MENU.CH%		= 12% &

150	IF 	FNO%(PPRINT.CH%,'PPRINT.DAT','/RO','') &
	THEN	V$=FNMESS$(CH%,FNS%,' PPRINT.DAT',0%,-1%) &
\		GOTO 17540 &

160	IF 	FNG%(PPRINT.CH%,REP.NUM$) &
	THEN	V$=FNMESS$(CH%,FNS%,' PPRINT.DAT  '+REP.NUM$,0%,-1%) &
\		GOTO 17540 &

170	FIELD #PPRINT.CH%+1%,FNL%+6% AS JUNK$,	&
		20% AS PPRINT.REPDESC$,		&
		30% AS PPRINT.MUNG$,		&
		01% AS PPRINT.HPITCH$,		&
		01% AS PPRINT.VPITCH$,		&
		01% AS PPRINT.LPAGE$,		&
		20% AS PPRINT.OUTDEV$,		&
		06% AS PPRINT.SPOOLR$,		&
		02% AS PPRINT.SPAGE$,		&
		02% AS PPRINT.EPAGE$,		&
		02% AS PPRINT.COPIES$,		&
		20% AS PPRINT.REPDATE$,		&
		44% AS JUNK$,			&
		20% AS PPRINT.UDF$(1%),		&
		44% AS JUNK$,			&
		20% AS PPRINT.UDF$(2%),		&
		44% AS JUNK$,			&
		20% AS PPRINT.UDF$(3%),		&
		44% AS JUNK$,			&
		20% AS PPRINT.UDF$(4%),		&
		44% AS JUNK$,			&
		20% AS PPRINT.UDF$(5%)		&
\	FIELD #PPRINT.CH%+1%,FNL%+441% AS JUNK$,&
		03% AS B.SYSNAM$,		&
		08% AS MUNG$,			&
		01% AS B.DISPLAY$		&
\	REPDESC$	=PPRINT.REPDESC$+'' 	&
\	HPITCH%		=ASCII(PPRINT.HPITCH$) 	&
\	VPITCH%		=ASCII(PPRINT.VPITCH$) 	&
\	LIN.PER.PAGE%	=ASCII(PPRINT.LPAGE$) 	&
\	OUTDEV$		=PPRINT.OUTDEV$+'' 	&
\	SPAGE%		=CVT$%(PPRINT.SPAGE$) 	&
\	EPAGE%		=CVT$%(PPRINT.EPAGE$) 	&
\	COPIES%		=CVT$%(PPRINT.COPIES$) 	&
\	REPDATE$	=CVT$$(PPRINT.REPDATE$,132%) 	&
\	DISPLAY$  	=B.DISPLAY$+'' 		&
!						&
\	COMP.STRING1$	=CVT$$(PPRINT.UDF$(1%),132%) 	&
\	COMP.STRING2$	=CVT$$(PPRINT.UDF$(2%),132%) 	&
\	COMP.STRING3$	=CVT$$(PPRINT.UDF$(3%),132%) 	&
\	COMP.STRING3$	='*' IF COMP.STRING3$='' &
\	V%=FNC%(PPRINT.CH%) ! Close PPRINT.DAT channel 	&

200	IF 	FNO%(DEVICE.CH%,'DEVICE.DAT','/RO','')=0% &
	THEN	BADCHK.DEVICE$=MID(FNL$,7%,20%)+'' &
			IF FNG%(DEVICE.CH%,'BADCHK')=0% &
\		V%=FNC%(DEVICE.CH%) &

210	GOTO 300 IF CHAIN.FLAG% &
\	V%=FNX%('ISM:ISMBLD',30000%,BADCHK.DEVICE$+'BADCHK.DA1/X@127-2;13-20$'+ &
				PRJPRG$+'#30900*~'+REP.NUM$) &

300	IF   	FNO%(BADCHK.CH%,BADCHK.DEVICE$+'BADCHK.DAX','/RO','')&
	THEN	V$=FNMESS$(CH%,FNS%,' BADCHK.DAX',0%,-1%) &
\		GOTO 17540 &

400	OPEN 'MENU.FIL/RO' FOR INPUT AS FILE MENU.CH% &
\	DIM #12%, A0$(1%)=64% &
\	COMPANY$=CVT$$(RIGHT(A0$(1%),2%),128%) &
\	CLOSE MENU.CH% &
	
510	TITLE$='BAD CHECK LIST' &
\	PAGE.TOP$=STRING$(2%,10%) + CHR$(13%) + 'Date: ' + DATE$(0%) + &
		SPACE$(50%-LEN(COMPANY$)/2%) + COMPANY$ + &
		SPACE$(58%-LEN(COMPANY$)/2%) + 'Page <<###>>' + CHR$(10%) + &
		CHR$(13%) + 'Time: ' + TIME$(0%) + SPACE$(51%-LEN(TITLE$)/2%) +&
		TITLE$ + SPACE$(57%-LEN(TITLE$)/2%) + VERSION.NO$ + CHR$(10%) +&
		CHR$(13%) + SPACE$(64%-LEN(REPDATE$)/2%) + REPDATE$ + &
		STRING$(2%,10%) + CHR$(13%) + &
		STRING$(130%,61%) + CHR$(10%) + CHR$(13%) &
\	PAGE.TOP1$='Location  Acct.           Name/Address                                   Amount     Date     Bank              ' + CHR$(10%)+CHR$(13%) &
\	USE1$	=  '   \\     \           \   \                                          \  ##,###.##  \      \  \              \' &
\	USE2$	=  '                          \                   \' &
\	USE3$   =  '                          \             \   \\' &
\	USE4$   =  '                          \        \  '&
\	BOT.MARGIN%=6% &
\	PAGE.BOT$=STRING$(BOT.MARGIN%,10%)+CHR$(13%) &
\	PAGE.BREAK%=LIN.PER.PAGE%-BOT.MARGIN% &
\	MORE.LINES%=2% &
\	DISPLAY.CONTROL%=15% &

10000	IF 	DISPLAY$<>'Y' &
	THEN	CLOSE CH% &
\		RESET.CH%=1% &
\		TEMP.CH%=12% &
\		OPEN OUTDEV$ AS FILE OUTPUT.CH%, MODE 2% &
\		IF 	SPAGE%>1%	THEN	OPEN 'NL:' AS FILE TEMP.CH% &
\						OUTPUT.CH%=TEMP.CH% &

17000	!---------------------------------------OUTPUT REPORT &
	START.TEMP$='' &
\	START.TEMP$=COMP.STRING1$ IF COMP.STRING1$<>'ALL' &
\	V%=FNG%(BADCHK.CH%,START.TEMP$) &
\	IF 	DISPLAY$='Y' &
	THEN	PRINT #OUTPUT.CH%, CLSCN$;R.ON$;COLM.ON$;PAGE.TOP1$;G.OFF$; &
			FNSR$('2;19');FNP$('19;1'); &
	ELSE	PRINT #OUTPUT.CH%, ENTER.COPY$;	&
			FNPAGE$(LIN.PER.PAGE%,0%,1%,PAGE.TOP$+PAGE.TOP1$,''); &
\		LINE.COUNT%=FNLINE% &

17030	FIELD #BADCHK.CH%+1%,FNL% AS JUNK$,	&
		12% AS BADCHK.ACCT.NUM$, 	&
		20% AS BADCHK.LAST.NAME$, 	&
		25% AS BADCHK.FIRST.NAME$, 	&
		20% AS BADCHK.ADDRESS1$, 	&
		15% AS BADCHK.CITY$, 	&
		02% AS BADCHK.STATE$,	&
		10% AS BADCHK.ZIP$, 	&
		08% AS BADCHK.AMOUNT$, 	&
		02% AS BADCHK.DATE$, 	&
		12% AS BADCHK.BANK$, 	&
		02% AS BADCHK.LOCATION$	&
\	GOTO 17400 IF BADCHK.LOCATION$>COMP.STRING2$ AND &
		COMP.STRING2$<>'' &
\	IF 	FNCOMP%(BADCHK.LOCATION$,COMP.STRING3$) &
	THEN	PRINT #OUTPUT.CH% USING USE1$,	&
			BADCHK.LOCATION$,	&
			BADCHK.ACCT.NUM$,	&
			CVT$$(BADCHK.LAST.NAME$,140%)+' ,'+BADCHK.FIRST.NAME$&
			CVT$F(BADCHK.AMOUNT$) ,	&
			FND6$(CVT$%(BADCHK.DATE$)),&
			BADCHK.BANK$	&
\		LINE.COUNT%=LINE.COUNT%+1% &
\		PRINT #OUTPUT.CH%, USING USE2$,  &
		BADCHK.ADDRESS1$	&
\		LINE.COUNT%=LINE.COUNT%+1% &
\		PRINT #OUTPUT.CH%, USING USE3$,  &
		BADCHK.CITY$,		&
		BADCHK.STATE$		&
\		LINE.COUNT%=LINE.COUNT%+1% &
\		PRINT #OUTPUT.CH%, USING USE4$,  &
		BADCHK.ZIP$		&
\		LINE.COUNT%=LINE.COUNT%+1% &
\		GOSUB 17600 &
\		GOTO 17400 IF END.FLAG% &	

17350	GOTO 17030 IF FNN%(BADCHK.CH%)=0% &

17400	!IF 	END.FLAG%=0% &
	!THEN	PRINT TOTAL LINE &

17500	IF 	DISPLAY$<>'Y' &
	THEN	PRINT #OUTPUT.CH%, FNPAGE$(LIN.PER.PAGE%,LINE.COUNT%,FNPAGE%, &
			'',PAGE.BOT$); &
	ELSE	PRINT #OUTPUT.CH%, FNP$('24;1');'End of report'; &
			FNP$('24;105');'Hit any key to continue'; &
\		INP$=FNINP$(CH%,128%,' ',1%,1%) &

17540	KILL BADCHK.DEVICE$+'BADCHK.DAX' &
	!KILL 'TEMPORARY.FILES' &

17550	PRINT #OUTPUT.CH%, EXIT.COPY$; IF DISPLAY$<>'Y' &
\	PRINT #OUTPUT.CH%, FNSR$('1;24');COLM.OFF$; IF DISPLAY$='Y' &
\	CLOSE OUTPUT.CH% &
\	V%=FNX%('',0%,'') &

17600	!Perform the pagination &
	IF 	DISPLAY$<>'Y' AND LINE.COUNT%+MORE.LINES%>PAGE.BREAK% &
	THEN	END.FLAG%=-1% IF PAGE%>=EPAGE% AND EPAGE%<>0% &
\		RETURN IF END.FLAG% &
\		PRINT #OUTPUT.CH%, FNPAGE$(LIN.PER.PAGE%,LINE.COUNT%, &
			FNPAGE%,PAGE.TOP$+PAGE.TOP1$,PAGE.BOT$); &
\		LINE.COUNT%=FNLINE% &
\		IF 	SPAGE%>1% &
		THEN	IF 	SPAGE%<=PAGE% &
			THEN	OUTPUT.CH%=RESET.CH% &
\				PRINT #OUTPUT.CH%, FNPAGE$(LIN.PER.PAGE%,0%, &
					PAGE%,PAGE.TOP$+PAGE.TOP1$,''); &
\				LINE.COUNT%=FNLINE% &
\				SPAGE%=0% &

17650	IF 	DISPLAY$='Y' AND LINE.COUNT%>DISPLAY.CONTROL% &
	THEN	PRINT #OUTPUT.CH%, FNP$('24;105');'Hit any key to continue'; &
\		INP$=FNINP$(CH%,128%,' ',1%,1%) &
\		LINE.COUNT%=0% &
\		PRINT #OUTPUT.CH%, FNP$('24;1');CLRLIN$;FNP$('18;132') &

17670	RETURN &

18000	! 18000-18800 SUBROUTINES &

18900	! SEND MESSAGE IF DETACHED KEYBOARD &

19000	!---------------------------------------ERROR TRAPPING 		&
	RESUME IF ERR=52% 						&
\	RESUME 17500 IF ERR=11%						&
\	RESUME 510 IF ERL=400% 						&
\	RESUME 17540 IF ERR=54% 					&
\	RESUME 17550 IF ERL=17540 					&

19010	IF ERR=28%	THEN	JUNK$ = SYS(CHR$(6%) + CHR$(-7%)) 	&
\				RESUME 17500 IF OUTPUT.CH%=CH% 		&

19015	IF ERR=27%	THEN	RESUME 70 IF ERL=60% 			&
\				RESUME 18900 				&

19900	ON ERROR GOTO 0 						&
						
30000	!---------------------------------------TERMINAL INPUT 		&
	DEF FNINP$(CHN%,KYP%,FILLCHAR$,INPUTLEN%,TO.ERR%)		&
\		PRINT #CHN%, STRING$(INPUTLEN%,ASCII(FILLCHAR$)); 	&
			  STRING$(INPUTLEN%,8%); 			&
\		PRINT #CHN%, RECORD 256%,CHR$(KYP%+INPUTLEN%)+FILLCHAR$;&
\		GET #CHN% 						&
\		FIELD #CHN%, RECOUNT AS BUFFER$ 			&
\		BUFFER$='%^C' IF INSTR(1%,BUFFER$,CHR$(3%)) 		&
\		FNINP$=CVT$$(BUFFER$,4%) 				&
\		V=SQR(-1) IF BUFFER$='%^C' AND TO.ERR% !^C Trappping	&
\	FNEND 								&
						
30200	DEF*FNP$(ROWCOL$)=ESC$+'['+ROWCOL$+'H'	!DIRECT CURSOR ADDRESS	&

30250	!---------------------------------------MESSAGE HANDLER		&
	DEF*FNMESS$(CHN%,ERRNUM%,DESC$,TO.ERR%,PRINT.TEST%)		&
\		MESG$=MID(SYS(CHR$(6%)+CHR$(9%)+			&
			CHR$(ERRNUM%)),3%,30%)+DESC$			&
\		IF 	PRINT.TEST%					&
		THEN	PRINT #CHN%, FNP$('24;1');CLRLIN$;MESG$;	&
				FNP$('24;55');'Hit any key to continue';&
\			NW$=FNINP$(CHN%,128%,' ',1%,TO.ERR%)		&

30260	FNMESS$=MESG$							&
\	FNEND 								&
						
30300	DEF*FNSR$(BEGEND$)=ESC$+'['+BEGEND$+'r'	!SCROLL CONTROL		&

30310	DEF FND6$(D9%)=RIGHT(NUM1$((D9% AND 15%*32%)/32%+100%),2%) 	&
		+ '/'+RIGHT(NUM1$((D9% AND 31%)+100%),2%) 		&
		+ '/'+RIGHT(NUM1$(((SWAP%(D9%) AND 254%)/2%)+100%),2%) 	&

30320	DEF FNFONE$(PHN$)='('+LEFT(PHN$,3%)+')'+!PH # FORMAT FUNCTION	&
		MID(PHN$,4%,3%)+'-'+RIGHT(PHN$,7%) 			&

30340	DEF FNZ(Y) 				!ROUNDING FUNCTION	&
\		TEMP=Y 							&
\		TEMP=-Y IF Y<0. 					&
\		TEMP=INT(TEMP*100.+.51)/100. 				&
\		TEMP=-TEMP IF Y<0. 					&
\		FNZ=TEMP 						&	
\	FNEND 								&

30600	DEF FNPAGE$(Y0%,Y1%,Y2%,Y0$,Y1$) 	!PAGING FUNCTION	&
\		Y2$='' 							&
\		Y2$=STRING$(Y0%-(Y1%+LEN(XLATE(Y1$,STRING$(10%,0%)+	&
			CHR$(10%)))),10%) IF Y1$<>'' 			&
\		PAGE.LINE%=LEN(XLATE(Y0$,STRING$(10%,0%)+CHR$(10%))) 	&
\		Y%=INSTR(1%,Y1$+Y0$,'<<#') 				&
\		Y3%=INSTR(1%,Y1$+Y0$,'#>>') 				&
\		Y$=RIGHT(NUM1$(100000+Y2%),8%-(Y3%-Y%)) 		&
\		Y3%=-3% IF Y%=0% 					&
\		PRINT #OUTPUT.CH%, Y2$;LEFT(Y1$+Y0$,Y%-1%);Y$; 		&
			RIGHT(Y1$+Y0$,Y3%+3%); 				&
\		PAGE%=Y2% 						&
\	FNEND 								&

30650	DEF FNPAGE%=PAGE%+1% 						&

30660	DEF FNLINE%=PAGE.LINE% 						&
						
30800	DEF FNCOMP%(Y$,Y2$)			!COMPARING FUNCTION	&
\	Y9%=0% 								&
\	Y9%=-1% IF Y2$='*' 						&
\	Y2$=Y2$+',' 							&

30820	IF 	Y9%=0% 							&
	THEN	Y1$=LEFT(Y2$,INSTR(1%,Y2$,',')-1%) 			&
\		Y2$=RIGHT(Y2$,LEN(Y1$)+2%) 				&
\		Y1%=INSTR(1%,Y1$,'/') 					&
\		IF 	Y1%+INSTR(1%,Y1$,'?')=0% 			&
		THEN	Y9%=Y$=Y1$ 					&
		ELSE 	IF 	Y1% 					&
			THEN	Y9%=LEFT(Y1$,Y1%-1%)<=Y$ 		&
					AND Y$<=RIGHT(Y1$,Y1%+1%)	&
			ELSE	CHANGE CVT$$(LEFT(Y$,30%),-1%) TO Y% 	&
\				CHANGE CVT$$(LEFT(Y1$,30%),-1%) TO Y1%	&
\				GOTO 30830 IF (Y%(Y3%)<>Y1%(Y3%))-	&
					(Y1%(Y3%)=63%) 	&
						FOR Y3%=1% TO Y1%(0%)	&
\				Y9%=-1% 				&

30830	GOTO 30820 IF Y2$<>'' AND Y9%=0% 				&
\	FNCOMP%=Y9% 							&
\	FNEND 								&

30900	CHAIN.FLAG%=-1% 			!CHAIN ENTER LINE	&

31000	!---------------------------------------REPORT ENTER LINE	&
	M$=SYS(CHR$(7%)) 						&
\	REP.NUM$=MID(M$,INSTR(1%,M$,'~')+1%,6%) 			&
\	GOTO 30 							&

32767	END &

