10	!--------------------------------------------------------------- &
	!	CASH RECEIPTS JOURNAL MAINTENANCE 	- &
	! &
	!	CRJMNT.B2S	V1.0	March 1985 &
	! &
	! Author - Randall Beard, Computer Management Center, Inc. &
	! &
	! Files-CUSTOM.DAT	-ISAM	Customer Description File &
	! Files-CHART.DAT	-ISAM	Chart of accounts File &
	! Files-CRJH.DAT	-ISAM	Header file &
	! Files-CRJL.DAT	-ISAM 	Line item file &
	! Files-ARREG.DAT	-ISAM	A/R file &
	! &
	!--------------------------------------------------------------- &
	! &
	! &
	!		      Copyright (c) 1984 by &
	!	  Computer Management Center, Idaho Falls, Idaho &
	! &
	! This software is furnished under a license and may be used &
	! and copied only in accordance with the terms of such license &
	! and with the inclusion of the above copyright notice.  This &
	! software or any other copies therof may not be provided or &
	! otherwise made available to any other person.  No title to &
	! and ownership of the software is hereby transferred. &
	! &
	! The information in this software is subject to change without &
	! notice and should not be construed as a committment by &
	! Computer Management Center. &
	! &
	! CMC assumes no responsibility for the use or reliability of &
	! its software on equipment which is not supported by CMC. &
	! &
	!--------------------------------------------------------------- &

50	ON ERROR GOTO 19000 &
\	DIM DEFAULT$(30%),SEARCH$(30%),REPLCE$(30%),CRJL.KEY%(200%), &
		CRJL.DATA%(200%) &

60	OPEN 'KB:' AS FILE #1%, MODE 8%+256% &
					!    8% - Echo Control &
					!   16% - Disable hibernation, Cntl-C &
\	CH%=1%				! Keyboard channel &
\	ESC$=CHR$(155%)			! Escape code for VT100 control &
\	CLSCN$=ESC$+'[H'+ESC$+'[J'	! Clear screen &
\	CLRLIN$=ESC$+'[2K'		! Erase entire line &
\	INSERT.LIN$=ESC$+'[1L'		! Insert a line &
\	G.OFF$=ESC$+'[m'		! Select graphic off &
\	B.ON$=ESC$+'[1m'		! Bold face on &
\	BLINK.ON$=ESC$+'[5m'		! Blinking &
\	R.ON$=ESC$+'[7m'		! Reverse video &
\	COLM.ON$=ESC$+'[?3h'		! 132 Column mode &
\	COLM.OFF$=ESC$+'[?3l'		! 80 Column mode &
\	CLRBOT$=ESC$+'[21;1H'+ESC$+'[J'	! Erace cursor to end of screen &
\	LDS.ON$=ESC$+'(0'		! Line drawing set &
\	USASCII$=ESC$+'(B'		! United States ASCII &

80	!COM(THIS) DROP.DEAD.DATE$ = 8, VERSION.NO$ = 6, SERIAL.NO$ = 10 &
	DROP.DEAD.DATE$='        ' &
\	VERSION.NO$='V1.0' &
\	IF DROP.DEAD.DATE$<>'' &
	THEN	IF DROP.DEAD.DATE$<MID(SYS(CHR$(6%)+CHR$(20%)+CHR$(0%)+CHR$(0%)+ &
				CHR$(1%)+CHR$(SWAP%(1%))),7%,8%) &
		THEN	MESG$=MID(SYS(CHR$(6%)+CHR$(9%)+CHR$(47%)), &
				3%,30%) &
\			PRINT #CH%, MESG$; &
\			V$=SYS(CHR$(9%)) &

100	DEVICE.CH%	= 02% &
\	APCCTL.CH%	= 02% &
\	CRJH.CH%	= 02% &
\	CRJL.CH%	= 04% &
\	CUSTOM.CH%	= 06% &
\	CHART.CH%	= 08% &
\	ARREG.CH%	= 10% &
\	MENU.CH%   	= 12% &
	&
\	LOOP.DATA%	= 16% &
\	CRJH.LOOP.DATA%	= 16% &
\	CRJL.LOOP.DATA%	= 21% &
\	CUSTOM.START%	= 4% &
\	CUSTOM.END%  	= 10% &

200	IF FNO%(DEVICE.CH%,'DEVICE.DAT','/RO','')=0% &
	THEN	CRJH.DEVICE$=CVT$$(MID(FNL$,7%,20%),-1%) &
			IF FNG%(DEVICE.CH%,'CRJH')=0% &
\		CUSTOM.DEVICE$=CVT$$(MID(FNL$,7%,20%),-1%) &
			IF FNG%(DEVICE.CH%,'CUSTOM')=0% &
\		CHART.DEVICE$=CVT$$(MID(FNL$,7%,20%),-1%) &
			IF FNG%(DEVICE.CH%,'CHART')=0% &
\		ARREG.DEVICE$=CVT$$(MID(FNL$,7%,20%),-1%) &
			IF FNG%(DEVICE.CH%,'ARREG ')=0% &
\		APCCTL.DEVICE$=CVT$$(MID(FNL$,7%,20%),-1%) &
			IF FNG%(DEVICE.CH%,'APCCTL ')=0% &
\		V%=FNC%(DEVICE.CH%) &

300	PRINT #CH%, CLSCN$; &
		FNP$('4;28');'CASH RECEIPTS JOURNAL ENTRY ROUTINE'; &
		FNP$('6;28');'Batch Number <01> '; &
\	JUNK$ = FNINP$(CH%, 128%, '_', 2%,0%) &
\	GOTO 1045 IF JUNK$='%^C' &
\	JUNK$ = "01" IF JUNK$ = "" &
\	IF LEN(JUNK$) = 1% &
	THEN	JUNK$ = '0' + JUNK$ &

310	PRINT #CH%, COLM.OFF$+FNP$('1;75');B.ON$;BLINK.ON$;R.ON$;'WAIT'; &
			G.OFF$;FNP$('24;1'); &
\	BATCH.NUM$ = JUNK$ &

320	DEFAULT.METHOD$ = 'O' &
\	IF FNO%(APCCTL.CH%,APCCTL.DEVICE$+'APCCTL.DAS','/SF/RO','')=0% &
	THEN	IF FNG%(APCCTL.CH%,'ARL   ')=0% &
		THEN	FIELD #APCCTL.CH%, 304%+FNL% AS TEMP$, &
				01% AS APCCTL.METHOD$ &
\			IF APCCTL.METHOD$='O' OR APCCTL.METHOD$='B' &
			THEN	DEFAULT.METHOD$ = APCCTL.METHOD$ + '' &

325	V%=FNC%(APCCTL.CH%) &
\	V%=FNO%(CRJH.CH%,CRJH.DEVICE$+'CRJH'+BATCH.NUM$+'.DAT','/RW','') &
\	V%=FNO%(CRJH.CH%,CRJH.DEVICE$+'CRJH'+BATCH.NUM$+'.DAT','/CR:10,128','') &
								IF FNS% = 5% &
\	IF FNS% &
	THEN	V$=FNMESS$(CH%,FNS%,'CRJH'+BATCH.NUM$+'.DAT',0%,-1%) &
\		GOTO 1045 &

330	V%=FNO%(CRJL.CH%,CRJH.DEVICE$+'CRJL'+BATCH.NUM$+'.DAT','/RW','') &
\	V%=FNO%(CRJL.CH%,CRJH.DEVICE$+'CRJL'+BATCH.NUM$+'.DAT','/CR:12,128','') &
								IF FNS% = 5% &
\	IF FNS% &
	THEN	V$=FNMESS$(CH%,FNS%,'CRJL'+BATCH.NUM$+'.DAT',0%,-1%) &
\		GOTO 1045 &

340	V%=FNO%(CUSTOM.CH%,CUSTOM.DEVICE$+'CUSTOM.DAT','/RO','') &
\	IF FNS% &
	THEN	V$=FNMESS$(CH%,FNS%,'CUSTOM.DAT',0%,-1%) &
\		GOTO 1045 &

350	CHART.FLAG%=FNO%(CHART.CH%,CHART.DEVICE$+'CHART.DAT','/RO','') &

360	ARREG.FLAG%=FNO%(ARREG.CH%,ARREG.DEVICE$+'ARREG.DAT','/RO','') &

400	OPEN 'MENU.FIL/RO' FOR INPUT AS FILE MENU.CH% &
\	DIM #12%, A0$(1%)=64% &
\	COMPANY$=CVT$$(RIGHT(A0$(1%),2%),128%) &
\	CLOSE MENU.CH% &

450	OPEN 'NL:' AS FILE 12%, RECORDSIZE 256%+128%+128% &
\	FIELD #12%,	06% AS CUSTOM.NUM$,		! Number &
			25% AS CUSTOM.NAME$,		! Name &
			25% AS CUSTOM.ADD1$,		! Address 1 &
			21% AS CUSTOM.ADD2$,		! Address 2 &
			15% AS CUSTOM.CITY$,		! City &
			02% AS CUSTOM.STATE$,		! State (Post office code) &
			05% AS CUSTOM.COUNTRY$,		! Country &
			10% AS CUSTOM.ZIP$,		! Zip code &
			10% AS CUSTOM.PHONE$,		! Phone Number (XXX-XXX-XXXX) &
			02% AS CUSTOM.SLSMAN$,		! Salesman &
			02% AS CUSTOM.TERR$,		! Territory &
			02% AS CUSTOM.LOC$,		! Inventory location &
			02% AS CUSTOM.TYPE$,		! Customer type &
			02% AS CUSTOM.CODE$,		! Customer code 2 &
			01% AS CUSTOM.TAXFLG$,		! Tax Flag &
			10% AS CUSTOM.STAXNUM$,		! Customer Sales tax ex number &
			02% AS CUSTOM.STAXLOC$,		! Sales tax location id &
			01% AS CUSTOM.METHOD$,		! Balance method &
							!  "O" = open item &
							!  "B" = balance foreward &
			01% AS CUSTOM.STMFLG$,		! Statement Flag &
							!  "1" = Gets a statement &
							!  "2" = Doesn't get statement &
			15% AS CUSTOM.ALPSRT$		! Alpha sort field &
\	FIELD #12%,	256% AS TEMP$, &
			08% AS CRJH.RECNUM$,	! Receipt # &
			06% AS CRJH.CUSNUM$,	! Customer # &
			06% AS CRJH.CHECK$,	! Check # &
			06% AS CRJH.DEPOSIT$,	! Deposit # &
			02% AS CRJH.TRADAT$,	! Receipt date &
			08% AS CRJH.ACCT$,	! Account # &
			08% AS CRJH.AMNT$,	! Receipt amount &
			02% AS CRJH.COUNT$,	! Line count &
			02% AS CRJH.TRATYP$,	! Receipt type &
						!  01 - ROA &
						!  02 - Other &
			16% AS CRJH.DESC$	! Description &
\	FIELD #12%,	256%+64% AS TEMP$, &
			08% AS CRJL.RECNUM$,	! Receipt # &
			02% AS CRJL.LINE$,	! Line # &
			08% AS CRJL.INVNUM$,	! Invoice number &
			08% AS CRJL.ACCT$,	! Account # &
			08% AS CRJL.AMOUNT$,	! Amount to apply &
			02% AS CRJL.TRATYP$,	! Apply to type &
						!  01 - ROA &
						!  02 - Other &
			06% AS CRJL.SUBACC$,	&
			02% AS CRJL.SALMAN$	&
\	FIELD #12%,	256%+64%+64% AS TEMP$, &
			06% AS ARREG.CUSNUM$,		&
			08% AS ARREG.INVNUM$,		&
			02% AS ARREG.TRATYP$,		&
			02% AS ARREG.TRADAT$,		&
			08% AS ARREG.SALAMT$,		&
			08% AS ARREG.DISAMT$,		&
			08% AS ARREG.OTHCHG$,		&
			08% AS ARREG.RECNUM$,		&
			06% AS ARREG.CHKNUM$,		&
			08% AS ARREG.ARACCT$,		&
			06% AS ARREG.CROSS$,		&
			25% AS ARREG.DESC$,		&
			02% AS ARREG.UPDDAT$,		&
			02% AS ARREG.SALMAN$	&
\	FIELD #12%, 256% AS CUSTOM.BUF$, 64% AS CRJH.BUF$, &
		64% AS CRJL.BUF$, 128% AS ARREG.BUF$ &

500	FRAME$ = FRAME$ + FNP$(NUM1$(I%)+';1')+' '+FNP$(NUM1$(I%)+';80')+' ' &
							FOR I%=2% TO 13% &
\	FRAME$ = FRAME$+ &
			FNP$('12;2')+SPACE$(78%)+ &
			FNP$('12;09')+'A'+ &
			FNP$('12;16')+'B'+ &
			FNP$('12;26')+'C'+ &
			FNP$('12;34')+'D'+ &
			FNP$('12;74')+'E'+ &
			FNP$('13;2')+' #  AppTyp Inv #     Acct #  SubAcc'+ &
			SPACE$(28%)+'   Amt-to-apply' &

1000	!--------------------------------------------PROGRAM RESTART POINT &
	V%=FNG%(CRJH.CH%,'') &
\	KEY.POINTER%=FNR(CRJH.CH%) &
\	DATA.POINTER%=FNR(CRJH.CH%+1%) &
\	IF FNS%=0% &
	THEN	1010 &
		ELSE	PRINT #CH%, G.OFF$; &
\			GOSUB 1060 &
\			GOTO 1030 &

1005	IF FNN%(CRJH.CH%) &
	THEN	18910 &
		ELSE	KEY.POINTER%=FNR(CRJH.CH%) &
\			DATA.POINTER%=FNR(CRJH.CH%+1%) &
\			GOTO 1015 &

1010	IF INP$='%^C' &
	THEN	Q%(CRJL.CH%,1%)=CRJL.KEY% IF CRJL.KEY% &
\		Q%(CRJL.CH%+1%,1%)=CRJL.DATA% IF CRJL.DATA% &

1011	PRINT #CH%, COLM.OFF$;FNSR$('1;24');G.OFF$; &
\	GOSUB 1060 &

1015	GOSUB 6000 &

1030	CRJL.KEY%,CRJL.DATA%,CHNG.FLAG%,ADD.FLAG%=0% &
\	PRINT #CH%, CLRBOT$;G.OFF$; &
		'COMMAND: Add Erase Change Blank Default '; &
		'Find Next Restore Quit '; &
\	OPT$=CVT$$(FNINP$(CH%,128%,' ',1%,1%),32%) &
\	OPT$='A' IF OPT$='' &
\	GOTO 1030 IF KEY.POINTER%=0% AND INSTR(1%,'AQD',OPT$)=0% &

1040	GOTO 1050 IF OPT$<>'Q' &

1045	PRINT #CH%, CLSCN$;FNSR$('1;24');FNP$('1;75'); &
		B.ON$;BLINK.ON$;R.ON$;'WAIT';G.OFF$;FNP$('24;1'); &
		FNX%('',0%,''); &

1050	GOTO 1000 	IF OPT$='R' 		! Restore &
\	GOTO 1005 	IF OPT$='N' 		! Next &
\	GOTO 2000 	IF OPT$='A' OR OPT$='F' ! Add, Find &
\	GOTO 2200 	IF OPT$='C' OR OPT$='B' ! Change, Blank &
\	GOTO 2400 	IF OPT$='E' 		! Erase &
\	GOTO 2500 	IF OPT$='D' 		! Default &
\	GOTO 1030 &

1060	TEMP$     = 'Cash Receipts Journal Maintenance' &
\	PRINT #CH%, CLSCN$;R.ON$;LEFT(TEMP$,39%);SPACE$(40%-LEN(TEMP$)); &
			SPACE$(40%-LEN(COMPANY$));COMPANY$; &
			FRAME$; FNP$('20;1');SPACE$(80%);G.OFF$; &
\	PRINT #CH%, &
			FNP$('02;02');'(01)*Receipt #'; &
			FNP$('03;02');'(02) Rec Type'; &
			FNP$('04;02');'(03) Cust #'; &
			FNP$('05;02');'(04) Name'; &
			FNP$('06;02');'(05) Add1'; &
			FNP$('07;02');'(06) Add2'; &
			FNP$('08;02');'(07) City'; &
			FNP$('08;32');'(08) St'; &
			FNP$('09;02');'(09) Zip'; &
			FNP$('09;32');'(10) Cty'; &
			FNP$('11;02');'(11) Desc'; &
\	PRINT #CH%,	FNP$('02;47');'(12) Trans Date'; &
			FNP$('04;47');'(13) Check #'; &
			FNP$('05;47');'(14) Deposit #'; &
			FNP$('07;47');'(15) Cash Acct #'; &
			FNP$('09;47');'(16) Amount'; &
			FNSR$('14;19'); &
\	PRINT #CH%,	FNP$(NUM1$(LOOP%)+';1');CLRLIN$;R.ON$;' '; &
			FNP$(NUM1$(LOOP%)+';80');' ';G.OFF$; &
				FOR LOOP%=14% TO 19% &
\	RETURN &

2000	!-------------------------------------------------SEARCH FOR KEYS &
	OPTION$='ADD ' &
\	OPTION$='FIND ' IF OPT$='F' &
\	INP$='' &
\	FOR LOOP%=1% TO LOOP.DATA% &
\		INP$=DEFAULT$(LOOP%) IF OPT$='A' &
\		GOSUB 6030 &
\	NEXT LOOP% &
\	PRINT #CH%,	FNP$(NUM1$(LOOP%)+';1');CLRLIN$;R.ON$;' '; &
			FNP$(NUM1$(LOOP%)+';80');' ';G.OFF$; &
				FOR LOOP%=14% TO 19% &
\	NET,AMOUNT=0. &
\	LOOP%=1% &
\	GOSUB 6900 &
\	LOOP%=1% &
\	GOSUB 6010 &
\	GOTO 1015 IF OPT$='F' AND INP$='' &
\	SEARCH.KEY$=CRJH.RECNUM$+'' &
\	V%=FNG%(CRJH.CH%,SEARCH.KEY$) &
\	IF FNS%=0% AND OPT$='A' OR OPT$='F' &
	THEN	KEY.POINTER%=FNR(CRJH.CH%) &
\		DATA.POINTER%=FNR(CRJH.CH%+1%) &
\		GOTO 1015 &

2100	!-----------------------------------------------------ADD RECORD &
	FOR LOOP%=2% TO CRJH.LOOP.DATA% &
\		GOSUB 6010 IF LOOP%<CUSTOM.START% OR LOOP%>CUSTOM.END% &
 !\		GOSUB 6010 IF LOOP%>=CUSTOM.START% AND LOOP%<=CUSTOM.END% &
 !			AND CUSTOM.DATA%=0% &
\		GOSUB 6009 IF LOOP%>=CUSTOM.START% AND LOOP%<=CUSTOM.END% &
 !			AND CUSTOM.DATA% &
\	NEXT LOOP% &

2120	LINE.NO%=14% &
\	CRJL.COUNT%,CUR.LINE%=0% &
\	TOP.LINE%=1% &
\	INVOICE.CHECK$='!' &
\	CRJL.KEY%=Q%(CRJL.CH%,1%) &
\	CRJL.DATA%=Q%(CRJL.CH%+1%,1%) &
\	ARREG.FLAG%=FNG%(ARREG.CH%,CRJH.CUSNUM$) &
\	ARREG.KEY%=FNR(ARREG.CH%) &
\	ARREG.DATA%=FNR(ARREG.CH%+1%) &

2130	LINE.NO$=NUM1$(LINE.NO%) &
\	PRINT #CH%, G.OFF$;LDS.ON$;FNP$(LINE.NO$+';5');CLRLIN$;CHR$(120%); &
			FNP$(LINE.NO$+';12');CHR$(120%); &
			FNP$(LINE.NO$+';21');CHR$(120%); &
			FNP$(LINE.NO$+';30');CHR$(120%); &
			FNP$(LINE.NO$+';37');CHR$(120%); &
			FNP$(LINE.NO$+';65');CHR$(120%); &
			USASCII$;FNP$(LINE.NO$+';1');G.OFF$;R.ON$;" "; &
			FNP$(LINE.NO$+';80');" "; &
			FNP$(LINE.NO$+';2');G.OFF$; &
\	PRINT #CH% USING '###',CRJL.COUNT%+1%+CRJH.LOOP.DATA%; &
\	ARREG.FLAG%=FNG%(ARREG.CH%,CRJH.CUSNUM$) IF ARREG.FLAG% &
\	ARREG.KEY%=FNR(ARREG.CH%) IF ARREG.FLAG% &
\	ARREG.DATA%=FNR(ARREG.CH%+1%) IF ARREG.FLAG% &
\	LSET ARREG.BUF$=FNL$ &
\	AMOUNT.DUE=0. &
\	SUBACC.TEST$,ARACCT.TEST$,INVNUM.TEST$='' &
\	LSET CUSTOM.METHOD$=DEFAULT.METHOD$ &
		IF CUSTOM.METHOD$<>'B' AND CUSTOM.METHOD$<>'O' &
\	ARACCT.TEST$ = '' &
\	ARACCT.TEST$ = ARREG.ARACCT$+'' IF CUSTOM.METHOD$='B' AND FNS% = 0% &
\	AMOUNT.DUE=-NET IF CUSTOM.METHOD$='B' OR CRJH.TRATYP$='02' &
\	LSET CRJL.INVNUM$,CRJL.ACCT$='' &
\	LSET CRJL.TRATYP$=CRJH.TRATYP$ &
\	FOR LOOP%=CRJH.LOOP.DATA%+1% TO CRJL.LOOP.DATA% &
\		GOTO 2138 IF CRJH.LOOP.DATA%+1%=LOOP% AND CRJH.TRATYP$='02' &
			OR CRJH.LOOP.DATA%+2%=LOOP% AND CRJL.TRATYP$='02' &
\		GOSUB 6010 &
\		GOTO 2130 IF INP$='%ABORT' &
\		GOTO 2150 IF INP$='QUIT' OR INP$='%END' OR INP$='%EXIT' &
\		GOTO 2138 IF CRJH.LOOP.DATA%+2%<>LOOP% &
\		GOTO 2138 IF CUSTOM.METHOD$='B' &
\		GOTO 2135 IF ARREG.FLAG% AND CRJL.INVNUM$='' &

2131		AMOUNT.DUE=0. &
\		FIND.FLAG%=0% &
\		ARREG.FLAG%=FNG%(-ARREG.CH%,NUM1$(ARREG.KEY%))+ &
			FNG%(-ARREG.CH%-1%,NUM1$(ARREG.DATA%)) &
\		ARREG.FLAG%=FNG%(ARREG.CH%,CRJH.CUSNUM$+CRJL.INVNUM$) &
			IF CRJL.INVNUM$<>'' &
\		IF ARREG.FLAG% &
		THEN	2138 &
			ELSE	LSET ARREG.BUF$=FNL$ &
\				ARREG.KEY%=FNR(ARREG.CH%) &
\				ARREG.DATA%=FNR(ARREG.CH%+1%) &
\				IF ARREG.CUSNUM$=CRJH.CUSNUM$ &
				THEN	GOTO 2133 &

2132		GOTO 2135 IF ARREG.FLAG% OR ARREG.CUSNUM$<>CRJH.CUSNUM$ &
			OR INVNUM.TEST$<>ARREG.INVNUM$ AND FIND.FLAG% &
\		GOTO 2134 IF INSTR(1%,INVOICE.CHECK$,ARREG.INVNUM$) &
			AND CRJL.INVNUM$='' &
\		IF INVNUM.TEST$<>'' AND INVNUM.TEST$<>ARREG.INVNUM$ &
		THEN	IF AMOUNT.DUE=0. &
			THEN	ARACCT.TEST$,SUBACC.TEST$, &
					SALMAN.TEST$,INVNUM.TEST$='' &
				ELSE	GOTO 2135 &

2133		IF ARREG.TRATYP$<>'02' &
		THEN	AMOUNT.DUE=AMOUNT.DUE+CVT$F(ARREG.SALAMT$) &
\			INVNUM.TEST$=ARREG.INVNUM$+'' IF FIND.FLAG%=0% &
\			ARACCT.TEST$=ARREG.ARACCT$+'' IF FIND.FLAG%=0% &
\			SUBACC.TEST$=ARREG.CROSS$+''  IF FIND.FLAG%=0% &
\			SALMAN.TEST$=ARREG.SALMAN$+''  IF FIND.FLAG%=0% &
\			FIND.FLAG%=-1% &

2134		ARREG.FLAG%=FNN%(ARREG.CH%) &
\		LSET ARREG.BUF$=FNL$ &
\		ARREG.KEY%=FNR(ARREG.CH%) &
\		ARREG.DATA%=FNR(ARREG.CH%+1%) &
\		GOTO 2132 &

2135		GOTO 2131 IF FNZ(AMOUNT.DUE)=0. AND ARREG.FLAG%=0% &
			AND ARREG.CUSNUM$=CRJH.CUSNUM$ AND CRJL.INVNUM$='' &
\		IF AMOUNT.DUE<>0. &
		THEN	AMOUNT.DUE=-AMOUNT.DUE &
			ELSE	IF CRJL.INVNUM$='' &
				THEN	INVNUM.TEST$='UNAPPLY' &
\					AMOUNT.DUE=-NET &

2136		RSET CRJL.INVNUM$=INVNUM.TEST$ &
\		INVOICE.CHECK$=INVOICE.CHECK$+INVNUM.TEST$+"!" &
\		PRINT #CH%, FNP$(NUM1$(LINE.NO%)+';13');B.ON$; &
			CRJL.INVNUM$;G.OFF$; &

2138	NEXT LOOP% &
\	CRJL.COUNT%,CUR.LINE%=CRJL.COUNT%+1% &
\	RSET CRJL.RECNUM$=CRJH.RECNUM$ &
\	LSET CRJL.LINE$=CVT%$(CRJL.COUNT%) &
\	LSET CRJL.SALMAN$=SALMAN.TEST$ &
\	IF FNA%(CRJL.CH%,CRJL.BUF$) &
	THEN	V$=FNMESS$(CH%,FNS%,'CRJH.DAT',0%,-1%) &
\		GOTO 1000 &

2139	CRJL.KEY%(CRJL.COUNT%)=FNR(CRJL.CH%) &
\	CRJL.DATA%(CRJL.COUNT%)=FNR(CRJL.CH%+1%) &

2140	PRINT #CH%, FNP$(NUM1$(LINE.NO%)+';1') &
\	LINE.NO%=LINE.NO%+1% &
\	TOP.LINE%=TOP.LINE%+1% IF LINE.NO%>19% &
\	LINE.NO%=19% IF LINE.NO%>19% &
\	GOTO 2130 &

2150	PRINT #CH%, FNP$(NUM1$(LINE.NO%)+';1');CLRLIN$;G.OFF$;R.ON$;' '; &
		FNP$(NUM1$(LINE.NO%)+';80');' ';G.OFF$; &
\	GOSUB 6920 &
\	CRJL.KEY%=Q%(CRJL.CH%,1%) &
\	CRJL.DATA%=Q%(CRJL.CH%+1%,1%) &
\	LSET CRJH.COUNT$=CVT%$(CRJL.COUNT%) &
\	GOTO 2195 IF ADD.FLAG% &
\	IF FNA%(CRJH.CH%,CRJH.BUF$) &
	THEN	V$=FNMESS$(CH%,FNS%,'CRJH.DAT',0%,-1%) &
\		GOTO 1000 &

2160	KEY.POINTER%=FNR(CRJH.CH%) &
\	DATA.POINTER%=FNR(CRJH.CH%+1%) &

2170 !	IF CUSTOM.DATA%=0% &
 !	THEN	LSET CUSTOM.NUM$=CRJH.CUSNUM$ &
 !\		IF FNA%(CUSTOM.CH%,CUSTOM.BUF$) &
 !		THEN	V$=FNMESS$(CH%,FNS%,'CRJH.DAT',0%,-1%) &
 !\			GOTO 1000 &

2180 !	IF CUSTOM.DATA%=0% &
 !	THEN	CUSTOM.KEY%=FNR(CUSTOM.CH%) &
 !\		CUSTOM.DATA%=FNR(CUSTOM.CH%+1%) &

2190	GOTO 1030 &

2195	LOOP%=LOOP.DATA% &
\	GOSUB 2900 &
\	OPT$='C' &

2200	!----------------------------------------CHANGE OR BLANK RECORD &
	IF OPT$='B' &
	THEN	OPTION$ = 'BLANK ' &
	ELSE	OPTION$ = 'CHANGE ' &
\		CHNG.FLAG% = -1% &

2210	PRINT #CH%, CLRBOT$; OPTION$+':'; &
\	INP.LEN% = 2% &
\	INP.LEN% = 3% IF LOOP.DATA% + CRJL.COUNT% + 1% >99% &
\	INP$=CVT$$(FNINP$(CH%,128%,' ',INP.LEN%,1%),32%) &
\	GOTO 1030 IF INP$='' &
\	LOOP%=VAL(INP$) &
\	GOTO 2210 IF LOOP%>LOOP.DATA%+CRJL.COUNT%+1% &
\	GOTO 2210 IF LOOP%<2% AND OPT$='C' &
\	GOTO 2210 IF LOOP%<2% AND OPT$='B' &
\	IF LOOP%<=LOOP.DATA% &
	THEN	GOSUB 6010 IF OPT$='C' &
\		INP$='' IF OPT$='B' &
\		GOSUB 6030 IF OPT$='B' &
\		GOTO 2290 &

2230	LOOP.TEMP%=LOOP% &
\	LOOP.CHNG%=LOOP%-LOOP.DATA% &
\	IF LOOP%>CRJL.COUNT%+LOOP.DATA% &
	THEN	LINE.NO% = 14% + CRJL.COUNT% - TOP.LINE% &
\		LINE.NO% = 19% IF LINE.NO%>19% &
\		CUR.LINE% = TOP.LINE% + 5% &
\		CUR.LINE% = CRJL.COUNT% IF CUR.LINE%>CRJL.COUNT% &
\		LINE.NO% = 13% IF CUR.LINE%<CRJL.COUNT%-5% &
\		TOP.LINE% = CRJL.COUNT%-4% IF CUR.LINE%<CRJL.COUNT%-5% &
\		CUR.LINE% = CRJL.COUNT%-5% IF CUR.LINE%<CRJL.COUNT%-5% &
\		FOR CUR.LINE%=CUR.LINE%+1% TO CRJL.COUNT% &
\			LINE.NO%=LINE.NO%+1% &
\			GOSUB 6040 &
\			GOSUB 6050 &
\		NEXT CUR.LINE% &
\		CHNG.FLAG%=0% &
\		ADD.FLAG%=-1% &
\		OPTION$='ADD' &
\		ARREG.FLAG%=-1% &
\		GOTO 2140 &

2240	IF TOP.LINE%<=LOOP.CHNG% AND &
		TOP.LINE%+5%>=LOOP.CHNG% &
	THEN	CUR.LINE%=LOOP.CHNG% &
\		LINE.NO%=14%+CUR.LINE%-TOP.LINE% &
\		GOSUB 6040 &
\		GOTO 2260 &

2250	IF LOOP.CHNG%<TOP.LINE%-5% &
		OR LOOP.CHNG%>TOP.LINE%+10% &
	THEN	STEP.TEMP%=1% &
\		LINE.NO%=13% &
\		CUR.LINE%,TOP.LINE%=LOOP.CHNG%-5% &
\		STEP.TEMP%=-1% IF LOOP.CHNG%<=6% &
\		LINE.NO%=20% IF LOOP.CHNG%<=6% &
\		CUR.LINE%=LOOP.CHNG%+5% IF LOOP.CHNG%<=6% &
\		TOP.LINE%=LOOP.CHNG% IF LOOP.CHNG%<=6% &
\		GOTO 2257 &

2255	STEP.TEMP%=1% &
\	LINE.NO%=14% &
\	STEP.TEMP%=-1% IF TOP.LINE%>LOOP.CHNG% &
\	LINE.NO%=19% IF STEP.TEMP%=1% &
\	CUR.LINE%=TOP.LINE%+6% &
\	CUR.LINE%=TOP.LINE%-1% IF STEP.TEMP%<1% &

2257	FOR CUR.LINE% = CUR.LINE% TO LOOP.CHNG% STEP STEP.TEMP% &
\		LINE.NO%=LINE.NO%+STEP.TEMP% &
\		GOSUB 6040 &
\		GOSUB 6050 &
\	NEXT CUR.LINE% &

2260	PRINT #CH%, CLRBOT$; "Item: A B C D E "; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",1%,1%),32%) &
\	GOTO 1010 IF INP$="%^C" &
\	GOTO 2200 IF INP$="" &
\	LOOP%=(INSTR(1%," A B C D E "," "+INP$+" ")+1%)/2% &
\	GOTO 2260 IF LOOP%=0% &
\	LOOP%=LOOP%+LOOP.DATA% &
\	GOSUB 6010 &

2290	GOSUB 2900 &
\	IF 	FNS%=0% &
	THEN	GOTO 2210 &
	ELSE	V$=FNMESS$(CH%,FNS%,'CRJH.DAT',0%,-1%) &
\		GOTO 1010 &

2400	!================================================ DELETE RECORD &
	PRINT #CH%, CLRBOT$;"Confirm deletion (Yes/No) "; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",1%,1%),32%) &
\	GOTO 1030 IF INP$<>"Y" &

2420	CRJL.COUNT%=1% &
\	RECNUM$ = CRJH.RECNUM$ +'' &
\	IF FND%(CRJH.CH%,"") &
	THEN	V$=FNMESS$(CH%,FNS%,'CRJH.DAT',0%,-1%) &

2430	IF FNG%(CRJL.CH%,RECNUM$+CVT%$(CRJL.COUNT%)) &
	THEN	2450 &
		ELSE	IF FND%(CRJL.CH%,"") &
			THEN	V$=FNMESS$(CH%,FNS%,'CRJL.DAT',0%,-1%) &

2440	CRJL.COUNT% = CRJL.COUNT% + 1% &
\	GOTO 2430 &

2450	GOTO 1005 &

2500	!-----------------------------------------------SET DEFAULT VALUES &
	FOR LOOP%=1% TO LOOP.DATA% &
\		INP$=DEFAULT$(LOOP%) &
\		GOSUB 6030 &
\	NEXT LOOP% &
\	NET,AMOUNT=0. &
\	PRINT #CH%,	FNP$(NUM1$(LOOP%)+';1');CLRLIN$;R.ON$;' '; &
			FNP$(NUM1$(LOOP%)+';80');' ';G.OFF$; &
				FOR LOOP%=14% TO 19% &
\	GOSUB 6900 &

2510	OPTION$='DEFAULT ' &
\	PRINT #CH%, CLRBOT$;'Default:'; &
\	INP$=CVT$$(FNINP$(CH%,128%,' ',2%,1%),32%) &
\	LOOP% = VAL(INP$) &
\	GOTO 2510 IF LOOP%<=1% OR LOOP%>LOOP.DATA% &
\	GOSUB 6010 &
\	DEFAULT$(LOOP%)=INP$ &
\	GOTO 2510 &

2900	!-------------------------------------------------UPDATE A RECORD &
	IF LOOP%<CUSTOM.START% OR LOOP%>CUSTOM.END% AND LOOP%<=CRJH.LOOP.DATA% &
	THEN	V%=FNG%(-CRJH.CH%,NUM1$(KEY.POINTER%))+ &
			FNG%(-CRJH.CH%-1%,NUM1$(DATA.POINTER%)) &
\		V%=FNU%(CRJH.CH%,CRJH.BUF$) IF LOOP%>1% AND FNS%=0% &
\		V%=FNU%(-CRJH.CH%,CRJH.BUF$) IF LOOP%<2% AND FNS%=0% &
\		RETURN &

2910 !	IF LOOP%>=CUSTOM.START% AND LOOP%<=CUSTOM.END% &
 !	THEN	V%=FNG%(-CUSTOM.CH%,NUM1$(CUSTOM.KEY%))+ &
 !			FNG%(-CUSTOM.CH%-1%,NUM1$(CUSTOM.DATA%)) &
 !\		V%=FNU%(CUSTOM.CH%,CUSTOM.BUF$) IF FNS%=0% &
 !\		RETURN &

2920	IF LOOP%>CRJH.LOOP.DATA% AND LOOP%<=CRJL.LOOP.DATA% &
	THEN	V%=FNG%(-CRJL.CH%,NUM1$(CRJL.KEY%(CUR.LINE%)))+ &
			FNG%(-CRJL.CH%-1%,NUM1$(CRJL.DATA%(CUR.LINE%))) &
\		V%=FNU%(CRJL.CH%,CRJL.BUF$) IF FNS%=0% &
\		RETURN &

2930	RETURN &

6000	!------------------------------------------------DATA ENTRY SECTION &
	RETURN IF KEY.POINTER%=0% OR FNG%(-CRJH.CH%,NUM1$(KEY.POINTER%))+ &
			FNG%(-CRJH.CH%-1%,NUM1$(DATA.POINTER%)) &
\	LSET CRJH.BUF$=FNL$ &
\	LSET CUSTOM.BUF$='' &
\	LSET CUSTOM.BUF$=FNL$ IF FNG%(CUSTOM.CH%,CRJH.CUSNUM$)=0% &
 !\	CUSTOM.KEY%,CUSTOM.DATA%=0% &
 !\	CUSTOM.KEY%=FNR(CUSTOM.CH%) IF FNS%=0% &
 !\	CUSTOM.DATA%=FNR(CUSTOM.CH%+1%) IF FNS%=0% &
\	NET,AMOUNT=0. &
\	INVOICE.CHECK$='!' &
\	PRINT #CH%,	FNP$(NUM1$(LOOP%)+';1');CLRLIN$;R.ON$;' '; &
			FNP$(NUM1$(LOOP%)+';80');' ';G.OFF$; &
				FOR LOOP%=14% TO 19% &
\	FOR LOOP%=1% TO LOOP.DATA% &
\		GOSUB 6009 &
\	NEXT LOOP% &
\	NET=CVT$F(CRJH.AMNT$) &
\	LINE.NO%=14% &
\	CRJL.COUNT%,CUR.LINE%=0% &
\	TOP.LINE%=1% &
\	CRJL.COUNT%=CVT$%(CRJH.COUNT$) &
\	FOR  CRJL.LOOP%=1% TO CRJL.COUNT% &
\		IF FNG%(CRJL.CH%,CRJH.RECNUM$+CVT%$(CRJL.LOOP%))=0% &
		THEN	LSET CRJL.BUF$=FNL$ &
\			NET=NET+CVT$F(CRJL.AMOUNT$) &
\			INVOICE.CHECK$=INVOICE.CHECK$+CRJL.INVNUM$+'!' &
\			CRJL.KEY%(CRJL.LOOP%)=FNR(CRJL.CH%) &
\			CRJL.DATA%(CRJL.LOOP%)=FNR(CRJL.CH%+1%) &
\			IF LINE.NO%<20% &
			THEN	CUR.LINE%=CRJL.LOOP% &
\				GOSUB 6050 &
\				LINE.NO%=LINE.NO%+1% &

6005	NEXT CRJL.LOOP% &
\	GOSUB 6905 &
\	GOSUB 6920 &
\	RETURN &

6009	ON LOOP% GOSUB	6220, 6560, 6240, 6260, 6280, 6300, 6320, 6340, &
			6360, 6380, 6580, 6400, 6420, 6440, 6460, 6480, &
			6600, 6500, 6520, 6620, 6540 &
\	RETURN &

6010	RETURN IF CRJH.TRATYP$='02' AND &
		LOOP%>=CUSTOM.START%-1% AND LOOP%<=CUSTOM.END% &
\	ON LOOP% GOSUB	6210, 6550, 6230, 6250, 6270, 6290, 6310, 6330, &
			6350, 6370, 6570, 6390, 6410, 6430, 6450, 6470, &
			6590, 6490, 6510, 6610, 6530 &
\	IF LOOP%=3% &
	THEN	LSET CUSTOM.BUF$='' &
\		LSET CUSTOM.BUF$=FNL$ IF FNG%(CUSTOM.CH%,CRJH.CUSNUM$)=0% &
 !\		CUSTOM.KEY%,CUSTOM.DATA%=0% &
 !\		CUSTOM.KEY%=FNR(CUSTOM.CH%) IF FNS%=0% &
 !\		CUSTOM.DATA%=FNR(CUSTOM.CH%+1%) IF FNS%=0% &

6019	RETURN &
		! INPUT DATA &

6030	ON LOOP% GOSUB	6217, 6557, 6237, 6257, 6277, 6297, 6317, 6337, &
			6357, 6377, 6577, 6397, 6417, 6437, 6457, 6477, &
			6597, 6497, 6517, 6617, 6537 &
\	RETURN &

6040	V%=FNG%(-CRJL.CH%,NUM1$(CRJL.KEY%(CUR.LINE%)))+ &
		FNG%(-CRJL.CH%-1%,NUM1$(CRJL.DATA%(CUR.LINE%))) &
\	LSET CRJL.BUF$=FNL$ &
\	RETURN &

6050	TOP.LINE%=1% IF TOP.LINE%=0% &
\	PRINT #CH%, FNP$('14;1'); INSERT.LIN$; IF LINE.NO%<14% &
\	PRINT #CH%, FNP$('19;1') IF LINE.NO%>19% &
\	TOP.LINE%=TOP.LINE%+1% IF LINE.NO%>19% &
\	LINE.NO%=19% IF LINE.NO%>19% &
\	TOP.LINE%=TOP.LINE%-1% IF LINE.NO%<14% &
\	LINE.NO%=14% IF LINE.NO%<14% &
\	LINE.NO$=NUM1$(LINE.NO%) &
\	PRINT #CH%, G.OFF$;LDS.ON$;FNP$(LINE.NO$+';5');CHR$(120%); &
			FNP$(LINE.NO$+';12');CHR$(120%); &
			FNP$(LINE.NO$+';21');CHR$(120%); &
			FNP$(LINE.NO$+';30');CHR$(120%); &
			FNP$(LINE.NO$+';37');CHR$(120%); &
			FNP$(LINE.NO$+';65');CHR$(120%); &
			USASCII$;FNP$(LINE.NO$+';1');G.OFF$;R.ON$;" "; &
			FNP$(LINE.NO$+';80');" "; &
			FNP$(LINE.NO$+';2');G.OFF$; &
\	PRINT #CH%  USING "###",CUR.LINE%+LOOP.DATA%; &
\	FOR LOOP%=CRJH.LOOP.DATA%+1% TO CRJL.LOOP.DATA% &
\		GOSUB 6009 &
\	NEXT LOOP% &
\	RETURN &

6200	!-----------------------------------------------DATA ENTRY SECTION &

6210	INP$=FNSIO$('','2;17',CRJH.RECNUM$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CRJH.RECNUM$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &
\	GOTO 6210 IF INP$='' AND OPT$='A' &
\	IF OPT$='C' AND INP$<>'' &
	THEN	IF FNG%(CRJL.CH%,SPACE$(8%-LEN(INP$))+INP$)=0% &
		THEN	PRINT #CH%, CLRBOT$;FNP$('24;1');INP$;' is a duplicate key'; &
				FNP$('21;1');'COMMAND: Reenter Exit '; &
\			INP$=FNINP$(CH%,128%,' ',1%,0%) &
\			PRINT #CH%, FNP$('2;17');B.ON$;CRJL.RECNUM$;G.OFF$; &
\			GOTO 6210 IF INP$='R' &
\			RETURN &

6217	RSET CRJH.RECNUM$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6220	PRINT #CH%, FNP$('2;17');B.ON$;CRJH.RECNUM$;G.OFF$; &
\	RETURN &

6230	INP$=FNSIO$('','4;14',CRJH.CUSNUM$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CRJH.CUSNUM$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &
\	GOTO 6230 IF INP$='' AND OPT$='A' &

6237	LSET CRJH.CUSNUM$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6240	PRINT #CH%, FNP$('4;14');B.ON$;CRJH.CUSNUM$;G.OFF$; &
\	RETURN &

6250 !	INP$=FNSIO$('','5;14',CUSTOM.NAME$,DEFAULT$(LOOP%),CHR$(CH%)+ &
 !			STRING$(2%,LEN(CUSTOM.NAME$))+CHR$(1%)+CHR$(0%)+ &
 !			OPTION$+' ALPHA') &

6257 !	LSET CUSTOM.NAME$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6260	PRINT #CH%, FNP$('5;14');B.ON$;CUSTOM.NAME$;G.OFF$; &
\	RETURN &

6270 !	INP$=FNSIO$('','6;14',CUSTOM.ADD1$,DEFAULT$(LOOP%),CHR$(CH%)+ &
 !			STRING$(2%,LEN(CUSTOM.ADD1$))+CHR$(1%)+CHR$(0%)+ &
 !			OPTION$+' ALPHA') &

6277 !	LSET CUSTOM.ADD1$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6280	PRINT #CH%, FNP$('6;14');B.ON$;CUSTOM.ADD1$;G.OFF$; &
\	RETURN &

6290 !	INP$=FNSIO$('','7;14',CUSTOM.ADD2$,DEFAULT$(LOOP%),CHR$(CH%)+ &
 !			STRING$(2%,LEN(CUSTOM.ADD2$))+CHR$(1%)+CHR$(0%)+ &
 !			OPTION$+' ALPHA') &

6297 !	LSET CUSTOM.ADD2$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6300	PRINT #CH%, FNP$('7;14');B.ON$;CUSTOM.ADD2$;G.OFF$; &
\	RETURN &

6310 !	INP$=FNSIO$('','8;14',CUSTOM.CITY$,DEFAULT$(LOOP%),CHR$(CH%)+ &
 !			STRING$(2%,LEN(CUSTOM.CITY$))+CHR$(1%)+CHR$(0%)+ &
 !			OPTION$+' ALPHA') &

6317 !	LSET CUSTOM.CITY$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6320	PRINT #CH%, FNP$('8;14');B.ON$;CUSTOM.CITY$;G.OFF$; &
\	RETURN &

6330 !	INP$=FNSIO$('','8;41',CUSTOM.STATE$,DEFAULT$(LOOP%),CHR$(CH%)+ &
 !			STRING$(2%,LEN(CUSTOM.STATE$))+CHR$(1%)+CHR$(0%)+ &
 !			OPTION$+' ALPHA') &

6337 !	LSET CUSTOM.STATE$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6340	PRINT #CH%, FNP$('8;41');B.ON$;CUSTOM.STATE$;G.OFF$; &
\	RETURN &

6350 !	INP$=FNSIO$('','9;14',CUSTOM.ZIP$,DEFAULT$(LOOP%),CHR$(CH%)+ &
 !			STRING$(2%,LEN(CUSTOM.ZIP$))+CHR$(1%)+CHR$(0%)+ &
 !			OPTION$+' ALPHA') &

6357 !	LSET CUSTOM.ZIP$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6360	PRINT #CH%, FNP$('9;14');B.ON$;CUSTOM.ZIP$;G.OFF$; &
\	RETURN &

6370 !	INP$=FNSIO$('','9;41',CUSTOM.COUNTRY$,DEFAULT$(LOOP%),CHR$(CH%)+ &
 !			STRING$(2%,LEN(CUSTOM.COUNTRY$))+CHR$(1%)+CHR$(0%)+ &
 !			OPTION$+' ALPHA') &

6377 !	LSET CUSTOM.COUNTRY$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6380	PRINT #CH%, FNP$('9;41');B.ON$;CUSTOM.COUNTRY$;G.OFF$; &
\	RETURN &

6390	TEMP$='' &
\	TEMP$=DEFAULT$(LOOP%) IF CHNG.FLAG%=0% &
\	LSET CRJH.TRADAT$=CVT%$(FND6%(TEMP$)) IF CHNG.FLAG%=0% &
\	INP$=FNSIO$('','2;65',CRJH.TRADAT$,TEMP$,CHR$(CH%)+ &
			CHR$(8%)+CHR$(6%)+CHR$(4%)+CHR$(0%)+OPTION$+' DATE') &
\	TEMP=VAL(INP$) &
\	DEFAULT$(LOOP%)=INP$ IF CHNG.FLAG%=0% AND OPT$='A' &
\	GOTO 6390 IF FNDREAL%(INP$)=0% &

6397	LSET CRJH.TRADAT$=CVT%$(FND6%(INP$)) IF INP$<>'' OR CHNG.FLAG%=0% &

6400	PRINT #CH%, FNP$('2;65');B.ON$;FND6$(CVT$%(CRJH.TRADAT$));G.OFF$; &
\	RETURN &

6410	INP$=FNSIO$('','4;65',CRJH.CHECK$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CRJH.CHECK$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &

6417	LSET CRJH.CHECK$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6420	PRINT #CH%, FNP$('4;65');B.ON$;CRJH.CHECK$;G.ON$; &
\	RETURN &

6430	INP$=FNSIO$('','5;65',CRJH.DEPOSIT$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CRJH.DEPOSIT$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &

6437	LSET CRJH.DEPOSIT$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6440	PRINT #CH%, FNP$('5;65');B.ON$;CRJH.DEPOSIT$;G.OFF$; &
\	RETURN &

6450	TEMP$='' &
\	TEMP$=DEFAULT$(LOOP%) IF CHNG.FLAG%=0% &
\	RSET CRJH.ACCT$=TEMP$ IF CHNG.FLAG%=0% &
\	INP$=FNSIO$('','7;65',CRJH.ACCT$,TEMP$,CHR$(CH%)+ &
			STRING$(2%,LEN(CRJH.ACCT$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &
\	IF INP$<>'' AND CHART.FLAG%=0% OR CHART.FLAG%=0% AND CHNG.FLAG%=0% &
	THEN	IF FNG%(CHART.CH%,SPACE$(8%-LEN(INP$))+INP$) &
		THEN	PRINT #CH%, FNP$('24;1');CLRLIN$; &
			'Undefined account number.';FNP$('24;55'); &
			'Entry anyway (Y/N) '; &
\			NW$=CVT$$(FNINP$(CH%,128%,'_',1%,1%),-1%) &
\			PRINT #CH%, FNP$('24;1');CLRLIN$; &
\			IF NW$<>'Y' &
			THEN	GOTO 6450 &

6455	DEFAULT$(LOOP%)=INP$ IF CHNG.FLAG%=0% AND OPT$='A' &

6457	RSET CRJH.ACCT$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6460	PRINT #CH%, FNP$('7;65');B.ON$;CRJH.ACCT$;G.OFF$; &
\	RETURN &

6470	INP$=FNSIO$('##,###,###.##','09;65',CRJH.AMNT$,DEFAULT$(LOOP%), &
			CHR$(CH%)+CHR$(13%)+CHR$(11%)+CHR$(5%)+CHR$(2%)+ &
			OPTION$+' ALPHA') &
\	TEMP=VAL(INP$) &

6477	AMOUNT=CVT$F(CRJH.AMNT$) IF CHNG.FLAG% &
\	GOSUB 6910 IF CHNG.FLAG% &
\	LSET CRJH.AMNT$=CVTF$(VAL(INP$)) IF INP$<>'' OR CHNG.FLAG%=0% &

6480	AMOUNT=CVT$F(CRJH.AMNT$) &
\	PRINT #CH% USING FNP$('09;65')+B.ON$+'##,###,###.##'+G.OFF$, &
		AMOUNT &
\	GOSUB 6900 &
\	RETURN &

6490	INP$=FNSIO$('',NUM1$(LINE.NO%)+';13',CRJL.INVNUM$,DEFAULT$(LOOP%), &
			CHR$(CH%)+STRING$(2%,LEN(CRJL.INVNUM$))+CHR$(1%)+ &
			CHR$(0%)+ &
			OPTION$+' ALPHA') &

6497	RSET CRJL.INVNUM$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6500	PRINT #CH%, FNP$(NUM1$(LINE.NO%)+';13');B.ON$;CRJL.INVNUM$;G.OFF$; &
\	RETURN &

6510	RSET CRJL.ACCT$=ARACCT.TEST$ IF CHNG.FLAG%=0% &
\	DEFAULT$(LOOP%)=ARACCT.TEST$ IF CHNG.FLAG%=0% &
\	INP$=FNSIO$('',NUM1$(LINE.NO%)+';22',CRJL.ACCT$,DEFAULT$(LOOP%), &
			CHR$(CH%)+STRING$(2%,LEN(CRJL.ACCT$)) &
			+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &
\	GOTO 6520 IF INP$='%END' OR INP$='%ABORT' &
\	IF INP$<>'' AND CHART.FLAG%=0% OR CHART.FLAG%=0% AND CHNG.FLAG%=0% &
	THEN	IF FNG%(CHART.CH%,SPACE$(8%-LEN(INP$))+INP$) &
		THEN	PRINT #CH%, FNP$('24;1');CLRLIN$; &
			'Undefined account number.';FNP$('24;55'); &
			'Entry anyway (Y/N) '; &
\			NW$=CVT$$(FNINP$(CH%,128%,'_',1%,1%),-1%) &
\			PRINT #CH%, FNP$('24;1');CLRLIN$; &
\			IF NW$<>'Y' &
			THEN	GOTO 6510 &

6517	RSET CRJL.ACCT$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6520	PRINT #CH%, FNP$(NUM1$(LINE.NO%)+';22');B.ON$;CRJL.ACCT$;G.OFF$; &
\	RETURN &

6530	LSET CRJL.AMOUNT$=CVTF$(AMOUNT.DUE) IF CHNG.FLAG%=0% &
\	DEFAULT$(LOOP%)=NUM1$(AMOUNT.DUE) IF CHNG.FLAG%=0% &
\	DEFAULT$(LOOP%)=DEFAULT$(LOOP%)+'.' &
		IF INSTR(1%,DEFAULT$(LOOP%),'.')=0% AND CHNG.FLAG%=0% &
\	INP$=FNSIO$('###,###,###.##',NUM1$(LINE.NO%)+';66',CRJL.AMOUNT$, &
			DEFAULT$(LOOP%),CHR$(CH%)+CHR$(14%)+CHR$(12%)+ &
			CHR$(5%)+CHR$(2%)+OPTION$+' VALUE') &
\	RETURN IF INP$='%ABORT' OR INP$='%END' &

6537	AMOUNT=CVT$F(CRJL.AMOUNT$) &
\	GOSUB 6910 IF CHNG.FLAG% AND INP$<>'' &
\	LSET CRJL.AMOUNT$=CVTF$(VAL(INP$)) IF INP$<>'' OR CHNG.FLAG%=0% &
\	AMOUNT=CVT$F(CRJL.AMOUNT$) &
\	GOSUB 6900 IF INP$<>'' OR CHNG.FLAG%=0% &

6540	PRINT #CH% USING FNP$(NUM1$(LINE.NO%)+';66')+B.ON$+ &
		"###,###,###.##"+G.OFF$,CVT$F(CRJL.AMOUNT$); &
\	RETURN &

6550	TEMP$='' &
\	TEMP$=DEFAULT$(LOOP%) IF CHNG.FLAG%=0% &
\	INP$=FNSIO$('','3;17',CRJH.TRATYP$,TEMP$,CHR$(CH%)+ &
			STRING$(2%,LEN(CRJH.TRATYP$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &
\	IF OPT$='A' OR INP$<>'' &
	THEN	IF INSTR(1%,'!01!02!','!'+INP$+'!')=0% &
		THEN	PRINT #CH%, FNP$('24;1');CLRLIN$; &
			"'01' ROA  or '02' Other"; &
			FNP$('24;55');'Hit any key to continue'; &
\			INP$=CVT$$(FNINP$(CH%,128%,'_',1%,1%),-1%) &
\			GOTO 6550 &

6555	DEFAULT$(LOOP%)=INP$ IF OPT$='A' &

6557	RSET CRJH.TRATYP$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6560	PRINT #CH%, FNP$('3;17');B.ON$;CRJH.TRATYP$;G.OFF$; &
\	RETURN &

6570	INP$=FNSIO$('','11;14',CRJH.DESC$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CRJH.DESC$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &

6577	LSET CRJH.DESC$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6580	PRINT #CH%, FNP$('11;14');B.ON$;CRJH.DESC$;G.OFF$; &
\	RETURN &

6590	LSET CRJL.TRATYP$=CRJH.TRATYP$ IF CHNG.FLAG%=0% &
\	DEFAULT$(LOOP%)=CRJL.TRATYP$+'' IF CHNG.FLAG%=0% &
\	INP$=FNSIO$('',NUM1$(LINE.NO%)+';08',CRJL.TRATYP$,DEFAULT$(LOOP%), &
			CHR$(CH%)+STRING$(2%,LEN(CRJL.TRATYP$)) &
			+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &
\	RETURN IF INP$='%ABORT' OR INP$='%END' &
\	IF OPT$='A' OR INP$<>'' &
	THEN	IF INSTR(1%,'!01!02!','!'+INP$+'!')=0% &
		THEN	PRINT #CH%, FNP$('24;1');CLRLIN$; &
			"'01' Apply to A/R  or '02' Apply to Other"; &
			FNP$('24;55');'Hit any key to continue'; &
\			INP$=CVT$$(FNINP$(CH%,128%,'_',1%,1%),-1%) &
\			GOTO 6590 &

6597	LSET CRJL.TRATYP$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6600	PRINT #CH%, FNP$(NUM1$(LINE.NO%)+';08');B.ON$;CRJL.TRATYP$;G.OFF$; &
\	RETURN &

6610	LSET CRJL.SUBACC$=SUBACC.TEST$ IF CHNG.FLAG%=0% &
\	DEFAULT$(LOOP%)=SUBACC.TEST$ IF CHNG.FLAG%=0% &
\	INP$=FNSIO$('',NUM1$(LINE.NO%)+';31',CRJL.SUBACC$,DEFAULT$(LOOP%), &
			CHR$(CH%)+STRING$(2%,LEN(CRJL.SUBACC$)) &
			+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &
\	GOTO 6620 IF INP$='%END' OR INP$='%ABORT' &

6617	LSET CRJL.SUBACC$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6620	PRINT #CH%, FNP$(NUM1$(LINE.NO%)+';31');B.ON$;CRJL.SUBACC$;G.OFF$; &
\	RETURN &

6900	!-----------------------------------------PRINT NET INVOICE &
	NET=NET+AMOUNT &

6905	PRINT #CH% USING FNP$('20;66')+R.ON$+'###,###,###.##'+ &
		G.OFF$,NET; &
\	RETURN &

6910	NET=NET-AMOUNT ! SUBTRACT FOR NET AMOUNT IF CHANGE &
\	RETURN &

6920	PRINT #CH% USING FNP$('20;2')+R.ON$+'Ending item ###'+G.OFF$, &
		CRJL.COUNT%+LOOP.DATA%; &
\	RETURN &

18910	!----------------------------------------------END OF FILE &
	PRINT #CH%, CLRBOT$;FNP$('24;1');'End of file has been reached.'; &
		FNP$('24;55');'Hit any key to continue '; &
\	INP$=FNINP$(CH%,128%,' ',1%,0%) &
\	GOTO 1000 &

19000	!------------------------------------------------ERROR TRAPPING &
	RESUME IF ERR=52% OR ERR=51% &
\	RESUME 450 IF ERL=400 &
\	INP$='%^C' IF ERR=54% &
\	RESUME 1010 IF ERR=54% &

19999	ON ERROR GOTO 0 &

30000	!----------------------PROGRAM FUNCTIONS------------------------ &
	!------------------------------------------------TERMINAL INPUT &
	DEF FNINP$(CHN%,KYP%,FILLCHAR$,INPUTLEN%,TO.ERR%) &
\		PRINT #CHN%, STRING$(INPUTLEN%,ASCII(FILLCHAR$)); &
			  STRING$(INPUTLEN%,8%); &
\		PRINT #CHN%, RECORD 256%,CHR$(KYP%+INPUTLEN%)+FILLCHAR$; &
\		GET #CHN% &
\		FIELD #CHN%, RECOUNT AS BUFFER$ &
\		IF ASCII(BUFFER$)=128% OR ASCII(BUFFER$)=8% &
			OR ASCII(BUFFER$)=23% &
		THEN	BUFFER$="%END" IF MID(BUFFER$,2%,4%)="[21~" &
				OR MID(BUFFER$,2%,2%)="OP" &
\			BUFFER$="%ABORT" IF MID(BUFFER$,2%,4%)="[19~" &
				OR MID(BUFFER$,2%,2%)="OQ" &

30010		BUFFER$='%^C' IF INSTR(1%,BUFFER$,CHR$(3%)) &
\		FNINP$=CVT$$(BUFFER$,4%) &
\		V=SQR(-1) IF BUFFER$='%^C' AND TO.ERR% !^C Trappping &
\	FNEND &

30200	DEF*FNP$(ROWCOL$)=ESC$+'['+ROWCOL$+'H' 	! Direct Cursor Address &

30250	!-----------------------------------------------MESSAGE HANDLER &
	DEF*FNMESS$(CHN%,ERRNUM%,DESC$,TO.ERR%,PRINT.TEST%) &
\		MESG$=MID(SYS(CHR$(6%)+CHR$(9%)+CHR$(ERRNUM%)),3%,30%)+ &
			DESC$ &
\		IF PRINT.TEST% &
		THEN	PRINT #CHN%, FNP$('24;1');CLRLIN$;MESG$; &
				FNP$('24;55'); &
				'Hit any key to continue.'; &
\			NW$=FNINP$(CHN%,128%,' ',1%,TO.ERR%) &

30260		FNMESS$=MESG$ &
\	FNEND &

30300	DEF*FNSR$(BEGEND$)=ESC$+'['+BEGEND$+'r'	! Scroll control &

30350	DEF FNFONE$(PHN$)='('+LEFT(PHN$,3%)+')'+MID(PHN$,4%,3%)+'-' &
		+RIGHT(PHN$,7%) &

30400	!-------------------------------------------------DATE HANDLERS &
	DEF FND8%(D8)=D8 						&

30410	DEF FND6%(D9$)=VAL(MID(D9$,3%,2%))+VAL(LEFT(D9$,2%))*32% 	&
		+ FND8%(VAL(RIGHT(D9$,5%)))*512% 			&

30420	DEF FND6$(D9%)=RIGHT(NUM1$((D9% AND 15%*32%)/32%+100%),2%) 	&
		+ '/'+RIGHT(NUM1$((D9% AND 31%)+100%),2%) 		&
		+ '/'+RIGHT(NUM1$(((SWAP%(D9%) AND 254%)/2%)+100%),2%) 	&

30450	DEF FNDREAL%(DATUM$) 			!CHECK FOR ACTUAL DATES	&
\	  	DAY%=VAL(MID(DATUM$,3%,2%)) 				&
\	  	YIR%=VAL(RIGHT(DATUM$,5%)) 				&
\	  	FNDREAL%,P%=0% &
\	  	FNDREAL%,P%=31% IF INSTR(1%,' 01 03 05 07 08 10 12 ',' '&
			+LEFT(DATUM$,2%)+' ') 				&
\	  	FNDREAL%,P%=30% IF INSTR(1%,' 04 06 09 11 ',' '+ 	&
			LEFT(DATUM$,2%)+' ') 				&
\	  	FNDREAL%,P%=28%+(1%-SGN(YIR%-4%*INT(YIR%/4%)))-		&
			(1%-SGN(YIR%)) 					&
				IF LEFT(DATUM$,2%)='02'			&
\	  	FNDREAL%=0% IF P%<DAY% OR DAY%=0% 			&
\	  	FNDREAL%=1% IF DATUM$='' 				&
\	FNEND 								&

30500	DEF FNZ(Y)=INT(ABS(Y)*100.+.5001)/100.*SGN(Y) &

30700	!========================================= DATA INPUT FUNCTIONS &
	DEF*FNSIO$(FRMAT$,ROWCOL$,VARABLE$,DEFAL$,ITEMS$) &
\		CHN%=ASCII(MID(ITEMS$,1%,1%)) &
\		KIND%=ASCII(MID(ITEMS$,4%,1%)) &
\		DECI=ASCII(MID(ITEMS$,5%,1%)) &
\		OPT$=MID(ITEMS$,6%,1%) &

30705		PRINT #CHN%, FNP$(ROWCOL$);R.ON$;B.ON$; &
\		IF OPT$='C' OR DEFAL$<>'' &
		THEN	ON KIND% GOTO 30710, 30715, 30720, 30725, 30730 &
		ELSE	PRINT #CHN%, SPACE$(ASCII(MID(ITEMS$,2%,1%))); &
\			GOTO 30735 &

30710		PRINT #CHN%, VARABLE$;			   ! ALPHA &
\		GOTO 30735 &

30715		PRINT #CHN% USING FRMAT$,ASCII(VARABLE$);  ! ASCII &
\		GOTO 30735 &

30720		PRINT #CHN% USING FRMAT$, CVT$%(VARABLE$)/(10.**DECI); &
\		GOTO 30735				   ! INTEGER &

30725		PRINT #CHN%, FND6$(CVT$%(VARABLE$));	   ! DATE &
\		GOTO 30735 &

30730		PRINT #CHN% USING FRMAT$,CVT$F(VARABLE$);  ! FLOAT &

30735		PRINT #CHN%,G.OFF$;CLRBOT$;RIGHT(ITEMS$,6%)+':  ';B.ON$; &
\		FIN$ = FNINP$(CHN%,0%,'_',ASCII(MID(ITEMS$,3%,1%)),1%) &
\		GOTO 30745 IF FIN$='%END' OR FIN$='%ABORT' &
\		V% = VAL(FIN$) IF KIND%=3% &
\		V  = VAL(FIN$) IF KIND%>3% &
\		GOTO 30705 IF KIND%=4% AND LEN(FIN$)<>6% AND FIN$<>'' &
\		IF FIN$='' &
		THEN	FIN$ = DEFAL$ IF INSTR(1%,'CF',OPT$)=0% &
\			GOTO 30745 &

30740		IF KIND%=3% OR KIND%=5% &
		THEN	TEMP = 1. &
\			TEMP = 10.**DECI IF INSTR(1%,FIN$,'.')=0% &
\			FIN$ = NUM1$(VAL(FIN$)/TEMP) &
\			FIN$ = FIN$ + '.' IF INSTR(1%,FIN$,'.')=0% &

30745		FNSIO$ = FIN$+'' &
\	FNEND &

32767	END &

