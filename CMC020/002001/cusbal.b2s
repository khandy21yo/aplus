10	!--------------------------------------------------------------- &
	!	ACCOUNTS RECEIVABLE SYSTEM 	-Customer Balance &
	! &
	!	CUSBAL.B2S	V1.0	March 1985 &
	! &
	! Author - Randall Beard, Computer Management Center, Inc. &
	! &
	! Files-CUSTOM.DAT	-ISAM	Customer Description File &
	! Files-CHART.DAT	-ISAM	Chart of accounts File &
	! Files-CUSBAL.DAT	-ISAM	Customer Balance File &
	! &
	!--------------------------------------------------------------- &
	! &
	! &
	!		      Copyright (c) 1984 by &
	!	  Computer Management Center, Idaho Falls, Idaho &
	! &
	! This software is furnished under a license and may be used &
	! and copied only in accordance with the terms of such license &
	! and with the inclusion of the above copyright notice.  This &
	! software or any other copies therof may not be provided or &
	! otherwise made available to any other person.  No title to &
	! and ownership of the software is hereby transferred. &
	! &
	! The information in this software is subject to change without &
	! notice and should not be construed as a committment by &
	! Computer Management Center. &
	! &
	! CMC assumes no responsibility for the use or reliability of &
	! its software on equipment which is not supported by CMC. &
	! &
	!--------------------------------------------------------------- &

50	ON ERROR GOTO 19000 &
\	DIM DEFAULT$(30%),SEARCH$(30%),REPLCE$(30%) &

60	OPEN 'KB:' AS FILE #1%, MODE 8%+256% &
					!    8% - Echo Control &
					!   16% - Disable hibernation, Cntl-C &
\	CH%=1%				! Keyboard channel &
\	ESC$=CHR$(155%)			! Escape code for VT100 control &
\	CLSCN$=ESC$+'[H'+ESC$+'[J'	! Clear screen &
\	CLRLIN$=ESC$+'[2K'		! Erase entire line &
\	G.OFF$=ESC$+'[m'		! Select graphic off &
\	B.ON$=ESC$+'[1m'		! Bold face on &
\	BLINK.ON$=ESC$+'[5m'		! Blinking &
\	R.ON$=ESC$+'[7m'		! Reverse video &
\	COLM.ON$=ESC$+'[?3h'		! 132 Column mode &
\	COLM.OFF$=ESC$+'[?3l'		! 80 Column mode &
\	CLRBOT$=ESC$+'[21;1H'+ESC$+'[J'	! Erace cursor to end of screen &
\	LDS.ON$=ESC$+'(0'		! Line drawing set &
\	USASCII$=ESC$+'(B'		! United States ASCII &

80	!COM(THIS) DROP.DEAD.DATE$ = 8, VERSION.NO$ = 6, SERIAL.NO$ = 10 &
	DROP.DEAD.DATE$='        ' &
\	VERSION.NO$='V1.0' &
\	IF DROP.DEAD.DATE$<>'' &
	THEN	IF DROP.DEAD.DATE$<MID(SYS(CHR$(6%)+CHR$(20%)+CHR$(0%)+CHR$(0%)+ &
				CHR$(1%)+CHR$(SWAP%(1%))),7%,8%) &
		THEN	MESG$=MID(SYS(CHR$(6%)+CHR$(9%)+CHR$(47%)), &
				3%,30%) &
\			PRINT #CH%, MESG$; &
\			V$=SYS(CHR$(9%)) &

100	DEVICE.CH%	= 02% &
\	CUSBAL.CH%	= 02% &
\	CUSTOM.CH%	= 04% &
\	CHART.CH%	= 06% &
\	MENU.CH%   	= 12% &
	&
\	LOOP.DATA%	= 19% &
\	CUSTOM.START%	= 3% &
\	CUSTOM.END%  	= 9% &

200	IF FNO%(DEVICE.CH%,'DEVICE.DAT','/R0','')=0% &
	THEN	CUSBAL.DEVICE$=CVT$$(MID(FNL$,7%,20%),-1%) &
			IF FNG%(DEVICE.CH%,'CUSBAL')=0% &
\		CUSTOM.DEVICE$=CVT$$(MID(FNL$,7%,20%),-1%) &
			IF FNG%(DEVICE.CH%,'CUSTOM')=0% &
\		CHART.DEVICE$=CVT$$(MID(FNL$,7%,20%),-1%) &
			IF FNG%(DEVICE.CH%,'CHART')=0% &
\		V%=FNC%(DEVICE.CH%) &

300	V%=FNO%(CUSBAL.CH%,CUSBAL.DEVICE$+'CUSBAL.DAT','/RW','') &
\	V%=FNO%(CUSBAL.CH%,CUSBAL.DEVICE$+'CUSBAL.DAT','/CR:16,128','') &
								IF FNS% = 5% &
\	IF FNS% &
	THEN	V$=FNMESS$(CH%,FNS%,'CUSBAL.DAT',0%,-1%) &
\		OPT$='Q' &
\		GOTO 1040 &

310	V%=FNO%(CUSTOM.CH%,CUSTOM.DEVICE$+'CUSTOM.DAT','/RW','') &
\	V%=FNO%(CUSTOM.CH%,CUSTOM.DEVICE$+'CUSTOM.DAT','/CR:8,128','') &
								IF FNS% = 5% &
\	IF FNS% &
	THEN	V$=FNMESS$(CH%,FNS%,'CUSTOM.DAT',0%,-1%) &
\		OPT$='Q' &
\		GOTO 1040 &

320	CHART.FLAG%=FNO%(CHART.CH%,CHART.DEVICE$+'CHART.DAT','/RO','') &

400	OPEN 'MENU.FIL/RO' FOR INPUT AS FILE MENU.CH% &
\	DIM #12%, A0$(1%)=64% &
\	COMPANY$=CVT$$(RIGHT(A0$(1%),2%),128%) &
\	CLOSE MENU.CH% &

450	OPEN 'NL:' AS FILE 12%, RECORDSIZE 256%+128% &
\	FIELD #12%,	06% AS CUSTOM.NUM$,		! Number &
			25% AS CUSTOM.NAME$,		! Name &
			25% AS CUSTOM.ADD1$,		! Address 1 &
			21% AS CUSTOM.ADD2$,		! Address 2 &
			15% AS CUSTOM.CITY$,		! City &
			02% AS CUSTOM.STATE$,		! State (Post office code) &
			05% AS CUSTOM.COUNTRY$,		! Country &
			10% AS CUSTOM.ZIP$,		! Zip code &
			10% AS CUSTOM.PHONE$,		! Phone Number (XXX-XXX-XXXX) &
			02% AS CUSTOM.SLSMAN$,		! Salesman &
			02% AS CUSTOM.TERR$,		! Territory &
			02% AS CUSTOM.LOC$,		! Inventory location &
			02% AS CUSTOM.TYPE$,		! Customer type &
			02% AS CUSTOM.CODE$,		! Customer code 2 &
			01% AS CUSTOM.TAXFLG$,		! Tax Flag &
			10% AS CUSTOM.STAXNUM$,		! Customer Sales tax ex number &
			02% AS CUSTOM.STAXLOC$,		! Sales tax location id &
			01% AS CUSTOM.METHOD$,		! Balance method &
							!  "O" = open item &
							!  "B" = balance foreward &
			01% AS CUSTOM.STMFLG$,		! Statement Flag &
							!  "1" = Gets a statement &
							!  "2" = Doesn't get statement &
			15% AS CUSTOM.ALPSRT$		! Alpha sort field &
\	FIELD #12%, 256% AS TEMP$, &
			06% AS CUSBAL.CUSNUM$,		&
			08% AS CUSBAL.ACCT$,		&
			08% AS CUSBAL.CUR$,		&
			08% AS CUSBAL.30$,		&
			08% AS CUSBAL.60$,		&
			08% AS CUSBAL.90$,		&
			08% AS CUSBAL.OVER$,		&
			08% AS CUSBAL.YTDSER$,		&
			02% AS CUSBAL.LSTCHRG$,		&
			02% AS CUSBAL.LSTPYMT$,		&
			08% AS CUSBAL.LIMIT$,		&
			02% AS CUSBAL.UPDDAT$		&
\	FIELD #12%, 256% AS CUSTOM.BUF$, 128% AS CUSBAL.BUF$ &

500	FRAME$ = FRAME$ + FNP$(NUM1$(I%)+';1')+' '+FNP$(NUM1$(I%)+';80')+' ' &
							FOR I%=2% TO 19% &
\	COMMAND$  = ' ' &
\	COMMAND$  = COMMAND$+RIGHT(NUM1$(I%+100%),2%) + ' ' &
		FOR I%=1% TO LOOP.DATA% &

1000	!--------------------------------------------PROGRAM RESTART POINT &
	V% = FNG%(CUSBAL.CH%,'') &
\	KEY.POINTER%=FNR(CUSBAL.CH%) &
\	DATA.POINTER%=FNR(CUSBAL.CH%+1%) &
\	IF V%=0% &
	THEN	1010 &
		ELSE	PRINT #CH%, G.OFF$; &
\			GOSUB 1060 &
\			GOTO 1030 &

1005	IF FNN%(CUSBAL.CH%) &
	THEN	18910 &
		ELSE	KEY.POINTER%=FNR(CUSBAL.CH%) &
\			DATA.POINTER%=FNR(CUSBAL.CH%+1%) &
\			GOTO 1015 &

1010	PRINT #CH%, COLM.OFF$;FNSR$('1;24');G.OFF$; &
\	GOSUB 1060 &

1015	GOSUB 6000 &

1030	CHNG.FLAG%=0% &
\	PRINT #CH%, CLRBOT$;G.OFF$; &
		'COMMAND: Add Erase Change Blank Initialize Default '; &
		'Find Next Restore ';FNP$('22;10'); &
		'View Quit '; &
\	OPT$=CVT$$(FNINP$(CH%,128%,' ',1%,1%),32%) &
\	OPT$='A' IF OPT$='' &
\	GOTO 1030 IF KEY.POINTER%=0% AND INSTR(1%,'AQD',OPT$)=0% &

1040	IF OPT$='Q' &
	THEN	PRINT #CH%, CLSCN$;FNSR$('1;24');FNP$('1;75'); &
			B.ON$;BLINK.ON$;R.ON$;'WAIT';G.OFF$;FNP$('24;1'); &
			FNX%('',0%,''); &

1050	GOTO 1000 	IF OPT$='R' 		! Restore &
\	GOTO 1005 	IF OPT$='N' 		! Next &
\	GOTO 2000 	IF OPT$='A' OR OPT$='F' ! Add, Find &
\	GOTO 2200 	IF OPT$='C' OR OPT$='B' ! Change, Blank &
\	GOTO 2400 	IF OPT$='E' 		! Erase &
\	GOTO 2500 	IF OPT$='D' 		! Default &
\	GOTO 2600 	IF OPT$='I' 		! Initialize &
\	GOTO 17000	IF OPT$='V' 		! View &
\	GOTO 1030 &

1060	TEMP$     = 'Customer Balance File Maintenance' &
\	PRINT #CH%, CLSCN$;R.ON$;LEFT(TEMP$,39%);SPACE$(40%-LEN(TEMP$)); &
			SPACE$(40%-LEN(COMPANY$));COMPANY$; &
			FRAME$; FNP$('20;1');SPACE$(80%);G.OFF$; &
\	PRINT #CH%, 	FNP$('03;03');'(01) Cust No.'; &
 			FNP$('04;03');'(02) A/R Acct #'; &
			FNP$('05;03');'(03) Name'; &
			FNP$('06;03');'(04) Add1'; &
			FNP$('07;03');'(05) Add2'; &
			FNP$('08;03');'(06) City'; &
			FNP$('09;03');'(07) St'; &
			FNP$('10;03');'(08) Zip'; &
			FNP$('11;03');'(09) Country'; &
\	PRINT #CH%,	FNP$('06;46');'(10) Current'; &
			FNP$('07;46');'(11) 30 Days'; &
			FNP$('08;46');'(12) 60 Days'; &
			FNP$('09;46');'(13) 90 Days'; &
			FNP$('10;46');'(14) Over 120 Days'; &
			FNP$('11;46');'     Balance'; &
			FNP$('14;21');'(15) YTD Service Chrg'; &
			FNP$('15;21');'(16) Last Charge Date'; &
			FNP$('16;21');'(17) Last Pymt Date'; &
			FNP$('17;21');'(18) Customer Limit'; &
			FNP$('18;21');'(19) Last update'; &
\	RETURN &

2000	!-------------------------------------------------SEARCH FOR KEYS &
	OPTION$='ADD ' &
\	OPTION$='FIND ' IF OPT$='F' &
\	INP$='' &
\	FOR LOOP%=1% TO LOOP.DATA% &
\		INP$=DEFAULT$(LOOP%) IF OPT$='A' &
\		GOSUB 6030 &
\	NEXT LOOP% &
\	NET,AMOUNT=0. &
\	GOSUB 6900 &
\	LOOP%=1% &
\	GOSUB 6010 &
\	GOTO 1015 IF OPT$='F' AND INP$='' &
\	SEARCH.KEY$=CUSBAL.CUSNUM$+'' &
\	LOOP%=2% &
\	GOSUB 6010 &
\	SEARCH.KEY$=SEARCH.KEY$+CUSBAL.ACCT$+'' &
\	IF	OPT$='F' &
	THEN	V%=FNG%(CUSBAL.CH%,SEARCH.KEY$) &
\		KEY.POINTER%=FNR(CUSBAL.CH%) &
\		DATA.POINTER%=FNR(CUSBAL.CH%+1%) &
\		GOTO 1015 &

2100	!-----------------------------------------------------ADD RECORD &
  	GOSUB 6010 FOR LOOP%=3% TO CUSTOM.END% IF CUSTOM.DATA%=0% &
\	GOSUB 6005 FOR LOOP%=3% TO CUSTOM.END% IF CUSTOM.DATA% &
\	GOSUB 6010 FOR LOOP%=CUSTOM.END%+1% TO LOOP.DATA% &
\	IF FNA%(CUSBAL.CH%,CUSBAL.BUF$) &
	THEN	V$=FNMESS$(CH%,FNS%,'CUSBAL.DAT',0%,-1%) &
\		GOTO 1000 &

2110	KEY.POINTER%=FNR(CUSBAL.CH%) &
\	DATA.POINTER%=FNR(CUSBAL.CH%+1%) &

2120	IF CUSTOM.DATA%=0% &
	THEN	LSET CUSTOM.NUM$ = CUSBAL.CUSNUM$ &
\		IF FNA%(CUSTOM.CH%,CUSTOM.BUF$) &
		THEN	V$=FNMESS$(CH%,FNS%,'CUSBAL.DAT',0%,-1%) &
\			GOTO 1000 &

2130	IF CUSTOM.DATA%=0% &
	THEN	CUSTOM.KEY%=FNR(CUSTOM.CH%) &
\		CUSTOM.DATA%=FNR(CUSTOM.CH%+1%) &

2140	GOTO 1030 &

2200	!----------------------------------------CHANGE OR BLANK RECORD &
	IF OPT$='B' &
	THEN	OPTION$ = 'BLANK ' &
	ELSE	OPTION$ = 'CHANGE ' &
\		CHNG.FLAG% = -1% &

2210	PRINT #CH%, CLRBOT$; OPTION$+':'; &
\	INP$=CVT$$(FNINP$(CH%,128%,' ',2%,1%),32%) &
\	GOTO 1030 IF INP$='' &
\	LOOP%=(INSTR(1%,COMMAND$,' '+INP$+' ')+2%)/3% &
\	GOTO 2210 IF (LOOP%=0% OR LOOP%>LOOP.DATA%) AND OPT$='C' &
\	GOTO 2210 IF (LOOP%<2% OR LOOP%>LOOP.DATA%) AND OPT$='B' &
\	IF OPT$='C' &
	THEN	GOSUB 6010 &
	ELSE	INP$='' &
\		GOSUB 6030 &

2220	GOSUB 2900 &
\	IF 	FNS%=0% &
	THEN	GOTO 2210 &
	ELSE	V$=FNMESS$(CH%,FNS%,'CUSBAL.DAT',0%,-1%) &
\		GOTO 1010 &

2400	!================================================ DELETE RECORD &
	PRINT #CH%, CLRBOT$;"Confirm deletion (Yes/No) "; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",1%,1%),32%) &
\	GOTO 1030 IF INP$<>"Y" &

2420	IF FND%(CUSBAL.CH%,"") &
	THEN	V$=FNMESS$(CH%,FNS%,'CUSBAL.DAT',0%,-1%) &
	ELSE	PRINT #CH%,FNP$('24;1');CLRLIN$;"Record has been erased."; &
			FNP$('24;55');"Hit any key to continue "; &
\		NW$=FNINP$(CH%,128%," ",1%,1%) &

2430	GOTO 1005 &

2500	!-----------------------------------------------SET DEFAULT VALUES &
	FOR LOOP%=1% TO LOOP.DATA% &
\		INP$=DEFAULT$(LOOP%) &
\		GOSUB 6030 &
\	NEXT LOOP% &
\	NET,AMOUNT=0. &
\	GOSUB 6900 &

2510	OPTION$='DEFAULT ' &
\	PRINT #CH%, CLRBOT$;'Default:'; &
\	INP$=CVT$$(FNINP$(CH%,128%,' ',2%,1%),32%) &
\	LOOP%=(INSTR(1%,COMMAND$,' '+INP$+' ')+2%)/3% &
\	GOTO 2510 IF LOOP%<=1% &
\	GOSUB 6010 &
\	DEFAULT$(LOOP%)=INP$ &
\	GOTO 2510 &

2600	!-----------------------------------------------INITIALIZATION &
	INP$='' &
\	GOSUB 6030 FOR LOOP%=1% TO LOOP.DATA% &
\	INITIALIZE$=' ' &
\	NET,AMOUNT=0. &
\	GOSUB 6900 &

2610	PRINT #CH%, CLRBOT$;FNP$('22;1');INITIALIZE$; &
		FNP$('21;1');'Field to initialize:'; &
\	INP$=CVT$$(FNINP$(CH%,128%,' ',2%,1%),32%) &
\	IF INP$<>'' &
	THEN	LOOP%=(INSTR(1%,COMMAND$,' '+INP$+' ')+2%)/3% &
\		INITIALIZE$=INITIALIZE$+INP$+' ' IF &
			LOOP%<>0% AND (INSTR(1%,INITIALIZE$,' '+INP$+' ') &
				+2%)/3%=0% &
\		GOTO 2610 &

2620	GOTO 1010 IF INITIALIZE$='' &
\	INP$='' &
\	GOSUB 6030 FOR LOOP%=1% TO LOOP.DATA% &
\	NET,AMOUNT=0. &
\	GOSUB 6900 &
\	OPT$='C' &
\	OPTION$='INITIALIZE ' &
\	CHNG.FLAG%=-1% &
\	GOTO 2660 &

2650	GOTO 18910 IF FNN%(CUSBAL.CH%) &
\	KEY.POINTER%=FNR(CUSBAL.CH%) &
\	DATA.POINTER%=FNR(CUSBAL.CH%+1%) &

2660	GOSUB 6000 &
\	FOR I%=1% TO LEN(INITIALIZE$)/3% &
\		LOOP%=(INSTR(1%,COMMAND$,MID(INITIALIZE$,I%*3%-2%,3%))+2%)/3% &
\		GOSUB 6010 &
\		GOTO 1030 IF INP$='%^C' &

2680	IF INP$<>'' &
	THEN	GOSUB 2900 &
\		IF 	FNS% &
		THEN	V$=FNMESS$(CH%,FNS%,'CUSBAL.DAT',0%,-1%) &
\		GOTO 1010 &

2690	NEXT I% &
\	GOTO 2650 &

2900	!-------------------------------------------------UPDATE A RECORD &
	IF LOOP%<CUSTOM.START% OR LOOP%>CUSTOM.END% &
	THEN	V%=FNG%(-CUSBAL.CH%,NUM1$(KEY.POINTER%))+ &
			FNG%(-CUSBAL.CH%-1%,NUM1$(DATA.POINTER%)) &
\		V%=FNU%(CUSBAL.CH%,CUSBAL.BUF$) IF LOOP%>2% AND FNS%=0% &
\		V%=FNU%(-CUSBAL.CH%,CUSBAL.BUF$) IF LOOP%<3% AND FNS%=0% &
\		RETURN &

2910	RETURN IF CUSTOM.DATA%<1% &
\	V%=FNG%(-CUSTOM.CH%,NUM1$(CUSTOM.KEY%))+ &
		FNG%(-CUSTOM.CH%-1%,NUM1$(CUSTOM.DATA%)) &
\	V%=FNU%(CUSTOM.CH%,CUSTOM.BUF$) IF FNS%=0% &
\	RETURN &

6000	!------------------------------------------------DATA ENTRY SECTION &
	RETURN IF KEY.POINTER%=0% OR FNG%(-CUSBAL.CH%,NUM1$(KEY.POINTER%)) + &
			FNG%(-CUSBAL.CH%-1%,NUM1$(DATA.POINTER%)) &
\	LSET CUSBAL.BUF$=FNL$ &
\	LSET CUSTOM.BUF$='' &
\	LSET CUSTOM.BUF$=FNL$ IF FNG%(CUSTOM.CH%,CUSBAL.CUSNUM$)=0% &
\	CUSTOM.KEY%,CUSTOM.DATA%=0% &
\	CUSTOM.KEY%=FNR(CUSTOM.CH%) IF FNS%=0% &
\	CUSTOM.DATA%=FNR(CUSTOM.CH%+1%) IF FNS%=0% &
\	NET,AMOUNT=0. &
\	FOR LOOP%=1% TO LOOP.DATA% &
\		GOSUB 6005 &
\	NEXT LOOP% &
\	RETURN &

6005	ON LOOP% GOSUB	6220, 6240, 6260, 6280, 6300, 6320, 6340, &
			6360, 6380, 6400, 6420, 6440, 6460, 6480, &
			6500, 6520, 6540, 6560, 6580 &
\	RETURN &

6010	ON LOOP% GOSUB	6210, 6230, 6250, 6270, 6290, 6310, 6330, &
			6350, 6370, 6390, 6410, 6430, 6450, 6470, &
			6490, 6510, 6530, 6550, 6570 &
\	IF LOOP%=1% &
	THEN	LSET CUSTOM.BUF$='' &
\		LSET CUSTOM.BUF$=FNL$ IF FNG%(CUSTOM.CH%,CUSBAL.CUSNUM$)=0% &
\		CUSTOM.KEY%,CUSTOM.DATA%=0% &
\		CUSTOM.KEY%=FNR(CUSTOM.CH%) IF FNS%=0% &
\		CUSTOM.DATA%=FNR(CUSTOM.CH%+1%) IF FNS%=0% &

6019	RETURN &
		! INPUT DATA &

6030	ON LOOP% GOSUB	6217, 6237, 6257, 6277, 6297, 6317, 6337, &
			6357, 6377, 6397, 6417, 6437, 6457, 6477, &
			6497, 6517, 6537, 6557, 6577 &
\	RETURN &

6200	!-----------------------------------------------DATA ENTRY SECTION &

6210	INP$=FNSIO$('','3;19',CUSBAL.CUSNUM$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CUSBAL.CUSNUM$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &
\	GOTO 6210 IF INP$='' AND OPT$='A' &

6217	LSET CUSBAL.CUSNUM$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6220	PRINT #CH%, FNP$('3;19');B.ON$;CUSBAL.CUSNUM$;G.OFF$; &
\	RETURN &

6230	INP$=FNSIO$('','4;19',CUSBAL.ACCT$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CUSBAL.ACCT$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &
\	IF INP$<>'' AND CHART.FLAG%=0% &
	THEN	IF FNG%(CHART.CH%,SPACE$(8%-LEN(INP$))+INP$) &
		THEN	PRINT #CH%, FNP$('24;1');CLRLIN$; &
			'Undefined account number.';FNP$('24;55'); &
			'Entry anyway (Y/N) '; &
\			NW$=CVT$$(FNINP$(CH%,128%,'_',1%,1%),-1%) &
\			PRINT #CH%, FNP$('24;1');CLRLIN$; &
\			IF NW$<>'Y' &
			THEN	GOTO 6230 &

6237	RSET CUSBAL.ACCT$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6240	PRINT #CH%, FNP$('4;19');B.ON$;CUSBAL.ACCT$;G.OFF$; &
\	CHART.DESC$='**  UNDEFINED  **   ' IF CHART.FLAG%=0% &
\	CHART.DESC$=MID(FNL$,9%,20%) IF FNG%(CHART.CH%,CUSBAL.ACCT$)=0% &
			AND CHART.FLAG%=0% &
\	PRINT #CH%, FNP$('4;28');CHART.DESC$; IF CHART.FLAG%=0% &
\	RETURN &

6250	INP$=FNSIO$('','5;19',CUSTOM.NAME$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CUSTOM.NAME$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &

6257	LSET CUSTOM.NAME$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6260	PRINT #CH%, FNP$('5;19');B.ON$;CUSTOM.NAME$;G.OFF$; &
\	RETURN &

6270	INP$=FNSIO$('','6;19',CUSTOM.ADD1$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CUSTOM.ADD1$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &

6277	LSET CUSTOM.ADD1$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6280	PRINT #CH%, FNP$('6;19');B.ON$;CUSTOM.ADD1$;G.OFF$; &
\	RETURN &

6290	INP$=FNSIO$('','7;19',CUSTOM.ADD2$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CUSTOM.ADD2$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &

6297	LSET CUSTOM.ADD2$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6300	PRINT #CH%, FNP$('7;19');B.ON$;CUSTOM.ADD2$;G.OFF$; &
\	RETURN &

6310	INP$=FNSIO$('','8;19',CUSTOM.CITY$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CUSTOM.CITY$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &

6317	LSET CUSTOM.CITY$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6320	PRINT #CH%, FNP$('8;19');B.ON$;CUSTOM.CITY$;G.OFF$; &
\	RETURN &

6330	INP$=FNSIO$('','9;19',CUSTOM.STATE$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CUSTOM.STATE$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &

6337	LSET CUSTOM.STATE$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6340	PRINT #CH%, FNP$('9;19');B.ON$;CUSTOM.STATE$;G.OFF$; &
\	RETURN &

6350	INP$=FNSIO$('','10;19',CUSTOM.ZIP$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CUSTOM.ZIP$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &

6357	LSET CUSTOM.ZIP$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6360	PRINT #CH%, FNP$('10;19');B.ON$;CUSTOM.ZIP$;G.OFF$; &
\	RETURN &

6370	INP$=FNSIO$('','11;19',CUSTOM.COUNTRY$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CUSTOM.COUNTRY$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &

6377	LSET CUSTOM.COUNTRY$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6380	PRINT #CH%, FNP$('11;19');B.ON$;CUSTOM.COUNTRY$;G.OFF$; &
\	RETURN &

6390	INP$=FNSIO$('##,###,###.##','6;65',CUSBAL.CUR$,DEFAULT$(LOOP%), &
			CHR$(CH%)+CHR$(13%)+CHR$(11%)+CHR$(5%)+CHR$(2%)+ &
			OPTION$+' VALUE') &

6397	AMOUNT=CVT$F(CUSBAL.CUR$) IF CHNG.FLAG% &
\	GOSUB 6910 IF CHNG.FLAG% &
\	LSET CUSBAL.CUR$=CVTF$(VAL(INP$)) IF INP$<>'' OR CHNG.FLAG%=0% &

6400	AMOUNT=CVT$F(CUSBAL.CUR$) &
\	PRINT #CH% USING FNP$('6;65')+B.ON$+'##,###,###.##'+G.OFF$, &
		AMOUNT; &
\	GOSUB 6900 &
\	RETURN &

6410	INP$=FNSIO$('##,###,###.##','7;65',CUSBAL.30$,DEFAULT$(LOOP%), &
			CHR$(CH%)+CHR$(13%)+CHR$(11%)+CHR$(5%)+CHR$(2%)+ &
			OPTION$+' VALUE') &

6417	AMOUNT=CVT$F(CUSBAL.30$) IF CHNG.FLAG% &
\	GOSUB 6910 IF CHNG.FLAG% &
\	LSET CUSBAL.30$=CVTF$(VAL(INP$)) IF INP$<>'' OR CHNG.FLAG%=0% &

6420	AMOUNT=CVT$F(CUSBAL.30$) &
\	PRINT #CH% USING FNP$('7;65')+B.ON$+'##,###,###.##'+G.OFF$, &
		AMOUNT; &
\	GOSUB 6900 &
\	RETURN &

6430	INP$=FNSIO$('##,###,###.##','8;65',CUSBAL.60$,DEFAULT$(LOOP%), &
			CHR$(CH%)+CHR$(13%)+CHR$(11%)+CHR$(5%)+CHR$(2%)+ &
			OPTION$+' VALUE') &

6437	AMOUNT=CVT$F(CUSBAL.60$) IF CHNG.FLAG% &
\	GOSUB 6910 IF CHNG.FLAG% &
\	LSET CUSBAL.60$=CVTF$(VAL(INP$)) IF INP$<>'' OR CHNG.FLAG%=0% &

6440	AMOUNT=CVT$F(CUSBAL.60$) &
\	PRINT #CH% USING FNP$('8;65')+B.ON$+'##,###,###.##'+G.OFF$, &
		AMOUNT; &
\	GOSUB 6900 &
\	RETURN &

6450	INP$=FNSIO$('##,###,###.##','9;65',CUSBAL.90$,DEFAULT$(LOOP%), &
			CHR$(CH%)+CHR$(13%)+CHR$(11%)+CHR$(5%)+CHR$(2%)+ &
			OPTION$+' VALUE') &

6457	AMOUNT=CVT$F(CUSBAL.90$) IF CHNG.FLAG% &
\	GOSUB 6910 IF CHNG.FLAG% &
\	LSET CUSBAL.90$=CVTF$(VAL(INP$)) IF INP$<>'' OR CHNG.FLAG%=0% &

6460	AMOUNT=CVT$F(CUSBAL.90$) &
\	PRINT #CH% USING FNP$('9;65')+B.ON$+'##,###,###.##'+G.OFF$, &
		AMOUNT; &
\	GOSUB 6900 &
\	RETURN &

6470	INP$=FNSIO$('##,###,###.##','10;65',CUSBAL.OVER$,DEFAULT$(LOOP%), &
			CHR$(CH%)+CHR$(13%)+CHR$(11%)+CHR$(5%)+CHR$(2%)+ &
			OPTION$+' VALUE') &

6477	AMOUNT=CVT$F(CUSBAL.OVER$) IF CHNG.FLAG% &
\	GOSUB 6910 IF CHNG.FLAG% &
\	LSET CUSBAL.OVER$=CVTF$(VAL(INP$)) IF INP$<>'' OR CHNG.FLAG%=0% &

6480	AMOUNT=CVT$F(CUSBAL.OVER$) &
\	PRINT #CH% USING FNP$('10;65')+B.ON$+'##,###,###.##'+G.OFF$, &
		AMOUNT; &
\	GOSUB 6900 &
\	RETURN &

6490	INP$=FNSIO$('##,###,###.##','14;43',CUSBAL.YTDSER$,DEFAULT$(LOOP%), &
			CHR$(CH%)+CHR$(13%)+CHR$(11%)+CHR$(5%)+CHR$(2%)+ &
			OPTION$+' VALUE') &

6497	LSET CUSBAL.YTDSER$=CVTF$(VAL(INP$)) IF INP$<>'' OR CHNG.FLAG%=0% &

6500	AMOUNT=CVT$F(CUSBAL.YTDSER$) &
\	PRINT #CH% USING FNP$('14;43')+B.ON$+'##,###,###.##'+G.OFF$, &
		AMOUNT; &
\	RETURN &

6510	INP$=FNSIO$('','15;43',CUSBAL.LSTCHRG$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			CHR$(8%)+CHR$(6%)+ &
			CHR$(4%)+CHR$(0%)+OPTION$+' DATE') &

6517	LSET CUSBAL.LSTCHRG$=CVT%$(FND6%(INP$)) IF INP$<>'' OR CHNG.FLAG%=0% &

6520	PRINT #CH%, FNP$('15;43');B.ON$;FND6$(CVT$%(CUSBAL.LSTCHRG$));G.OFF$; &
\	RETURN &

6530	INP$=FNSIO$('','16;43',CUSBAL.LSTPYMT$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			CHR$(8%)+CHR$(6%)+ &
			CHR$(4%)+CHR$(0%)+OPTION$+' DATE') &

6537	LSET CUSBAL.LSTPYMT$=CVT%$(FND6%(INP$)) IF INP$<>'' OR CHNG.FLAG%=0% &

6540	PRINT #CH%, FNP$('16;43');B.ON$;FND6$(CVT$%(CUSBAL.LSTPYMT$));G.OFF$; &
\	RETURN &

6550	INP$=FNSIO$('##,###,###.##','17;43',CUSBAL.LIMIT$,DEFAULT$(LOOP%), &
			CHR$(CH%)+CHR$(13%)+CHR$(11%)+CHR$(5%)+CHR$(2%)+ &
			OPTION$+' VALUE') &

6557	LSET CUSBAL.LIMIT$=CVTF$(VAL(INP$)) IF INP$<>'' OR CHNG.FLAG%=0% &

6560	AMOUNT=CVT$F(CUSBAL.LIMIT$) &
\	PRINT #CH% USING FNP$('17;43')+B.ON$+'##,###,###.##'+G.OFF$, &
		AMOUNT; &
\	RETURN &

6570	INP$=FNSIO$('','18;43',CUSBAL.UPDDAT$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			CHR$(8%)+CHR$(6%)+ &
			CHR$(4%)+CHR$(0%)+OPTION$+' DATE') &

6577	LSET CUSBAL.UPDDAT$=CVT%$(FND6%(INP$)) IF INP$<>'' OR CHNG.FLAG%=0% &

6580	PRINT #CH%, FNP$('18;43');B.ON$;FND6$(CVT$%(CUSBAL.UPDDAT$));G.OFF$; &
\	RETURN &

6900	!-----------------------------------------PRINT NET INVOICE &
	NET=NET+AMOUNT &
\	PRINT #CH% USING FNP$('11;65')+G.OFF$+'##,###,###.##',NET; &
\	RETURN &

6910	NET=NET-AMOUNT ! SUBTRACT FOR NET AMOUNT IF CHANGE &
\	RETURN &

17000	!-----------------------------------------------VIEW ROUTINE &
	PRINT #CH%, COLM.ON$; R.ON$;FNP$('1;1');SPACE$(132%); &
		FNP$('20;1');SPACE$(132%);FNSR$('4;19'); &
\	PRINT #CH%, FNP$(NUM1$(I%)+';1');' ';FNP$(NUM1$(I%)+';132');' '; &
		FOR I%=2% TO 19% &
\	PRINT #CH%, G.OFF$;FNP$('2;2');'Cust #';FNP$('2;9');'Acct #'; &
		FNP$('2;24');'Current';FNP$('2;38');'30 days'; &
		FNP$('2;52');'60 days';FNP$('2;66');'90 days'; &
		FNP$('2;80');'Over 120';FNP$('2;94');'Balance'; &
		FNP$('2;102');'LastChrg';FNP$('2;111');'LastPymt'; &
		FNP$('2;125');'Limit'; &
\	PRINT #CH%, R.ON$;LDS.ON$;FNP$('3;2');STRING$(130%,113%); &
		FNP$('3;8');CHR$(110%);FNP$('3;17');CHR$(110%); &
		FNP$('3;31');CHR$(110%);FNP$('3;45');CHR$(110%); &
		FNP$('3;59');CHR$(110%);FNP$('3;73');CHR$(110%); &
		FNP$('3;87');CHR$(110%);FNP$('3;101');CHR$(110%); &
		FNP$('3;110');CHR$(110%);FNP$('3;119');CHR$(110%); &
		USASCII$;G.OFF$; &
\	PRINT #CH%, R.ON$;LDS.ON$;FNP$('2;8');CHR$(120%); &
		FNP$('2;8');CHR$(120%);FNP$('2;17');CHR$(120%); &
		FNP$('2;31');CHR$(120%);FNP$('2;45');CHR$(120%); &
		FNP$('2;59');CHR$(120%);FNP$('2;73');CHR$(120%); &
		FNP$('2;87');CHR$(120%);FNP$('2;101');CHR$(120%); &
		FNP$('2;110');CHR$(120%);FNP$('2;119');CHR$(120%); &
		USASCII$;G.OFF$; &
\	DISPLAY.LINE%=0% &
\	PRINT.LINE%=4% &

17010	V%=FNG%(-CUSBAL.CH%,NUM1$(KEY.POINTER%))+ &
		FNG%(-CUSBAL.CH%-1%,NUM1$(DATA.POINTER%)) &
\	GOTO 17105 &

17100	IF FNN%(CUSBAL.CH%) &
	THEN	17200 &

17105	LSET CUSBAL.BUF$=FNL$ &
\	I$=NUM1$(PRINT.LINE%) &
\	PRINT #CH% USING FNP$(I$+';2')+'\    \'+ &
		FNP$(I$+';9')+'\      \'+ &
		FNP$(I$+';18')+'##,###,###.##'+ &
		FNP$(I$+';32')+'##,###,###.##'+ &
		FNP$(I$+';46')+'##,###,###.##'+ &
		FNP$(I$+';60')+'##,###,###.##', &
			CUSBAL.CUSNUM$, &
			CUSBAL.ACCT$, &
			CVT$F(CUSBAL.CUR$), &
			CVT$F(CUSBAL.30$), &
			CVT$F(CUSBAL.60$), &
			CVT$F(CUSBAL.90$); &
\	PRINT #CH% USING FNP$(I$+';74')+'##,###,###.##'+ &
		FNP$(I$+';88')+'##,###,###.##'+ &
		FNP$(I$+';102')+'\      \'+ &
		FNP$(I$+';111')+'\      \'+ &
		FNP$(I$+';120')+'##,###,###', &
			CVT$F(CUSBAL.OVER$), &
			CVT$F(CUSBAL.CUR$)+CVT$F(CUSBAL.30$)+ &
			CVT$F(CUSBAL.60$)+CVT$F(CUSBAL.90$)+ &
			CVT$F(CASBAL.90$), &
			FND6$(CVT$%(CUSBAL.LSTCHRG$)), &
			FND6$(CVT$%(CUSBAL.LSTPYMT$)), &
			CVT$F(CUSBAL.LIMIT$); &

17106	PRINT #CH%, R.ON$;LDS.ON$;FNP$(I$+';8');CHR$(120%); &
		FNP$(I$+';8');CHR$(120%);FNP$(I$+';17');CHR$(120%); &
		FNP$(I$+';31');CHR$(120%);FNP$(I$+';45');CHR$(120%); &
		FNP$(I$+';59');CHR$(120%);FNP$(I$+';73');CHR$(120%); &
		FNP$(I$+';87');CHR$(120%);FNP$(I$+';101');CHR$(120%); &
		FNP$(I$+';110');CHR$(120%);FNP$(I$+';119');CHR$(120%); &
		USASCII$;G.OFF$; &
\	PRINT.LINE%=PRINT.LINE%+1% IF PRINT.LINE%<19% &

17110	IF DISPLAY.LINE%>14% &
	THEN	PRINT #CH%, FNP$('24;105');'Hit any key to continue '; &
\		INP$=FNINP$(CH%,128%,' ',1%,0%) &
\		GOTO 17210 IF INP$='%^C' &
\		DISPLAY.LINE%=0% &
\		PRINT #CH%, CLRBOT$;FNP$(NUM1$(PRINT.LINE%)+';1'); &

17120	PRINT #CH% &
\	DISPLAY.LINE%=DISPLAY.LINE%+1% &
\	IF PRINT.LINE%=19% &
	THEN	PRINT #CH%, FNP$(NUM1$(PRINT.LINE%)+';1');R.ON$;' '; &
			FNP$(NUM1$(PRINT.LINE%)+';132');' ';G.OFF$; &

17190	GOTO 17100 &

17200	IF DISPLAY.LINE%>0% &
	THEN	PRINT #CH%, FNP$('24;1');'End of file.  '; &
			FNP$('24;105');'Hit any key to continue '; &
\		INP$=FNINP$(CH%,128%,' ',1%,0%) &

17210	V%=FNG%(-CUSBAL.CH%,NUM1$(KEY.POINTER%))+ &
		FNG%(-CUSBAL.CH%-1%,NUM1$(DATA.POINTER%)) &
\	PRINT #CH%, FNP$('1;1');FNSR$('1;24');COLM.OFF$; &
\	GOTO 1010 &

18910	!----------------------------------------------END OF FILE &
	PRINT #CH%, CLRBOT$;FNP$('24;1');'End of file has been reached.'; &
		FNP$('24;55');'Hit any key to continue '; &
\	INP$=FNINP$(CH%,128%,' ',1%,0%) &
\	GOTO 1000 &

19000	!------------------------------------------------ERROR TRAPPING &
	RESUME IF ERR=52% OR ERR=51% &
\	RESUME 450 IF ERL=400 &
\	RESUME 1010 IF ERR=54% &

19999	ON ERROR GOTO 0 &

30000	!----------------------PROGRAM FUNCTIONS------------------------ &
	!------------------------------------------------TERMINAL INPUT &
	DEF FNINP$(CHN%,KYP%,FILLCHAR$,INPUTLEN%,TO.ERR%) &
\		PRINT #CHN%, STRING$(INPUTLEN%,ASCII(FILLCHAR$)); &
			  STRING$(INPUTLEN%,8%); &
\		PRINT #CHN%, RECORD 256%,CHR$(KYP%+INPUTLEN%)+FILLCHAR$; &
\		GET #CHN% &
\		FIELD #CHN%, RECOUNT AS BUFFER$ &
\		BUFFER$='%^C' IF INSTR(1%,BUFFER$,CHR$(3%)) &
\		FNINP$=CVT$$(BUFFER$,4%) &
\		V=SQR(-1) IF BUFFER$='%^C' AND TO.ERR% !^C Trappping &
\	FNEND &

30200	DEF*FNP$(ROWCOL$)=ESC$+'['+ROWCOL$+'H' 	! Direct Cursor Address &

30250	!-----------------------------------------------MESSAGE HANDLER &
	DEF*FNMESS$(CHN%,ERRNUM%,DESC$,TO.ERR%,PRINT.TEST%) &
\		MESG$=MID(SYS(CHR$(6%)+CHR$(9%)+CHR$(ERRNUM%)),3%,30%)+ &
			DESC$ &
\		IF PRINT.TEST% &
		THEN	PRINT #CHN%, FNP$('24;1');CLRLIN$;MESG$; &
				FNP$('24;55'); &
				'Hit any key to continue.'; &
\			NW$=FNINP$(CHN%,128%,' ',1%,TO.ERR%) &

30260		FNMESS$=MESG$ &
\	FNEND &

30300	DEF*FNSR$(BEGEND$)=ESC$+'['+BEGEND$+'r'	! Scroll control &

30350	DEF FNFONE$(PHN$)='('+LEFT(PHN$,3%)+')'+MID(PHN$,4%,3%)+'-' &
		+RIGHT(PHN$,7%) &

30400	!-------------------------------------------------DATE HANDLERS &
	DEF FND8%(D8)=D8 						&

30410	DEF FND6%(D9$)=VAL(MID(D9$,3%,2%))+VAL(LEFT(D9$,2%))*32% 	&
		+ FND8%(VAL(RIGHT(D9$,5%)))*512% 			&

30420	DEF FND6$(D9%)=RIGHT(NUM1$((D9% AND 15%*32%)/32%+100%),2%) 	&
		+ '/'+RIGHT(NUM1$((D9% AND 31%)+100%),2%) 		&
		+ '/'+RIGHT(NUM1$(((SWAP%(D9%) AND 254%)/2%)+100%),2%) 	&

30500	DEF FNZ(Y) &
\		TEMP=Y &
\		TEMP=-Y IF Y<0. &
\		TEMP=INT(TEMP*100.+.51)/100. &
\		TEMP=-TEMP IF Y<0. &
\		FNZ=TEMP &
\	FNEND &

30700	!========================================= DATA INPUT FUNCTIONS &
	DEF*FNSIO$(FRMAT$,ROWCOL$,VARABLE$,DEFAL$,ITEMS$) &
\		CHN%=ASCII(MID(ITEMS$,1%,1%)) &
\		KIND%=ASCII(MID(ITEMS$,4%,1%)) &
\		DECI=ASCII(MID(ITEMS$,5%,1%)) &
\		OPT$=MID(ITEMS$,6%,1%) &

30705		PRINT #CHN%, FNP$(ROWCOL$);R.ON$;B.ON$; &
\		IF OPT$='C' &
		THEN	ON KIND% GOTO 30710, 30715, 30720, 30725, 30730 &
		ELSE	PRINT #CHN%, SPACE$(ASCII(MID(ITEMS$,2%,1%))); &
\			GOTO 30735 &

30710		PRINT #CHN%, VARABLE$;			   ! ALPHA &
\		GOTO 30735 &

30715		PRINT #CHN% USING FRMAT$,ASCII(VARABLE$);  ! ASCII &
\		GOTO 30735 &

30720		PRINT #CHN% USING FRMAT$, CVT$%(VARABLE$)/(10.**DECI); &
\		GOTO 30735				   ! INTEGER &

30725		PRINT #CHN%, FND6$(CVT$%(VARABLE$));	   ! DATE &
\		GOTO 30735 &

30730		PRINT #CHN% USING FRMAT$,CVT$F(VARABLE$);  ! FLOAT &

30735		PRINT #CHN%,G.OFF$;CLRBOT$;RIGHT(ITEMS$,6%)+':  ';B.ON$; &
\		FIN$ = FNINP$(CHN%,0%,'_',ASCII(MID(ITEMS$,3%,1%)),1%) &
\		V% = VAL(FIN$) IF KIND%=3% &
\		V  = VAL(FIN$) IF KIND%>3% &
\		GOTO 30705 IF KIND%=4% AND LEN(FIN$)<>6% AND FIN$<>'' &
\		IF FIN$='' &
		THEN	FIN$ = DEFAL$ IF INSTR(1%,'CF',OPT$)=0% &
\			GOTO 30745 &

30740		IF KIND%=3% OR KIND%=5% &
		THEN	TEMP = 1. &
\			TEMP = 10.**DECI IF INSTR(1%,FIN$,'.')=0% &
\			FIN$ = NUM1$(VAL(FIN$)/TEMP) &
\			FIN$ = FIN$ + '.' IF INSTR(1%,FIN$,'.')=0% &

30745		FNSIO$ = FIN$+'' &
\	FNEND &

32767	END &

