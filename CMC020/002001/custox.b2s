10	!--------------------------------------------------------------- &
	!	ACCOUNTS RECEIVABLE SYSTEM 	-Customer &
	!	Customer File Maintenance &
	! &
	!	CUSTOM.B2S	V1.0	March 1985 &
	! &
	! Author - Randall Beard, Computer Management Center, Inc. &
	! &
	! Files-CUSTOM.DAT	-ISAM	Vendor Description File &
	! &
	!--------------------------------------------------------------- &
	! &
	! &
	!		      Copyright (c) 1984 by &
	!	  Computer Management Center, Idaho Falls, Idaho &
	! &
	! This software is furnished under a license and may be used &
	! and copied only in accordance with the terms of such license &
	! and with the inclusion of the above copyright notice.  This &
	! software or any other copies therof may not be provided or &
	! otherwise made available to any other person.  No title to &
	! and ownership of the software is hereby transferred. &
	! &
	! The information in this software is subject to change without &
	! notice and should not be construed as a committment by &
	! Computer Management Center. &
	! &
	! CMC assumes no responsibility for the use or reliability of &
	! its software on equipment which is not supported by CMC. &
	! &
	!--------------------------------------------------------------- &

50	ON ERROR GOTO 19000 &
\	DIM DEFAULT$(30%),SEARCH$(30%),REPLCE$(30%) &

60	OPEN 'KB:' AS FILE #1%, MODE 8%+256% &
					!    8% - Echo Control &
					!   16% - Disable hibernation, Cntl-C &
\	CH%=1%				! Keyboard channel &
\	ESC$=CHR$(155%)			! Escape code for VT100 control &
\	CLSCN$=ESC$+'[H'+ESC$+'[J'	! Clear screen &
\	CLRLIN$=ESC$+'[2K'		! Erase entire line &
\	G.OFF$=ESC$+'[m'		! Select graphic off &
\	B.ON$=ESC$+'[1m'		! Bold face on &
\	BLINK.ON$=ESC$+'[5m'		! Blinking &
\	R.ON$=ESC$+'[7m'		! Reverse video &
\	COLM.ON$=ESC$+'[?3h'		! 132 Column mode &
\	COLM.OFF$=ESC$+'[?3l'		! 80 Column mode &
\	CLRBOT$=ESC$+'[21;1H'+ESC$+'[J'	! Erace cursor to end of screen &
\	LDS.ON$=ESC$+'(0'		! Line drawing set &
\	USASCII$=ESC$+'(B'		! United States ASCII &

80	!COM(THIS) DROP.DEAD.DATE$ = 8, VERSION.NO$ = 6, SERIAL.NO$ = 10 &
	DROP.DEAD.DATE$='        ' &
\	VERSION.NO$='V1.0' &
\	IF DROP.DEAD.DATE$<>'' &
	THEN	IF DROP.DEAD.DATE$<MID(SYS(CHR$(6%)+CHR$(20%)+CHR$(0%)+CHR$(0%)+ &
				CHR$(1%)+CHR$(SWAP%(1%))),7%,8%) &
		THEN	MESG$=MID(SYS(CHR$(6%)+CHR$(9%)+CHR$(47%)), &
				3%,30%) &
\			PRINT #CH%, MESG$; &
\			V$=SYS(CHR$(9%)) &

100	DEVICE.CH%	= 02% &
\	CUSTOM.CH%	= 04% &
\	MENU.CH%   	= 12% &
	&
\	LOOP.DATA% = 22% &

200	IF FNO%(DEVICE.CH%,'DEVICE.DAT','/R0','')=0% &
	THEN	CUSTOM.DEVICE$=CVT$$(MID(FNL$,7%,20%),-1%) &
			IF FNG%(DEVICE.CH%,'CUSTOM')=0% &
\		V%=FNC%(DEVICE.CH%) &

300	V%=FNO%(CUSTOM.CH%,CUSTOM.DEVICE$+'CUSTOM.DAT','/RW','') &
\	V%=FNO%(CUSTOM.CH%,CUSTOM.DEVICE$+'CUSTOM.DAT','/CR:8,256','') &
								IF FNS% = 5% &
\	IF FNS% &
	THEN	V$=FNMESS$(CH%,FNS%,'CUSTOM.DAT',0%,-1%) &
\		OPT$='Q' &
\		GOTO 1040 &

400	OPEN 'MENU.FIL/RO' FOR INPUT AS FILE MENU.CH% &
\	DIM #12%, A0$(1%)=64% &
\	COMPANY$=CVT$$(RIGHT(A0$(1%),2%),128%) &
\	CLOSE MENU.CH% &

450	OPEN 'NL:' AS FILE 12%, RECORDSIZE 256% &
\	FIELD #12%,	06% AS CUSTOM.NUM$,		! Number &
			25% AS CUSTOM.NAME$,		! Name &
			25% AS CUSTOM.ADD1$,		! Address 1 &
			21% AS CUSTOM.ADD2$,		! Address 2 &
			15% AS CUSTOM.CITY$,		! City &
			02% AS CUSTOM.STATE$,		! State (Post office code) &
			05% AS CUSTOM.SUBCOD$,		! SUBCODE &
			10% AS CUSTOM.ZIP$,		! Zip code &
			10% AS CUSTOM.PHONE$,		! Phone Number (XXX-XXX-XXXX) &
			02% AS CUSTOM.SLSMAN$,		! Salesman &
			02% AS CUSTOM.TERR$,		! Territory &
			02% AS TEMP$,			! &
			02% AS CUSTOM.TYPE$,		! Customer type &
			02% AS TEMP$,			! &
			01% AS CUSTOM.TAXFLG$,		! Tax Flag &
			10% AS CUSTOM.STAXNUM$,		! Customer Sales tax ex number &
			02% AS CUSTOM.STAXLOC$,		! Sales tax location id &
			01% AS CUSTOM.METHOD$,		! Balance method &
							!  "O" = open item &
							!  "B" = balance foreward &
			01% AS CUSTOM.STMFLG$,		! Statement Flag &
							!  "1" = Gets a statement &
							!  "2" = Doesn't get statement &
			15% AS CUSTOM.ALPSRT$,		! Alpha sort field &
			01% AS CUSTOM.SERVICE$,		! Service charge y/n &
			06% AS CUSTOM.LOC$,		! Customer Location &
			04% AS CUSTOM.SEQ$,		! Delivery order &
			01% AS CUSTOM.PURGE$,		! Purge flag &
			06% AS CUSTOM.CODE$		! Customer code 2 &
\	FIELD #12%, 256% AS CUSTOM.BUF$ &

500	FRAME$ = FRAME$ + FNP$(NUM1$(I%)+';1')+' '+FNP$(NUM1$(I%)+';80')+' ' &
							FOR I%=2% TO 19% &
\	COMMAND$  = ' ' &
\	COMMAND$  = COMMAND$+RIGHT(NUM1$(I%+100%),2%) + ' ' &
		FOR I%=1% TO LOOP.DATA% &

1000	!--------------------------------------------PROGRAM RESTART POINT &
	V% = FNG%(CUSTOM.CH%,'') &
\	KEY.POINTER%=FNR(CUSTOM.CH%) &
\	DATA.POINTER%=FNR(CUSTOM.CH%+1%) &
\	IF FNS%=0% &
	THEN	1010 &
		ELSE	PRINT #CH%, G.OFF$; &
\			GOSUB 1060 &
\			GOTO 1030 &

1005	IF FNN%(CUSTOM.CH%) &
	THEN	18910 &
		ELSE	KEY.POINTER%=FNR(CUSTOM.CH%) &
\			DATA.POINTER%=FNR(CUSTOM.CH%+1%) &
\			GOTO 1015 &

1010	PRINT #CH%, COLM.OFF$;FNSR$('1;24');G.OFF$; &
\	GOSUB 1060 &

1015	GOSUB 6000 &

1030	CHNG.FLAG%=0% &
\	PRINT #CH%, CLRBOT$;G.OFF$; &
		'COMMAND: Add Erase Change Blank Initialize Default '; &
		'Find Next Restore ';FNP$('22;10'); &
		'View Quit '; &
\	OPT$=CVT$$(FNINP$(CH%,128%,' ',1%,1%),32%) &
\	OPT$='A' IF OPT$='' &
\	GOTO 1030 IF KEY.POINTER%=0% AND INSTR(1%,'AQD',OPT$)=0% &

1040	IF OPT$='Q' &
	THEN	PRINT #CH%, CLSCN$;FNSR$('1;24');FNP$('1;75'); &
			B.ON$;BLINK.ON$;R.ON$;'WAIT';G.OFF$;FNP$('24;1'); &
			FNX%('',0%,''); &

1050	GOTO 1000 	IF OPT$='R' 		! Restore &
\	GOTO 1005 	IF OPT$='N' 		! Next &
\	GOTO 2000 	IF OPT$='A' OR OPT$='F' ! Add, Find &
\	GOTO 2200 	IF OPT$='C' OR OPT$='B' ! Change, Blank &
\	GOTO 2400 	IF OPT$='E' 		! Erase &
\	GOTO 2500 	IF OPT$='D' 		! Default &
\	GOTO 2600 	IF OPT$='I' 		! Initialize &
\	GOTO 17000	IF OPT$='V' 		! View &
\	GOTO 1030 &

1060	TEMP$     = 'Customer Master File Maintenance' &
\	PRINT #CH%, CLSCN$;R.ON$;LEFT(TEMP$,39%);SPACE$(40%-LEN(TEMP$)); &
			SPACE$(40%-LEN(COMPANY$));COMPANY$; &
			FRAME$; FNP$('20;1');SPACE$(80%);G.OFF$; &
\	PRINT #CH%, 	FNP$('02;02');'(01) Cust #'; &
			FNP$('03;02');'(02) Name'; &
			FNP$('04;02');'(03) Add1'; &
			FNP$('05;02');'(04) Add2'; &
			FNP$('06;02');'(05) City'; &
			FNP$('06;33');'(06) St'; &
			FNP$('07;02');'(07) Zip'; &
			FNP$('08;02');'(08) Phone'; &
			FNP$('10;02');'(09) Location'; &
			FNP$('11;02');'(10) Sequence'; &
			FNP$('12;02');'(11) Territory'; &
\	PRINT #CH%, 	FNP$('02;46');'(12) SubCode'; &
			FNP$('03;46');'(13) Type'; &
			FNP$('04;46');'(14) Code'; &
			FNP$('06;46');'(15) Tax Flag     (Y or N)'; &
			FNP$('07;46');'(16) Tax #'; &
			FNP$('08;46');'(17) Tax Loc'; &
			FNP$('10;46');'(18) Method       (O or B)'; &
			FNP$('11;46');'(19) Statement    (Y or N)'; &
			FNP$('12;46');'(20) Srvc Chrg    (Y or N)'; &
			FNP$('14;46');'(21) Alpha sort'; &
			FNP$('15;46');'(22) Purge (y/N)'; &
\	RETURN &

2000	!-------------------------------------------------SEARCH FOR KEYS &
	OPTION$='ADD ' &
\	OPTION$='FIND ' IF OPT$='F' &
\	INP$='' &
\	FOR LOOP%=1% TO LOOP.DATA% &
\		INP$=DEFAULT$(LOOP%) IF OPT$='A' &
\		GOSUB 6030 &
\	NEXT LOOP% &
\	GOSUB 6210 &
\	TEST.KEY$=INP$ &
\	TEST.KEY$=INP$+SPACE$(6%-LEN(INP$)) IF OPT$='A' &
\	V%=FNG%(CUSTOM.CH%,TEST.KEY$) &
\	IF FNS%=0% AND OPT$='A' OR OPT$='F' &
	THEN	KEY.POINTER%=FNR(CUSTOM.CH%) &
\		DATA.POINTER%=FNR(CUSTOM.CH%+1%) &
\		GOTO 1015 &

2100	!-----------------------------------------------------ADD RECORD &
  	GOSUB 6010 FOR LOOP%=2% TO LOOP.DATA% &
\	IF FNA%(CUSTOM.CH%,CUSTOM.BUF$) &
	THEN	V$=FNMESS$(CH%,FNS%,'CUSTOM.DAT',0%,-1%) &
\		GOTO 1000 &

2110	KEY.POINTER%=FNR(CUSTOM.CH%) &
\	DATA.POINTER%=FNR(CUSTOM.CH%+1%) &
\	GOTO 1030 &

2200	!----------------------------------------CHANGE OR BLANK RECORD &
	IF OPT$='B' &
	THEN	OPTION$ = 'BLANK ' &
	ELSE	OPTION$ = 'CHANGE ' &
\		CHNG.FLAG% = -1% &

2210	PRINT #CH%, CLRBOT$; OPTION$+':'; &
\	INP$=CVT$$(FNINP$(CH%,128%,' ',2%,1%),32%) &
\	GOTO 1030 IF INP$='' &
\	LOOP%=(INSTR(1%,COMMAND$,' '+INP$+' ')+2%)/3% &
\	GOTO 2210 IF (LOOP%=0% OR LOOP%>LOOP.DATA%) AND OPT$='C' &
\	GOTO 2210 IF (LOOP%<2% OR LOOP%>LOOP.DATA%) AND OPT$='B' &
\	IF OPT$='C' &
	THEN	GOSUB 6010 &
	ELSE	INP$='' &
\		GOSUB 6030 &

2220	GOSUB 2900 &
\	IF 	FNS%=0% &
	THEN	GOTO 2210 &
	ELSE	V$=FNMESS$(CH%,FNS%,'CUSTOM.DAT',0%,-1%) &
\		GOTO 1010 &

2400	!================================================ DELETE RECORD &
	PRINT #CH%, CLRBOT$;"Confirm deletion (Yes/No) "; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",1%,1%),32%) &
\	GOTO 1030 IF INP$<>"Y" &

2420	IF FND%(CUSTOM.CH%,"") &
	THEN	V$=FNMESS$(CH%,FNS%,'CUSTOM.DAT',0%,-1%) &
	ELSE	PRINT #CH%,FNP$('24;1');CLRLIN$;"Record has been erased."; &
			FNP$('24;55');"Hit any key to continue "; &
\		NW$=FNINP$(CH%,128%," ",1%,1%) &

2430	GOTO 1005 &

2500	!-----------------------------------------------SET DEFAULT VALUES &
	FOR LOOP%=1% TO LOOP.DATA% &
\		INP$=DEFAULT$(LOOP%) &
\		GOSUB 6030 &
\	NEXT LOOP% &

2510	OPTION$='DEFAULT ' &
\	PRINT #CH%, CLRBOT$;'Default:'; &
\	INP$=CVT$$(FNINP$(CH%,128%,' ',2%,1%),32%) &
\	LOOP%=(INSTR(1%,COMMAND$,' '+INP$+' ')+2%)/3% &
\	GOTO 2510 IF LOOP%<=1% &
\	GOSUB 6010 &
\	DEFAULT$(LOOP%)=INP$ &
\	GOTO 2510 &

2600	!-----------------------------------------------INITIALIZATION &
	INP$='' &
\	GOSUB 6030 FOR LOOP%=1% TO LOOP.DATA% &
\	INITIALIZE$=' ' &

2610	PRINT #CH%, CLRBOT$;FNP$('22;1');INITIALIZE$; &
		FNP$('21;1');'Field to initialize:'; &
\	INP$=CVT$$(FNINP$(CH%,128%,' ',2%,1%),32%) &
\	IF INP$<>'' &
	THEN	LOOP%=(INSTR(1%,COMMAND$,' '+INP$+' ')+2%)/3% &
\		INITIALIZE$=INITIALIZE$+INP$+' ' IF &
			LOOP%<>0% AND (INSTR(1%,INITIALIZE$,' '+INP$+' ') &
				+2%)/3%=0% &
\		GOTO 2610 &

2620	GOTO 1010 IF INITIALIZE$='' &
\	INP$='' &
\	GOSUB 6030 FOR LOOP%=1% TO LOOP.DATA% &
\	OPT$='C' &
\	OPTION$='INITIALIZE ' &
\	CHNG.FLAG%=-1% &
\	GOTO 2660 &

2650	GOTO 18910 IF FNN%(CUSTOM.CH%) &
\	KEY.POINTER%=FNR(CUSTOM.CH%) &
\	DATA.POINTER%=FNR(CUSTOM.CH%+1%) &

2660	GOSUB 6000 &
\	FOR I%=1% TO LEN(INITIALIZE$)/3% &
\		LOOP%=(INSTR(1%,COMMAND$,MID(INITIALIZE$,I%*3%-2%,3%))+2%)/3% &
\		GOSUB 6010 &
\		GOTO 1030 IF INP$='%^C' &

2680	IF INP$<>'' &
	THEN	GOSUB 2900 &
\		IF 	FNS% &
		THEN	V$=FNMESS$(CH%,FNS%,'CUSTOM.DAT',0%,-1%) &
\		GOTO 1010 &

2690	NEXT I% &
\	GOTO 2650 &

2900	!-------------------------------------------------UPDATE A RECORD &
	V%=FNG%(-CUSTOM.CH%,NUM1$(KEY.POINTER%))+ &
		FNG%(-CUSTOM.CH%-1%,NUM1$(DATA.POINTER%)) &
\	V%=FNU%(CUSTOM.CH%,CUSTOM.BUF$) IF LOOP%<>1% AND FNS%=0% &
\	V%=FNU%(-CUSTOM.CH%,CUSTOM.BUF$) IF LOOP%=1% AND FNS%=0% &
\	RETURN &

6000	!------------------------------------------------DATA ENTRY SECTION &
	RETURN IF KEY.POINTER%=0% OR FNG%(-CUSTOM.CH%,NUM1$(KEY.POINTER%))+ &
		FNG%(-CUSTOM.CH%-1%,NUM1$(DATA.POINTER%)) &
\	LSET CUSTOM.BUF$=FNL$ &
\	FOR LOOP%=1% TO LOOP.DATA% &
\		ON LOOP% GOSUB	6220, 6240, 6260, 6280, 6300, 6320, 6340, &
				6380, 6400, 6420, 6440, 6360, 6460, 6480, &
				6500, 6520, 6540, 6560, 6580, 6600, 6620, &
				6640 &
\	NEXT LOOP% &
\	RETURN &

6010	ON LOOP% GOSUB	6210, 6230, 6250, 6270, 6290, 6310, 6330, &
			6370, 6390, 6410, 6430, 6350, 6450, 6470, &
			6490, 6510, 6530, 6550, 6570, 6590, 6610, &
			6630 &
\	RETURN &
		! INPUT DATA &

6030	ON LOOP% GOSUB	6217, 6237, 6257, 6277, 6297, 6317, 6337, &
			6377, 6397, 6417, 6437, 6357, 6457, 6477, &
			6497, 6517, 6537, 6557, 6577, 6597, 6617, &
			6637 &
\	RETURN &

6200	!-----------------------------------------------DATA ENTRY SECTION &

6210	INP$=FNSIO$('','2;14',CUSTOM.NUM$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CUSTOM.NUM$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &
\	GOTO 6210 IF INP$='' AND OPT$='A' &
\	IF OPT$='C' AND INP$<>'' &
	THEN	IF FNG%(CUSTOM.CH%,INP$+SPACE$(6%-LEN(INP$)))=0% &
		THEN	PRINT #CH%, CLRBOT$;FNP$('24;1');INP$;' is a duplicate key'; &
				FNP$('21;1');'COMMAND: Reenter Exit '; &
\			INP$=FNINP$(CH%,128%,' ',1%,0%) &
\			PRINT #CH%, FNP$('2;14');B.ON$;CUSTOM.NUM$;G.OFF$; &
\			GOTO 6210 IF INP$='R' &
\			RETURN &

6217	LSET CUSTOM.NUM$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6220	PRINT #CH%, FNP$('2;14');B.ON$;CUSTOM.NUM$;G.OFF$; &
\	RETURN &

6230	INP$=FNSIO$('','3;14',CUSTOM.NAME$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CUSTOM.NAME$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &

6237	LSET CUSTOM.NAME$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6240	PRINT #CH%, FNP$('3;14');B.ON$;CUSTOM.NAME$;G.OFF$; &
\	RETURN &

6250	INP$=FNSIO$('','4;14',CUSTOM.ADD1$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CUSTOM.ADD1$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &

6257	LSET CUSTOM.ADD1$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6260	PRINT #CH%, FNP$('4;14');B.ON$;CUSTOM.ADD1$;G.OFF$; &
\	RETURN &

6270	INP$=FNSIO$('','5;14',CUSTOM.ADD2$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CUSTOM.ADD2$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &

6277	LSET CUSTOM.ADD2$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6280	PRINT #CH%, FNP$('5;14');B.ON$;CUSTOM.ADD2$;G.OFF$; &
\	RETURN &

6290	INP$=FNSIO$('','6;14',CUSTOM.CITY$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CUSTOM.CITY$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &

6297	LSET CUSTOM.CITY$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6300	PRINT #CH%, FNP$('6;14');B.ON$;CUSTOM.CITY$;G.OFF$; &
\	RETURN &

6310	INP$=FNSIO$('','6;41',CUSTOM.STATE$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CUSTOM.STATE$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &

6317	RSET CUSTOM.STATE$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6320	PRINT #CH%, FNP$('6;41');B.ON$;CUSTOM.STATE$;G.OFF$; &
\	RETURN &

6330	INP$=FNSIO$('','7;14',CUSTOM.ZIP$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CUSTOM.ZIP$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &

6337	LSET CUSTOM.ZIP$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6340	PRINT #CH%, FNP$('7;14');B.ON$;CUSTOM.ZIP$;G.OFF$; &
\	RETURN &

6350	INP$=FNSIO$('','2;62',CUSTOM.SUBCOD$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CUSTOM.SUBCOD$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &

6357	LSET CUSTOM.SUBCOD$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6360	PRINT #CH%, FNP$('2;62');B.ON$;CUSTOM.SUBCOD$;G.OFF$; &
\	RETURN &

6370	INP$=FNSIO$('','8;14',FNFONE$(CUSTOM.PHONE$),DEFAULT$(LOOP%),CHR$(CH%)+ &
			CHR$(LEN(CUSTOM.PHONE$)+3%)+CHR$(LEN(CUSTOM.PHONE$))+ &
			CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &

6377	RSET CUSTOM.PHONE$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6380	PRINT #CH%, FNP$('8;14');B.ON$;FNFONE$(CUSTOM.PHONE$);G.OFF$; &
\	RETURN &

6390	INP$=FNSIO$('','10;17',CUSTOM.LOC$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CUSTOM.LOC$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &

6397	LSET CUSTOM.LOC$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6400	PRINT #CH%, FNP$('10;17');B.ON$;CUSTOM.LOC$;G.OFF$; &
\	RETURN &

6410	INP$=FNSIO$('','11;17',CUSTOM.SEQ$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CUSTOM.SEQ$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &

6417	LSET CUSTOM.SEQ$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6420	PRINT #CH%, FNP$('11;17');B.ON$;CUSTOM.SEQ$;G.OFF$; &
\	RETURN &

6430	INP$=FNSIO$('','12;17',CUSTOM.TERR$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CUSTOM.TERR$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &

6437	LSET CUSTOM.TERR$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6440	PRINT #CH%, FNP$('12;17');B.ON$;CUSTOM.TERR$;G.OFF$; &
\	RETURN &

6450	INP$=FNSIO$('','3;62',CUSTOM.TYPE$,DEFAULT$(LOOP%), &
		CHR$(CH%)+STRING$(2%,LEN(CUSTOM.TYPE$))+CHR$(1%)+CHR$(0%)+ &
		OPTION$+' ALPHA') &

6457	LSET CUSTOM.TYPE$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6460	PRINT #CH%, FNP$('3;62');B.ON$;CUSTOM.TYPE$;G.OFF$; &
\	RETURN &

6470	INP$=FNSIO$('','4;62',CUSTOM.CODE$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CUSTOM.CODE$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &

6477	LSET CUSTOM.CODE$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6480	PRINT #CH%, FNP$('4;62');B.ON$;CUSTOM.CODE$;G.OFF$; &
\	RETURN &

6490	INP$=FNSIO$('','6;62',CUSTOM.TAXFLG$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CUSTOM.TAXFLG$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &
\	GOTO 6500 IF INP$='' AND CHNG.FLAG% &
\	INP$=CVT$$(INP$,-1%) &
\	IF INP$<>'Y' &
	THEN	INP$='N' &

6497	LSET CUSTOM.TAXFLG$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6500	PRINT #CH%, FNP$('6;62');B.ON$;CUSTOM.TAXFLG$;G.OFF$; &
\	RETURN &

6510	INP$=FNSIO$('','7;62',CUSTOM.STAXNUM$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CUSTOM.STAXNUM$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &

6517	LSET CUSTOM.STAXNUM$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6520	PRINT #CH%, FNP$('7;62');B.ON$;CUSTOM.STAXNUM$;G.OFF$; &
\	RETURN &

6530	INP$=FNSIO$('','8;62',CUSTOM.STAXLOC$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CUSTOM.STAXLOC$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &

6537	LSET CUSTOM.STAXLOC$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6540	PRINT #CH%, FNP$('8;62');B.ON$;CUSTOM.STAXLOC$;G.OFF$; &
\	RETURN &

6550	INP$=FNSIO$('','10;62',CUSTOM.METHOD$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CUSTOM.METHOD$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &
\	GOTO 6560 IF INP$='' AND CHNG.FLAG% &
\	GOTO 6550 IF INP$<>'O' AND INP$<>'B' &

6557	LSET CUSTOM.METHOD$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6560	PRINT #CH%, FNP$('10;62');B.ON$;CUSTOM.METHOD$;G.OFF$; &
\	RETURN &

6570	INP$=FNSIO$('','11;62',CUSTOM.STMFLG$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CUSTOM.STMFLG$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &
\	GOTO 6580 IF INP$='' AND CHNG.FLAG% &
\	INP$='Y' IF INP$<>'N' &

6577	LSET CUSTOM.STMFLG$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6580	PRINT #CH%, FNP$('11;62');B.ON$;CUSTOM.STMFLG$;G.OFF$; &
\	RETURN &

6590	INP$=FNSIO$('','12;62',CUSTOM.SERVICE$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(CUSTOM.SERVICE$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &

6597	LSET CUSTOM.SERVICE$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6600	PRINT #CH%, FNP$('12;62');B.ON$;CUSTOM.SERVICE$;G.OFF$; &
\	RETURN &

6610	TEMP$=LEFT(CUSTOM.NAME$,LEN(CUSTOM.ALPSRT$))+'' &
\	TEMP$=DEFAULT$(LOOP%) IF DEFAULT$(LOOP%)<>'' OR OPT$<>'A' &
\	INP$=FNSIO$('','14;62',CUSTOM.ALPSRT$,TEMP$,CHR$(CH%)+ &
			STRING$(2%,LEN(CUSTOM.ALPSRT$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &

6617	LSET CUSTOM.ALPSRT$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6620	PRINT #CH%, FNP$('14;62');B.ON$;CUSTOM.ALPSRT$;G.OFF$; &
\	RETURN &

6630	INP$=FNSIO$('','15;62',CUSTOM.PURGE$,TEMP$,CHR$(CH%)+ &
			STRING$(2%,LEN(CUSTOM.PURGE$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &

6637	LSET CUSTOM.PURGE$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6640	PRINT #CH%, FNP$('15;62');B.ON$;CUSTOM.PURGE$;G.OFF$; &
\	RETURN &

17000	!-----------------------------------------------VIEW ROUTINE &
	PRINT #CH%, COLM.ON$; R.ON$;FNP$('1;1');SPACE$(132%); &
		FNP$('20;1');SPACE$(132%);FNSR$('4;19'); &
\	PRINT #CH%, FNP$(NUM1$(I%)+';1');' ';FNP$(NUM1$(I%)+';132');' '; &
		FOR I%=2% TO 19% &
\	PRINT #CH%, G.OFF$;FNP$('2;2');'Cust #';FNP$('2;9');'Name'; &
		FNP$('2;35');'Address';FNP$('2;82');'City, State'; &
		FNP$('2;109');'Zip Code'; &
\	PRINT #CH%, R.ON$;LDS.ON$;FNP$('3;2');STRING$(130%,113%); &
		FNP$('3;8');CHR$(110%);FNP$('3;34');CHR$(110%); &
		FNP$('3;81');CHR$(110%);FNP$('3;108');CHR$(110%); &
		USASCII$;G.OFF$; &
\	PRINT #CH%, R.ON$;LDS.ON$;FNP$('2;8');CHR$(120%); &
		FNP$('2;34');CHR$(120%);FNP$('2;81');CHR$(120%); &
		FNP$('2;108');CHR$(120%);USASCII$;G.OFF$; &
\	DISPLAY.LINE%=0% &
\	PRINT.LINE%=4% &

17010	V%=FNG%(-CUSTOM.CH%,NUM1$(KEY.POINTER%))+ &
		FNG%(-CUSTOM.CH%-1%,NUM1$(DATA.POINTER%)) &
\	GOTO 17105 &

17100	IF FNN%(CUSTOM.CH%) &
	THEN	17200 &

17105	LSET CUSTOM.BUF$=FNL$ &
\	PRINT #CH%, FNP$(NUM1$(PRINT.LINE%)+';2');CUSTOM.NUM$; &
		FNP$(NUM1$(PRINT.LINE%)+';9');CUSTOM.NAME$; &
		FNP$(NUM1$(PRINT.LINE%)+';35');CUSTOM.ADD1$;' '; &
				CUSTOM.ADD2$; &
		FNP$(NUM1$(PRINT.LINE%)+';82');CVT$$(CUSTOM.CITY$,140%);', '; &
				CUSTOM.STATE$;'  ';CUSTOM.COUNTRY$; &
		FNP$(NUM1$(PRINT.LINE%)+';109');CUSTOM.ZIP$; &
\	I%=PRINT.LINE% &
\	PRINT #CH%, R.ON$;LDS.ON$; &
		FNP$(NUM1$(I%)+';8');CHR$(120%); &
		FNP$(NUM1$(I%)+';34');CHR$(120%); &
		FNP$(NUM1$(I%)+';81');CHR$(120%); &
		FNP$(NUM1$(I%)+';108');CHR$(120%);USASCII$;G.OFF$; &
\	PRINT.LINE%=PRINT.LINE%+1% IF PRINT.LINE%<19% &

17110	IF DISPLAY.LINE%>14% &
	THEN	PRINT #CH%, FNP$('24;105');'Hit any key to continue '; &
\		INP$=FNINP$(CH%,128%,' ',1%,0%) &
\		GOTO 17210 IF INP$='%^C' &
\		DISPLAY.LINE%=0% &
\		PRINT #CH%, CLRBOT$;FNP$(NUM1$(PRINT.LINE%)+';1'); &

17120	PRINT #CH% &
\	DISPLAY.LINE%=DISPLAY.LINE%+1% &
\	IF PRINT.LINE%=19% &
	THEN	PRINT #CH%, FNP$(NUM1$(PRINT.LINE%)+';1');R.ON$;' '; &
			FNP$(NUM1$(PRINT.LINE%)+';132');' ';G.OFF$; &

17190	GOTO 17100 &

17200	IF DISPLAY.LINE%>0% &
	THEN	PRINT #CH%, FNP$('24;1');'End of file.  '; &
			FNP$('24;105');'Hit any key to continue '; &
\		INP$=FNINP$(CH%,128%,' ',1%,0%) &

17210	V%=FNG%(-CUSTOM.CH%,NUM1$(KEY.POINTER%))+ &
		FNG%(-CUSTOM.CH%-1%,NUM1$(DATA.POINTER%)) &
\	PRINT #CH%, FNP$('1;1');FNSR$('1;24');COLM.OFF$; &
\	GOTO 1010 &

18910	!----------------------------------------------END OF FILE &
	PRINT #CH%, CLRBOT$;FNP$('24;1');'End of file has been reached.'; &
		FNP$('24;55');'Hit any key to continue '; &
\	INP$=FNINP$(CH%,128%,' ',1%,0%) &
\	GOTO 1000 &

19000	!------------------------------------------------ERROR TRAPPING &
	RESUME IF ERR=52% OR ERR=51% &
\	RESUME 450 IF ERL=400 &
\	RESUME 1010 IF ERR=54% &

19999	ON ERROR GOTO 0 &

30000	!----------------------PROGRAM FUNCTIONS------------------------ &
	!------------------------------------------------TERMINAL INPUT &
	DEF FNINP$(CHN%,KYP%,FILLCHAR$,INPUTLEN%,TO.ERR%) &
\		PRINT #CHN%, STRING$(INPUTLEN%,ASCII(FILLCHAR$)); &
			  STRING$(INPUTLEN%,8%); &
\		PRINT #CHN%, RECORD 256%,CHR$(KYP%+INPUTLEN%)+FILLCHAR$; &
\		GET #CHN% &
\		FIELD #CHN%, RECOUNT AS BUFFER$ &
\		BUFFER$='%^C' IF INSTR(1%,BUFFER$,CHR$(3%)) &
\		FNINP$=CVT$$(BUFFER$,4%) &
\		V=SQR(-1) IF BUFFER$='%^C' AND TO.ERR% !^C Trappping &
\	FNEND &

30200	DEF*FNP$(ROWCOL$)=ESC$+'['+ROWCOL$+'H' 	! Direct Cursor Address &

30250	!-----------------------------------------------MESSAGE HANDLER &
	DEF*FNMESS$(CHN%,ERRNUM%,DESC$,TO.ERR%,PRINT.TEST%) &
\		MESG$=MID(SYS(CHR$(6%)+CHR$(9%)+CHR$(ERRNUM%)),3%,30%)+ &
			' '+DESC$ &
\		IF PRINT.TEST% &
		THEN	PRINT #CHN%, FNP$('24;1');CLRLIN$;MESG$; &
				FNP$('24;55'); &
				'Hit any key to continue.'; &
\			NW$=FNINP$(CHN%,128%,' ',1%,TO.ERR%) &

30260		FNMESS$=MESG$ &
\	FNEND &

30300	DEF*FNSR$(BEGEND$)=ESC$+'['+BEGEND$+'r'	! Scroll control &

30400	DEF FNFONE$(PHN$)='('+LEFT(PHN$,3%)+')'+MID(PHN$,4%,3%)+'-' &
		+RIGHT(PHN$,7%) &

30500	DEF FNZ(Y) &
\		TEMP=Y &
\		TEMP=-Y IF Y<0. &
\		TEMP=INT(TEMP*100.+.51)/100. &
\		TEMP=-TEMP IF Y<0. &
\		FNZ=TEMP &
\	FNEND &

30700	!========================================= DATA INPUT FUNCTIONS &
	DEF*FNSIO$(FRMAT$,ROWCOL$,VARABLE$,DEFAL$,ITEMS$) &
\		CHN%=ASCII(MID(ITEMS$,1%,1%)) &
\		KIND%=ASCII(MID(ITEMS$,4%,1%)) &
\		DECI=ASCII(MID(ITEMS$,5%,1%)) &
\		OPT$=MID(ITEMS$,6%,1%) &

30705		PRINT #CHN%, FNP$(ROWCOL$);R.ON$;B.ON$; &
\		VARABLE$=DEFAL$ IF LEFT(OPT$,1%)='A' &
\		IF OPT$='C' OR DEFAL$<>'' AND LEFT(OPT$,1%)='A' &
		THEN	ON KIND% GOTO 30710, 30715, 30720, 30725, 30730 &
		ELSE	PRINT #CHN%, SPACE$(ASCII(MID(ITEMS$,2%,1%))); &
\			GOTO 30735 &

30710		PRINT #CHN%, VARABLE$;			   ! ALPHA &
\		GOTO 30735 &

30715		PRINT #CHN% USING FRMAT$,ASCII(VARABLE$);  ! ASCII &
\		GOTO 30735 &

30720		PRINT #CHN% USING FRMAT$, CVT$%(VARABLE$)/(10.**DECI); &
\		GOTO 30735				   ! INTEGER &

30725		GOTO 30735 &

30730		PRINT #CHN% USING FRMAT$,CVT$F(VARABLE$);  ! FLOAT &

30735		PRINT #CHN%,G.OFF$;CLRBOT$;RIGHT(ITEMS$,6%)+':  ';B.ON$; &
\		FIN$ = FNINP$(CHN%,0%,'_',ASCII(MID(ITEMS$,3%,1%)),1%) &
\		V% = VAL(FIN$) IF KIND%=3% &
\		V  = VAL(FIN$) IF KIND%>3% &
\		GOTO 30705 IF KIND%=4% AND LEN(FIN$)<>6% AND FIN$<>'' &
\		IF FIN$='' &
		THEN	FIN$ = DEFAL$ IF INSTR(1%,'CF',OPT$)=0% &
\			GOTO 30745 &

30740		IF KIND%=3% OR KIND%=5% &
		THEN	TEMP = 1. &
\			TEMP = 10.**DECI IF INSTR(1%,FIN$,'.')=0% &
\			FIN$ = NUM1$(VAL(FIN$)/TEMP) &
\			FIN$ = FIN$ + '.' IF INSTR(1%,FIN$,'.')=0% &

30745		FNSIO$ = FIN$+'' &
\	FNEND &

32767	END &

