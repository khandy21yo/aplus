10	!--------------------------------------------------------------- &
	!	SYSTEM NAME		-Sub System &
	!	Program Description Name &
	! &
	! &
	! Author - Author's name , Computer Management Center, Inc. &
	! &
	!--------------------------------------------------------------- &
	!		      Copyright (c) 1984 by &
	!	  Computer Management Center, Idaho Falls, Idaho &
	! &
	! This software is furnished under a license and may be used &
	! and copied only in accordance with the terms of such license &
	! and with the inclusion of the above copyright notice.  This &
	! software or any other copies therof may not be provided or &
	! otherwise made available to any other person.  No title to &
	! and ownership of the software is hereby transferred. &
	!      The information in this software is subject to change &
	! without notice and should not be construed as a committment &
	! by Computer Management Center. &
	!      CMC assumes no responsibility for the use or reliability &
	! of its software on equipment which is not supported by CMC. &
	!--------------------------------------------------------------- &

30	ON ERROR GOTO 19000 &
\	JUNK$	= SYS(CHR$(6%)+CHR$(-7%)) 		!^C ERROR TRAPPING &
	! &
\	TEMP$	= SYS(CHR$(12%)) 			!LAST FILE OPENED &
\	PRJPRG$	= MID(TEMP$,23%,2%)+NUM1$(ASCII(MID(TEMP$,25%,1%))) + ':'+ &
		'['+NUM1$(ASCII(MID(TEMP$,6%,1%)))+','+NUM1$(ASCII(MID(TEMP$, &
		5%,1%)))+']'+RAD$(ASCII(MID(TEMP$,7%,1%))+SWAP%(ASCII(MID( &
		TEMP$,8%,1%))))+RAD$(ASCII(MID(TEMP$,9%,1%))+SWAP%(ASCII(MID( &
		TEMP$,10%,1%)))) &
	! &
\	TEMP$	= SYS(CHR$(6%)+CHR$(9%)) 		!GET JOB NUMBER &
\	JJ%	= ASCII(LEFT(TEMP$,1%))/2% &
\	JJ$	= RIGHT(NUM1$(JJ%+100%),2%) &

60	CH%,OUTPUT.CH% = 1%			! Keybrd,Output channel &
\	OPEN 'KB:' AS FILE 1%, MODE 8%+256% 	! Echo,Disbl Hibrn,%^C &

70	ESC$		= CHR$(155%)		! Escape code for VT100 &
\	COLM.ON$	= ESC$+'[?3h'		! 132 Column mode &
\	COLM.OFF$	= ESC$+'[?3l'		! 80 Column mode &
\	R.ON$		= ESC$+'[7m'      	! Reverse video &
\	G.OFF$		= ESC$+'[m'		! Select graphic off &
\	CLRLIN$		= ESC$+'[2K'		! Erase entire line &
\	CLSCN$		= ESC$+'[H'+ESC$+'[J'	! Clear entire screen &
\	ENTER.COPY$	= ESC$+'[5i'		! Enter media copy &
\	EXIT.COPY$	= ESC$+'[4i'		! Exit media copy &
\	HID.CURSOR$	= ESC$+"[?25l"+ESC$+"[24;1h"	! Hide cursor &
\	CUR.ON$		= ESC$+"[?25h"		! Cursor on &

80	!COM(THIS) DROP.DEAD.DATE$ = 8, VERSION.NO$ = 6, SERIAL.NO$ = 10 &
	DROP.DEAD.DATE$	= '        ' &
\	VERSION.NO$	= 'V1.0' &
\	DATE.TIME$	= SYS(CHR$(6%)+CHR$(20%)+CHR$(0%)+CHR$(0%)+CHR$(1%)+ &
				CHR$(SWAP%(1%))+STRING$(11%,0%)+ &
				CHR$(SWAP%(0%))+CHR$(1%)+CHR$(SWAP%(1%))) &
\	IF 	DROP.DEAD.DATE$<>'' &
	THEN	IF 	DROP.DEAD.DATE$<MID(DATE.TIME$,7%,8%) &
		THEN	MESG$	= MID(SYS(CHR$(6%)+CHR$(9%)+CHR$(47%)),3%,30%) &
\			PRINT #CH%, MESG$;SYS(CHR$(9%)) &

100	DIM TAX.RATE(40%), PRDNUM$(40%) &
\	DEVICE.CH%,PRNT.TMP%	= 02% &
\	ETXCRX.CH%		= 02% &
\	ETXMNT.CH%		= 02% &
\	INVACT.CH%		= 04% &
\	INVDES.CH%		= 06% &
\	CUSTOM.CH%		= 08% &
\	TEMPJJ.CH%		= 10% &
\	MENU.CH%		= 12% &
\	PRINT.WIDTH%		= 132% &
\	WIDTH%			= PRINT.WIDTH%-32% &

120	!---------------------------------------OPEN THE ASCII PRINT FILE &
	OPEN 'PRNT'+JJ$+'.TMP' FOR INPUT AS FILE PRNT.TMP% &
\	NEXT.REPORT%	= 0% &
\	CHAIN.LINE%	= 0% &
\	CHAIN.PROGRAM$	= '' &

130	INPUT LINE #PRNT.TMP%, TEMP$ &
\	PR$	= LEFT(TEMP$,2%) &
\	CMD$	= CVT$$(RIGHT(TEMP$,4%),140%) &
\	IF	PR$='RN'	THEN	GOTO 160 IF NEXT.REPORT% &
\					NEXT.REPORT% = -1% &

140	LIN.PER.PAGE%	= VAL(CMD$)	IF PR$ = 'LP' &
\	SPAGE%		= VAL(CMD$)	IF PR$ = 'SP' &
\	EPAGE%		= VAL(CMD$)	IF PR$ = 'EP' &
\	COPIES%		= VAL(CMD$)	IF PR$ = 'CP' &
\	REPDATE$	= CMD$		IF PR$ = 'RD' &
\	DISPLAY$	= CMD$		IF PR$ = 'DP' &
\	AUTOSCROLL$	= CMD$		IF PR$ = 'AS' &
\	SPOOLR$		= CMD$		IF PR$ = 'SL' &
\	OUTDEV$		= CMD$		IF PR$ = 'OD' &
\	MS$		= CMD$		IF PR$ = 'MS' &
\	UD1$		= CMD$		IF PR$ = 'U1' &
\	UD2$		= CMD$		IF PR$ = 'U2' &
\	UD3$		= CMD$		IF PR$ = 'U3' &
\	UD4$		= CMD$		IF PR$ = 'U4' &
\	UD5$		= CMD$		IF PR$ = 'U5' &
\	IF	PR$ = 'PC' &
	THEN	TEMP%	= 1% &
\		TEMP%	= 2% IF LEFT(CMD$,1%) = '\' &
\		PC$	= PC$ + CHR$(VAL(MID(CMD$,TEMP%+(LOOP%-1%),3%))) &
				FOR LOOP%=1% TO LEN(CMD$) STEP 4% &

150	GOTO 130 &

160	GOTO 190 IF CHAIN.FLAG%=0% &
\	KILL 'PRNT'+JJ$+'.TMP' &
\	GOTO 190 IF PR$<>'RN' &

170	OPEN 'PRNT'+JJ$+'.TMP' FOR OUTPUT AS FILE PRNT.TMP% + 1% &
\	CHAIN.LINE%	= 0% &

180	PRINT #PRNT.TMP% + 1%, TEMP$; ! Dont forget this semicolon &
\	INPUT LINE #PRNT.TMP%, TEMP$ &
\	CHAIN.PROGRAM$	= CVT$$(RIGHT(TEMP$,4%),140%) IF LEFT(TEMP$,2%)='PG' &
		AND CHAIN.PROGRAM$='' &
\	GOTO 180 &
	&
\			FRM.LEN%=VAL(XLATE(FRM$(I6%),STRING$(48%,0%)+ &
				"0123456789"))
190	CLOSE PRNT.TMP%, PRNT.TMP% + 1% &
\	FRM.DAT% = FND6%(XLATE(LEFT(UD1$,6%),STRING$(48%,0%)+'0123456789')) &
\	TO.DAT% = FND6%(XLATE(RIGHT(UD1$,8%),STRING$(48%,0%)+'0123456789')) &
\	MON.NAM$='  JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC' &
\	TEMP$ = FND6$(FRM.DAT%) &
\	YEAR$ = RIGHT(TEMP$,7%) &
\	MONTH% = VAL(LEFT(TEMP$,2%)) &
\	MONTH1$ = MID(MON.NAM$,MONTH%*3%,3%) &
\	TEMP$ = FND6$(TO.DAT%) &
\	YEAR$ = RIGHT(TEMP$,7%) &
\	MONTH% = VAL(LEFT(TEMP$,2%)) &
\	MONTH2$ = MID(MON.NAM$,MONTH%*3%,3%) &
\	TAXCAT$ = UD2$ &
\	TAXABLE$ = UD3$ &
\	EXEMPT$ = UD4$ &
\	REP.DESC$ = UD5$ &

200	IF 	FNO%(DEVICE.CH%,'DEVICE.DAT','/RO','')=0% &
	THEN	ETXMNT.DEVICE$	= MID(FNL$,7%,20%)+'' &
			IF FNG%(DEVICE.CH%,'ETXMNT')=0% &
\		ETXCRX.DEVICE$	= MID(FNL$,7%,20%)+'' &
			IF FNG%(DEVICE.CH%,'ETXCRX')=0% &
\		INVACT.DEVICE$	= MID(FNL$,7%,20%)+'' &
			IF FNG%(DEVICE.CH%,'INVACT')=0% &
\		INVDES.DEVICE$	= MID(FNL$,7%,20%)+'' &
			IF FNG%(DEVICE.CH%,'INVDES')=0% &
\		CUSTOM.DEVICE$	= MID(FNL$,7%,20%)+'' &
			IF FNG%(DEVICE.CH%,'CUSTOM')=0% &
\		VENDES.DEVICE$	= MID(FNL$,7%,20%)+'' &
			IF FNG%(DEVICE.CH%,'VENDES')=0% &
\		V%	= FNC%(DEVICE.CH%) &

210	ETX.LOOP% = 0% &
\	IF FNO%(ETXCRX.CH%,ETXCRX.DEVICE$+'ETXCRX.DAT','/RO','') &
			OR FNG%(ETXCRX.CH%,'') &
	THEN	V$=FNMESS$(CH%,FNS%,'ETXCRX.DAT',0%,-1%) &
\		V% = FNX%('',0%,'') &

215	FIELD #ETXCRX.CH%+1%, FNL% AS TEMP$, &
			01% AS ETXCRX.TAXCAT$, &
			01% AS ETXCRX.TAX.TYPE$, &
			15% AS ETXCRX.PRDNUM$, &
			06% AS ETXCRX.PRDCAT$, &
			08% AS ETXCRX.RATE$, &
			08% AS ETXCRX.LIAACC$ &
\	IF ETXCRX.TAXCAT$ = TAXCAT$ AND ETXCRX.TAX.TYPE$ = TAXABLE$ &
		OR ETXCRX.TAXCAT$ = TAXCAT$ AND ETXCRX.TAX.TYPE$ = EXEMPT$ &
	THEN	ETX.LOOP% = ETX.LOOP% + 1% &
\		TAX.RATE(ETX.LOOP%) = CVT$F(ETXCRX.RATE$) &
\		PRDNUM$(ETX.LOOP%) = ETXCRX.PRDNUM$ + '' &

220	GOTO 215 IF FNN%(ETXCRX.CH%)=0% &
\	V% = FNC%(ETXCRX.CH%) &
\	GOTO 300 IF CHAIN.FLAG% &
\	OPEN 'TEMP'+JJ$+'.TMP' FOR OUTPUT AS FILE TEMPJJ.CH% &
\	DIM #10%, TEMPJJ$(32767%)=64% &
\	RECORD.COUNT%=0% &
\	MONTH$ = MONTH1$ &

222	IF FNO%(ETXMNT.CH%,ETXMNT.DEVICE$+'ETX'+MONTH$+'.'+YEAR$+'S', &
		'/SF/RO','') &
	THEN	GOTO 250 &

225	GOTO 250 IF FNG%(ETXMNT.CH%,'') &

230	FIELD #ETXMNT.CH%, FNL% AS TEMP$, &
			01% AS ETXMNT.TAXCAT$, &
			01% AS ETXMNT.TAXTYP$, &
			15% AS ETXMNT.PRONUM$, &
			06% AS ETXMNT.CUSVEN$, &
			08% AS ETXMNT.QTY$, &
			02% AS ETXMNT.DATE$ &
\	IF ETXMNT.TAXCAT$ = TAXCAT$ AND ETXMNT.TAXTYP$ = TAXABLE$ &
		OR ETXMNT.TAXCAT$ = TAXCAT$ AND ETXMNT.TAXTYP$ = EXEMPT$ &
	THEN	IF CVT$%(ETXMNT.DATE$)>=FRM.DAT% AND &
			TO.DAT%>=CVT$%(ETXMNT.DATE$) &
		THEN	RECORD.COUNT% = RECORD.COUNT% + 1% &
\			TEMPJJ$(RECORD.COUNT%) = &
				ETXMNT.PRONUM$ + &
				ETXMNT.CUSVEN$+ &
				ETXMNT.TAXTYP$+ &
				ETXMNT.QTY$+ &
				ETXMNT.DATE$ &

240	GOTO 230 IF FNN%(ETXMNT.CH%)=0% &

250	V%=FNO%(INVACT.CH%,INVACT.DEVICE$+"ACT"+MONTH$+"."+YEAR$+"T","/RO","") &
\	V%=FNO%(INVACT.CH%,INVACT.DEVICE$+"ACT"+MONTH$+".DAT","/RO" &
		,"") IF FNS%=5% &
\	IF FNS% &
	THEN	290 &

260	FOR LOOP% = 1% TO ETX.LOOP% &
\		IF FNG%(INVACT.CH%,PRDNUM$(LOOP%)) &
		THEN	280 &

265		FIELD #INVACT.CH%+1%, FNL% AS TEMP$, &
				15% AS INVACT.INVNUM$, &
				03% AS INVACT.STORNUM$, &
				08% AS INVACT.PRIMREF$, &
				08% AS INVACT.SECREF$, &
				06% AS INVACT.XREF$, &
				06% AS INVACT.SALESMAN$, &
				08% AS INVACT.INVDOL$, &
				02% AS INVACT.DATE$, &
				01% AS INVACT.TRANSA$, &
				08% AS INVACT.QTYA$, &
				01% AS INVACT.TRANSB$, &
				08% AS INVACT.QTYB$, &
				01% AS INVACT.UPDTFLG$, &
				08% AS INVACT.SALDOL$ &
\		GOTO 280 IF INVACT.INVNUM$<>PRDNUM$(LOOP%) &
\		IF INSTR(1%,'SR',INVACT.TRANSA$) OR &
			INSTR(1%,'SR',INVACT.TRANSB$) &
		THEN	IF CVT$%(INVACT.DATE$)>=FRM.DAT% AND &
				TO.DAT%>=CVT$%(INVACT.DATE$) &
			THEN	RECORD.COUNT% = RECORD.COUNT% + 1% &
\				QTY$ = INVACT.QTYA$+'' IF &
					INSTR(1%,'SR',INVACT.TRANSA$) &
					AND CVT$$(INVACT.TRANSA$,-1%)<>'' &
\				QTY$ = INVACT.QTYB$+'' IF &
					INSTR(1%,'SR',INVACT.TRANSB$) &
					AND CVT$$(INVACT.TRANSB$,-1%)<>'' &
\				TEMPJJ$(RECORD.COUNT%) = &
					INVACT.INVNUM$ + &
					INVACT.XREF$+ &
					' '+ &
					QTY$+ &
					INVACT.DATE$+'' &

270		GOTO 265 IF FNN%(INVACT.CH%)=0% &

280	NEXT LOOP% &

285	V% = FNC%(INVACT.CH%) + FNC%(ETXMNT.CH%) &
\	IF MONTH1$<>MONTH2$ &
	THEN	MONTH$,MONTH1$ = MONTH2$ &
\		GOTO 222 &

290	TEMPJJ$(0%)=CVT%$(RECORD.COUNT%)+CVT%$(64%)+'U'+CHR$(128%) &
\	CLOSE TEMPJJ.CH% &
\	Q0$=SYS(CHR$(7%)) &
\	Q5%=INSTR(1%,Q0$,CHR$(255%)) &
\	Q2%=INSTR(1%,Q0$,CHR$(14%)) &
\	Q2%=Q5%+12% IF Q2%=0% &
\	Q0$=MID(Q0$+SPACE$(12%),Q5%,Q2%-Q5%) IF Q5% &
\	Q0$="" IF Q5%=0% &
\	Q3$=CVT%$(30999%)+PRJPRG$ &
\	Q3$=Q3$+"  " &
\	Q9$=CHR$(13%)+ED.DEVICE$+'TEMP'+JJ$+'.TMP'+CHR$(13%)+CHR$(13%)+ &
		Q0$+CHR$(14%) &
\	Q$=LEFT(CHR$(LEN(Q3$))+Q3$+Q9$,127%) &
\	Q$=SYS(CHR$(8%)+Q$) &
\	CHAIN "ISM:FSTSRS.TSK" &

300	IF   	FNO%(TEMPJJ.CH%,ETXMNT.DEVICE$+'TEMP'+JJ$+'.TMP','/SF/RO','') &
	THEN	V$	= FNMESS$(CH%,FNS%,' TEMPJJ.TMP ',0%,-1%) &
\		GOTO 17540 &

310	INVDES.FLAG% = FNO%(INVDES.CH%,INVDES.DEVICE$+'INVDES.DAT','/RO','') &
\	CUSTOM.FLAG% = FNO%(CUSTOM.CH%,CUSTOM.DEVICE$+'CUSTOM.DAT','/RO','') &

400	OPEN 'MENU.FIL/RO' FOR INPUT AS FILE MENU.CH% &
\	DIM #12%, A0$(1%)=64% &
\	COMPANY$	= CVT$$(RIGHT(A0$(1%),2%),128%) &
\	CLOSE MENU.CH% &

510	COLM.ON$	= '' IF PRINT.WIDTH% = 80% &
\	REPDATE$	= 'FROM '+FND6$(FRM.DAT%)+' TO '+FND6$(TO.DAT%) &
\	TITLE$		= 'FUEL TAXES SUMMARY' &
\	TITLE$		= LEFT(SPACE$((WIDTH%-LEN(TITLE$))/2%)+ &
				TITLE$+SPACE$(WIDTH%),WIDTH%) &
\	COMPANY$	= CVT$$(LEFT(COMPANY$,WIDTH%),128%) &
\	COMPANY$	= LEFT(SPACE$((WIDTH%-LEN(COMPANY$))/2%)+ &
				COMPANY$+SPACE$(WIDTH%),WIDTH%) &
\	PAGE.TOP$	= STRING$(2%,10%)+CHR$(13%)+'Date: '+DATE$(0%)+ &
				' '+COMPANY$+'        Page <<###>>'+ &
				CHR$(10%)+CHR$(13%)+'Time: '+TIME$(0%)+ &
				'  '+TITLE$+'        '+VERSION.NO$+ &
				CHR$(10%)+CHR$(13%)+ &
				SPACE$((PRINT.WIDTH%-LEN(REPDATE$))/2%)+ &
				REPDATE$+STRING$(2%,10%)+CHR$(13%)+ &
				REP.DESC$ +CHR$(10%)+CHR$(13%)+ &
				STRING$(PRINT.WIDTH%,61%)+ &
				CHR$(10%)+CHR$(13%) &
\	PAGE.TOP1$	= 'PRODUCT #        DESCRIPTION'+ &
			'              CUS/VEN NAME              '+ &
			'      TAXABLE       EXEMPT        TOTAL'+ &
				CHR$(10%)+CHR$(13%) &
\	USE1$		= '\             \  \          '+ &
			'           \  \    \  \                \'+ &
			' #,###,###.## #,###,###.## #,###,###.##' &
\	BOT.MARGIN%	= 6% &
\	PAGE.BOT$	= STRING$(BOT.MARGIN%,10%)+CHR$(13%) &
\	PAGE.BREAK%	= LIN.PER.PAGE% - BOT.MARGIN% &
\	MORE.LINES%	= 2% &
\	DISPLAY.CONTROL%= 15% &
\	IF	FROM.ITEM$<>'ALL' 	THEN	START.TEMP$=FROM.ITEM$ &
					ELSE	START.TEMP$='' &

10000	IF 	DISPLAY$<>'Y' &
	THEN	CLOSE CH% &
\		RESET.CH%	= 1% &
\		TEMP.CH%	= 12% &
\		OPEN OUTDEV$ AS FILE OUTPUT.CH%, MODE 2% &
\		IF 	SPAGE%>1%	THEN	OPEN 'NL:' AS FILE TEMP.CH% &
\						OUTPUT.CH%=TEMP.CH% &

17000	!---------------------------------------OUTPUT REPORT &
	V%	= FNG%(TEMPJJ.CH%,START.TEMP$) &
\	IF 	DISPLAY$='Y' &
	THEN	PRINT #OUTPUT.CH%, CLSCN$;R.ON$;COLM.ON$;PAGE.TOP1$;G.OFF$; &
			FNSR$('2;19');FNP$('19;01'); &
	ELSE	PRINT #OUTPUT.CH%, ENTER.COPY$; &
			FNPAGE$(LIN.PER.PAGE%,0%,1%,PAGE.TOP$+PAGE.TOP1$,''); &
\		LINE.COUNT%=FNLINE% &

17030	FIELD #TEMPJJ.CH%, FNL% AS JUNK$, &
			15% AS TEMPJJ.PRONUM$, &
			06% AS TEMPJJ.XREF$, &
			01% AS TEMPJJ.TAXTYP$, &
			08% AS TEMPJJ.QTY$, &
			02% AS TEMPJJ.DATE$ &
\	GOSUB 18000 IF TEMPJJ.PRONUM$ + TEMPJJ.XREF$ <> TEST.PRONUM$ + TEST.XREF$ &
		AND TEST.PRONUM$ + TEST.XREF$ <> '' &
\	GOSUB 18100 IF TEMPJJ.PRONUM$ <> TEST.PRONUM$ AND TEST.PRONUM$<>'' &
\	GOTO 17400 IF END.FLAG% &
\	TEST.PRONUM$ = TEMPJJ.PRONUM$ + '' &
\	TEST.XREF$ = TEMPJJ.XREF$ + '' &
\	QTY1 = FNZ(QTY1 - CVT$F(TEMPJJ.QTY$)) IF TEMPJJ.TAXTYP$ <> '' &
\	QTY2 = FNZ(QTY2 + CVT$F(TEMPJJ.QTY$)) IF TEMPJJ.TAXTYP$ <> '' &
\	QTY2 = FNZ(QTY2 - CVT$F(TEMPJJ.QTY$)) IF TEMPJJ.TAXTYP$ = '' &

17350	GOTO 17030 IF FNN%(TEMPJJ.CH%)=0% &

17400	IF 	END.FLAG%=0% &
	THEN	GOSUB 18000 &
\		GOSUB 18100 &

17500	IF 	DISPLAY$<>'Y' &
	THEN	PRINT #OUTPUT.CH%, FNPAGE$(LIN.PER.PAGE%,LINE.COUNT%,FNPAGE%, &
			'',PAGE.BOT$); &
	ELSE	V$	= FNMESS$(OUTPUT.CH%,0%,'End of report',0%,-1%) &

17540	KILL EXTMNT.DEVICE$+'TEMP'+JJ$+'.TMP' &

17550	PRINT #OUTPUT.CH%, EXIT.COPY$; IF DISPLAY$<>'Y' &
\	PRINT #OUTPUT.CH%, FNSR$('1;24');COLM.OFF$; IF DISPLAY$='Y' &
\	CLOSE OUTPUT.CH% &
\	V%	= FNX%(CHAIN.PROGRAM$,CHAIN.LINE%,'') &

17600	!---------------------------------------PAGINATION SUBROUTINE &
	IF 	DISPLAY$<>'Y' AND LINE.COUNT%+MORE.LINES%>PAGE.BREAK% &
	THEN	END.FLAG% = -1% IF PAGE%>=EPAGE% AND EPAGE%<>0% &
\		RETURN IF END.FLAG% &
\		PRINT #OUTPUT.CH%, FNPAGE$(LIN.PER.PAGE%,LINE.COUNT%, &
			FNPAGE%,PAGE.TOP$+PAGE.TOP1$,PAGE.BOT$); &
\		LINE.COUNT%	= FNLINE% &
\		IF 	SPAGE%>1% &
		THEN	IF 	SPAGE%<=PAGE% &
			THEN	OUTPUT.CH% = RESET.CH% &
\				PRINT #OUTPUT.CH%, ENTER.COPY$; &
					FNPAGE$(LIN.PER.PAGE%,0%, &
					PAGE%,PAGE.TOP$+PAGE.TOP1$,''); &
\				LINE.COUNT% = FNLINE% &
\				SPAGE% = 0% &

17650	IF 	DISPLAY$='Y' AND LINE.COUNT%>DISPLAY.CONTROL% &
	THEN	V$	= FNMESS$(OUTPUT.CH%,0%,'',-1%,-1%) &
\		LINE.COUNT%	= 0% &
\		PRINT #OUTPUT.CH%, FNP$('24;1');CLRLIN$;FNP$('18;132') &

17670	RETURN &

18000	!--------------------------------------------------------------- &
	! PRINT LINE &
	!--------------------------------------------------------------- &
	TAXABLE, EXEMPT = 0.0 &
\	TAXABLE = QTY1 IF TAXABLE$ <> '' &
\	TAXABLE = QTY2 IF TAXABLE$ = '' &
\	EXEMPT = QTY1 IF EXEMPT$ <> '' &
\	EXEMPT = QTY2 IF EXEMPT$ = '' &
\	PRODESC$ = '** UNDEFINED **' &
\	PRODESC$ = MID(FNL$,16%,30%)+'' IF FNG%(INVDES.CH%,TEST.PRONUM$)=0% &
\	XREFDESC$ = '** UNDEFINED **' &
\	XREFDESC$ = MID(FNL$,7%,25%)+'' IF FNG%(CUSTOM.CH%,TEST.XREF$)=0% &
\	PRINT #OUTPUT.CH% USING USE1$, &
		TEST.PRONUM$, &
		PRODESC$, &
		TEST.XREF$, &
		XREFDESC$, &
		TAXABLE, &
		EXEMPT, &
		TAXABLE+EXEMPT &
\	TOTAL.TAXABLE = TOTAL.TAXABLE + TAXABLE &
\	TOTAL.EXEMPT = TOTAL.EXEMPT + EXEMPT &
\	QTY1, QTY2 = 0.0 &
\	LINE.COUNT%	= LINE.COUNT% + 1% &
\	GOSUB 17600 &

18090	RETURN &

18100	!--------------------------------------------------------------- &
	! PRINT TOTAL BY PRODUCT &
	!--------------------------------------------------------------- &
	PRINT #OUTPUT.CH% USING USE1$, &
		'', &
		'', &
		'', &
		'PRODUCT TOTAL', &
		TOTAL.TAXABLE, &
		TOTAL.EXEMPT, &
		TOTAL.TAXABLE+TOTAL.EXEMPT &
\	TOTAL.TAXABLE, TOTAL.EXEMPT = 0.0 &
\	PRINT #OUTPUT.CH% &
\	LINE.COUNT% = LINE.COUNT% + 2% &

18190	RETURN &

18900	! SEND MESSAGE IF DETACHED KEYBOARD &

19000	!---------------------------------------ERROR TRAPPING &
	RESUME 17500	IF ERR=11% AND ERL=17030% &
\	RESUME 510	IF ERL=400% &
\	RESUME 17540	IF ERR=54% &
\	RESUME 17550	IF ERL=17540 &
\	RESUME 160	IF ERL=130% &
\	RESUME 190	IF ERL=160% OR ERL=180% &
\	IF 	ERR=28%	THEN	JUNK$ = SYS(CHR$(6%) + CHR$(-7%)) &
\				RESUME 17500	IF OUTPUT.CH%=CH% &

19010	IF 	ERR=27%	THEN	RESUME 70	IF ERL=60% &
\				RESUME 18900 &

19020	IF 	ERL=120% &
	THEN	V$=FNMESS$(CH%,0%,'Missing print control file.  '+ &
			'Aborting',0%,-1%) &
\		RESUME 17500 &

19900	ON ERROR GOTO 0 &

30000	!---------------------------------------TERMINAL INPUT &
	DEF FNINP$(CHN%,KYP%,FILLCHAR$,INPUTLEN%,TO.ERR%) &
\		PRINT #CHN%, STRING$(INPUTLEN%,ASCII(FILLCHAR$)); &
			  STRING$(INPUTLEN%,8%); &
\		PRINT #CHN%, RECORD 256%,CHR$(KYP%+INPUTLEN%)+FILLCHAR$; &
\		GET #CHN% &
\		FIELD #CHN%, RECOUNT AS BUFFER$ &
\		BUFFER$	= '%^C' IF INSTR(1%,BUFFER$,CHR$(3%)) &
\		FNINP$	= CVT$$(BUFFER$,4%) &
\		V=SQR(-1) IF BUFFER$='%^C' AND TO.ERR% !^C Trappping &
\	FNEND &

30200	DEF*FNP$(ROWCOL$)=ESC$+'['+ROWCOL$+'H'	!DIRECT CURSOR ADDRESS &

30250	!---------------------------------------MESSAGE HANDLER &
	DEF*FNMESS$(CHN%,ERRNUM%,DESC$,TO.ERR%,PRINT.TEST%) &
\		MESG$	= MID(SYS(CHR$(6%)+CHR$(9%)+CHR$(ERRNUM%)), &
			3%,30%)+DESC$ &
\		MESG$	= DESC$+'' IF ERRNUM%<1% &
\		IF 	PRINT.TEST% &
		THEN	PRINT #CHN%, FNP$('24;1');CLRLIN$;MESG$; &
				FNP$('24;'+NUM1$(PRINT.WIDTH%-25%))+ &
				'Hit any key to continue'; &
\			NW$=FNINP$(CHN%,128%,' ',1%,TO.ERR%) &
\			PRINT #CHN%, CLRBOT$; &

30260	FNMESS$=MESG$ &
\	FNEND &

30280	DEF*FNSR$(BEGEND$)=ESC$+'['+BEGEND$+'r'	!SCROLL CONTROL &

30400	!---------------------------------------DATE HANDLERS &
	DEF FND8%(D8)=D8 &

30410	DEF FND6%(D9$)=VAL(MID(D9$,3%,2%))+VAL(LEFT(D9$,2%))*32% &
		+ FND8%(VAL(RIGHT(D9$,5%)))*512% &

30420	DEF FND6$(D9%)=RIGHT(NUM1$((D9% AND 15%*32%)/32%+100%),2%) &
		+ '/'+RIGHT(NUM1$((D9% AND 31%)+100%),2%) &
		+ '/'+RIGHT(NUM1$(((SWAP%(D9%) AND 254%)/2%)+100%),2%) &

30500	DEF FNZ(Y)=INT(ABS(Y)*100.+.5001)/100.*SGN(Y) &

30600	DEF FNPAGE$(Y0%,Y1%,Y2%,Y0$,Y1$) 	!PAGING FUNCTION &
\		Y2$	= '' &
\		Y2$	= STRING$(Y0%-(Y1%+LEN(XLATE(Y1$,STRING$(10%,0%)+ &
					CHR$(10%)))),10%) IF Y1$<>'' &
\		PAGE.LINE%	= LEN(XLATE(Y0$,STRING$(10%,0%)+CHR$(10%))) &
\		Y%	= INSTR(1%,Y1$+Y0$,'<<#') &
\		Y3%	= INSTR(1%,Y1$+Y0$,'#>>') &
\		Y$	= RIGHT(NUM1$(100000+Y2%),8%-(Y3%-Y%)) &
\		Y3%	= -3% IF Y%=0% &
\		PRINT #OUTPUT.CH%, Y2$;LEFT(Y1$+Y0$,Y%-1%);Y$; &
			RIGHT(Y1$+Y0$,Y3%+3%); &
\		PAGE%	= Y2% &
\	FNEND &

30650	DEF FNPAGE%	= PAGE% + 1% &

30660	DEF FNLINE%	= PAGE.LINE% &

30999	CHAIN.FLAG%=-1% 			!CHAIN ENTER LINE &

31000	GOTO 30					!REPORT ENTER LINE &

32767	END &

