10	!---------------------------------------------------------------&
	!	ACCOUNTS PAYABLE SYSTEM 	-Vendor			&
	!	APV File Maintenance					&
	!								&
	!	APVMNT.B2S	V1.0	March 1985			&
	!								&
	! Author - Randall Beard, Computer Management Center, Inc.	&
	!								&
	! Files-VENDES.DAT	-ISAM	Vendor Description File		&
	!---------------------------------------------------------------&
	!		      Copyright (c) 1984 by 			&
	!	  Computer Management Center, Idaho Falls, Idaho	&
	! 								&
	! This software is furnished under a license and may be used	&
	! and copied only in accordance with the terms of such license 	&
	! and with the inclusion of the above copyright notice.  This 	&
	! software or any other copies therof may not be provided or 	&
	! otherwise made available to any other person.  No title to 	&
	! and ownership of the software is hereby transferred.		&
	!      The information in this software is subject to change	&
	! without notice and should not be construed as a committment 	&
	! by Computer Management Center. 				&
	!      CMC assumes no responsibility for the use or reliability &
	! of its software on equipment which is not supported by CMC. 	&
	!---------------------------------------------------------------&

50	ON ERROR GOTO 19000 &
\	V$=SYS(CHR$(6%)+CHR$(-7%)) &
\	DIM DEFAULT$(19%),SEARCH$(19%) &

60	CH%=1%					! Keyboard channel 	&
\	OPEN 'KB:' AS FILE #1%, MODE 8%+256% 	! Echo,Disbl Hibrn,%^C 	&

70	ESC$=CHR$(155%)				! Escape code for VT100 &
\	CLSCN$=ESC$+'[H'+ESC$+'[J'		! Clear entire screen 	&
\	CLRLIN$=ESC$+'[2K'			! Erase entire line 	&
\	CLRBOT$=ESC$+'[21;1H'+ESC$+'[J'		! Erase to end of scrn	&
\	R.ON$=ESC$+'[7m'			! Reverse video 	&
\	B.ON$=ESC$+'[1m'			! Bold lettering  	&
\	BLINK.ON$=ESC$+'[5m'			! Blinking cursor 	&
\	G.OFF$=ESC$+'[m'			! Select graphic off 	&
\	COLM.ON$=ESC$+'[?3h'			! 132 Column mode 	&
\	COLM.OFF$=ESC$+'[?3l'			! 80 Column mode 	&
\	LDS.ON$=ESC$+'(0'			! Line drawing set 	&
\	USASCII$=ESC$+'(B'			! United States ASCII 	&

80	!COM(THIS) DROP.DEAD.DATE$ = 8,VERSION.NO$ = 6,SERIAL.NO$ = 10	&
	DROP.DEAD.DATE$='        ' 					&
\	VERSION.NO$='V1.0' 						&
\	DATE.TIME$=SYS(CHR$(6%)+CHR$(20%)+STRING$(2%,0%)+CHR$(1%)+ 	&
		CHR$(SWAP%(1%))+STRING$(11%,0%)+CHR$(SWAP%(0%))+	&
		CHR$(1%)+CHR$(SWAP%(1%))) 				&
\	IF 	DROP.DEAD.DATE$<>'' 					&
	THEN	IF 	DROP.DEAD.DATE$<MID(DATE.TIME$,7%,8%) 		&
		THEN	MESG$=MID(SYS(CHR$(6%)+CHR$(9%)+		&
				CHR$(47%)),3%,30%) 			&
\			PRINT #CH%, MESG$;SYS(CHR$(9%))			&

100	DEVICE.CH%	= 02% &
\	VENDES.CH%	= 02% &
\	MENU.CH%   	= 12% &
\	LOOP.DATA% 	= 13% &

200	IF 	FNO%(DEVICE.CH%,'DEVICE.DAT','/R0','')=0% &
	THEN	VENDES.DEVICE$=CVT$$(MID(FNL$,7%,20%),-1%) &
			IF FNG%(DEVICE.CH%,'VENDES')=0% &
\		V%=FNC%(DEVICE.CH%) &

300	V%=FNO%(VENDES.CH%,VENDES.DEVICE$+'VENDES.DAT','/RW','') &
\	V%=FNO%(VENDES.CH%,VENDES.DEVICE$+'VENDES.DAT','/CR:8,256','') &
		IF FNS% = 5% &
\	IF 	FNS% &
	THEN	V$=FNMESS$(CH%,FNS%,' VENDES.DAT',0%,-1%) &
\		GOTO 1040 &

400	OPEN 'MENU.FIL/RO' FOR INPUT AS FILE MENU.CH% &
\	DIM #12%, A0$(1%)=64% &
\	COMPANY$=CVT$$(RIGHT(A0$(1%),2%),128%) &
\	CLOSE MENU.CH% &

450	OPEN 'NL:' AS FILE 11%, RECORDSIZE 256% &
\	FIELD #11%,	06% AS VENDES.NUM$,	&
			29% AS VENDES.NAM$,	&
			29% AS VENDES.ADD$,	&
			29% AS VENDES.CST$,	&
			29% AS VENDES.ZIP$,	&
			05% AS VENDES.JUNK$,	&
			01% AS VENDES.PURGE$,	&
			10% AS VENDES.PHN$,	&
			13% AS VENDES.FED$,	&
			01% AS VENDES.199$,	&
			02% AS VENDES.DUE$,	&
			02% AS VENDES.DISDAY$,	&
			02% AS VENDES.DISC$,	&
			36% AS JUNK$,	&
			02% AS VENDES.UPDFLAG$,	&
			02% AS VENDES.UPDCOUNT$,&
			02% AS VENDES.LMU$,	&
			01% AS VENDES.OK.TO.PURGE$, &
			15% AS VENDES.SORTKEY$ &
\	FIELD #11%, 256% AS VENDES.BUF$ &

500	FRAME$ = FRAME$ + FNP$(NUM1$(I%)+';1')+' '+FNP$(NUM1$(I%)+';80')+' ' &
			FOR I%=2% TO 19% &
\	COMMAND$  = ' ' &
\	COMMAND$  = COMMAND$+RIGHT(NUM1$(I%+100%),2%) + ' ' &
		FOR I%=1% TO LOOP.DATA% &

1000	!--------------------------------------------PROGRAM RESTART POINT &
	IF 	FNG%(VENDES.CH%,'')=0% &
	THEN	KEY.POINTER%=FNR(VENDES.CH%) &
\		DATA.POINTER%=FNR(VENDES.CH%+1%) &
\		GOTO 1010 &

1002	PRINT #CH%, G.OFF$; &
\	GOSUB 1060 &
\	GOTO 1030 &

1005	IF 	FNN%(VENDES.CH%) &
	THEN	18910 &
	ELSE	KEY.POINTER%=FNR(VENDES.CH%) &
\		DATA.POINTER%=FNR(VENDES.CH%+1%) &
\		GOTO 1015 &

1010	PRINT #CH%, COLM.OFF$;FNSR$('1;24');G.OFF$; &
\	GOSUB 1060 &

1015	GOSUB 6000 &

1030	CHNG.FLAG%=0% &
\	PRINT #CH%, CLRBOT$;G.OFF$; &
		'COMMAND: Add Erase Change Blank Initialize Default '; &
		'Find Next Restore ';FNP$('22;10'); &
		'View Quit '; &
\	OPT$=CVT$$(FNINP$(CH%,128%,' ',1%,1%),32%) &
\	OPT$='A' IF OPT$='' &
\	GOTO 1030 IF KEY.POINTER%=0% AND INSTR(1%,'AQD',OPT$)=0% &
\	GOTO 1050 IF OPT$<>'Q' &

1040	PRINT #CH%, CLSCN$;FNSR$('1;24');FNP$('1;75');B.ON$;BLINK.ON$;R.ON$; &
		'WAIT';G.OFF$;FNP$('24;1');FNX%('',0%,'') &

1050	GOTO 1000 	IF OPT$='R' 		! Restore	&
\	GOTO 1005 	IF OPT$='N' 		! Next		&
\	GOTO 2000 	IF OPT$='A' OR OPT$='F' ! Add, Find	&
\	GOTO 2200 	IF OPT$='C' OR OPT$='B' ! Change, Blank	&
\	GOTO 2400 	IF OPT$='E' 		! Erase 	&
\	GOTO 2500 	IF OPT$='D' 		! Default	&
\	GOTO 2600 	IF OPT$='I' 		! Initialize	&
\	GOTO 17000      IF OPT$='V' 		! View		&
\	GOTO 1030 		    		&

1060	TITLE$     = 'Vendor Master File Maintenance' &
\	PRINT #CH%, CLSCN$;R.ON$;LEFT(TITLE$,39%);SPACE$(40%-LEN(TITLE$)); &
			SPACE$(40%-LEN(COMPANY$));COMPANY$; &
			FRAME$; FNP$('20;1');SPACE$(80%);G.OFF$; &
\	PRINT #CH%, 	FNP$('03;02');'(01) Vendor #'; &
			FNP$('04;02');'(02) Name'; &
			FNP$('05;02');'(03) Address'; &
			FNP$('06;02');'(04) City/St'; &
			FNP$('07;02');'(05) Zip Code'; &
			FNP$('08;02');'(06) Phone #'; &
			FNP$('10;02');'(07) Sortkey'; &
			FNP$('11;02');'(08) Fed ID #'; &
			FNP$('12;02');'(09) 1099 ?      (Y/N)'; &
			FNP$('13;02');'(10) Due Days'; &
		 	FNP$('14;02');'(11) Dis Days'; &
			FNP$('15;02');'(12) Dis Pct'; &
			FNP$('17;02');'(13) Purge ?     (Y/N)'; &
\	RETURN &
						
2000	!---------------------------------------KEY SEARCH &
	IF	OPT$='A'	THEN	OPTION$='ADD ' &
				ELSE	OPTION$='FIND ' &

2010	INP$='' &
\	FOR LOOP%=1% TO LOOP.DATA% &
\		INP$=DEFAULT$(LOOP%) IF OPT$='A' &
\		GOSUB 6030 &
\	NEXT LOOP% &
\	GOSUB 6210 &
\	IF	OPT$='F' OR FNG%(VENDES.CH%,INP$)=0% &
	THEN	KEY.POINTER%=FNR(VENDES.CH%) &
\		DATA.POINTER%=FNR(VENDES.CH%+1%) &
\		GOTO 1015 &
						
2100	!---------------------------------------ADD RECORD &
  	GOSUB 6010 FOR LOOP%=2% TO LOOP.DATA% &
\	IF 	FNA%(VENDES.CH%,VENDES.BUF$) &
	THEN	V$=FNMESS$(CH%,FNS%,' VENDES.DAT',0%,-1%) &
\		GOTO 1000 &

2110	KEY.POINTER%=FNR(VENDES.CH%) &
\	DATA.POINTER%=FNR(VENDES.CH%+1%) &
\	GOTO 1030 &

2200	!---------------------------------------CHANGE AND BLANK RECORD &
	GOSUB 2910 &
\	IF 	INP$='' 	THEN	1030	ELSE	1010 &

2400	!---------------------------------------ERASE RECORD &
	GOSUB 2920 &
\	IF 	INP$<>'Y' 	THEN	1030	ELSE	1005 &

2500	!---------------------------------------SET DEFAULT VALUES &
	OPTION$='DEFAULT ' &
\	FOR LOOP%=1% TO LOOP.DATA% &
\		INP$=DEFAULT$(LOOP%) &
\		GOSUB 6030 &
\	NEXT LOOP% &

2510	PRINT #CH%, CLRBOT$;'Default:'; &
\	INP$=CVT$$(FNINP$(CH%,128%,' ',2%,1%),32%) &
\	GOTO 1010 IF INP$='' &
\	LOOP%=(INSTR(1%,COMMAND$,' '+INP$+' ')+2%)/3% &
\	GOTO 2510 IF LOOP%<2% &
\	GOSUB 6010 &
\	DEFAULT$(LOOP%)=INP$ &
\	GOTO 2510 &
						
2600	!---------------------------------------INITIALIZATION &
	INITIALIZE$=' ' &
\	INP$='' &
\	GOSUB 6030 FOR LOOP%=1% TO LOOP.DATA% &

2610	PRINT #CH%, CLRBOT$;FNP$('22;1');INITIALIZE$; &
		FNP$('21;1');'Field to initialize:'; &
\	INP$=CVT$$(FNINP$(CH%,128%,' ',2%,1%),32%) &
\	IF 	INP$<>'' &
	THEN	LOOP%=(INSTR(1%,COMMAND$,' '+INP$+' ')+2%)/3% &
\		INITIALIZE$=INITIALIZE$+INP$+' ' IF &
			LOOP%<>0% AND (INSTR(1%,INITIALIZE$,' '+INP$+' ') &
				+2%)/3%=0% &
\		GOTO 2610 &

2620	GOTO 1010 IF INITIALIZE$='' &
\	OPT$='C' &
\	OPTION$='INITIALIZE ' &
\	CHNG.FLAG%=-1% &
\	INP$='' &
\	GOSUB 6030 FOR LOOP%=1% TO LOOP.DATA% &
\	GOTO 2660 &

2650	GOTO 18910 IF FNN%(VENDES.CH%) &
\	KEY.POINTER%=FNR(VENDES.CH%) &
\	DATA.POINTER%=FNR(VENDES.CH%+1%) &

2660	GOSUB 6000 &
\	FOR I%=1% TO LEN(INITIALIZE$)/3% &
\		LOOP%=(INSTR(1%,COMMAND$,MID(INITIALIZE$,I%*3%-2%,3%))+2%)/3% &
\		GOSUB 6010 &
\		IF 	INP$<>'' &
		THEN	GOSUB 2900 &
\			IF 	FNS% &
			THEN	V$=FNMESS$(CH%,FNS%,' VENDES.DAT',0%,-1%) &
\				GOTO 1010 &

2670	NEXT I% &
\	GOTO 2650 &

2700	!---------------------------------------GLOBAL SEARCH &
	PRINT #CH%,FNP$('20;54');R.ON$;'Record ';FNR(VENDES.CH%); &
		FNP$('20;67');' of ';FNT;G.OFF$; &
\	INP$='' &
\	GOSUB 6030 FOR LOOP%=1% TO LOOP.DATA% &
\	OPT$='F' &
\	OPTION$='SEARCH FOR ' &
\	SEARCH$=' ' &
\	SEARCH$(LOOP%)='' FOR LOOP%=1% TO LOOP.DATA% &

2710	PRINT #CH%, CLRBOT$;'Field to search for '; &
\	INP$=CVT$$(FNINP$(CH%,128%,' ',2%,1%),32%) &
\	IF INP$=''	THEN	GOTO 1010 IF SEARCH$=' ' &
\				SEARCH.LEN%=LEN(SEARCH$)/3% &
\				CLOSE CH% &
\				JUNK$=SYS(CHR$(3%)) ! Turn off terminal echo &
\				PRINT R.ON$;FNP$('20;38');'Searching. . . '; &
					G.OFF$ &
\				GLOBAL%=1% &
\				GOTO 2735 &

2715	LOOP%=(INSTR(1%,COMMAND$,' '+INP$+' ')+2%)/3% &
\	GOTO 2710 IF LOOP%=0% OR (INSTR(1%,SEARCH$,' '+INP$+' ')+2%)/3% &
\	SEARCH$=SEARCH$+INP$+' ' &
\	GOSUB 6010 &
\	SEARCH$(LOOP%)=GLOBAL$(LOOP%)+'' &
\	GOTO 2710 &

2720	IF FNN%(VENDES.CH%)	THEN	GLOBAL%=0% &
\					OPEN 'KB:' AS FILE #CH%, MODE 8%+256% &
\					JUNK$=SYS(CHR$(2%)) ! Turn on echo &
\					GOTO 18910 &

2725	KEY.POINTER%=FNR(VENDES.CH%) &
\	DATA.POINTER%=FNR(VENDES.CH%+1%) ! Not needed on a single file &

2735	PRINT R.ON$;FNP$('20;60');KEY.POINTER%;G.OFF$; &
\	LSET VENDES.BUF$=FNL$+'' &
\	FOR I%=1% TO SEARCH.LEN% &
\		LOOP%=(INSTR(1%,COMMAND$,MID(SEARCH$,I%*3%-2%,3%))+2%)/3% &
\		GOTO 2740 IF GLOBAL$(LOOP%)=SEARCH$(LOOP%) &
\	NEXT I% &
\	GOTO 2720 &

2740	OPEN 'KB:' AS FILE #CH%, MODE 8%+256% &
\	JUNK$=SYS(CHR$(2%)) ! Turn on echo &
\	GOSUB 6000 &
\	PRINT #CH%, CLRBOT$;'COMMAND: Blank Change Erase'; &
\	OPT$=CVT$$(FNINP$(CH%,128%,' ',1%,1%),32%) &
\	IF 	OPT$<>'' &
	THEN	IF	OPT$='E' 	THEN 	GOSUB 2920 	&
					ELSE	GOSUB 2910 &

2750	CLOSE CH% &
\	JUNK$=SYS(CHR$(3%)) ! Turn off terminal echo &
\	GOTO 2720 &

2900	!-------------------------------------------------UPDATE A RECORD &
	V%=FNG%(-VENDES.CH%,NUM1$(KEY.POINTER%))+ &
		FNG%(-VENDES.CH%-1%,NUM1$(DATA.POINTER%)) &
\	V%=FNU%(VENDES.CH%,VENDES.BUF$) IF LOOP%>1% AND FNS%=0% &
\	V%=FNU%(-VENDES.CH%,VENDES.BUF$) IF LOOP%=1% AND FNS%=0% &
\	RETURN &

2910	!---------------------------------------CHANGE AND BLANK SUBROUTINE &	
	IF 	OPT$='B' &
	THEN	OPTION$ = 'BLANK ' &
	ELSE	OPTION$ = 'CHANGE ' &
\		CHNG.FLAG% = -1% &

2915	PRINT #CH%, CLRBOT$; OPTION$+':'; &
\	INP$=CVT$$(FNINP$(CH%,128%,' ',2%,1%),32%) &
\	GOTO 2919 IF INP$='' &
\	LOOP%=(INSTR(1%,COMMAND$,' '+INP$+' ')+2%)/3% &
\	GOTO 2915 IF (LOOP%=0% AND OPT$='C') OR (LOOP%<2% AND OPT$='B') &
\	IF 	OPT$='C' 	THEN	GOSUB 6010 	ELSE	INP$=''    &
\								GOSUB 6030 &

2917	GOSUB 2900 &
\	GOTO 2915 IF FNS%=0% &
\	V$=FNMESS$(CH%,FNS%,' VENDES.DAT',0%,-1%) &

2919	RETURN &
						
2920	!-----------------------DELETE A RECORD &
	PRINT #CH%, CLRBOT$;"Confirm deletion (Yes/No) "; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",1%,1%),32%) &
\	RETURN IF INP$<>"Y" &

2930	IF FND%(VENDES.CH%,"") &
	THEN	V$=FNMESS$(CH%,FNS%,'VENDES.DAT',0%,-1%) &

2940	RETURN &

6000	!---------------------------------------DATA MAINTENANCE GOSUB SECTION &
	RETURN IF DATA.POINTER%=0% OR FNG%(-VENDES.CH%,NUM1$(KEY.POINTER%))+ &
		FNG%(-VENDES.CH%-1%,NUM1$(DATA.POINTER%)) &
\	LSET VENDES.BUF$=FNL$+'' &
\	ON LOOP% GOSUB	6220, 6240, 6260, 6280, 6300, 6320, 6340, 6360, 6380, &
			6400, 6420, 6440, 6460	FOR LOOP%=1% TO LOOP.DATA% &
\	RETURN &

6010	ON LOOP% GOSUB	6210, 6230, 6250, 6270, 6290, 6310, 6330, 6350, 6370, &
			6390, 6410, 6430, 6450 &
\	RETURN &

6030	ON LOOP% GOSUB	6217, 6237, 6257, 6277, 6297, 6317, 6337, 6357, 6377, &
			6397, 6417, 6437, 6457 &
\	RETURN &
						
6200	!---------------------------------------DATA MAINTENANCE SUBROUTINES &

6210	INP$=FNSIO$('','3;17',VENDES.NUM$,DEFAULT$(LOOP%),CHR$(CH%)+ &
		STRING$(2%,LEN(VENDES.NUM$))+CHR$(1%)+CHR$(0%)+ &
		OPTION$+' ALPHA') &
\	GOTO 6210 IF INP$='' AND OPT$='A' &
\	IF 	OPT$='C' AND INP$<>'' &
	THEN	IF 	FNG%(VENDES.CH%,INP$+SPACE$(6%-LEN(INP$)))=0% &
		THEN	PRINT #CH%, CLRBOT$;FNP$('24;1');INP$;' is a duplicate key'; &
				FNP$('21;1');'COMMAND: Re-enter Exit'; &
\			IF 	FNINP$(CH%,128%,' ',1%,0%)='R' &
			THEN	GOTO 6210	ELSE	GOTO 6220 &

6217	LSET VENDES.NUM$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6220	PRINT #CH%, FNP$('3;17');B.ON$;VENDES.NUM$;G.OFF$; &
\	RETURN &

6230	INP$=FNSIO$('','4;17',VENDES.NAM$,DEFAULT$(LOOP%),CHR$(CH%)+ &
		STRING$(2%,LEN(VENDES.NAM$))+CHR$(1%)+CHR$(0%)+ &
		OPTION$+' ALPHA') &

6237	LSET VENDES.NAM$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6240	PRINT #CH%, FNP$('4;17');B.ON$;VENDES.NAM$;G.OFF$; &
\	RETURN &

6250	INP$=FNSIO$('','5;17',VENDES.ADD$,DEFAULT$(LOOP%),CHR$(CH%)+ &
		STRING$(2%,LEN(VENDES.ADD$))+CHR$(1%)+CHR$(0%)+ &
		OPTION$+' ALPHA') &

6257	LSET VENDES.ADD$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6260	PRINT #CH%, FNP$('5;17');B.ON$;VENDES.ADD$;G.OFF$; &
\	RETURN &

6270	INP$=FNSIO$('','6;17',VENDES.CST$,DEFAULT$(LOOP%),CHR$(CH%)+ &
		STRING$(2%,LEN(VENDES.CST$))+CHR$(1%)+CHR$(0%)+ &
		OPTION$+' ALPHA') &

6277	LSET VENDES.CST$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6280	PRINT #CH%, FNP$('6;17');B.ON$;VENDES.CST$;G.OFF$; &
\	RETURN &

6290	INP$=FNSIO$('','7;17',VENDES.ZIP$,DEFAULT$(LOOP%),CHR$(CH%)+ &
		STRING$(2%,LEN(VENDES.ZIP$))+CHR$(1%)+CHR$(0%)+ &
		OPTION$+' ALPHA') &

6297	LSET VENDES.ZIP$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6300	PRINT #CH%, FNP$('7;17');B.ON$;VENDES.ZIP$;G.OFF$; &
\	RETURN &

6310	INP$=FNSIO$('','8;17',VENDES.PHN$,DEFAULT$(LOOP%),CHR$(CH%)+ &
		CHR$(13%)+CHR$(10%)+CHR$(1%)+CHR$(0%)+OPTION$+' ALPHA') &

6317	RSET VENDES.PHN$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6320	PRINT #CH%, FNP$('8;17');B.ON$;FNFONE$(VENDES.PHN$);G.OFF$; &
\	RETURN &

6330	INP$=FNSIO$('','10;17',VENDES.SORTKEY$,DEFAULT$(LOOP%),CHR$(CH%)+ &
		STRING$(2%,LEN(VENDES.SORTKEY$))+CHR$(1%)+CHR$(0%)+ &
		OPTION$+' ALPHA') &

6337	LSET VENDES.SORTKEY$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6340	PRINT #CH%, FNP$('10;17');B.ON$;VENDES.SORTKEY$;G.OFF$; &
\	RETURN &

6350	INP$=FNSIO$('','11;17',VENDES.FED$,DEFAULT$(LOOP%),CHR$(CH%)+ &
		STRING$(2%,LEN(VENDES.FED$))+CHR$(1%)+CHR$(0%)+ &
		OPTION$+' ALPHA') &

6357	LSET VENDES.FED$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6360	PRINT #CH%, FNP$('11;17');B.ON$;VENDES.FED$;G.OFF$; &
\	RETURN &

6370	INP$=FNSIO$('','12;17',VENDES.199$,DEFAULT$(LOOP%),CHR$(CH%)+ &
		STRING$(2%,LEN(VENDES.199$))+CHR$(1%)+CHR$(0%)+ &
		OPTION$+' ALPHA') &
\	INP$='N' IF INP$<>'Y' AND OPT$<>'C' &

6377	LSET VENDES.199$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6380	PRINT #CH%, FNP$('12;17');B.ON$;VENDES.199$;G.OFF$; &
\	RETURN &

6390	INP$=FNSIO$('###','13;20',VENDES.DUE$,DEFAULT$(LOOP%),CHR$(CH%)+ &
		STRING$(2%,3%)+CHR$(3%)+CHR$(0%)+OPTION$+' VALUE') &

6397	LSET VENDES.DUE$=CVT%$(VAL(INP$)) IF INP$<>'' OR CHNG.FLAG%=0% &

6400	PRINT #CH% USING FNP$('13;20')+B.ON$+'###'+G.OFF$,CVT$%(VENDES.DUE$); &
\	RETURN &

6410	INP$=FNSIO$('###','14;20',VENDES.DISDAY$,DEFAULT$(LOOP%),CHR$(CH%)+ &
		STRING$(2%,3%)+CHR$(3%)+CHR$(0%)+OPTION$+' VALUE') &

6417	LSET VENDES.DISDAY$=CVT%$(VAL(INP$)) IF INP$<>'' OR CHNG.FLAG%=0% &

6420	PRINT #CH% USING FNP$('14;20')+B.ON$+'###'+G.OFF$,CVT$%(VENDES.DISDAY$); &
\	RETURN &

6430	INP$=FNSIO$('###.##%','15;17',VENDES.DISC$,DEFAULT$(LOOP%),CHR$(CH%)+ &
		CHR$(7%)+CHR$(5%)+CHR$(3%)+CHR$(2%)+OPTION$+' VALUE') &

6437	LSET VENDES.DISC$=CVT%$(FNZ(VAL(INP$)*100%)) IF INP$<>'' OR CHNG.FLAG%=0% &

6440	PRINT #CH% USING FNP$('15;17')+B.ON$+'###.##%'+G.OFF$,CVT$%(VENDES.DISC$)/100.; &
\	RETURN &

6450	INP$=FNSIO$('','17;17',VENDES.PURGE$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(VENDES.PURGE$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &
\	INP$='N' IF INP$<>'Y' AND OPT$<>'C' &

6457	LSET VENDES.PURGE$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6460	PRINT #CH%, FNP$('17;17');B.ON$;VENDES.PURGE$;G.OFF$; &
\	RETURN &

17000	!-----------------------------------------------VIEW ROUTINE &
	PRINT #CH%, CLSCN$;COLM.ON$;R.ON$;FNSR$('4;19'); &
		FNP$('1;1');SPACE$(132%);FNP$('20;1');SPACE$(132%);LDS.ON$; &
		FNP$('2;8');CHR$(120%);  &
		FNP$('2;36');CHR$(120%); &
		FNP$('2;64');CHR$(120%); &
		FNP$('2;92');CHR$(120%); &
		FNP$('3;2');STRING$(130%,113%); &
		FNP$('3;8');CHR$(110%);  &
		FNP$('3;36');CHR$(110%); &
		FNP$('3;64');CHR$(110%); &
		FNP$('3;92');CHR$(110%);USASCII$; &
\	PRINT #CH%, FNP$(NUM1$(I%)+';1');' ';FNP$(NUM1$(I%)+';132');' '; &
		FOR I%=2% TO 19% &
\	PRINT #CH%, G.OFF$; &
		FNP$('2;2');'Vend #'; &
		FNP$('2;9');'Name'; &
		FNP$('2;37');'Address'; &
		FNP$('2;65');'City, State'; &
		FNP$('2;93');'Zip Code'; &
\	DISPLAY.LINE%=0% &
\	PRINT.LINE%=4% &

17010	V%=FNG%(-VENDES.CH%,NUM1$(KEY.POINTER%))+ &
		FNG%(-VENDES.CH%-1%,NUM1$(DATA.POINTER%)) &
\	GOTO 17105 &

17100	GOTO 17200 IF FNN%(VENDES.CH%) &

17105	LSET VENDES.BUF$=FNL$ &
\	PRINT #CH%, FNP$(NUM1$(PRINT.LINE%)+';2');VENDES.NUM$; &
		FNP$(NUM1$(PRINT.LINE%)+';9');VENDES.NAM$; &
		FNP$(NUM1$(PRINT.LINE%)+';37');VENDES.ADD$; &
		FNP$(NUM1$(PRINT.LINE%)+';65');VENDES.CST$; &
		FNP$(NUM1$(PRINT.LINE%)+';93');VENDES.ZIP$; &
\	PRINT #CH%, R.ON$;LDS.ON$; &
		FNP$(NUM1$(PRINT.LINE%)+';8');CHR$(120%); &
		FNP$(NUM1$(PRINT.LINE%)+';36');CHR$(120%); &
		FNP$(NUM1$(PRINT.LINE%)+';64');CHR$(120%); &
		FNP$(NUM1$(PRINT.LINE%)+';92');CHR$(120%);USASCII$;G.OFF$; &
\	PRINT.LINE%=PRINT.LINE%+1% IF PRINT.LINE%<19% &

17110	IF 	DISPLAY.LINE%>14% &
	THEN	PRINT #CH%, FNP$('24;105');'Hit any key to continue '; &
\		INP$=FNINP$(CH%,128%,' ',1%,0%) &
\		GOTO 17210 IF INP$='%^C' &
\		DISPLAY.LINE%=0% &
\		PRINT #CH%, CLRBOT$;FNP$(NUM1$(PRINT.LINE%)+';1'); &

17120	PRINT #CH% &
\	DISPLAY.LINE%=DISPLAY.LINE%+1% &
\	IF 	PRINT.LINE%=19% &
	THEN	PRINT #CH%, FNP$(NUM1$(PRINT.LINE%)+';1');R.ON$;' '; &
			FNP$(NUM1$(PRINT.LINE%)+';132');' ';G.OFF$; &

17190	GOTO 17100 &

17200	IF	DISPLAY.LINE%>0% &
	THEN	PRINT #CH%, FNP$('24;1');'End of file.  '; &
			FNP$('24;105');'Hit any key to continue '; &
\		INP$=FNINP$(CH%,128%,' ',1%,0%) &

17210	V%=FNG%(-VENDES.CH%,NUM1$(KEY.POINTER%))+ &
		FNG%(-VENDES.CH%-1%,NUM1$(DATA.POINTER%)) &
\	PRINT #CH%, FNP$('1;1');FNSR$('1;24');COLM.OFF$; &
\	GOTO 1010 &

18910	!----------------------------------------------END OF FILE &
	PRINT #CH%, CLRBOT$;FNP$('24;1');'End of file has been reached.'; &
		FNP$('24;55');'Hit any key to continue '; &
\	INP$=FNINP$(CH%,128%,' ',1%,0%) &
\	GOTO 1000 &

19000	!------------------------------------------------ERROR TRAPPING &
	RESUME IF ERR=52% OR ERR=51% 					&
\	RESUME 450 IF ERL=400 						&
\	RESUME 1010 IF ERR=54% 						&
\	ON ERROR GOTO 0 						&
						
30000	!---------------------------------------TERMINAL INPUT 		&
	DEF FNINP$(CHN%,KYP%,FILLCHAR$,INPUTLEN%,TO.ERR%)		&
\		PRINT #CHN%, STRING$(INPUTLEN%,ASCII(FILLCHAR$)); 	&
			  STRING$(INPUTLEN%,8%); 			&
\		PRINT #CHN%, RECORD 256%,CHR$(KYP%+INPUTLEN%)+FILLCHAR$;&
\		GET #CHN% 						&
\		FIELD #CHN%, RECOUNT AS BUFFER$ 			&
\		BUFFER$='%^C' IF INSTR(1%,BUFFER$,CHR$(3%)) 		&
\		FNINP$=CVT$$(BUFFER$,4%) 				&
\		V=SQR(-1) IF BUFFER$='%^C' AND TO.ERR% !^C Trappping	&
\	FNEND 								&

30200	DEF*FNP$(ROWCOL$)=ESC$+'['+ROWCOL$+'H' 	!DIRECT CURSOR ADDRESS	&

30250	!---------------------------------------MESSAGE HANDLER		&
	DEF*FNMESS$(CHN%,ERRNUM%,DESC$,TO.ERR%,PRINT.TEST%)		&
\		MESG$=MID(SYS(CHR$(6%)+CHR$(9%)+CHR$(ERRNUM%)),3%,30%)+	&
			DESC$						&
\		IF 	PRINT.TEST%					&
		THEN	PRINT #CHN%, FNP$('24;1');CLRLIN$;MESG$;	&
				FNP$('24;55');'Hit any key to continue';&
\			NW$=FNINP$(CHN%,128%,' ',1%,TO.ERR%)		&

30260		FNMESS$=MESG$						&
\	FNEND 								&
						
30300	DEF*FNSR$(BEGEND$)=ESC$+'['+BEGEND$+'r'	!SCROLL CONTROL		&

30400	DEF FNFONE$(PHN$)='('+LEFT(PHN$,3%)+')'+!PHONE NO FORMATTER	&
		MID(PHN$,4%,3%)+'-'+RIGHT(PHN$,7%) 			&

30500	DEF FNZ(Y) 				!ROUNDING FUNCTION	&
\		TEMP=Y 							&
\		TEMP=-Y IF Y<0. 					&
\		TEMP=INT(TEMP*100.+.51)/100. 				&
\		TEMP=-TEMP IF Y<0. 					&
\		FNZ=TEMP 						&
\	FNEND 								&

30700	!---------------------------------------DATA MAINT FUNCTION	&
	DEF*FNSIO$(FRMAT$,ROWCOL$,VARABLE$,DEFAL$,ITEMS$)		&
\		CHN%=ASCII(MID(ITEMS$,1%,1%))				&
\		KIND%=ASCII(MID(ITEMS$,4%,1%))				&
\		DECI=ASCII(MID(ITEMS$,5%,1%))				&
\		OPT$=MID(ITEMS$,6%,1%)					&

30705		PRINT #CHN%, FNP$(ROWCOL$);R.ON$;B.ON$;			&
\		IF 	OPT$='C'					&
		THEN	ON KIND% GOTO 30710, 30715, 30720, 30725, 30730	&
		ELSE	PRINT #CHN%, SPACE$(ASCII(MID(ITEMS$,2%,1%)));	&
\			GOTO 30735					&

30710		PRINT #CHN%,FNFONE$(VARABLE$); IF LOOP%=6%		&
\		PRINT #CHN%, VARABLE$; IF LOOP%<>6%	   ! ALPHA	&
\		GOTO 30735						&

30715		PRINT #CHN% USING FRMAT$,ASCII(VARABLE$);  ! ASCII	&
\		GOTO 30735						&

30720		PRINT #CHN% USING FRMAT$, CVT$%(VARABLE$)/(10.**DECI);	&
\		GOTO 30735				   ! INTEGER	&

30725		PRINT #CHN%, FND6$(CVT$%(VARABLE$));	   ! DATE	&
\		GOTO 30735						&

30730		PRINT #CHN% USING FRMAT$,CVT$F(VARABLE$);  ! FLOAT	&

30735		PRINT #CHN%,G.OFF$;CLRBOT$;RIGHT(ITEMS$,6%)+':  ';B.ON$;&
\		FIN$ = FNINP$(CHN%,0%,'_',ASCII(MID(ITEMS$,3%,1%)),1%)	&
\		V% = VAL(FIN$) IF KIND%=3%				&
\		V  = VAL(FIN$) IF KIND%>3%				&
\		IF 	FIN$=''						&
		THEN	FIN$ = DEFAL$ IF INSTR(1%,'CF',OPT$)=0%		&
\			GOTO 30745					&

30740		IF 	KIND%=3% OR KIND%=5%				&
		THEN	TEMP = 1.					&
\			TEMP = 10.**DECI IF INSTR(1%,FIN$,'.')=0%	&
\			FIN$ = NUM1$(VAL(FIN$)/TEMP)			&
\			FIN$ = FIN$ + '.' IF INSTR(1%,FIN$,'.')=0%	&

30745		FNSIO$ = FIN$+''					&
\	FNEND								&

32767	END &

