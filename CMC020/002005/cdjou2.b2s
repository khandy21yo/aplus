10	! EDIT THE CASH DISBURSEMENTS JOURNAL &
	!-------------------------------------------------------------------- &
	! &
	! &
	!		COPYRIGHT (c) 1984 BY &
	!		COMPUTER MANAGEMENT CENTER, IDAHO FALLS, IDAHO &
	! &
	! THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND &
	! COPIED ONLY IN ACCORDANCE WITH THE TERMS OF SUCH LICENSE AND WITH &
	! THE INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR ANY &
	! OTHER COPIES THEROF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAIL- &
	! ABLE TO ANY OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF THE SOFT- &
	! WARE IS HEREBY TRANSFERRED. &
	! &
	! THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE WITHOUT NO- &
	! TICE AND SHOULD NOT BE CONSTRUED AS A COMMITTMENT BY COMPUTER &
	! MANAGEMENT CENTER. &
	! &
	! CMC ASSUMES NO RESPONSIBILITY FOR THE USE OR RELIABILITY OF &
	! ITS SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPORTED BY CMC. &
	! &
	! ++ &
	! &
	! ABSTRACT: &
	! &
	! &
	! &
	! &
	! ENVIRONMENT: &
	! &
	!	PDP-11 UNDER RSTS/E BP2 &
	! &
	! AUTHOR: &
	! &
	! MODIFIED BY: &
	! &
	!	11/03/93 - Kevin Handy &
	!		Clean up variables &
	! &
	!----------------------------------------------------------------- &

30	ON ERROR GOTO 19000 &

60	OPEN "KB:" AS FILE #1%, MODE 8%+256% &
					!    8% - Echo Control &
					!   16% - Disable hibernation, Cntl-C &
\	CH%=1%				! Keyboard channel &
\	ESC$=CHR$(155%)			! Escape code for VT100 control &
\	CLSCN$=ESC$+"[H"+ESC$+"[J"	! Clear screen &
\	CLRLIN$=ESC$+"[2K"		! Erase entire line &
\	G.OFF$=ESC$+"[m"		! Select graphic off &
\	B.ON$=ESC$+"[1m"		! Bold face on &
\	BLINK.ON$=ESC$+"[5m"		! Blinking &
\	R.ON$=ESC$+"[7m"		! Reverse video &
\	SCROLL.ON$=ESC$+"[1;24r"	! Scroll control &
\	SCROLL.REGION$=ESC$+"[5;19r"	! Scroll region &
\	COLM.ON$=ESC$+"[?3h"		! 132 Column mode &
\	COLM.OFF$=ESC$+"[?3l"		! 80 Column mode &
\	CLRBOT$=ESC$+"[21;1H"+ESC$+"[J"	! Erase cursor to end of screen &
\	LDS.ON$=ESC$+"(0"		! Line drawing set &
\	USASCII$=ESC$+"(B"		! United States ASCII &

100	FILE.CH%   = 6% &
\	DEVICE.CH% = 2% &
\	VENDES.CH% = 2% &
\	CHART.CH%  = 4% &
\	CDJOUR.CH% = 6% &
\	APCCTL.CH% = 8% &
\	MENU.CH%   = 12% &
\	LOOP.DATA% = 14% &

110	DIM DEFAULT$(14%),SEARCH$(14%) &

200	IF FNO%(DEVICE.CH%,"DEVICE.DAT","/R0","")=0% &
	THEN	VENDES.DEVICE$=CVT$$(MID(FNL$,7%,20%),-1%) &
				IF FNG%(DEVICE.CH%,"VENDES")=0% &
\		CDJOUR.DEVICE$=CVT$$(MID(FNL$,7%,20%),-1%) &
				IF FNG%(DEVICE.CH%,"CDJOUR")=0% &
\		APCCTL.DEVICE$=CVT$$(MID(FNL$,7%,20%),-1%) &
				IF FNG%(DEVICE.CH%,"APCCTL")=0% &
\		CHART.DEVICE$=CVT$$(MID(FNL$,7%,20%),-1%) &
				IF FNG%(DEVICE.CH%,"CHART")=0% &
\		V%=FNC%(DEVICE.CH%) &

300	IF FNO%(VENDES.CH%,VENDES.DEVICE$+"VENDES.DAT","/RO","") &
	THEN	PRINT #CH%, FNP$(24%,1%);CLRLIN$; &
			"Error";FNS%;"While opening VENDES.DAT";FNP$(24%,55%); &
			"Hit any key to continue"; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		PRINT #CH%, CLSCN$;SCROLL.ON$;BLINK.ON$;R.ON$;B.ON$; &
			FNP$(1%,75%);"WAIT";G.OFF$;FNX%("",0%,"") &

310	V%=FNO%(FILE.CH%,CDJOUR.DEVICE$+"CDJOUR.DAT","/RW","") &
\	V%=FNO%(FILE.CH%,CDJOUR.DEVICE$+"CDJOUR.DAT","/CR:14,128","") &
		IF FNS%=5% &
\	IF FNS% &
	THEN	PRINT #CH%, FNP$(24%,1%);CLRLIN$; &
			"Error";FNS%;"While opening CDJOUR.DAT";FNP$(24%,55%); &
			"Hit any key to continue"; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		PRINT #CH%, CLSCN$;SCROLL.ON$;BLINK.ON$;R.ON$;B.ON$; &
			FNP$(1%,75%);"WAIT";G.OFF$;FNX%("",0%,"") &

320	CHART.FLAG%=FNO%(CHART.CH%,CHART.DEVICE$+"CHART.DAT","/RO","") &

330	V%=FNO%(APCCTL.CH%,APCCTL.DEVICE$+"APCCTL.DAS","/RW/SF/NS","") &
\	V%=FNG%(APCCTL.CH%,'APL') &
\	IF FNS% &
	THEN	PRINT #CH%, CLRBOT$; FNP$(24%,1%);"Error";FNS%; &
		"in AP Control file"; FNP$(24%,55%); "Hit any key to continue "; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		PRINT #CH%, CLSCN$;SCROLL.ON$;BLINK.ON$;R.ON$;B.ON$; &
			FNP$(1%,75%);'WAIT';G.OFF$;FNX%("",0%,"") &

340	FIELD #APCCTL.CH%, 	304% AS TEMP$, &
				08% AS APCCTL.APCASH$, &
				08% AS APCCTL.DISLOST$, &
				06% AS APCCTL.VOUCH$ &

350	IF CVT$$(APCCTL.APCASH$,-1%)<>'' &
	THEN	CASH.ACCT$=APCCTL.APCASH$+'' &
		ELSE	PRINT #CH%, CLSCN$;FNP$(24%,1%); &
			"Please enter the A/P cash account # "; &
\			INP$=FNINP$(CH%,0%,"_",8%) &
\			PRINT #CH%, CLSCN$;SCROLL.ON$;BLINK.ON$;R.ON$;B.ON$; &
				FNP$(1%,75%);'WAIT';G.OFF$;FNX%("",0%,"") &
					IF INP$='' OR INP$="%^C" &
\			CASH.ACCT$=SPACE$(8%-LEN(INP$))+INP$ &
\			V%=FNU%(-APCCTL.CH%,LEFT(FNL$,304%)+CASH.ACCT$+ &
				RIGHT(FNL$,313%)) &

360	IF CVT$$(APCCTL.DISLOST$,-1%)<>'' &
	THEN	DISC.LOST.ACCT$=APCCTL.DISLOST$+'' &
		ELSE	PRINT #CH%, CLSCN$;FNP$(24%,1%); &
			"Please enter the Discount lost account # "; &
\			INP$=FNINP$(CH%,0%,"_",8%) &
\			PRINT #CH%, CLSCN$;SCROLL.ON$;BLINK.ON$;R.ON$;B.ON$; &
				FNP$(1%,75%);'WAIT';G.OFF$;FNX%("",0%,"") &
					IF INP$='' OR INP$="%^C" &
\			DISC.LOST.ACCT$=SPACE$(8%-LEN(INP$))+INP$ &
\			V%=FNU%(-APCCTL.CH%,LEFT(FNL$,312%)+DISC.LOST.ACCT$+ &
				RIGHT(FNL$,321%)) &

370	V%=FNC%(APCCTL.CH%) &

400	OPEN "MENU.FIL/RO" FOR INPUT AS FILE MENU.CH% &
\	DIM #12%, A0$(255%)=64% &
\	COMPANY$=CVT$$(RIGHT(A0$(1%),2%),128%) &
\	CLOSE MENU.CH% &

450	!---------------RESUME LINE FOR ERROR ON LINE 400 &
	OPEN "NL:" AS FILE 12%, RECORDSIZE 512% &
\	FIELD #12%,	06% AS CDJ.VENNUM$,	&
			06% AS CDJ.VOUNUM$,	&
			02% AS CDJ.VOUDAT$,	&
			15% AS CDJ.INVOIC$,	&
			02% AS CDJ.INVDAT$,	&
			20% AS CDJ.CKDESC$,	&
			08% AS CDJ.AMT$,	&
			08% AS CDJ.DISC$,	&
			08% AS CDJ.APACCT$,	&
			08% AS CDJ.ACCT$,	&
			06% AS CDJ.CKNUM$,	&
			02% AS CDJ.CKDAT$,	&
			08% AS CDJ.DISCLOST$,	&
			08% AS CDJ.DISCACCT$,	&
			06% AS CDJ.PONUM$,	&
			02% AS CDJ.DISDAT$,	&
			02% AS CDJ.DUEDAT$,	&
			11% AS CDJ.SORTKEY$	&
\	FIELD #12%, 128% AS TEMP$,		&
			06% AS AP.VENNUM$,		&
			29% AS AP.VENNAM$,		&
			29% AS AP.VENADD$,		&
			29% AS AP.VENCST$,		&
			29% AS AP.VENZIP$,		&
			05% AS AP.VENXXX$,		&
			01% AS AP.VENPURGE$,		&
			10% AS AP.VENPHN$,		&
			13% AS AP.VENFED$,		&
			01% AS AP.VEN199$,		&
			02% AS AP.VENDUE$,		&
			02% AS AP.VENDISDAY$,		&
			02% AS AP.VENDISC$,		&
			08% AS AP.VENBILMTD$,		&
			02% AS AP.VENVOUMTD$,		&
			08% AS AP.VENBILYTD$,		&
			02% AS AP.VENVOUYTD$,		&
			08% AS AP.VENPAIDMTD$,		&
			08% AS AP.VENPAIDYTD$,		&
			02% AS AP.VENUPDFLAG$,		&
			02% AS AP.VENUPDCOUNT$,		&
			02% AS AP.VENLMU$,		&
			01% AS AP.VEN.OK.TO.PURGE$,	&
			15% AS AP.SORTKEY$		&
\	FIELD #12%,	128% AS CDJOUR.BUF$,	&
			256% AS APVEND.BUF$	&

500	FRAME$    = FRAME$+FNP$(I%,1%)+" "+FNP$(I%,80%)+" " FOR I%=2% TO 19% &
\	COMMAND$  = " " &
\	COMMAND$  = COMMAND$+RIGHT(NUM1$(I%+100%),2%) + " " &
		FOR I%=1% TO LOOP.DATA% &

1000	!--------------------------------------------PROGRAM RESTART POINT &
	IF FNG%(FILE.CH%,"")=0% &
	THEN	1010 &
		ELSE	GOSUB 1060 &
\			GOTO 1030 &

1005	IF FNN%(FILE.CH%) &
	THEN	18910 &
		ELSE	1015 &

1010	GOSUB 1060 &

1015	KEY.POINTER%=FNR(FILE.CH%) &
\	DATA.POINTER%=FNR(FILE.CH%+1%) &
\	GOSUB 6000 &

1030	FLAG%,CHNG.FLAG%=0% &
\	PRINT #CH%, CLRBOT$;G.OFF$; &
		"COMMAND: Add Erase Change Blank Initialize Default "; &
		"Global Find Next Restore ";FNP$(22%,10%); &
		"View Quit "; &
\	OPT$=CVT$$(FNINP$(CH%,128%," ",1%),32%) &
\	GOTO 1010 IF OPT$="%^C" &
\	OPT$="A" IF OPT$="" &
\	OPT$='' IF INSTR(1%,"ADQ",OPT$)=0% AND KEY.POINTER%=0% &

1040	IF OPT$="Q" &
	THEN	PRINT #CH%, SCROLL.ON$;CLSCN$; &
			BLINK.ON$;B.ON$;R.ON$;FNP$(1%,75%);"WAIT"; &
				G.OFF$;FNX%('',0%,'') &

1050	GOTO 2000 IF OPT$="A" &
\	GOTO 2400 IF OPT$="E" &
\	GOTO 2200 IF OPT$="C" &
\	GOTO 2300 IF OPT$="B" &
\	GOTO 2000 IF OPT$="F" &
\	GOTO 1005 IF OPT$="N" &
\	GOTO 17000 IF OPT$="V" &
\	GOTO 2500 IF OPT$="D" &
\	GOTO 2600 IF OPT$="I" &
\	GOTO 2700 IF OPT$="G" &
\	GOTO 1000 IF OPT$="R" &
\	GOTO 1030 &

1060	TEMP$     = "CASH DISBURSEMENT FILE MAINTENANCE" &
\	PRINT #CH%, COLM.OFF$;SCROLL.ON$; &
			CLSCN$;R.ON$;LEFT(TEMP$,39%);SPACE$(40%-LEN(TEMP$)); &
			SPACE$(40%-LEN(COMPANY$));COMPANY$; &
			FRAME$;FNP$(20%,1%);SPACE$(80%);G.OFF$; &
			FNP$(2%,2%);"(01)*VENDOR #"; &
			FNP$(3%,2%);"     NAME"; &
			FNP$(4%,2%);"     ADDRESS"; &
			FNP$(5%,2%);"     CITY/ST"; &
			FNP$(6%,2%);"     ZIP CODE"; &
			FNP$(9%,2%);"(02) VOUCHER#"; &
			FNP$(10%,2%);"(03) VOU DATE"; &
			FNP$(11%,2%);"(04) INVOICE#"; &
			FNP$(12%,2%);"(05) INV DATE"; &
			FNP$(13%,2%);"(06) DESC"; &
\	PRINT #CH%, &
			FNP$(2%,43%);"(07) AMOUNT"; &
			FNP$(3%,43%);"(08) DISCOUNT"; &
			FNP$(4%,43%);"(09) DISC LOST"; &
			FNP$(5%,43%);" **  PAYABLE"; &
			FNP$(9%,43%);"(10) AP ACCT"; &
			FNP$(10%,43%);"(11) CASH ACCT"; &
			FNP$(11%,43%);"(12) DISC LOST"; &
			FNP$(13%,43%);"(13) CHECK #"; &
			FNP$(14%,43%);"(14) CK DATE"; &
\	RETURN &

2000	!-------------------------------------------------SEARCH FOR KEYS &
	FOR LOOP%=1% TO LOOP.DATA% &
\		INP$='' &
\		INP$=DEFAULT$(LOOP%) IF OPT$='A' &
\		GOSUB 6030 &
\	NEXT LOOP% &
\	OPTION$="FIND " IF OPT$="F" &
\	OPTION$="ADD " IF OPT$="A" &

2010	GOSUB 6020 &
\	GOTO 1010  IF TEMP.KEY$="" OR INP$="%^C" &

2030	V%=FNG%(FILE.CH%,TEMP.KEY$) IF OPT$="A" &
\	V%=FNG%(FILE.CH%,CVT$$(TEMP.KEY$,128%)) IF OPT$="F" &
\	IF OPT$<>"A" OR FNS%=0% &
	THEN	GOTO 1015 &

2100	!-----------------------------------------------------ADD RECORD &
	OPTION$="ADD " &
\	FLAG%=-1% &
\	FOR LOOP%=3% TO LOOP.DATA% &
\	  	GOSUB 6010 &
\		GOTO 1010 IF INP$="%^C" OR INP$="%ABORT" &
\	NEXT LOOP% &

2110	IF FNA%(FILE.CH%,CDJOUR.BUF$) &
	THEN	PRINT #CH%, FNP$(24%,1%);"Error";FNS%;"while adding to file"; &
			FNP$(24%,55%);"Hit any key to continue"; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		GOTO 1000 &

2120	KEY.POINTER%=FNR(FILE.CH%) &
\	DATA.POINTER%=FNR(FILE.CH%+1%) &
\	GOTO 1030 &

2200	!--------------------------------------------------CHANGE RECORD &
	OPTION$ = "CHANGE " &
\	FLAG%, CHNG.FLAG% = -1% &
\	PRINT #CH%, CLRBOT$; "Change:"; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",2%),32%) &
\	GOTO 1010 IF INP$="%^C" &
\	GOTO 1030 IF INP$="" &
\	LOOP%=(INSTR(1%,COMMAND$," "+INP$+" ")+2%)/3% &
\	GOTO 2200 IF LOOP%=0% &
\	GOSUB 6010 &
\	GOTO 1010 IF INP$="%^C" &
\	GOTO 2200 IF INP$="%ABORT" &

2210	V%=FNG%(-FILE.CH%,NUM1$(KEY.POINTER%))+ &
		FNG%(-FILE.CH%-1%,NUM1$(DATA.POINTER%)) &
\	V%=FNU%(FILE.CH%,CDJOUR.BUF$) IF LOOP%<>1% AND FNS%=0% &
\	V%=FNU%(-FILE.CH%,CDJOUR.BUF$) IF LOOP%=1% AND FNS%=0% &
\	IF FNS% &
	THEN	PRINT #CH%, FNP$(24%,1%);"Error";FNS%;"while changing to file"; &
			FNP$(24%,55%);"Hit any key to continue"; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		GOTO 1010 &

2220	GOTO 2200 &

2300	!--------------------------------------------------CHANGE RECORD &
	OPTION$ = "BLANK " &
\	PRINT #CH%, CLRBOT$; "Blank:"; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",2%),32%) &
\	GOTO 1010 IF INP$="%^C" &
\	GOTO 1030 IF INP$="" &
\	LOOP%=(INSTR(1%,COMMAND$," "+INP$+" ")+2%)/3% &
\	GOTO 2300 IF LOOP%=0% OR LOOP%=1% &
\	INP$="" &
\	GOSUB 6030 &

2310	V%=FNG%(-FILE.CH%,NUM1$(KEY.POINTER%))+ &
		FNG%(-FILE.CH%-1%,NUM1$(DATA.POINTER%)) &
\	V%=FNU%(FILE.CH%,CDJOUR.BUF$) IF LOOP%<>1% AND FNS%=0% &
\	V%=FNU%(-FILE.CH%,CDJOUR.BUF$) IF LOOP%=1% AND FNS%=0% &
\	IF FNS% &
	THEN	PRINT #CH%, FNP$(24%,1%);"Error";FNS%;"while changing to file"; &
			FNP$(24%,55%);"Hit any key to continue"; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		GOTO 1010 &

2320	GOTO 2300 &

2400	!-----------------------------------------------DELETE RECORD &

2410	PRINT #CH%, CLRBOT$;"Confirm deletion (Yes/No) "; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",1%),32%) &
\	GOTO 1010 IF INP$="%^C" &
\	IF INP$<>"Y" &
	THEN	PRINT #CH%, FNP$(24%,1%);CLRLIN$;"This record has "; B.ON$; &
			BLINK.ON$;"NOT ";G.OFF$; "been deleted."; &
			FNP$(24%,55%);"Hit any key to continue "; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		GOTO 1030 &

2420	IF FND%(FILE.CH%,"")=0% &
	THEN	PRINT #CH%, FNP$(24%,1%);CLRLIN$;"This record has been deleted."; &
			FNP$(24%,55%);"Hit any key to continue "; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		GOTO 1005 &

2430	PRINT #CH%, FNP$(24%,1%);"Error";FNS%;"while deleting to file"; &
		FNP$(24%,55%);"Hit any key to continue"; &
\	INP$=FNINP$(CH%,128%," ",1%) &
\	GOTO 1005 &

2500	!-----------------------------------------------SET DEFAULT VALUES &
	FOR LOOP%=1% TO LOOP.DATA% &
\		INP$='' &
\		INP$=DEFAULT$(LOOP%) &
\		GOSUB 6030 &
\	NEXT LOOP% &

2510	OPTION$="DEFAULT " &
\	PRINT #CH%, CLRBOT$;"Default:"; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",2%),32%) &
\	GOTO 1010 IF INP$="%^C" OR INP$="" &
\	LOOP%=(INSTR(1%,COMMAND$," "+INP$+" ")+2%)/3% &
\	GOTO 2510 IF LOOP%=0% &
\	GOSUB 6010 &
\	GOTO 1010 IF INP$="%^C" &
\	DEFAULT$(LOOP%)=INP$ &
\	GOTO 2510 &

2600	!-----------------------------------------------INITIALIZATION &
	FOR LOOP%=1% TO LOOP.DATA% &
\		INP$='' &
\		INP$=DEFAULT$(LOOP%) IF OPT$='A' &
\		GOSUB 6030 &
\	NEXT LOOP% &
\	INITIALIZE$=" " &

2610	PRINT #CH%, CLRBOT$;FNP$(22%,1%);INITIALIZE$; &
		FNP$(21%,1%);"Field to initialize:"; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",2%),32%) &
\	GOTO 1010 IF INP$="%^C" &
\	IF INP$<>"" &
	THEN	LOOP%=(INSTR(1%,COMMAND$," "+INP$+" ")+2%)/3% &
\		INITIALIZE$=INITIALIZE$+INP$+" " IF &
			LOOP%<>0% AND (INSTR(1%,INITIALIZE$," "+INP$+" ") &
				+2%)/3%=0% &
\		GOTO 2610 &

2620	GOTO 1010 IF INITIALIZE$="" &
\	OPT$="C" &
\	OPTION$="INITIALIZE " &
\	FLAG%,CHNG.FLAG%=-1% &
\	GOTO 2660 &

2650	GOTO 18910 IF FNN%(FILE.CH%) &
\	KEY.POINTER%=FNR(FILE.CH%) &
\	DATA.POINTER%=FNR(FILE.CH%+1%) &

2660	GOSUB 6000 &
\	FOR I%=1% TO LEN(INITIALIZE$)/3% &
\		LOOP%=(INSTR(1%,COMMAND$,MID(INITIALIZE$,I%*3%-2%,3%))+2%)/3% &
\		GOSUB 6010 &
\		GOTO 1030 IF INP$="%^C" &

2680		IF INP$<>"" AND INP$<>"%ABORT" &
		THEN	V%=FNG%(-FILE.CH%,NUM1$(KEY.POINTER%))+ &
				FNG%(-FILE.CH%-1%,NUM1$(DATA.POINTER%)) &
\			V%=FNU%(FILE.CH%,CDJOUR.BUF$) IF LOOP%<>1% AND FNS%=0% &
\			V%=FNU%(-FILE.CH%,CDJOUR.BUF$) IF LOOP%=1% AND FNS%=0% &
\			IF FNS% &
			THEN	PRINT #CH%, FNP$(24%,1%);"Error";FNS%; &
					"while changing to file"; &
				FNP$(24%,55%);"Hit any key to continue"; &
\				INP$=FNINP$(CH%,128%," ",1%) &
\				GOTO 1010 &

2690	NEXT I% &
\	GOTO 2650 &

2700	!--------------------------------GLOBAL SEARCH AND REPLACE/DELETE &
	FOR LOOP%=1% TO LOOP.DATA% &
\		INP$='' &
\		GOSUB 6030 &
\	NEXT LOOP% &
\	OPT$="F" &
\	OPTION$="SEARCH FOR " &
\	SEARCH$=" " &
\	SEARCH$(LOOP%)="" FOR LOOP%=1% to LOOP.DATA% &

2710	PRINT #CH%, CLRBOT$;"Field to search for "; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",2%),32%) &
\	GOTO 1010 IF INP$="%^C" &
\	IF INP$<>"" &
	THEN	LOOP%=(INSTR(1%,COMMAND$," "+INP$+" ")+2%)/3% &
\		SEARCH$=SEARCH$+INP$+" " IF &
			LOOP%<>0% AND (INSTR(1%,SEARCH$," "+INP$+" ")+2%)/3%=0% &
\		GOSUB 6010 &
\		GOTO 1010 IF INP$="%^C" &
\		SEARCH$(LOOP%)=INP$ &
\		GOTO 2710 &

2730	OPT$="C" &
\	FLAG%,CHNG.FLAG%=-1% &

2740	GOTO 1010 IF SEARCH$="" &
\	PRINT #CH%, CLRBOT$;"COMMAND: Delete Replace "; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",1%),32%) &
\	GOTO 1010 IF INP$="%^C" &
\	GOTO 2760 IF INP$="D" &
\	GOTO 2800 IF INP$="R" &
\	GOTO 2740 &

2750	GOTO 18910 IF FNN%(FILE.CH%) &
\	KEY.POINTER%=FNR(FILE.CH%) &
\	DATA.POINTER%=FNR(FILE.CH%+1%) &

2760	PRINT #CH%, CLRBOT$;"Searching. . . "; &
\	LSET CDJOUR.BUF$=FNL$ &
\	SEARCH.FLAG%=0% &
\	FOR I%=1% TO LEN(SEARCH$)/3% &
\		LOOP%=(INSTR(1%,COMMAND$,MID(SEARCH$,I%*3%-2%,3%))+2%)/3% &
\		GOSUB 6040 &
\		GOTO 2770 IF SEARCH.FLAG%=-1% &
\	NEXT I% &
\	GOTO 2750 &

2770	GOSUB 6000 &

2780	PRINT #CH%, CLRBOT$;"Confirm deletion (Yes/No) "; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",1%),32%) &
\	GOTO 1030 IF INP$="%^C" &
\	IF INP$<>"Y" &
	THEN	PRINT #CH%, FNP$(24%,1%);CLRLIN$;"This record has "; B.ON$; &
			BLINK.ON$;"NOT ";G.OFF$; "been deleted."; &
			FNP$(24%,55%);"Hit any key to continue "; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		GOTO 1010 IF INP$="%^C" &
\		GOTO 2750 &

2790	IF FND%(FILE.CH%,"") &
	THEN	PRINT #CH%, FNP$(24%,1%);"Error";FNS%;"while deleting this "; &
			"record";FNP$(24%,55%);"Hit any key to continue"; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		GOTO 1010 &

2795	PRINT #CH%, FNP$(24%,1%);CLRLIN$;"This record has been deleted."; &
		FNP$(24%,55%);"Hit any key to continue "; &
\	INP$=FNINP$(CH%,128%," ",1%) &
\	GOTO 1010 IF INP$="%^C" &
\	GOTO 2750 &

2800	OPTION$="REPLACE " &
\	GOTO 2820 &

2810	GOTO 18910 IF FNN%(FILE.CH%) &
\	KEY.POINTER%=FNR(FILE.CH%) &
\	DATA.POINTER%=FNR(FILE.CH%+1%) &

2820	PRINT #CH%, CLRBOT$;"Searching. . . "; &
\	LSET CDJOUR.BUF$=FNL$ &
\	SEARCH.FLAG%=0% &
\	FOR I%=1% TO LEN(SEARCH$)/3% &
\		LOOP%=(INSTR(1%,COMMAND$,MID(SEARCH$,I%*3%-2%,3%))+2%)/3% &
\		GOSUB 6040 &
\		GOTO 2825 IF SEARCH.FLAG% &
\	NEXT I% &
\	GOTO 2810 &

2825	GOSUB 6000 &

2830	PRINT #CH%, CLRBOT$; "Change:"; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",2%),32%) &
\	GOTO 1010 IF INP$="%^C" &
\	GOTO 2810 IF INP$="" &
\	LOOP%=(INSTR(1%,COMMAND$," "+INP$+" ")+2%)/3% &
\	GOTO 2830 IF LOOP%=0% &
\	GOSUB 6010 &
\	GOTO 1010 IF INP$="%^C" &
\	GOTO 2830 IF INP$="%ABORT" &

2840	IF INP$<>"" AND INP$<>"%ABORT" &
	THEN	V%=FNG%(-FILE.CH%,NUM1$(KEY.POINTER%))+ &
				FNG%(-FILE.CH%-1%,NUM1$(DATA.POINTER%)) &
\			V%=FNU%(FILE.CH%,CDJOUR.BUF$) IF LOOP%<>1% AND FNS%=0% &
\			V%=FNU%(-FILE.CH%,CDJOUR.BUF$) IF LOOP%=1% AND FNS%=0% &
\			IF FNS% &
			THEN	PRINT #CH%, FNP$(24%,1%);"Error";FNS%; &
					"while changing to file"; &
				FNP$(24%,55%);"Hit any key to continue"; &
\				INP$=FNINP$(CH%,128%," ",1%) &
\				GOTO 1010
2850	GOTO 2830 &

6000	!------------------------------------------------DATA ENTRY SECTION &
	RETURN IF KEY.POINTER%=0% &
\	V%=FNG%(-FILE.CH%,NUM1$(KEY.POINTER%))+ &
		FNG%(-FILE.CH%-1%,NUM1$(DATA.POINTER%)) &
\	LSET CDJOUR.BUF$=FNL$+"" &
\	PAYABLE=CVT$F(CDJ.AMT$)-CVT$F(CDJ.DISC$)+CVT$F(CDJ.DISCLOST$) &
\	FOR LOOP%=1% TO LOOP.DATA% &
\		ON LOOP% GOSUB	6220, 6240, 6260, 6280, 6300, 6320, 6340, &
				6360, 6380, 6400, 6420, 6440, 6460, 6480  &
\	NEXT LOOP% &
\	RETURN &

6010	ON LOOP% GOSUB	6210, 6230, 6250, 6270, 6290, 6310, 6330, &
			6350, 6370, 6390, 6410, 6430, 6450, 6470  &
\	RETURN &
		! INPUT DATA &

6020	TEMP.KEY$="" &
\	GOSUB 6210 &
\	RETURN IF INP$="" OR INP$="%^C" &
\	TEMP.KEY$=CDJ.VENNUM$ &
\	GOSUB 6230 &
\	RETURN IF INP$="%^C" &
\	TEMP.KEY$=TEMP.KEY$+CDJ.VOUNUM$ &
\	RETURN &
		! SEARCH KEY &

6030	ON LOOP% GOSUB	6217, 6237, 6257, 6277, 6297, 6317, 6337, &
			6357, 6377, 6397, 6417, 6437, 6457, 6477  &
\	RETURN &
		! DEFAULT &

6040	SEARCH.FLAG%=-1% IF CDJ.VENNUM$=SEARCH$(LOOP%) AND LOOP%=1% &
\	SEARCH.FLAG%=-1% IF CDJ.VOUNUM$=SEARCH$(LOOP%) AND LOOP%=2% &
\	SEARCH.FLAG%=-1% IF FNRDATE$(CDJ.VOUDAT$)=SEARCH$(LOOP%) AND LOOP%=3% &
\	SEARCH.FLAG%=-1% IF CDJ.INVOIC$=SEARCH$(LOOP%) AND LOOP%=4% &
\	SEARCH.FLAG%=-1% IF FNRDATE$(CDJ.INVDAT$)=SEARCH$(LOOP%) AND LOOP%=5% &
\	SEARCH.FLAG%=-1% IF CDJ.CKDESC$=SEARCH$(LOOP%) AND LOOP%=6% &
\	SEARCH.FLAG%=-1% IF CVT$F(CDJ.AMT$)=VAL(SEARCH$(LOOP%)) AND LOOP%=7% &
\	SEARCH.FLAG%=-1% IF CVT$F(CDJ.DISC$)=VAL(SEARCH$(LOOP%)) AND LOOP%=8% &
\	SEARCH.FLAG%=-1% IF CVT$F(CDJ.DIS$)=VAL(SEARCH$(LOOP%)) AND LOOP%=9% &
\	SEARCH.FLAG%=-1% IF CDJ.APACCT$=SEARCH$(LOOP%) AND LOOP%=10% &
\	SEARCH.FLAG%=-1% IF CDJ.ACCT$=SEARCH$(LOOP%) AND LOOP%=11% &
\	SEARCH.FLAG%=-1% IF CDJ.DISCACCT$=SEARCH$(LOOP%) AND LOOP%=12% &
\	SEARCH.FLAG%=-1% IF CDJ.CKNUM$=SEARCH$(LOOP%) AND LOOP%=13% &
\	SEARCH.FLAG%=-1% IF FNRDATE$(CDJ.CKDAT$)=SEARCH$(LOOP%) AND LOOP%=14% &
\	RETURN &
		! SEARCH FOR MATCH &

6200	!-----------------------------------------------DATA ENTRY SECTION &

6210	F.LEN%=LEN(CDJ.VENNUM$) &
\	PRINT #CH%, FNP$(2%,16%);R.ON$;B.ON$; &
\	PRINT #CH%, SPACE$(F.LEN%); IF CHNG.FLAG%=0% &
\	PRINT #CH%, CDJ.VENNUM$; IF CHNG.FLAG% &
\	PRINT #CH%, G.OFF$;CLRBOT$;OPTION$;"ALPHA: ";B.ON$; &
\	INP$=FNINP$(CH%,0%,"_",F.LEN%) &
\	INP$=DEFAULT$(LOOP%) IF INP$="" AND OPT$<>"F" &
\	GOTO 6210 IF INP$="" AND CHNG.FLAG%=0% AND OPT$<>"F" &
\	GOTO 6220 IF INP$="%^C" &

6212	IF OPT$="C" AND INP$<>"" &
	THEN	IF FNG%(FILE.CH%,LEFT(INP$+SPACE$(6%),6%)+CDJ.VOUNUM$)=0% &
		THEN	PRINT #CH%, CLRBOT$;FNP$(24%,1%);INP$;CDJ.VOUNUM; &
				" is a duplicate key"; &
				FNP$(21%,1%);"COMMAND: Resume Exit "; &
\			INP$=FNINP$(CH%,128%," ",1%) &
\			PRINT #CH%, FNP$(2%,18%);B.ON$;CDJ.VENNUM$;G.OFF$; &
\			GOTO 6210 IF INP$="R" &
\			INP$="%ABORT" &
\			RETURN &

6217	LSET CDJ.VENNUM$=INP$ IF INP$<>"" OR CHNG.FLAG%=0% &

6220	PRINT #CH%, FNP$(2%,16%);B.ON$;CDJ.VENNUM$;G.OFF$; &
\	LSET AP.VENNAM$,AP.VENADD$,AP.VENCST$,AP.VENZIP$="" &
\	V%=FNG%(VENDES.CH%,CDJ.VENNUM$) &
\	LSET APVEND.BUF$=FNL$+"" IF FNS%=0% &
\	PRINT #CH%, FNP$(3%,16%);LEFT(AP.VENNAM$,25%); &
		FNP$(4%,16%);LEFT(AP.VENADD$,25%); &
		FNP$(5%,16%);LEFT(AP.VENCST$,25%); &
		FNP$(6%,16%);LEFT(AP.VENZIP$,25%); &
\	RETURN &

6230	F.LEN%=LEN(CDJ.VOUNUM$) &
\	PRINT #CH%, FNP$(9%,16%);R.ON$;B.ON$; &
\	PRINT #CH%, SPACE$(F.LEN%); IF CHNG.FLAG%=0% &
\	PRINT #CH%, CDJ.VOUNUM$; IF CHNG.FLAG% &
\	PRINT #CH%, G.OFF$;CLRBOT$;OPTION$;"ALPHA: ";B.ON$; &
\	INP$=FNINP$(CH%,0%,"_",F.LEN%) &
\	INP$=DEFAULT$(LOOP%) IF INP$="" AND OPT$<>"F" &
\	GOTO 6240 IF INP$="%^C" &

6232	IF FLAG%<>0% AND INP$<>"" &
	THEN	IF FNG%(FILE.CH%,CDJ.VENNUM$+LEFT(INP$+SPACE$(6%),6%))=0% &
		THEN	PRINT #CH%, CLRBOT$;FNP$(24%,1%);CDJ.VENNUM$;INP$; &
				" is a duplicate key"; &
				FNP$(21%,1%);"COMMAND: Resume Exit "; &
\			INP$=FNINP$(CH%,128%," ",1%) &
\			PRINT #CH%, FNP$(9%,16%);B.ON$;CDJ.VOUNUM$;G.OFF$; &
\			GOTO 6310 IF INP$="R" &
\			INP$="%ABORT" &
\			RETURN &

6237	LSET CDJ.VOUNUM$=INP$ IF INP$<>"" OR CHNG.FLAG%=0% &

6240	PRINT #CH%, FNP$(9%,16%);B.ON$;CDJ.VOUNUM$;G.OFF$; &
\	RETURN &

6250	F.LEN%=6% &
\	PRINT #CH%, FNP$(10%,16%);R.ON$;B.ON$; &
\	PRINT #CH%, SPACE$(F.LEN%+2%); IF CHNG.FLAG%=0% &
\	PRINT #CH%, FNDATE.PRINT$(FNRDATE$(CDJ.VOUDAT$)); IF CHNG.FLAG% &
\	PRINT #CH%, G.OFF$;CLRBOT$;OPTION$;"ALPHA: ";B.ON$; &
\	INP$=FNINP$(CH%,0%,"_",F.LEN%) &
\	INP$=DEFAULT$(LOOP%) IF INP$="" AND OPT$<>"F" &
\	GOTO 6260 IF INP$="%^C" &
\	IF INP$<>"" OR CHNG.FLAG%=1% &
	THEN	TEST$=FNDATE.CHECK$(INP$) &
\		GOTO 6260 IF INP$="%^C" &
\		GOTO 6250 IF TEST$<>"" &

6257	LSET CDJ.VOUDAT$=FNSDATE$(INP$) IF INP$<>"" OR CHNG.FLAG%=1% &

6260	PRINT #CH%, FNP$(10%,16%);B.ON$; &
		FNDATE.PRINT$(FNRDATE$(CDJ.VOUDAT$));G.OFF$; &
\	RETURN &

6270	F.LEN%=LEN(CDJ.INVOIC$) &
\	PRINT #CH%, FNP$(11%,16%);R.ON$;B.ON$; &
\	PRINT #CH%, SPACE$(F.LEN%); IF CHNG.FLAG%=0% &
\	PRINT #CH%, CDJ.INVOIC$; IF CHNG.FLAG% &
\	PRINT #CH%, G.OFF$;CLRBOT$;OPTION$;"ALPHA: ";B.ON$; &
\	INP$=FNINP$(CH%,0%,"_",F.LEN%) &
\	INP$=DEFAULT$(LOOP%) IF INP$="" &
\	GOTO 6280 IF INP$="%^C" &

6277	LSET CDJ.INVOIC$=INP$ IF INP$<>"" OR CHNG.FLAG%=0% &

6280	PRINT #CH%, FNP$(11%,16%);B.ON$;CDJ.INVOIC$;G.OFF$; &
\	RETURN &

6290	F.LEN%=6% &
\	PRINT #CH%, FNP$(12%,16%);R.ON$;B.ON$; &
\	PRINT #CH%, SPACE$(F.LEN%+2%); IF CHNG.FLAG%=0% &
\	PRINT #CH%, FNDATE.PRINT$(FNRDATE$(CDJ.INVDAT$)); IF CHNG.FLAG% &
\	PRINT #CH%, G.OFF$;CLRBOT$;OPTION$;"ALPHA: ";B.ON$; &
\	INP$=FNINP$(CH%,0%,"_",F.LEN%) &
\	INP$=DEFAULT$(LOOP%) IF INP$="" AND OPT$<>"F" &
\	GOTO 6260 IF INP$="%^C" &
\	IF INP$<>"" OR CHNG.FLAG%=1% &
	THEN	TEST$=FNDATE.CHECK$(INP$) &
\		GOTO 6300 IF INP$="%^C" &
\		GOTO 6290 IF TEST$<>"" &

6297	LSET CDJ.INVDAT$=FNSDATE$(INP$) IF INP$<>"" OR CHNG.FLAG%=1% &

6300	PRINT #CH%, FNP$(12%,16%);B.ON$; &
		FNDATE.PRINT$(FNRDATE$(CDJ.INVDAT$));G.OFF$; &
\	RETURN &

6310	F.LEN%=LEN(CDJ.CKDESC$) &
\	PRINT #CH%, FNP$(13%,16%);R.ON$;B.ON$; &
\	PRINT #CH%, SPACE$(F.LEN%); IF CHNG.FLAG%=0% &
\	PRINT #CH%, CDJ.CKDESC$; IF CHNG.FLAG% &
\	PRINT #CH%, G.OFF$;CLRBOT$;OPTION$;"ALPHA: ";B.ON$; &
\	INP$=FNINP$(CH%,0%,"_",F.LEN%) &
\	INP$=DEFAULT$(LOOP%) IF INP$="" AND OPT$<>"F" &
\	GOTO 6320 IF INP$="%^C" &

6317	RSET CDJ.CKDESC$=INP$ IF INP$<>"" OR CHNG.FLAG%=0% &

6320	PRINT #CH%, FNP$(13%,16%);B.ON$;CDJ.CKDESC$;G.OFF$; &
\	RETURN &

6330	F.LEN%=10% &
\	PRINT #CH%, FNP$(2%,58%);R.ON$;B.ON$; &
\	PRINT #CH%, SPACE$(F.LEN%+3%); IF CHNG.FLAG%=0% &
\	PRINT #CH%  USING "##,###,###.##",CVT$F(CDJ.AMT$); IF CHNG.FLAG% &
\	PRINT #CH%, G.OFF$;CLRBOT$;OPTION$;"VALUE: ";B.ON$; &
\	INP$=FNINP$(CH%,0%,"_",F.LEN%) &
\	INP$=DEFAULT$(LOOP%) IF INP$="" AND OPT$<>"F" &
\	GOTO 6340 IF INP$="%^C" &
\	TEMP%=100% &
\	TEMP%=1% IF INSTR(1%,INP$,".") &
\	INP$=NUM1$(VAL(INP$)/TEMP%) &
\	INP$=INP$+"." IF INSTR(1%,INP$,".")=0% &

6337	PAYABLE=PAYABLE-CVT$F(CDJ.AMT$) IF INP$<>"" OR CHNG.FLAG%=0% &
\	LSET CDJ.AMT$=CVTF$(VAL(INP$)) IF INP$<>"" OR CHNG.FLAG%=0% &
\	PAYABLE=PAYABLE+CVT$F(CDJ.AMT$) IF INP$<>"" OR CHNG.FLAG%=0% &

6340	PRINT #CH%, FNP$(2%,58%);B.ON$; &
\	PRINT #CH%  USING "##,###,###.##",CVT$F(CDJ.AMT$); &
\	PRINT #CH%, G.OFF$; &
\	PRINT #CH%, FNP$(5%,58%); &
\	PRINT #CH%  USING "##,###,###.##",PAYABLE; &
\	RETURN &

6350	F.LEN%=10% &
\	PRINT #CH%, FNP$(3%,58%);R.ON$;B.ON$; &
\	PRINT #CH%, SPACE$(F.LEN%+3%); IF CHNG.FLAG%=0% &
\	PRINT #CH%  USING "##,###,###.##",CVT$F(CDJ.DISC$); IF CHNG.FLAG% &
\	PRINT #CH%, G.OFF$;CLRBOT$;OPTION$;"VALUE: ";B.ON$; &
\	INP$=FNINP$(CH%,0%,"_",F.LEN%) &
\	INP$=DEFAULT$(LOOP%) IF INP$="" AND OPT$<>"F" &
\	GOTO 6360 IF INP$="%^C" &
\	TEMP%=100% &
\	TEMP%=1% IF INSTR(1%,INP$,".") &
\	INP$=NUM1$(VAL(INP$)/TEMP%) &
\	INP$=INP$+"." IF INSTR(1%,INP$,".")=0% &

6357	PAYABLE=PAYABLE+CVT$F(CDJ.DISC$) IF INP$<>"" OR CHNG.FLAG%=0% &
\	LSET CDJ.DISC$=CVTF$(VAL(INP$)) IF INP$<>"" OR CHNG.FLAG%=0% &
\	PAYABLE=PAYABLE-CVT$F(CDJ.DISC$) IF INP$<>"" OR CHNG.FLAG%=0% &

6360	PRINT #CH%, FNP$(3%,58%);B.ON$; &
\	PRINT #CH%  USING "##,###,###.##",CVT$F(CDJ.DISC$); &
\	PRINT #CH%, G.OFF$; &
\	PRINT #CH%, FNP$(5%,58%); &
\	PRINT #CH%  USING "##,###,###.##",PAYABLE; &
\	RETURN &

6370	F.LEN%=10% &
\	PRINT #CH%, FNP$(4%,58%);R.ON$;B.ON$; &
\	PRINT #CH%, SPACE$(F.LEN%+3%); IF CHNG.FLAG%=0% &
\	PRINT #CH%  USING "##,###,###.##",CVT$F(CDJ.DISCLOST$); IF CHNG.FLAG% &
\	PRINT #CH%, G.OFF$;CLRBOT$;OPTION$;"VALUE: ";B.ON$; &
\	INP$=FNINP$(CH%,0%,"_",F.LEN%) &
\	INP$=DEFAULT$(LOOP%) IF INP$="" AND OPT$<>"F" &
\	GOTO 6380 IF INP$="%^C" &
\	TEMP%=100% &
\	TEMP%=1% IF INSTR(1%,INP$,".") &
\	INP$=NUM1$(VAL(INP$)/TEMP%) &
\	INP$=INP$+"." IF INSTR(1%,INP$,".")=0% &

6377	PAYABLE=PAYABLE-CVT$F(CDJ.DISCLOST$) IF INP$<>"" OR CHNG.FLAG%=0% &
\	LSET CDJ.DISCLOST$=CVTF$(VAL(INP$)) IF INP$<>"" OR CHNG.FLAG%=0% &
\	PAYABLE=PAYABLE+CVT$F(CDJ.DISCLOST$) IF INP$<>"" OR CHNG.FLAG%=0% &

6380	PRINT #CH%, FNP$(4%,58%);B.ON$; &
\	PRINT #CH%  USING "##,###,###.##",CVT$F(CDJ.DISCLOST$); &
\	PRINT #CH%, G.OFF$; &
\	PRINT #CH%, FNP$(5%,58%); &
\	PRINT #CH%  USING "##,###,###.##",PAYABLE; &
\	RETURN &

6390	F.LEN%=LEN(CDJ.APACCT$) &
\	PRINT #CH%, FNP$(9%,58%);R.ON$;B.ON$; &
\	PRINT #CH%, SPACE$(F.LEN%); IF CHNG.FLAG%=0% &
\	PRINT #CH%, CDJ.APACCT$; IF CHNG.FLAG% &
\	PRINT #CH%, G.OFF$;CLRBOT$;OPTION$;"ALPHA: ";B.ON$; &
\	INP$=FNINP$(CH%,0%,"_",F.LEN%) &
\	INP$=DEFAULT$(LOOP%) IF INP$="" AND OPT$<>"F" &
\	GOTO 6400 IF INP$="%^C" &
\	IF INP$<>"" AND CHART.FLAG%=0% &
	THEN	IF FNG%(CHART.CH%,SPACE$(8%-LEN(INP$))+INP$) &
		THEN	PRINT #CH%, FNP$(24%,1%);CLRLIN$;G.OFF$; &
			"Undefined account #.  Confirm entry (y/n)"; &
\			INP1$=CVT$$(FNINP$(CH%,128%," ",1%),-1%) &
\			RETURN IF INP1$="%^C" &
\			GOTO 6390 IF INP1$<>'Y' &

6397	RSET CDJ.APACCT$=INP$ IF INP$<>"" OR CHNG.FLAG%=0% &

6400	PRINT #CH%, FNP$(9%,58%);B.ON$;CDJ.APACCT$;G.OFF$; &
\	RETURN &

6410	F.LEN%=LEN(CDJ.ACCT$) &
\	PRINT #CH%, FNP$(10%,58%);R.ON$;B.ON$; &
\	PRINT #CH%, SPACE$(F.LEN%); IF CHNG.FLAG%=0% &
\	PRINT #CH%, CDJ.ACCT$; IF CHNG.FLAG% &
\	PRINT #CH%, G.OFF$;CLRBOT$;OPTION$;"ALPHA: ";B.ON$; &
\	INP$=FNINP$(CH%,0%,"_",F.LEN%) &
\	INP$=DEFAULT$(LOOP%) IF INP$="" &
\	GOTO 6420 IF INP$="%^C" &
\	IF INP$<>"" AND CHART.FLAG%=0% &
	THEN	IF FNG%(CHART.CH%,SPACE$(8%-LEN(INP$))+INP$) &
		THEN	PRINT #CH%, FNP$(24%,1%);CLRLIN$;G.OFF$; &
			"Undefined account #.  Confirm entry (y/n)"; &
\			INP1$=CVT$$(FNINP$(CH%,128%," ",1%),-1%) &
\			RETURN IF INP1$="%^C" &
\			GOTO 6410 IF INP1$<>'Y' &

6417	RSET CDJ.ACCT$=INP$ IF INP$<>"" OR CHNG.FLAG%=0% &

6420	PRINT #CH%, FNP$(10%,58%);B.ON$;CDJ.ACCT$;G.OFF$; &
\	RETURN &

6430	F.LEN%=LEN(CDJ.DISCACCT$) &
\	PRINT #CH%, FNP$(11%,58%);R.ON$;B.ON$; &
\	PRINT #CH%, SPACE$(F.LEN%); IF CHNG.FLAG%=0% &
\	PRINT #CH%, CDJ.DISCACCT$; IF CHNG.FLAG% &
\	PRINT #CH%, G.OFF$;CLRBOT$;OPTION$;"VALUE: ";B.ON$; &
\	INP$=FNINP$(CH%,0%,"_",F.LEN%) &
\	INP$=DEFAULT$(LOOP%) IF INP$="" AND OPT$<>"F" &
\	GOTO 6440 IF INP$="%^C" &
\	IF INP$<>"" AND CHART.FLAG%=0% &
	THEN	IF FNG%(CHART.CH%,SPACE$(8%-LEN(INP$))+INP$) &
		THEN	PRINT #CH%, FNP$(24%,1%);CLRLIN$;G.OFF$; &
			"Undefined account #.  Confirm entry (y/n)"; &
\			INP1$=CVT$$(FNINP$(CH%,128%," ",1%),-1%) &
\			RETURN IF INP1$="%^C" &
\			GOTO 6430 IF INP1$<>'Y' &

6437	RSET CDJ.DISCACCT$=INP$ IF INP$<>"" OR CHNG.FLAG%=0% &

6440	PRINT #CH%, FNP$(11%,58%);B.ON$;CDJ.DISCACCT$;G.OFF$; &
\	RETURN &

6450	F.LEN%=LEN(CDJ.CKNUM$) &
\	PRINT #CH%, FNP$(13%,58%);R.ON$;B.ON$; &
\	PRINT #CH%, SPACE$(F.LEN%); IF CHNG.FLAG%=0% &
\	PRINT #CH%, CDJ.CKNUM$; IF CHNG.FLAG% &
\	PRINT #CH%, G.OFF$;CLRBOT$;OPTION$;"ALPHA: ";B.ON$; &
\	INP$=FNINP$(CH%,0%,"_",F.LEN%) &
\	INP$=DEFAULT$(LOOP%) IF INP$="" &
\	GOTO 6460 IF INP$="%^C" &

6457	RSET CDJ.CKNUM$=INP$ IF INP$<>"" OR CHNG.FLAG%=0% &

6460	PRINT #CH%, FNP$(13%,58%);B.ON$;CDJ.CKNUM$;G.OFF$; &
\	RETURN &

6470	F.LEN%=6% &
\	PRINT #CH%, FNP$(14%,58%);R.ON$;B.ON$; &
\	PRINT #CH%, SPACE$(F.LEN%+2%); IF CHNG.FLAG%=0% &
\	PRINT #CH%, FNDATE.PRINT$(FNRDATE$(CDJ.CKDAT$)); IF CHNG.FLAG% &
\	PRINT #CH%, G.OFF$;CLRBOT$;OPTION$;"ALPHA: ";B.ON$; &
\	INP$=FNINP$(CH%,0%,"_",F.LEN%) &
\	INP$=DEFAULT$(LOOP%) IF INP$="" &
\	GOTO 6480 IF INP$="%^C" &
\	IF INP$<>"" OR CHNG.FLAG%=1% &
	THEN	TEST$=FNDATE.CHECK$(INP$) &
\		GOTO 6300 IF INP$="%^C" &
\		GOTO 6290 IF TEST$<>"" &

6477	LSET CDJ.CKDAT$=FNSDATE$(INP$) IF INP$<>"" OR CHNG.FLAG%=1% &

6480	PRINT #CH%, FNP$(14%,58%);B.ON$; &
		FNDATE.PRINT$(FNRDATE$(CDJ.CKDAT$));G.OFF$; &
\	RETURN &

17000	!-----------------------------------------------VIEW SECTION &
	PRINT #CH%, CLSCN$;COLM.ON$;SCROLL.REGION$; &
		R.ON$;SPACE$(132%);FNP$(20%,1%); SPACE$(132%); &
\	PRINT #CH%, FNP$(I%,1%);" ";FNP$(I%,132%);" "; FOR I%=2% TO 19% &
\	PRINT #CH%, G.OFF$;FNP$(2%,2%);"VENDOR";FNP$(3%,4%);"#"; &
		FNP$(3%,9%);"NAME"; &
		FNP$(2%,27%);"VOUCHER";FNP$(3%,29%);"#"; &
		FNP$(2%,35%);"INVOICE";FNP$(3%,37%);"#"; &
		FNP$(2%,51%);"INVOICE";FNP$(3%,51%);"DATE"; &
		FNP$(2%,60%);"P.O.";FNP$(3%,61%);"#"; &
\	PRINT #CH%,FNP$(3%,67%);"DESCRIPTION"; &
		FNP$(3%,82%);"AMOUNT"; &
		FNP$(3%,93%);"DISC"; &
		FNP$(3%,104%);"NET"; &
		FNP$(2%,109%);"CHECK"; &
		FNP$(3%,111%);"#"; &
		FNP$(2%,116%);"CHECK"; &
		FNP$(3%,116%);"DATE"; &

17005	PRINT #CH%, R.ON$;LDS.ON$;FNP$(4%,2%);STRING$(130%,113%); &
		FNP$(4%,8%);CHR$(110%);FNP$(4%,26%);CHR$(110%); &
		FNP$(4%,34%);CHR$(110%);FNP$(4%,50%);CHR$(110%); &
		FNP$(4%,59%);CHR$(110%);FNP$(4%,66%);CHR$(110%); &
		FNP$(4%,77%);CHR$(110%);FNP$(4%,88%);CHR$(110%); &
		FNP$(4%,97%);CHR$(110%);FNP$(4%,108%);CHR$(110%); &
		FNP$(4%,115%);CHR$(110%);FNP$(4%,124%);CHR$(110%); &
		USASCII$;G.OFF$; &
\	PRINT #CH%, R.ON$;LDS.ON$;FNP$(I%,8%);CHR$(120%); &
		FNP$(I%,26%);CHR$(120%);FNP$(I%,34%);CHR$(120%); &
		FNP$(I%,50%);CHR$(120%);FNP$(I%,59%);CHR$(120%); &
		FNP$(I%,66%);CHR$(120%);FNP$(I%,77%);CHR$(120%); &
		FNP$(I%,88%);CHR$(120%);FNP$(I%,97%);CHR$(120%); &
		FNP$(I%,108%);CHR$(120%);FNP$(I%,115%);CHR$(120%); &
		FNP$(I%,124%);CHR$(120%); USASCII$;G.OFF$; &
			FOR I%=2% TO 3% &
\	DISPLAY.LINE%=0% &
\	PRINT.LINE%=5% &

17010	V%=FNG%(-FILE.CH%,NUM1$(KEY.POINTER%))+ &
		FNG%(-FILE.CH%-1%,NUM1$(DATA.POINTER%)) &
\	GOTO 17105 &

17100	IF FNN%(FILE.CH%) &
	THEN	17200 &

17105	LSET CDJOUR.BUF$=FNL$ &
\	AMOUNT=CVT$F(CDJ.AMT$) &
\	DISC=CVT$F(CDJ.DISC$) &
\	LOST=CVT$F(CDJ.DISCLOST$) &
\	V%=FNG%(VENDES.CH%,CDJ.VENNUM$) &
\	LSET AP.VENNAM$=STRING$(30%,42%) &
\	LSET APVEND.BUF$=FNL$+"" IF FNS%=0% &
\	PRINT #CH%, FNP$(PRINT.LINE%,2%);CDJ.VENNUM$; &
		FNP$(PRINT.LINE%,9%);LEFT(AP.VENNAM$,18%); &
		FNP$(PRINT.LINE%,27%);CDJ.VOUNUM$; &
		FNP$(PRINT.LINE%,35%);CDJ.INVOIC$; &
		FNP$(PRINT.LINE%,51%);FNRDATE$(CDJ.INVDAT$); &
		FNP$(PRINT.LINE%,60%);CDJ.PONUM$; &
		FNP$(PRINT.LINE%,67%);LEFT(CDJ.CKDESC$,10%); &
		FNP$(PRINT.LINE%,109%);CDJ.CKNUM$; &
		FNP$(PRINT.LINE%,116%);FNDATE.PRINT$(FNRDATE$(CDJ.CKDAT$)); &
		FNP$(PRINT.LINE%,78%); &
\	PRINT #CH%  USING "#######.##",AMOUNT; &
\	PRINT #CH%, FNP$(PRINT.LINE%,89%); &
\	PRINT #CH%  USING "#####.##",DISC-LOST; &
\	PRINT #CH%, FNP$(PRINT.LINE%,98%); &
\	PRINT #CH%  USING "#######.##",AMOUNT-DISC+LOST; &
\	TOT.AMT=TOT.AMT+AMOUNT &
\	TOT.DIS=TOT.DIS+DISC &
\	TOT.LOS=TOT.LOS+LOST &
\	I%=PRINT.LINE% &
\	PRINT #CH%, R.ON$;LDS.ON$;FNP$(I%,8%);CHR$(120%); &
		FNP$(I%,26%);CHR$(120%);FNP$(I%,34%);CHR$(120%); &
		FNP$(I%,50%);CHR$(120%);FNP$(I%,59%);CHR$(120%); &
		FNP$(I%,66%);CHR$(120%);FNP$(I%,77%);CHR$(120%); &
		FNP$(I%,88%);CHR$(120%);FNP$(I%,97%);CHR$(120%); &
		FNP$(I%,108%);CHR$(120%);FNP$(I%,115%);CHR$(120%); &
		FNP$(I%,124%);CHR$(120%); USASCII$;G.OFF$; &
\	PRINT.LINE%=PRINT.LINE%+1% IF PRINT.LINE%<19% &

17110	IF DISPLAY.LINE%>13% &
	THEN	PRINT #CH%, FNP$(24%,105%);"Hit any key to continue "; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		GOTO 1010 IF INP$="%^C" &
\		DISPLAY.LINE%=0% &
\		PRINT #CH%, CLRBOT$;FNP$(PRINT.LINE%,1%); &

17120	PRINT #CH% &
\	DISPLAY.LINE%=DISPLAY.LINE%+1% &
\	IF PRINT.LINE%=19% &
	THEN	PRINT #CH%, FNP$(PRINT.LINE%,1%);R.ON$;" "; &
			FNP$(PRINT.LINE%,132%);" ";G.OFF$; &

17190	GOTO 17100 &

17200	IF DISPLAY.LINE%>0% &
	THEN	PRINT #CH%, FNP$(24%,1%);"End of file.  "; &
			FNP$(24%,105%);"Hit any key to continue "; &
\		INP$=FNINP$(CH%,128%," ",1%) &

17210	V%=FNG%(-FILE.CH%,NUM1$(KEY.POINTER%))+ &
		FNG%(-FILE.CH%-1%,NUM1$(DATA.POINTER%)) &
\	PRINT #CH%, FNP$(1%,1%);SCROLL.ON$;COLM.OFF$; &
\	GOTO 1010 &

18910	!----------------------------------------------END OF FILE &
	PRINT #CH%, CLRBOT$;FNP$(24%,1%);"End of file has been reached."; &
		FNP$(24%,55%);"Hit any key to continue "; &
\	INP$=FNINP$(CH%,128%," ",1%) &
\	GOTO 1000 &

19000	!-------------------------------------------------ERROR TRAPPING &
	IF ERR=28% &
	THEN	JUNK%=CTRLC &
\		IF ERL<1000% &
		THEN	OPT$="Q" &
\			RESUME 1040 &

19010	IF ERR=28% &
	THEN	V%=FNG%(-FILE.CH%,NUM1$(KEY.POINTER%))+ &
			FNG%(-FILE.CH%-1%,NUM1$(DATA.POINTER%)) &
\		RESUME 1010 &

19020	IF ERR=52% &
	THEN	RESUME &

19030	IF ERR=11% &
	THEN	RESUME 1000 &

19040	IF ERL=400% &
	THEN	RESUME 450 &

19900	ON ERROR GOTO 0 &

30000	DEF FNINP$(CHN%,KYP%,FILLCHAR$,INPUTLEN%) &
\		PRINT #CHN%, STRING$(INPUTLEN%,ASCII(FILLCHAR$)); &
			  STRING$(INPUTLEN%,8%); &
\		PRINT #CHN%, RECORD 256%, CHR$(KYP%+INPUTLEN%)+FILLCHAR$; &
\		GET #CHN% &
\		FIELD #CHN%, RECOUNT AS BUFFER$ &
\		IF ASCII(BUFFER$)=128% OR ASCII(BUFFER$)=8% &
			OR ASCII(BUFFER$)=23% &
		THEN	BUFFER$="%END" IF MID(BUFFER$,2%,4%)="[21~" &
				OR ASCII(BUFFER$)=8% &
\			BUFFER$="%ABORT" IF MID(BUFFER$,2%,4%)="[19~" &
				OR ASCII(BUFFER$)=23% &

30010		BUFFER$="%^C" IF ASCII(BUFFER$)=3% &
\		FNINP$=CVT$$(BUFFER$,4%) &
\	FNEND &

30050	  !------------------------------------------------------------ &
	  ! A N S I   D I R E C T   C U R S O R   A D D R E S S I N G &
	  !------------------------------------------------------------ &
	  DEF*FNP$(ROW%,COL%)=ESC$+"["+NUM1$(ROW%)+";"+NUM1$(COL%)+"H" &

30060	!------------------------------------------CHECK DATE &
	DEF*FNDATE.CHECK$(Y$) &
\		Y$=XLATE(Y$,STRING$(48%,0%)+"0123456789") &
\		IF LEN(Y$)=6% AND VAL(LEFT(Y$,2%))<13% AND &
			VAL(MID(Y$,3%,2%))<32% &
		THEN	Y$="" &
			ELSE	PRINT #CH%, FNP$(24%,1%);CLRLIN$;G.OFF$; &
					"Invalid date format.  Please re-enter."; &
					FNP$(24%,55%);"Hit any key to continue "; &
\				Y$=FNINP$(CH%,128%," ",1%) &
\				Y$="BAD" IF Y$="" &

30070		FNDATE.CHECK$=Y$ &
\	FNEND &

30080	!-----------------------------------------PRINT A DATE &
	DEF*FNDATE.PRINT$(Y$)= &
		LEFT(Y$,2%)+"/"+MID(Y$,3%,2%)+"/"+RIGHT(Y$,5%) &

30120	DEF FNSDATE$(Y$)=CVT%$(VAL(MID(Y$,3%,2%))+VAL(LEFT(Y$,2%))*32%+ &
		FNI%(VAL(RIGHT(Y$,5%)))*512%) &

30130	DEF FNI%(Y)=Y &

30140	DEF FNRDATE$(Y$) &
\		Y%=CVT$%(Y$) &
\		FNRDATE$=RIGHT(NUM1$((Y% AND 480%)/32%+100%),2%)+ &
			RIGHT(NUM1$((Y% AND 31%)+100%),2%)+ &
			RIGHT(NUM1$((SWAP%(Y%) AND 254%)/2%+100%),2%) &
\	FNEND &

32767	END &


