10	!---------------------------------------------------------------&
	!	ACCOUNTS PAYABLE SYSTEM		-Purchases Journal	&
	!	PJ Voucher Writer (Old System)				&
	!								&
	!	PJRVCH.B2S	V1.0	June 1985			&
	!								&
	! Author - Randall Beard, Computer Management Center, Inc.	&
	!								&
	! Files-PPRINT.DAT	  -ISAM	Printing Information		&
	!	APJ(Btch ID+#).DAT-ISAM	Purchases Journal Header 	&
	!	AP1(Btch ID+#).DAT-ISAM	Purchases Journal Line		&
	!	VENDES.DAT	  -ISAM	Vendor Description		&
	!---------------------------------------------------------------&
	!		      Copyright (c) 1984 by 			&
	!	  Computer Management Center, Idaho Falls, Idaho	&
	! 								&
	! This software is furnished under a license and may be used	&
	! and copied only in accordance with the terms of such license 	&
	! and with the inclusion of the above copyright notice.  This 	&
	! software or any other copies therof may not be provided or 	&
	! otherwise made available to any other person.  No title to 	&
	! and ownership of the software is hereby transferred.		&
	!      The information in this software is subject to change	&
	! without notice and should not be construed as a committment 	&
	! by Computer Management Center. 				&
	!      CMC assumes no responsibility for the use or reliability &
	! of its software on equipment which is not supported by CMC. 	&
	!---------------------------------------------------------------&

30	ON ERROR GOTO 19000 &
\	JUNK$=SYS(CHR$(6%)+CHR$(-7%)) &
\	JUNK$ = SYS(CHR$(6%) + CHR$(9%)) &
\	JJ%  = ASCII(LEFT(JUNK$,1%))/2% &
\	JJ$  = RIGHT(NUM1$(JJ%+100%),2%) &

60	CH%,OUTPUT.CH%=1%			! Keybrd,Output channel &
\	OPEN 'KB:' AS FILE 1%, MODE 8%+256% 	! Echo,Disbl Hibrn,%^C 	&

70	ESC$=CHR$(155%)				! Escape code for VT100 &
\	CLSCN$=ESC$+'[H'+ESC$+'[J'		! Clear screen 		&
\	CLRLIN$=ESC$+'[2K'			! Erase entire line 	&
\	COLM.ON$=ESC$+'[?3h'			! 132 Column mode 	&
\	COLM.OFF$=ESC$+'[?3l'			! 80 Column mode 	&
\	R.ON$=ESC$+'[7m'      			! Reverse video 	&
\	G.OFF$=ESC$+'[m'			! Select graphic off 	&
\	ENTER.COPY$=ESC$+'[5i'			! Enter media copy 	&
\	EXIT.COPY$=ESC$+'[4i'			! Exit media copy 	&

80	!COM(THIS) DROP.DEAD.DATE$ = 8,VERSION.NO$ = 6,SERIAL.NO$ = 10 	&
	DROP.DEAD.DATE$='        ' &
\	VERSION.NO$='V1.0' &
\	DATE.TIME$=SYS(CHR$(6%)+CHR$(20%)+CHR$(0%)+CHR$(0%)+CHR$(1%)+ &
		CHR$(SWAP%(1%))+STRING$(11%,0%)+CHR$(SWAP%(0%))+ &
		CHR$(1%)+CHR$(SWAP%(1%))) &
\	IF 	DROP.DEAD.DATE$<>'' &
	THEN	IF 	DROP.DEAD.DATE$<MID(DATE.TIME$,7%,8%) &
		THEN	MESG$=MID(SYS(CHR$(6%)+CHR$(9%)+CHR$(47%)),3%,30%) &
\			PRINT #CH%, MESG$;V$=SYS(CHR$(9%)) &

100	DEVICE.CH%,PRNT.TMP%	= 02% &
\	PJ.CH% 			= 02% &
\	PJL.CH%			= 04% &
\	VENDES.CH%		= 06% &
\	MENU.CH%		= 12% &

150	OPEN 'PRNT'+JJ$+'.TMP' FOR INPUT AS FILE PRNT.TMP% &
\	KILL 'PRNT'+JJ$+'.TMP' &
\	NEXT.REPORT%=0% &
\	CHAIN.PROGRAM$='' &
\	CHAIN.LINE%=0% &

160	INPUT LINE #PRNT.TMP%, A$ &
\	PR$=LEFT(A$,2%) &
\	CMD$=CVT$$(RIGHT(A$,4%),140%) &
\	IF PR$='RN' &
	THEN	IF NEXT.REPORT% &
		THEN	170 &
			ELSE	NEXT.REPORT%=-1% &

165	LIN.PER.PAGE%	= VAL(CMD$) IF PR$='LP' &
\	SPAGE%		= VAL(CMD$) IF PR$='SP' &
\	EPAGE%		= VAL(CMD$) IF PR$='EP' &
\	COPIES%		= VAL(CMD$) IF PR$='CP' &
\	REPDATE$	= CMD$ IF PR$='RD' &
\	DISPLAY$	= CMD$ IF PR$='DP' &
\	AUTOSCROLL$	= CMD$ IF PR$='AS' &
\	SPOOLR$		= CMD$ IF PR$='SL' &
\	OUTDEV$		= CMD$ IF PR$='OD' &
\	MS$		= CMD$ IF PR$='MS' &
\	REGCON$		= CMD$ IF PR$='U1' &
\	BATCH$		= CMD$ IF PR$='U2' &
\	GOTO 160 &

170	OPEN 'PRNT'+JJ$+'.TMP' FOR OUTPUT AS FILE PRNT.TMP%+1% &
\	CHAIN.LINE%=31000% &

180	PRINT #PRNT.TMP%+1%, A$ &
\	INPUT LINE #PRNT.TMP%, A$ &
\	CHAIN.PROGRAM$=CVT$$(RIGHT(A$,4%),140%) IF LEFT(A$,2%)='PG' &
		AND CHAIN.TEMP%=0% &
\	CHAIN.TEMP%=-1% IF LEFT(TEMP$,2%)='PG' &
\	GOTO 180 &

200	BATCH.NAME$=REGCON$+BATCH$ &
\	IF 	FNO%(DEVICE.CH%,'DEVICE.DAT','/RO','')=0% &
	THEN	APJOUR.DEVICE$=MID(FNL$,7%,20%)+'' &
			IF FNG%(DEVICE.CH%,'APJOUR')=0% &
\		VENDES.DEVICE$=MID(FNL$,7%,20%)+'' &
			IF FNG%(DEVICE.CH%,'VENDES')=0% &
\		V%=FNC%(DEVICE.CH%) &

300	IF 	FNO%(PJ.CH%,APJOUR.DEVICE$+'APJ'+BATCH.NAME$+'.DAT','/RO','') &
	THEN	V$=FNMESS$(CH%,FNS%,' APJ'+BATCH.NAME$+'.DAT',0%,-1%) &
\		GOTO 17540 &

310	IF 	FNO%(PJL.CH%,APJOUR.DEVICE$+'AP1'+BATCH.NAME$+'.DAT','/RO','') &
	THEN	V$=FNMESS$(CH%,FNS%,' AP1'+BATCH.NAME$+'.DAT',0%,-1%) &
\		GOTO 17540 &

320	IF 	FNO%(VENDES.CH%,VENDES.DEVICE$+'VENDES.DAT','/RO','') &
	THEN	V$=FNMESS$(CH%,FNS%,' VENDES.DAT',0%,-1%) &
\		GOTO 17540 &

400	OPEN 'MENU.FIL/RO' FOR INPUT AS FILE MENU.CH% &
\	DIM #12%, A0$(1%)=64% &
\	COMPANY$=CVT$$(RIGHT(A0$(1%),2%),128%) &
\	CLOSE MENU.CH% &

510	TITLE$='Invoice Attachment' &
\	PAGE.TOP$=STRING$(2%,10%) + CHR$(13%) + 'Date: ' + DATE$(0%) + &
		SPACE$(50%-LEN(COMPANY$)/2%) + COMPANY$ + &
		SPACE$(59%-LEN(COMPANY$)/2%) + 'Page <<###>>' + CHR$(10%) + &
		CHR$(13%) + 'Time: ' + TIME$(0%) + SPACE$(51%-LEN(TITLE$)/2%) +&
		TITLE$ + SPACE$(63%-LEN(TITLE$)/2%) + VERSION.NO$ + CHR$(10%) +&
		CHR$(13%) &
\	PAGE.TOP1$=STRING$(132%,45%)+CHR$(13%)+CHR$(10%)+ &
		'       Vchr #    Vchr Date    Vend #    Vendor Name'+ &
		'             Inv Date    Invoice #          Dsc Date'+ &
		'    Due Date    Chk #        ' &
\	PAGE.TOP1.1$=CHR$(10%)+CHR$(13%)+ &
		'        1099    PO #    A/P #     CASH #    UT   DESC       '+ &
		'              USE JOB' &
\	PAGE.TOP2$=STRING$(132%,45%)+CHR$(13%)+CHR$(10%)+ &
		'          Acct #          Sub #   Phase       Description'+ &
		'                         Units               Amount' &
\	USE1$=	'       \    \    \      \     \    \    \                  \'+&
		'    \      \    \             \    \      \    \      \'+ &
		'    \    \       ' &
\	USE1.1$='        \\      \    \  \      \  \      \  \\ \            '+&
		'            \ \   \' &
\	USE2$=	'        \      \          \    \  \        \'+ &
		'  \                        \       '+ &
		' #####.# #####,###,###,###.##' &  
\	USE3$=	SPACE$(46%)+'Voucher Totals'+SPACE$(28%)+'#####,###,###,###.##' &
\	BOT.MARGIN%=6% &
\	PAGE.BOT$=STRING$(6%,10%)+CHR$(13%) &
\	PAGE.BREAK%=LIN.PER.PAGE%-BOT.MARGIN% &
\	DISPLAY.CONTROL%=17% &
\	IF 	FROM.ITEM$<>'ALL' 	THEN	START.TEMP$=FROM.ITEM$ 	&
					ELSE	START.TEMP$='' &

10000	IF 	DISPLAY$<>'Y' &
	THEN	CLOSE CH% &
\		RESET.CH%=1% &
\		TEMP.CH%=12% &
\		OPEN OUTDEV$ AS FILE OUTPUT.CH%, MODE 2% &
\		IF 	SPAGE%>1% 	THEN	OPEN 'NL:' AS FILE TEMP.CH% &
\						OUTPUT.CH%=TEMP.CH% &

17000	!-------------------------------------------------OUTPUT REPORT &
	V%=FNG%(PJ.CH%,START.TEMP$) &
\	IF 	DISPLAY$='Y' &
	THEN	PRINT #OUTPUT.CH%, CLSCN$;COLM.ON$;FNSR$('6;19');FNP$('1;1'); &
	ELSE	PRINT #OUTPUT.CH%, ENTER.COPY$;FNPAGE$(LIN.PER.PAGE%,0%,1%,PAGE.TOP$,''); &
\		LINE.COUNT%=FNLINE% &

17030	PRINT #OUTPUT.CH%, CLSCN$;FNP$('1;1'); IF DISPLAY$='Y' &
\	MORE.LINES%=3% &
\	FIELD #PJ.CH%+1%,FNL% AS JUNK$,	!(Old File Layout) &
		06% AS PJ.VOUNUM$,	&			
		06% AS PJ.VENNUM$,	&
		02% AS PJ.VOUDAT$,	&
		15% AS PJ.INVOICE$,	&
		02% AS PJ.INVDAT$,	&
		08% AS PJ.INVAMT$,	&
		02% AS PJ.1099CD$,	&
		06% AS PJ.PO.NUM$,	&
		02% AS PJ.DISCDAT$,	&
		08% AS PJ.DISCAMT$,	&
		02% AS PJ.DUEDAT$,	&
		08% AS PJ.APACCNUM$,	&
		06% AS PJ.CKNUM$,	&
		02% AS PJ.CKDAT$,	&
		08% AS PJ.CASHACCNUM$,	&
		01% AS PJ.USETAX$,	&
		08% AS PJ.1099AMT$,	&
		08% AS PJ.USEAMT$,	&
		20% AS PJ.CKDESC$,	&
		02% AS PJ.UPDATE$,	&
		06% AS PJ.USEJOB$	&
\	VENDES.NAM$='Undefined' &
\	VENDES.NAM$=MID(FNL$,7%,29%) IF FNG%(VENDES.CH%,PJ.VENNUM$)=0% &
\	GOSUB 18000 &
\	PJL.GROSS=0. &

17040	IF 	FNG%(PJL.CH%,PJ.VOUNUM$) &
	THEN	GOSUB 18010 &
\		GOTO 17350 &
	
17100	FIELD #PJL.CH%+1%,FNL% AS JUNK$,!(Old File Layout) &
		 06% AS PJL.VOUNUM$,	&
		 02% AS PJL.PO.LINE$,	&
		 08% AS PJL.ACCNUM$,	&
		 06% AS PJL.SUBCOD$,	&
		 10% AS PJL.PHASE$,	&
		 16% AS PJL.DESC$,	&
		 08% AS PJL.UNITS$,	&
		 08% AS PJL.AMOUNT$	&
\	PJL.UNITS	=CVT$F(PJL.UNITS$) &
\	PJL.AMOUNT	=CVT$F(PJL.AMOUNT$) &
\	PJL.GROSS	=FNZ(PJL.GROSS+PJL.AMOUNT) &
\	PRINT #OUTPUT.CH% USING USE2$,	&
		CVT$$(PJL.ACCNUM$,140%),	&
		PJL.SUBCOD$,	&
		PJL.PHASE$,	&
		PJL.DESC$,	&
		PJL.UNITS,	&
		PJL.AMOUNT	&
\	LINE.COUNT%=LINE.COUNT%+1% &
\	GOSUB 17600 &
\	IF FNN%(PJL.CH%) &
	THEN	GOSUB 18010 &
	ELSE	IF 	LEFT(FNL$,6%)=PJ.VOUNUM$	THEN	GOTO 17100 &
							ELSE	GOSUB 18010 &

17350	IF FNN%(PJ.CH%)=0% &
	THEN	GOSUB 18020 &
\		GOTO 17030 &

17500	IF 	DISPLAY$<>'Y' &
	THEN	PRINT #OUTPUT.CH%, FNPAGE$(LIN.PER.PAGE%,LINE.COUNT%,FNPAGE%, &
			'',PAGE.BOT$); &
	ELSE	PRINT #OUTPUT.CH%, FNP$('24;1');'End of Report'; &
			FNP$('24;105');'Hit any key to continue'; &
\		INP$=FNINP$(CH%,128%,' ',1%,1%) &

17540	! KILL 'TEMPORARY.FILES' &

17550	PRINT #OUTPUT.CH%, EXIT.COPY$; IF DISPLAY$<>'Y' &
\	PRINT #OUTPUT.CH%, FNSR$('1;24');COLM.OFF$; IF DISPLAY$='Y' &
\	CLOSE OUTPUT.CH% &
\	V%=FNX%('',0%,'') &
						
17600	!---------------------------------------PAGING SUBROUTINE &
	IF 	DISPLAY$<>'Y' AND LINE.COUNT%+MORE.LINES%>PAGE.BREAK% &
	THEN	END.FLAG%=-1% IF PAGE%>=EPAGE% AND EPAGE%<>0% &
\		RETURN IF END.FLAG% &
\		PRINT #OUTPUT.CH%, FNPAGE$(LIN.PER.PAGE%,LINE.COUNT%, &
			FNPAGE%,PAGE.TOP$,PAGE.BOT$); &
\		LINE.COUNT%=FNLINE% &
\		GOSUB 18005 IF MORE.LINES%<>100% &
\		IF 	SPAGE%>1% &
		THEN	IF 	SPAGE%<=PAGE% &
			THEN	OUTPUT.CH%=RESET.CH% &
\				PRINT #OUTPUT.CH%, FNPAGE$(LIN.PER.PAGE%,0%, &
					PAGE%,PAGE.TOP$,''); &
\				LINE.COUNT%=FNLINE% &
\				SPAGE%=0% &

17650	IF 	DISPLAY$='Y' AND LINE.COUNT%>DISPLAY.CONTROL% &
	THEN	PRINT #OUTPUT.CH%, FNP$('24;105');'Hit any key to continue'; &
\		INP$=FNINP$(CH%,128%,' ',1%,1%) &
\		LINE.COUNT%=0% &
\		PRINT #OUTPUT.CH%, FNP$('24;1');CLRLIN$;FNP$('18;132') &

17670	RETURN &

18000	PRINT #OUTPUT.CH%, PAGE.TOP1$ &
\	PRINT #OUTPUT.CH% USING USE1$,	&
		PJ.VOUNUM$,	&
		FND6$(CVT$%(PJ.VOUDAT$)),	&
		PJ.VENNUM$,	&
		VENDES.NAM$,	&
		FND6$(CVT$%(PJ.INVDAT$)),	&
		PJ.INVOICE$,	&
		FND6$(CVT$%(PJ.DISCDAT$)),	&
		FND6$(CVT$%(PJ.DUEDAT$)),	&
		PJ.CKNUM$	&
\	PRINT #OUTPUT.CH%, PAGE.TOP1.1$ &
\	PRINT #OUTPUT.CH% USING USE1.1$, &
		PJ.1099CD$,	&
		PJ.PO.NUM$,	&
		CVT$$(PJ.APACCNUM$,140%),	&
		CVT$$(PJ.CASHACCNUM$,140%),	&
		PJ.USETAX$,	&
		PJ.CKDESC$,	&
		PJ.USEJOB$	&
\	LINE.COUNT%=LINE.COUNT%+6% &

18005	PRINT #OUTPUT.CH%, PAGE.TOP2$ &
\	LINE.COUNT%=LINE.COUNT%+2% &
\	RETURN &

18010	PRINT #OUTPUT.CH% &
\	PRINT #OUTPUT.CH% USING USE3$,FNZ(PJL.GROSS)	&
\	LINE.COUNT%=LINE.COUNT%+2% IF DISPLAY$<>'Y' &
\	RETURN &

18020	LINE.COUNT%=20% IF DISPLAY$='Y' &
\	MORE.LINES%=100% IF LINE.COUNT%>0% AND DISPLAY$<>'Y' &
\	GOSUB 17600 &
\	RETURN &

18900	! SEND MESSAGE IF DETACHED KEYBOARD &
						
19000	!---------------------------------------ERROR TRAPPING &
	IF ERR=28% &
	THEN	JUNK$ = SYS(CHR$(6%) + CHR$(-7%)) &
\		RESUME 17500 &

19010	RESUME 200 IF ERL=160% OR ERL=170% OR ERL=180% &
\	RESUME 450 IF ERL=400% &
\	RESUME 17540 IF ERR=54% &
\	RESUME 17550 IF ERL=17540 &

19100	IF ERL=150% &
	THEN	PRINT #CH%, FNP$('24;1');CLRLIN$; &
			'Unable to find print control file.  Aborting'; &
			FNP$('24;55');'Hit any key to continue'; &
\		INP$=FNINP$(CH%,128%,' ',1%,0%) &
\		RESUME 17500 &

19900	ON ERROR GOTO 0 &

30000	!---------------------------------------TERMINAL INPUT 		&
	DEF FNINP$(CHN%,KYP%,FILLCHAR$,INPUTLEN%,TO.ERR%)		&
\		PRINT #CHN%, STRING$(INPUTLEN%,ASCII(FILLCHAR$)); 	&
			  STRING$(INPUTLEN%,8%); 			&
\		PRINT #CHN%, RECORD 256%,CHR$(KYP%+INPUTLEN%)+FILLCHAR$;&
\		GET #CHN% 						&
\		FIELD #CHN%, RECOUNT AS BUFFER$ 			&
\		BUFFER$='%^C' IF INSTR(1%,BUFFER$,CHR$(3%)) 		&
\		FNINP$=CVT$$(BUFFER$,4%) 				&
\		V=SQR(-1) IF BUFFER$='%^C' AND TO.ERR% !^C Trappping	&
\	FNEND 								&
						
30200	DEF*FNP$(ROWCOL$)=ESC$+'['+ROWCOL$+'H'	!DIRECT CURSOR ADDRESS	&

30250	!---------------------------------------MESSAGE HANDLER		&
	DEF*FNMESS$(CHN%,ERRNUM%,DESC$,TO.ERR%,PRINT.TEST%)		&
\		MESG$=MID(SYS(CHR$(6%)+CHR$(9%)+CHR$(ERRNUM%)),3%,30%)+	&
			DESC$						&
\		IF 	PRINT.TEST%					&
		THEN	PRINT #CHN%, FNP$('24;1');CLRLIN$;MESG$;	&
				FNP$('24;55');'Hit any key to continue';&
\			NW$=FNINP$(CHN%,128%,' ',1%,TO.ERR%)		&

30260	FNMESS$=MESG$							&
\	FNEND 								&

30300	DEF*FNSR$(BEGEND$)=ESC$+'['+BEGEND$+'r'	!SCROLL CONTROL		&

30310	DEF FND6$(D9%)=RIGHT(NUM1$((D9% AND 15%*32%)/32%+100%),2%) 	&
		+ '/'+RIGHT(NUM1$((D9% AND 31%)+100%),2%) 		&
		+ '/'+RIGHT(NUM1$(((SWAP%(D9%) AND 254%)/2%)+100%),2%) 	&

30340	DEF FNZ(Y) 				!ROUNDING FUNCTION	&
\		TEMP=Y 							&
\		TEMP=-Y IF Y<0. 					&
\		TEMP=INT(TEMP*100.+.51)/100. 				&
\		TEMP=-TEMP IF Y<0. 					&
\		FNZ=TEMP 						&	
\	FNEND 								&
						
30610	DEF FNPAGE$(Y0%,Y1%,Y2%,Y0$,Y1$) 	!PAGING FUNCTION	&
\		Y2$='' 							&
\		Y2$=STRING$(Y0%-(Y1%+LEN(XLATE(Y1$,STRING$(10%,0%)+	&
			CHR$(10%)))),10%) IF Y1$<>'' 			&
\		PAGE.LINE%=LEN(XLATE(Y0$,STRING$(10%,0%)+CHR$(10%))) 	&
\		Y%=INSTR(1%,Y1$+Y0$,'<<#') 				&
\		Y3%=INSTR(1%,Y1$+Y0$,'#>>') 				&
\		Y$=RIGHT(NUM1$(100000+Y2%),8%-(Y3%-Y%)) 		&
\		Y3%=-3% IF Y%=0% 					&
\		PRINT #OUTPUT.CH%, Y2$;LEFT(Y1$+Y0$,Y%-1%);Y$; 		&
			RIGHT(Y1$+Y0$,Y3%+3%); 				&
\		PAGE%=Y2% 						&
\	FNEND 								&

30650	DEF FNPAGE%=PAGE%+1% 						&

30660	DEF FNLINE%=PAGE.LINE% 						&

30900	CHAIN.FLAG%=-1% 						&

31000	! Chain entry 							&
	GOTO 30 							&

32767	END &

