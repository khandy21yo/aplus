10	!--------------------------------------------------------------- &
	!	BUDGET SET PROGRAM &
	! &
	! &
	! &
	! &
	!--------------------------------------------------------------- &
	! &
	!		      Copyright (c) 1984 by &
	!	  Computer Management Center, Idaho Falls, Idaho &
	! &
	! This software is furnished under a license and may be used &
	! and copied only in accordance with the terms of such license &
	! and with the inclusion of the above copyright notice.  This &
	! software or any other copies therof may not be provided or &
	! otherwise made available to any other person.  No title to &
	! and ownership of the software is hereby transferred. &
	! &
	! The information in this software is subject to change without &
	! notice and should not be construed as a committment by &
	! Computer Management Center. &
	! &
	! CMC assumes no responsibility for the use or reliability of &
	! its software on equipment which is not supported by CMC. &
	! &
	!--------------------------------------------------------------- &

50	ON ERROR GOTO 19000 &

60	OPEN 'KB:' AS FILE #1%, MODE 8%+256% &
					!    8% - Echo Control &
					!   16% - Disable hibernation, Cntl-C &
\	CH%=1%				! Keyboard channel &
\	ESC$=CHR$(155%)			! Escape code for VT100 control &
\	CLSCN$=ESC$+'[H'+ESC$+'[J'	! Clear screen &
\	CLRLIN$=ESC$+'[2K'		! Erase entire line &
\	G.OFF$=ESC$+'[m'		! Select graphic off &
\	B.ON$=ESC$+'[1m'		! Bold face on &
\	BLINK.ON$=ESC$+'[5m'		! Blinking &
\	R.ON$=ESC$+'[7m'		! Reverse video &
\	COLM.ON$=ESC$+'[?3h'		! 132 Column mode &
\	COLM.OFF$=ESC$+'[?3l'		! 80 Column mode &
\	CLRBOT$=ESC$+'[21;1H'+ESC$+'[J'	! Erase cursor to end of screen &
\	LDS.ON$=ESC$+'(0'		! Line drawing set &
\	USASCII$=ESC$+'(B'		! United States ASCII &

80	!COM(THIS) DROP.DEAD.DATE$ = 8, VERSION.NO$ = 6, SERIAL.NO$ = 10 &
	DROP.DEAD.DATE$='        ' &
\	VERSION.NO$='V1.0' &
\	IF DROP.DEAD.DATE$<>'' &
	THEN	IF DROP.DEAD.DATE$<MID(SYS(CHR$(6%)+CHR$(20%)+CHR$(0%)+CHR$(0%)+ &
				CHR$(1%)+CHR$(SWAP%(1%))),7%,8%) &
		THEN	MESG$=MID(SYS(CHR$(6%)+CHR$(9%)+CHR$(47%)), &
				3%,30%) &
\			PRINT #CH%, MESG$; &
\			V$=SYS(CHR$(9%)) &

100	DIM BUDGET.AMT$(13%), INI%(13%), AMOUNT(13%) &
\	DEVICE.CH%	= 02% &
\	BDGTBA.CH%	= 02% &
\	CHART.CH%	= 04% &
\	BUDGET.CH%	= 06% &
\	CONTRL.CH%	= 08% &
\	MENU.CH%   	= 12% &
	&
\	LOOP.DATA% = 8% &

200	IF FNO%(DEVICE.CH%,'DEVICE.DAT','/R0','')=0% &
	THEN	BUDGET.DEVICE$=CVT$$(MID(FNL$,7%,20%),-1%) &
			IF FNG%(DEVICE.CH%,'BUDGET')=0% &
\		CHART.DEVICE$=CVT$$(MID(FNL$,7%,20%),-1%) &
			IF FNG%(DEVICE.CH%,'CHART')=0% &
\		CONTRL.DEVICE$=CVT$$(MID(FNL$,7%,20%),-1%) &
			IF FNG%(DEVICE.CH%,'CONTRL')=0% &
\		V%=FNC%(DEVICE.CH%) &

300	IF FNO%(CONTRL.CH%,CONTRL.DEVICE$+'CONTRL.DAT','/RO','') &
	THEN	V$=FNMESS$(CH%,FNS%,' CONTRL.DAT',0%,-1%) &
\		OPT$='Q' &
\		GOTO 1040 &

305	IF FNG%(CONTRL.CH%,'CHART ')=0% &
	THEN	FF% = CVT$%(MID(FNL$,17%,2%)) &
		ELSE	V$=FNMESS$(CH%,FNS%,' CONTRL.DAT',0%,-1%) &
\			V%=FNX%('',-1%,'') &

309	V%=FNC%(CONTRL.CH%) &

310	V%=FNO%(BDGTBA.CH%,BUDGET.DEVICE$+'BDGTBA.DAT','/RW','') &
\	V%=FNO%(BDGTBA.CH%,BUDGET.DEVICE$+'BDGTBA.DAT','/CR:,10,64','') &
								IF FNS% = 5% &
\	IF FNS% &
	THEN	V$=FNMESS$(CH%,FNS%,'BDGTBA.DAT',0%,-1%) &
\		OPT$='Q' &
\		GOTO 1040 &

320	CHART.FLAG%=FNO%(CHART.CH%,CHART.DEVICE$+'CHART.DAT','/RO','') &

400	OPEN 'MENU.FIL/RO' FOR INPUT AS FILE MENU.CH% &
\	DIM #12%, A0$(1%)=64% &
\	COMPANY$=CVT$$(RIGHT(A0$(1%),2%),128%) &
\	CLOSE MENU.CH% &

450	OPEN 'NL:' AS FILE 12%, RECORDSIZE 64%+128% &
\	FIELD #12%,	08% AS BDGTBA.RESULT$, &
			08% AS BDGTBA.BASE$(1%), &
			08% AS BDGTBA.BASE$(2%), &
			08% AS BDGTBA.BASE$(3%), &
			08% AS BDGTBA.BASE$(4%), &
			08% AS BDGTBA.BASE$(5%), &
			08% AS BDGTBA.BASE$(6%), &
			08% AS BDGTBA.FACTOR$ &
\	FIELD #12%, 64% AS TEMP$, &
			08% AS BUDGET.ACCNO$, &
			08% AS BUDGET.BEGBAL$, &
			08% AS BUDGET.AMT$(1%), &
			08% AS BUDGET.AMT$(2%), &
			08% AS BUDGET.AMT$(3%), &
			08% AS BUDGET.AMT$(4%), &
			08% AS BUDGET.AMT$(5%), &
			08% AS BUDGET.AMT$(6%), &
			08% AS BUDGET.AMT$(7%), &
			08% AS BUDGET.AMT$(8%), &
			08% AS BUDGET.AMT$(9%), &
			08% AS BUDGET.AMT$(10%), &
			08% AS BUDGET.AMT$(11%), &
			08% AS BUDGET.AMT$(12%), &
			08% AS BUDGET.AMT$(13%) &
\	FIELD #12%, 64% AS BDGTBA.BUF$, 128% AS BUDGET.BUF$ &

500	FRAME$ = FRAME$ + FNP$(NUM1$(I%)+';1')+' '+FNP$(NUM1$(I%)+';80')+' ' &
							FOR I%=2% TO 19% &
\	COMMAND$  = ' ' &
\	COMMAND$  = COMMAND$+RIGHT(NUM1$(I%+100%),2%) + ' ' &
		FOR I%=1% TO LOOP.DATA% &

1000	!--------------------------------------------PROGRAM RESTART POINT &
	IF FNG%(BDGTBA.CH%,'')=0% &
	THEN	1010 &
		ELSE	PRINT #CH%, G.OFF$; &
\			GOSUB 1060 &
\			GOTO 1030 &

1005	IF FNN%(BDGTBA.CH%) &
	THEN	18910 &
		ELSE	1015 &

1010	PRINT #CH%, COLM.OFF$;FNSR$('1;24');G.OFF$; &
\	GOSUB 1060 &

1015	KEY.POINTER%=FNR(BDGTBA.CH%) &
\	DATA.POINTER%=FNR(BDGTBA.CH%+1%) &
\	GOSUB 6000 &

1030	CHNG.FLAG%=0% &
\	PRINT #CH%, CLRBOT$;G.OFF$; &
		'COMMAND: Add Erase Change Blank Initialize Default '; &
		'Find Next Restore'; &
		FNP$('22;10');'caLculate Quit '; &
\	OPT$=CVT$$(FNINP$(CH%,128%,' ',1%,1%),32%) &
\	OPT$='A' IF OPT$='' &
\	GOTO 1030 IF KEY.POINTER%=0% AND INSTR(1%,'AQD',OPT$)=0% &

1040	IF OPT$='Q' &
	THEN	PRINT #CH%, CLSCN$;FNSR$('1;24'); &
			FNP$('1;75');B.ON$;BLINK.ON$;R.ON$;'WAIT';G.OFF$; &
			FNP$('24;1'); &
\		V%=FNX%('',0%,'') &

1050	GOTO 1000       IF OPT$='R' 		! Restore &
\	GOTO 1005       IF OPT$='N' 		! Next &
\	GOTO 2000       IF OPT$='A' OR OPT$='F' ! Add, Find &
\	GOTO 2200       IF OPT$='C' OR OPT$='B' ! Change, Blank &
\	GOTO 2400       IF OPT$='E' 		! Erase &
\	GOTO 2500       IF OPT$='D' 		! Default &
\	GOTO 2600       IF OPT$='I' 		! Initialize &
\	GOTO 3000       IF OPT$='L' 		! Calculate &
\	GOTO 1030 &

1060	TEMP$     = 'Budget Set File Maintenance' &
\	PRINT #CH%, CLSCN$;R.ON$;LEFT(TEMP$,39%);SPACE$(40%-LEN(TEMP$)); &
			SPACE$(40%-LEN(COMPANY$));COMPANY$; &
			FRAME$; FNP$('20;1');SPACE$(80%);G.OFF$; &
\	PRINT #CH%,     FNP$('03;25');'(01) Result Acct' &
			FNP$('04;25');'(02) Base Acct 1' &
			FNP$('05;25');'(03) Base Acct 2' &
			FNP$('06;25');'(04) Base Acct 3' &
			FNP$('07;25');'(05) Base Acct 4' &
			FNP$('08;25');'(06) Base Acct 5' &
			FNP$('09;25');'(07) Base Acct 6' &
			FNP$('10;25');'(08) Factor'; &
\	RETURN &

2000	!-------------------------------------------------SEARCH FOR KEYS &
	OPTION$='ADD ' &
\	OPTION$='FIND ' IF OPT$='F' &
\	INP$='' &
\	FOR LOOP%=1% TO LOOP.DATA% &
\		INP$=DEFAULT$(LOOP%) IF OPT$='A' &
\		GOSUB 6030 &
\	NEXT LOOP% &
\	SEARCH.KEY$='' &
\	GOSUB 6210 &
\	SEARCH.KEY$=BDGTBA.RESULT$ &
\	IF OPT$='F' OR FNG%(BDGTBA.CH%,SEARCH.KEY$)=0% &
	THEN	GOTO 1015 &

2100	!-----------------------------------------------------ADD RECORD &
	GOSUB 6010 FOR LOOP%=2% TO LOOP.DATA% &
\	IF FNA%(BDGTBA.CH%,BDGTBA.BUF$) &
	THEN	V$=FNMESS$(CH%,FNS%,'BDGTBA.DAT',0%,-1%) &
\		GOTO 1000 &

2110	KEY.POINTER%=FNR(BDGTBA.CH%) &
\	GOTO 1030 &

2200	!----------------------------------------CHANGE OR BLANK RECORD &
	IF OPT$='B' &
	THEN	OPTION$ = 'BLANK ' &
	ELSE	OPTION$ = 'CHANGE ' &
\		CHNG.FLAG% = -1% &

2210	PRINT #CH%, CLRBOT$; OPTION$+':'; &
\	INP$=CVT$$(FNINP$(CH%,128%,' ',2%,1%),32%) &
\	GOTO 1030 IF INP$='' &
\	LOOP%=(INSTR(1%,COMMAND$,' '+INP$+' ')+2%)/3% &
\	GOTO 2210 IF (LOOP%=0% OR LOOP%>LOOP.DATA%) AND OPT$='C' &
\	GOTO 2210 IF (LOOP%<2% OR LOOP%>LOOP.DATA%) AND OPT$='B' &
\	IF OPT$='C' &
	THEN	GOSUB 6010 &
	ELSE	INP$='' &
\		GOSUB 6030 &

2220	GOSUB 2900 &
\	IF 	FNS%=0% &
	THEN	GOTO 2210 &
	ELSE	V$=FNMESS$(CH%,FNS%,'BDGTBA.DAT',0%,-1%) &
\		GOTO 1010 &

2400	!------------------------------------------------ ERASE RECORD &
	PRINT #CH%, CLRBOT$;'Confirm erasure (Yes/No) '; &
\	INP$=CVT$$(FNINP$(CH%,128%,' ',1%,1%),32%) &
\	GOTO 1030 IF INP$<>'Y' &

2420	IF FND%(BDGTBA.CH%,'') &
	THEN	V$=FNMESS$(CH%,FNS%,'BDGTBA.DAT',0%,-1%) &
	ELSE	PRINT #CH%,FNP$('24;1');CLRLIN$;'Record has been erased.'; &
			FNP$('24;55');'Hit any key to continue '; &
\		NW$=FNINP$(CH%,128%,' ',1%,1%) &

2430	GOTO 1005 &

2500	!-----------------------------------------------SET DEFAULT VALUES &
	OPTION$='DEFAULT ' &
\	FOR LOOP%=1% TO LOOP.DATA% &
\		INP$=DEFAULT$(LOOP%) &
\		GOSUB 6030 &
\	NEXT LOOP% &

2510	PRINT #CH%, CLRBOT$;'Default:'; &
\	INP$=CVT$$(FNINP$(CH%,128%,' ',2%,1%),32%) &
\	GOTO 1010 IF INP$='' &
\	LOOP%=(INSTR(1%,COMMAND$,' '+INP$+' ')+2%)/3% &
\	GOTO 2510 IF LOOP%<2% &
\	GOSUB 6010 &
\	DEFAULT$(LOOP%)=INP$ &
\	GOTO 2510 &

2600	!-----------------------------------------------INITIALIZATION &
	INITIALIZE$=' ' &
\	INP$='' &
\	GOSUB 6030 FOR LOOP%=1% TO LOOP.DATA% &

2610	PRINT #CH%, CLRBOT$;FNP$('22;1');INITIALIZE$; &
		FNP$('21;1');'Field to initialize:'; &
\	INP$=CVT$$(FNINP$(CH%,128%,' ',2%,1%),32%) &
\	IF INP$<>'' &
	THEN	LOOP%=(INSTR(1%,COMMAND$,' '+INP$+' ')+2%)/3% &
\		INITIALIZE$=INITIALIZE$+INP$+' ' IF &
			LOOP%<>0% AND (INSTR(1%,INITIALIZE$,' '+INP$+' ') &
				+2%)/3%=0% &
\		GOTO 2610 &

2620	GOTO 1010 IF INITIALIZE$='' &
\	OPT$='C' &
\	OPTION$='INITIALIZE ' &
\	CHNG.FLAG%=-1% &
\	INP$='' &
\	GOSUB 6030 FOR LOOP%=1% TO LOOP.DATA% &
\	GOTO 2660 &

2650	GOTO 18910 IF FNN%(BDGTBA.CH%) &
\	KEY.POINTER%=FNR(BDGTBA.CH%) &
\	DATA.POINTER%=FNR(BDGTBA.CH%+1%) &

2660	GOSUB 6000 &
\	FOR I%=1% TO LEN(INITIALIZE$)/3% &
\		LOOP%=(INSTR(1%,COMMAND$,MID(INITIALIZE$,I%*3%-2%,3%))+2%)/3% &
\		GOSUB 6010 &
\		IF 	INP$<>'' &
		THEN	GOSUB 2900 &
\			IF 	FNS% &
			THEN	V$=FNMESS$(CH%,FNS%,'BDGTBA.DAS',0%,-1%) &
\				GOTO 1010 &

2670	NEXT I% &
\	GOTO 2650 &

2900	!-------------------------------------------------UPDATE A RECORD &
	V%=FNG%(-BDGTBA.CH%,NUM1$(KEY.POINTER%))+ &
		FNG%(-BDGTBA.CH%-1%,NUM1$(DATA.POINTER%)) &
\	V%=FNU%(-BDGTBA.CH%,BDGTBA.BUF$) IF LOOP%<2% AND FNS%=0% &
\	V%=FNU%(BDGTBA.CH%,BDGTBA.BUF$) IF LOOP%>1% AND FNS%=0% &
\	RETURN &

3000	!==================================================================== &
	! CALCULATE BUDGETS FOR THE AUTOMATIC BUDGET FILE &
	!==================================================================== &
	YEAR$=RIGHT(DATE$(0%),8%) &
\	PRINT #CH%, CLSCN$;FNP$('6;24');'Fiscal year for budget file '; &
			'<';YEAR$;'>'; &
\	INP$=CVT$$(FNINP$(CH%,0%,'_',2%,0%),32%) &
\	GOTO 1000 IF INP$='%^C' &
\	YEAR$=INP$ IF INP$<>'' &

3010	V%=FNO%(BUDGET.CH%,BUDGET.DEVICE$+'BDGT'+YEAR$+'.DAT','/RW','') &
\	V%=FNO%(BUDGET.CH%,BUDGET.DEVICE$+'BDGT'+YEAR$+'.DAT','/CR:10,128','') &
								IF FNS%=5% &
\	IF 	FNS% &
	THEN	V$=FNMESS$(CH%,FNS%,' BDGT'+YEAR$+'.DAT',0%,-1%) &
\		GOTO 1000 &

3020	IF FNG%(BDGTBA.CH%,"") &
	THEN	PRINT FNP$('24;1');CLRLIN$; &
			'The automatic budget file is empty.'; &
			FNP$('24;55');'Hit any key to continue '; &
\		INP$=FNINP$(CH%,128%,' ',1%,1%) &
\		GOTO 1000 &

3030	IF FNG%(BUDGET.CH%,"") &
	THEN	PRINT FNP$('24;1');CLRLIN$; &
			'The automatic budget file is empty.'; &
			FNP$('24;55');'Hit any key to continue '; &
\		INP$=FNINP$(CH%,128%,' ',1%,1%) &
\		GOTO 1000 &

3040	TEMP$='AUTOMATIC BUDGET CALCULATION' &
\	PRINT #CH%, CLSCN$;R.ON$;LEFT(TEMP$,39%);SPACE$(40%-LEN(TEMP$)); &
		SPACE$(40%-LEN(COMPANY$));COMPANY$; &
		FRAME$;FNP$('20;1');SPACE$(80%);G.OFF$; &
\	PRINT #CH%, FNP$(NUM1$(I%+4%)+';20');'(';RIGHT(NUM1$(I%+100%),2%); &
		') Period ';RIGHT(NUM1$(I%+100%),2%); FOR I%=1% TO 12% &

3050	INI%(I%)=0% FOR I%=1% TO 12% &
\	FOR I%=1% TO 12% &
\		PRINT #CH%, FNP$(NUM1$(I%+4%)+';40');'(';FNM1$(FF%);')'; &
			' N'; &
\		FF%=FF%+1% &
\		FF%=1% IF FF%>12% &
\	NEXT I% &

3100	PRINT #CH%, CLRBOT$; 'Item #:'; &
\	INP$=CVT$$(FNINP$(CH%,128%,' ',2%,0%),32%) &
\	GOTO 3200 IF INP$='' OR INP$='%^C' &
\	LOOP%=VAL(INP$) &
\	GOTO 3100 IF LOOP%<1% OR LOOP%>12% &
\	TEMP$='N' &
\	TEMP$='Y' IF INI%(LOOP%) &
\	PRINT #CH%, FNP$(NUM1$(LOOP%+4%)+';46');R.ON$;TEMP$;G.OFF$; &
		CLRBOT$; 'Option (Y/n) '; &
\	INP$=CVT$$(FNINP$(CH%,128%,'_',1%,0%),-1%) &
\	INI%(LOOP%)=0% &
\	INI%(LOOP%)=-1% IF INP$<>'N' &
\	TEMP$='N' &
\	TEMP$='Y' IF INI%(LOOP%) &
\	PRINT #CH%, FNP$(NUM1$(LOOP%+4%)+';46');B.ON$;TEMP$;G.OFF$; &
\	GOTO 3100 &

3200	FLAG%=0% &
\	FLAG%=-1% IF INI%(LOOP%) FOR LOOP%=1% TO 13% &
\	GOTO 3390 IF FLAG%=0% &

3210	PRINT #CH%, CLRBOT$;'Confirm (y/N) '; &
\	INP$=CVT$$(FNINP$(CH%,128%,' ',1%,1%),-1%) &
\	GOTO 3390 IF INP$<>'Y' &
\	PRINT #CH%, CLRBOT$; &
\	V%=FNG%(BDGTBA.CH%,'') &
\	REC.CNT=FNT &
\	PC$=FNPC.SPACE$(REC.CNT) &
\	REC.CNT=FNPC(REC.CNT) &
\	COUNTER%,PERCENT%=0% &
\	COL%=10% &
\	PRINT #CH%, FNP$('16;2'); "Starting to set values to be calculated "; &
		"to zero"; &
		FNP$('17;10');"0%   10%  20%  30%  40%  50%"; &
		"  60%  70%  80%  90% 100%";FNP$('24;80'); &

3220	COUNTER%=COUNTER%+1% &
\	IF COUNTER%>=REC.CNT &
	THEN	COUNTER%=0% &
\		PERCENT%=PERCENT%+LEN(PC$)*2% &
\		IF PERCENT%<100% &
		THEN	PRINT #CH%, FNP$('18;'+NUM1$(COL%));R.ON$;PC$; &
				G.OFF$;FNP$('24;80'); &
\			COL%=COL%+LEN(PC$) &

3230	LSET BDGTBA.BUF$=FNL$ &
\	IF FNG%(BUDGET.CH%,BDGTBA.RESULT$) &
	THEN	V%=FNA%(BUDGET.CH%,BDGTBA.RESULT$+STRING$(120%,0%)) &

3240	LSET BUDGET.BUF$=FNL$ &
\	LSET BUDGET.AMT$(LOOP%)=CVTF$(0.0) IF INI%(LOOP%) FOR LOOP%=1% TO 13% &
\	V%=FNU%(BUDGET.CH%,BUDGET.BUF$) &
\	IF FNN%(BDGTBA.CH%)=0% &
	THEN	GOTO 3220 &

3300	!==================================================================== &
	! CALCULATE AMOUNTS &
	!==================================================================== &
	V%=FNG%(BDGTBA.CH%,'') &
\	COUNTER%,PERCENT%=0% &
\	COL%=10% &
\	PRINT #CH%, FNP$('16;2'); "Starting to calculate the budgets.  Plea"; &
		"se wait."; &
		FNP$('17;10');"0%   10%  20%  30%  40%  50%"; &
		"  60%  70%  80%  90% 100%"; &
		FNP$('18;10');SPACE$(60%); &
		FNP$('24;80'); &

3320	COUNTER%=COUNTER%+1% &
\	IF COUNTER%>=REC.CNT &
	THEN	COUNTER%=0% &
\		PERCENT%=PERCENT%+LEN(PC$)*2% &
\		IF PERCENT%<100% &
		THEN	PRINT #CH%, FNP$('18;'+NUM1$(COL%));R.ON$;PC$; &
				G.OFF$;FNP$('24;80'); &
\			COL%=COL%+LEN(PC$) &

3330	LSET BDGTBA.BUF$=FNL$ &
\	AMOUNT(LOOP%)=0.0 FOR LOOP%=1% TO 13% &
\	FOR LOOP%=1% TO 6% &
\		IF CVT$$(BDGTBA.BASE$(LOOP%),-1%)<>'' &
		THEN	IF FNG%(BUDGET.CH%,BDGTBA.BASE$(LOOP%))=0% &
			THEN	LSET BUDGET.BUF$=FNL$ &
\				AMOUNT(I%)=AMOUNT(I%)+CVT$F(BUDGET.AMT$(I%)) &
					FOR I%=1% TO 13% &

3340	NEXT LOOP% &
\	IF FNG%(BUDGET.CH%,BDGTBA.RESULT$)=0% &
	THEN	LSET BUDGET.BUF$=FNL$ &
\		LSET BUDGET.AMT$(I%)=CVTF$(INT(AMOUNT(I%)* &
			CVT$F(BDGTBA.FACTOR$)/100.)) &
					IF INI%(I%) FOR I%=1% TO 13% &
\		V%=FNU%(BUDGET.CH%,BUDGET.BUF$) &

3350	IF FNN%(BDGTBA.CH%)=0% &
	THEN	GOTO 3320 &

3390	V%=FNC%(BUDGET.CH%) &
\	GOTO 1000 &

6000	!------------------------------------------------DATA ENTRY SECTION &
	RETURN IF KEY.POINTER%<=0% &
\	V%=FNG%(-BDGTBA.CH%,NUM1$(KEY.POINTER%))+ &
		FNG%(-BDGTBA.CH%-1%,NUM1$(DATA.POINTER%)) &
\	LSET BDGTBA.BUF$=FNL$ &
\	RETURN IF LEFT(FNL$,8%)='DDDDDDDD' &
\	FOR LOOP%=1% TO LOOP.DATA% &
\		ON LOOP% GOSUB	6220, 6240, 6240, 6240, 6240, 6240, 6240, 6260 &
\	NEXT LOOP% &
\	RETURN &

6010	ON LOOP% GOSUB	6210, 6230, 6230, 6230, 6230, 6230, 6230, 6250 &
\	RETURN &
		! INPUT DATA &

6030	ON LOOP% GOSUB	6217, 6237, 6237, 6237, 6237, 6237, 6237, 6257 &
\	RETURN &
		! DEFAULT &

6200	!-----------------------------------------------DATA ENTRY SECTION &

6210	INP$=FNSIO$('','3;48',BDGTBA.RESULT$,DEFAULT$(LOOP%),CHR$(CH%)+ &
			STRING$(2%,LEN(BDGTBA.RESULT$))+CHR$(1%)+CHR$(0%)+ &
			OPTION$+' ALPHA') &
\	GOTO 6210 IF INP$='' AND CHNG.FLAG%=0% AND OPT$<>'F' &
\	IF INP$<>'' AND CHNG.FLAG% &
	THEN	IF FNG%(BDGTBA.CH%,SPACE$(8%-LEN(INP$))+INP$)=0% &
		THEN	PRINT #CH%, CLRBOT$;FNP$('24;1');INP$;' is a duplicate key'; &
				FNP$('21;1');'COMMAND: Resume Exit '; &
\			INP$=FNINP$(CH%,128%,' ',1%,1%) &
\			GOTO 6210 IF INP$='R' &
\			RETURN &

6217	RSET BDGTBA.RESULT$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6220	PRINT #CH%, FNP$('3;48');B.ON$;BDGTBA.RESULT$;G.OFF$; &
\	DESC$='** UNDEFINED **' &
\	DESC$=MID(FNL$,9%,30%)+'' IF FNG%(CHART.CH%,BDGTBA.RESULT$)=0% &
\	PRINT #CH% USING FNP$('3;60')+'\                \',DESC$; &
\	RETURN &

6230	LOCATION$=NUM1$(2%+LOOP%)+';48' &
\	INP$=FNSIO$('',LOCATION$,BDGTBA.BASE$(LOOP%-1%),DEFAULT$(LOOP%), &
			CHR$(CH%)+STRING$(2%,LEN(BDGTBA.BASE$(LOOP%-1%)))+ &
			CHR$(1%)+CHR$(0%)+OPTION$+' ALPHA') &
\	GOTO 6240 IF INP$='' AND CHNG.FLAG% &

6237	RSET BDGTBA.BASE$(LOOP%-1%)=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6240	LOCATION$=NUM1$(2%+LOOP%)+';48' &
\	PRINT #CH%, FNP$(LOCATION$);B.ON$;BDGTBA.BASE$(LOOP%-1%);G.OFF$; &
\	DESC$=SPACE$(20%) &
\	IF CVT$$(BDGTBA.BASE$(LOOP%-1%),-1%)<>'' &
	THEN	DESC$='** UNDEFINED **' &
\		DESC$=MID(FNL$,9%,30%)+'' IF FNG%(CHART.CH%,BDGTBA.BASE$(LOOP%-1%))=0% &

6245	LOCATION$=NUM1$(2%+LOOP%)+';60' &
\	PRINT #CH% USING FNP$(LOCATION$)+'\                \',DESC$; &
\	RETURN &

6250	INP$=FNSIO$('#,###.###%','10;48',BDGTBA.FACTOR$, &
			DEFAULT$(LOOP%),CHR$(CH%)+CHR$(10%)+CHR$(7%)+ &
			CHR$(5%)+CHR$(3%)+OPTION$+' VALUE') &
\	GOTO 6260 IF INP$='' AND CHNG.FLAG% &

6257	LSET BDGTBA.FACTOR$=CVTF$(VAL(INP$)) IF INP$<>'' OR CHNG.FLAG%=0% &

6260	PRINT #CH% USING FNP$('10;48')+B.ON$+ &
		"#,###.###%"+G.OFF$,CVT$F(BDGTBA.FACTOR$); &
\	RETURN &

18910	!----------------------------------------------END OF FILE &
	PRINT #CH%, CLRBOT$;FNP$('24;1');'End of file has been reached.'; &
		FNP$('24;55');'Hit any key to continue '; &
\	INP$=FNINP$(CH%,128%,' ',1%,0%) &
\	GOTO 1000 &

19000	!------------------------------------------------ERROR TRAPPING &
	RESUME IF ERR=52% OR ERR=51% &
\	RESUME 450 IF ERL=400 &
\	RESUME 1010 IF ERR=54% &

19999	ON ERROR GOTO 0 &

30000	!----------------------PROGRAM FUNCTIONS------------------------ &
	!------------------------------------------------TERMINAL INPUT &
	DEF FNINP$(CHN%,KYP%,FILLCHAR$,INPUTLEN%,TO.ERR%) &
\		PRINT #CHN%, STRING$(INPUTLEN%,ASCII(FILLCHAR$)); &
			  STRING$(INPUTLEN%,8%); &
\		PRINT #CHN%, RECORD 256%,CHR$(KYP%+INPUTLEN%)+FILLCHAR$; &
\		GET #CHN% &
\		FIELD #CHN%, RECOUNT AS BUFFER$ &
\		BUFFER$='%^C' IF INSTR(1%,BUFFER$,CHR$(3%)) &
\		FNINP$=CVT$$(BUFFER$,4%) &
\		V=SQR(-1) IF BUFFER$='%^C' AND TO.ERR% !^C Trappping &
\	FNEND &

30200	DEF*FNP$(ROWCOL$)=ESC$+'['+ROWCOL$+'H' 	! Direct Cursor Address &

30250	!-----------------------------------------------MESSAGE HANDLER &
	DEF*FNMESS$(CHN%,ERRNUM%,DESC$,TO.ERR%,PRINT.TEST%) &
\		MESG$=MID(SYS(CHR$(6%)+CHR$(9%)+CHR$(ERRNUM%)),3%,30%)+ &
			DESC$ &
\		IF PRINT.TEST% &
		THEN	PRINT #CHN%, FNP$('24;1');CLRLIN$;MESG$; &
				FNP$('24;55'); &
				'Hit any key to continue.'; &
\			NW$=FNINP$(CHN%,128%,' ',1%,TO.ERR%) &

30260		FNMESS$=MESG$ &
\	FNEND &

30550	DEF FNM1$(ARG%)= &
	MID('JanFebMarAprMayJunJulAugSepOctNovDec',ARG%*3%-2%,3%) &

30300	DEF*FNSR$(BEGEND$)=ESC$+'['+BEGEND$+'r'	! Scroll control &

30400	!-------------------------------------------------DATE HANDLERS &
	DEF FND8%(D8)=D8 &

30410	DEF FND6%(D9$)=VAL(MID(D9$,3%,2%))+VAL(LEFT(D9$,2%))*32% &
		+ FND8%(VAL(RIGHT(D9$,5%)))*512% &

30420	DEF FND6$(D9%)=RIGHT(NUM1$((D9% AND 15%*32%)/32%+100%),2%) &
		+ '/'+RIGHT(NUM1$((D9% AND 31%)+100%),2%) &
		+ '/'+RIGHT(NUM1$(((SWAP%(D9%) AND 254%)/2%)+100%),2%) &

30700	!========================================= DATA INPUT FUNCTIONS &
	DEF*FNSIO$(FRMAT$,ROWCOL$,VARABLE$,DEFAL$,ITEMS$) &
\		CHN%=ASCII(MID(ITEMS$,1%,1%)) &
\		KIND%=ASCII(MID(ITEMS$,4%,1%)) &
\		DECI=ASCII(MID(ITEMS$,5%,1%)) &
\		OPT$=MID(ITEMS$,6%,1%) &

30705		PRINT #CHN%, FNP$(ROWCOL$);R.ON$;B.ON$; &
\		IF OPT$='C' &
		THEN	ON KIND% GOTO 30710, 30715, 30720, 30725, 30730 &
		ELSE	PRINT #CHN%, SPACE$(ASCII(MID(ITEMS$,2%,1%))); &
\			GOTO 30735 &

30710		PRINT #CHN%, VARABLE$;			   ! ALPHA &
\		GOTO 30735 &

30715		PRINT #CHN% USING FRMAT$,ASCII(VARABLE$);  ! ASCII &
\		GOTO 30735 &

30720		PRINT #CHN% USING FRMAT$, CVT$%(VARABLE$)/(10.**DECI); &
\		GOTO 30735				   ! INTEGER &

30725		PRINT #CHN%, FND6$(CVT$%(VARABLE$));	   ! DATE &
\		GOTO 30735 &

30730		PRINT #CHN% USING FRMAT$,CVT$F(VARABLE$);  ! FLOAT &

30735		PRINT #CHN%,G.OFF$;CLRBOT$;RIGHT(ITEMS$,6%)+':  ';B.ON$; &
\		FIN$ = FNINP$(CHN%,0%,'_',ASCII(MID(ITEMS$,3%,1%)),1%) &
\		V% = VAL(FIN$) IF KIND%=3% &
\		V  = VAL(FIN$) IF KIND%>3% &
\		GOTO 30705 IF KIND%=4% AND LEN(FIN$)<>6% AND FIN$<>'' &
\		IF FIN$='' &
		THEN	FIN$ = DEFAL$ IF INSTR(1%,'CF',OPT$)=0% &
\			GOTO 30745 &

30740		IF KIND%=3% OR KIND%=5% &
		THEN	TEMP = 1. &
\			TEMP = 10.**DECI IF INSTR(1%,FIN$,'.')=0% &
\			FIN$ = NUM1$(VAL(FIN$)/TEMP) &
\			FIN$ = FIN$ + '.' IF INSTR(1%,FIN$,'.')=0% &

30745		FNSIO$ = FIN$+'' &
\	FNEND &

30950	!=============================================================== &
	! CALCULATE THE NUMBER OF SPACES TO PRINT FOR PERCENT COMPLETE &
	! ============================================================== &
	DEF*FNPC.SPACE$(TOTREC) &
\		TEMP=TOTREC/50. &
\		SP%=1% &
\		SP%=2% 	IF TEMP	< .99 &
\		SP%=5% 	IF TEMP	< .49 &
\		SP%=10% IF TEMP	< .19 &
\		SP%=25% IF TEMP	< .09 &
\		SP%=50% IF TEMP	< .03 &
\		FNPC.SPACE$=SPACE$(SP%) &
\	FNEND &

30960	!=============================================================== &
	! CALCULATE THE NUMBER TO USE TO PRINT NEXT PERCENT COMPLETE &
	!=============================================================== &
	DEF*FNPC(TOTREC) &
\		TEMP=TOTREC/50. &
\		PC=INT(TOTREC/50.) &
\		PC=INT(TOTREC/25.) IF TEMP	< .99 &
\		PC=INT(TOTREC/10.) IF TEMP	< .49 &
\		PC=INT(TOTREC/5.) IF TEMP	< .19 &
\		PC=INT(TOTREC/2.) IF TEMP	< .09 &
\		PC=INT(TOTREC/1.) IF TEMP	< .03 &
\		FNPC=PC &
\	FNEND &

32767	END &

