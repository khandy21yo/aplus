10	!-------------------------------------------------------------------- &
	! &
	! &
	!		Copyright (c) 1984 BY &
	!		Computer Management Center, Idaho Falls, Idaho &
	! &
	! This software is furnished under a license and may be used and &
	! copied only in accordance with the terms of such license and with &
	! the inclusion of the above copyright notice.  This software or any &
	! other copies therof may not be provided or otherwise made avail- &
	! able to any other person.  No title to and ownership of the soft- &
	! ware is hereby transferred. &
	! &
	! The information in this software is subject to change without no- &
	! tice and should not be construed as a committment by Computer &
	! Management Center. &
	! &
	! CMC assumes no responsibility for the use or reliability of &
	! its software on equipment which is not supported by CMC. &
	! &
	!----------------------------------------------------------------- &

50	ON ERROR GOTO 19000 &
\	JUNK$ = SYS(CHR$(6%) + CHR$(16%) + CHR$(0%) + CHR$(255%) + &
		CHR$(81%) + STRING$(5%,0%) + CHR$(255%) + &
		STRING$(14%,0%) + CHR$(255%) + CHR$(128%+26%) + STRING$(6%,0%)) &

60	OPEN "KB:" AS FILE #1%, MODE 8%+256% &
					!    8% - Echo Control &
					!   16% - Disable hibernation, Cntl-C &
\	CH%=1%				! Keyboard channel &
\	ESC$=CHR$(155%)			! Escape code for VT100 control &
\	CLSCN$=ESC$+"[H"+ESC$+"[J"	! Clear screen &
\	CLRLIN$=ESC$+"[2K"		! Erase entire line &
\	CLREL$=ESC$+"[K"		! Cursor to end of line &
\	INSERT.LIN$=ESC$+"[1L"		! Insert a line &
\	G.OFF$=ESC$+"[m"		! Select graphic off &
\	B.ON$=ESC$+"[1m"		! Bold face on &
\	BLINK.ON$=ESC$+"[5m"		! Blinking &
\	R.ON$=ESC$+"[7m"		! Reverse video &
\	SCROLL.ON$=ESC$+"[1;24r"	! Scroll control &
\	SCROLL.REGION$=ESC$+"[5;19r"	! Scroll region &
\	S.RESET$=ESC$+"c"		! Reset Screen &
\	COLM.ON$=ESC$+"[?3h"		! 132 Column mode &
\	COLM.OFF$=ESC$+"[?3l"		! 80 Column mode &
\	ENTER.COPY$=ESC$+"[?5i"		! Enter media copy &
\	EXIT.COPY$=ESC$+"[?4i"		! Exit media copy &
\	CLRBOT$=ESC$+"[21;1H"+ESC$+"[J"	! Erase cursor to end of screen &
\	LDS.ON$=ESC$+"(0"		! Line drawing set &
\	USASCII$=ESC$+"(B"		! United States ASCII &
\	HID.CURSOR$=ESC$+"[24;80H"	! Hid the cursor in the corner &
\	PRINT #CH%, ESC$;"<";		! Enter ANSI mode &

63	PRINT #CH%, CLSCN$; &

100	FILE.CH%   = 2% &
\	UNIQUE.CH% = 12% &
\	LOOP.DATA% = 2% &
\	LOOP.KEY%  = 1% &
\	KEY.LEN%   = 8% &
\	DATA.LEN%  = 18% &

200	CHACVT.DEVICE$='SY:' &
\	IF FNO%(FILE.CH%,"DEVICE.DAT","/R0","") &
	THEN	300 &
		ELSE	IF FNG%(FILE.CH%,"CHACVT")=0% &
			THEN	CHACVT.DEVICE$=CVT$$(MID(FNL$,7%,20%),-1%) &

290	V%=FNC%(FILE.CH%) &

300	V%=FNO%(FILE.CH%,CHACVT.DEVICE$+"CHACVT.DAT","/SF/RW","") &
\	V%=FNO%(FILE.CH%,CHACVT.DEVICE$+"CHACVT.DAT","/SF/CR:"+ &
		NUM1$(DATA.LEN%),"") IF FNS%=5% &
\	IF FNS% &
	THEN	PRINT #CH%, FNP$(24%,1%);CLRLIN$; &
			"Error";FNS%;"While open CHACVT.DAT";FNP$(24%,55%); &
			"Hit any key to continue"; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		PRINT #CH%, CLRLIN$;SCROLL.ON$; &
\		V%=FNX%("",0%,"") &

400	OPEN "MENU.FIL/RO" FOR INPUT AS FILE  UNIQUE.CH% &
\	DIM #12%, A0$(255%)=64% &
\	COMPANY$=CVT$$(RIGHT(A0$(1%),2%),128%) &
\	CLOSE UNIQUE.CH% &

450	OPEN "NL:" AS FILE 12%, RECORDSIZE 16% &
\	FIELD #12%,	08% AS N.ACC$,		&
			08% AS O.ACC$		&
\	FIELD #12%, 16% AS CHACVT.BUF$ &

500	COMMAND$  = " " &
\	COMMAND$  = COMMAND$+RIGHT(NUM1$(I%+100%),2%) + " " &
		FOR I%=1% TO LOOP.DATA% &

1000	!--------------------------------------------PROGRAM RESTART POINT &
	IF FNG%(FILE.CH%,"")=0% &
	THEN	1010 &
		ELSE	PRINT #CH%, G.OFF; &
\			GOSUB 1060 &
\			GOTO 1030 &

1005	IF FNN%(FILE.CH%) &
	THEN	18910 &
		ELSE	1015 &

1010	PRINT #CH%, SCROLL.ON$;G.OFF$; &
\	GOSUB 1060 &

1015	KEY.POINTER%=FNR(FILE.CH%) &
\	GOSUB 6000 &

1030	FLAG%,CHNG.FLAG%=0% &
\	PRINT #CH%, CLRBOT$;G.OFF$; &
		"COMMAND: Add Erase Change Blank Initialize Default "; &
		"Global Find Next Restore ";FNP$(22%,10%); &
		"View Quit "; &
\	OPT$=CVT$$(FNINP$(CH%,128%," ",1%),32%) &
\	GOTO 1010 IF OPT$="%^C" &
\	OPT$="A" IF OPT$="" &
\	GOTO 1030 IF KEY.POINTER%=0% AND INSTR(1%,"AQI",INP$)=0% &

1040	IF OPT$="Q" &
	THEN	PRINT #CH%, CLSCN$;COLM.OFF$; &
			"Returning to previous menu."; &
			FNP$(1%,64%);"Please wait. . . " &
\		V%=FNX%("",0%,"") &

1050	GOTO 2000 IF OPT$="A" &
\	GOTO 2400 IF OPT$="E" &
\	GOTO 2200 IF OPT$="C" &
\	GOTO 2300 IF OPT$="B" &
\	GOTO 2000 IF OPT$="F" &
\	GOTO 1005 IF OPT$="N" &
\	GOTO 17000 IF OPT$="V" &
\	GOTO 2500 IF OPT$="D" &
\	GOTO 2600 IF OPT$="I" &
\	GOTO 2700 IF OPT$="G" &
\	GOTO 1000 IF OPT$="R" &
\	GOTO 1030 &

1060	TEMP$     = "New/Old Account Equivalence Table" &
\	PRINT #CH%, CLSCN$;R.ON$;LEFT(TEMP$,39%);SPACE$(40%-LEN(TEMP$)); &
			SPACE$(40%-LEN(COMPANY$));COMPANY$; &
\	PRINT #CH%, FNP$(I%,1%);" ";FNP$(I%,80%);" "; FOR I%=2% TO 19% &
\	PRINT #CH%, FNP$(20%,1%);SPACE$(80%);G.OFF$; &
\	PRINT #CH%, FNP$(8%,20%);'(01) New Account #'; &
		FNP$(10%,20%);'(02) Old Account #'; &
\	RETURN &

2000	!-------------------------------------------------SEARCH FOR KEYS &
	FOR LOOP%=1% TO LOOP.DATA% &
\		INP$='' &
\		INP$=DEFAULT$(LOOP%) IF OPT$='A' &
\		GOSUB 6030 &
\	NEXT LOOP% &
\	OPTION$="FIND " IF OPT$="F" &
\	OPTION$="ADD " IF OPT$="A" &
\	FLAG%=(OPT$='A') &

2010	SEARCH.KEY$="" &
\	FOR LOOP%=1% TO 1% &
\		GOSUB 6020 &
\		GOTO 1010 IF INP$="%^C" &
\	NEXT LOOP% &
\	GOTO 1010 IF SEARCH.KEY$="" &

2030	IF OPT$<>'A' &
	THEN	V%=FNG%(FILE.CH%,CVT$$(SEARCH.KEY$,128%)) &
\		GOTO 1015 &

2100	!-----------------------------------------------------ADD RECORD &
	OPTION$="ADD " &
\	FLAG%=-1% &
\	FOR LOOP%=2% TO LOOP.DATA% &
\	  	GOSUB 6010 &
\		GOTO 1010 IF INP$="%^C" OR INP$="%ABORT" &
\	NEXT LOOP% &

2110	IF FNA%(FILE.CH%,CHACVT.BUF$) &
	THEN	PRINT #CH%, FNP$(24%,1%);"Error";FNS%;"while adding to file"; &
			FNP$(24%,55%);"Hit any key to continue"; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		GOTO 1000 &

2120	KEY.POINTER%=FNR(FILE.CH%) &
\	GOTO 1030 &

2200	!--------------------------------------------------CHANGE RECORD &
	OPTION$ = "CHANGE " &
\	FLAG%, CHNG.FLAG% = -1% &
\	PRINT #CH%, CLRBOT$; "Change:"; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",2%),32%) &
\	GOTO 1010 IF INP$="%^C" &
\	GOTO 1030 IF INP$="" &
\	LOOP%=(INSTR(1%,COMMAND$," "+INP$+" ")+2%)/3% &
\	GOTO 2200 IF LOOP%=0% &
\	GOSUB 6010 &
\	GOTO 1010 IF INP$="%^C" &
\	GOTO 2200 IF INP$="%ABORT" &

2210	V%=FNG%(-FILE.CH%,NUM1$(KEY.POINTER%)) &
			IF KEY.POINTER%<>0% &
\	V%=FNU%(-FILE.CH%,CHACVT.BUF$) IF FNS%=0% &
\	IF FNS% &
	THEN	PRINT #CH%, FNP$(24%,1%);"Error";FNS%;"while changing the file"; &
			FNP$(24%,55%);"Hit any key to continue"; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		GOTO 1010 &

2220	GOTO 2200 &

2300	!--------------------------------------------------CHANGE RECORD &
	OPTION$ = "BLANK " &
\	TEMP%=1% &
\	PRINT #CH%, CLRBOT$; "Blank:"; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",2%),32%) &
\	GOTO 1010 IF INP$="%^C" &
\	GOTO 1030 IF INP$="" &
\	LOOP%=(INSTR(1%,COMMAND$," "+INP$+" ")+2%)/3% &
\	GOTO 2300 IF LOOP%=0% OR LOOP%<4% &
\	INP$="" &
\	GOSUB 6030 &

2310	V%=FNG%(-FILE.CH%,NUM1$(KEY.POINTER%)) &
			IF KEY.POINTER%<>0% &
\	V%=FNU%(-FILE.CH%,CHACVT.BUF$) IF FNS%=0% &
\	IF FNS% &
	THEN	PRINT #CH%, FNP$(24%,1%);"Error";FNS%;"while changing to file"; &
			FNP$(24%,55%);"Hit any key to continue"; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		GOTO 1010 &

2320	GOTO 2300 &

2400	!-----------------------------------------------DELETE RECORD &
	PRINT #CH%, CLRBOT$;"Confirm deletion (Yes/No) "; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",1%),32%) &
\	GOTO 1010 IF INP$="%^C" &
\	IF INP$<>"Y" &
	THEN	PRINT #CH%, FNP$(24%,1%);CLRLIN$;"This record has "; B.ON$; &
			BLINK.ON$;"NOT ";G.OFF$; "been deleted."; &
			FNP$(24%,55%);"Hit any key to continue "; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		GOTO 1030 &

2420	IF FND%(FILE.CH%,"")=0% &
	THEN	PRINT #CH%, FNP$(24%,1%);CLRLIN$;"This record has been deleted."; &
			FNP$(24%,55%);"Hit any key to continue "; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		GOTO 1005 &

2430	PRINT #CH%, FNP$(24%,1%);"Error";FNS%;"while deleting to file"; &
		FNP$(24%,55%);"Hit any key to continue"; &
\	INP$=FNINP$(CH%,128%," ",1%) &
\	GOTO 1005 &

2500	!-----------------------------------------------SET DEFAULT VALUES &
	FOR LOOP%=1% TO LOOP.DATA% &
\		INP$=DEFAULT$(LOOP%) &
\		GOSUB 6030 &
\	NEXT LOOP% &

2510	OPTION$="DEFAULT " &
\	PRINT #CH%, CLRBOT$;"Default:"; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",2%),32%) &
\	GOTO 1010 IF INP$="%^C" OR INP$="" &
\	LOOP%=(INSTR(1%,COMMAND$," "+INP$+" ")+2%)/3% &
\	GOTO 2510 IF LOOP%=0% &
\	GOSUB 6010 &
\	GOTO 1010 IF INP$="%^C" &
\	DEFAULT$(LOOP%)=INP$ &
\	GOTO 2510 &

2600	!-----------------------------------------------INITIALIZATION &
	INP$='' &
\	GOSUB 6030 FOR LOOP%=1% TO LOOP.DATA% &
\	INITIALIZE$=" " &

2610	PRINT #CH%, CLRBOT$;FNP$(22%,1%);INITIALIZE$; &
		FNP$(21%,1%);"Field to initialize:"; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",2%),32%) &
\	GOTO 1010 IF INP$="%^C" &
\	IF INP$<>"" &
	THEN	LOOP%=(INSTR(1%,COMMAND$," "+INP$+" ")+2%)/3% &
\		INITIALIZE$=INITIALIZE$+INP$+" " IF &
			LOOP%<>0% AND (INSTR(1%,INITIALIZE$," "+INP$+" ") &
				+2%)/3%=0% &
\		GOTO 2610 &

2620	GOTO 1010 IF INITIALIZE$="" &
\	OPT$="C" &
\	OPTION$="INITIALIZE " &
\	FLAG%,CHNG.FLAG%=-1% &
\	GOTO 2660 &

2650	GOTO 18910 IF FNN%(FILE.CH%) &
\	KEY.POINTER%=FNR(FILE.CH%) &

2660	GOSUB 6000 &
\	FOR I%=1% TO LEN(INITIALIZE$)/3% &
\		LOOP%=(INSTR(1%,COMMAND$,MID(INITIALIZE$,I%*3%-2%,3%))+2%)/3% &
\		GOSUB 6010 &
\		GOTO 1030 IF INP$="%^C" &

2680		IF INP$<>"" AND INP$<>"%ABORT" &
		THEN	V%=FNG%(-FILE.CH%,NUM1$(KEY.POINTER%)) &
				IF KEY.POINTER%<>0% &
\			V%=FNU%(-FILE.CH%,CHACVT.BUF$) IF FNS%=0% &
\			IF FNS% &
			THEN	PRINT #CH%, FNP$(24%,1%);"Error";FNS%; &
					"while changing to file"; &
				FNP$(24%,55%);"Hit any key to continue"; &
\				INP$=FNINP$(CH%,128%," ",1%) &
\				GOTO 1010 &

2690	NEXT I% &
\	GOTO 2650 &

2700	!--------------------------------GLOBAL SEARCH AND REPLACE/DELETE &
	INP$='' &
\	GOSUB 6030 FOR LOOP%=1% TO LOOP.DATA% &
\	OPT$="F" &
\	OPTION$="SEARCH FOR " &
\	SEARCH$=" " &
\	SEARCH$(LOOP%),REPLCE$(LOOP%)="" FOR LOOP%=1% to LOOP.DATA% &

2710	PRINT #CH%, CLRBOT$;"Field to search for "; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",2%),32%) &
\	GOTO 1010 IF INP$="%^C" &
\	IF INP$<>"" &
	THEN	LOOP%=(INSTR(1%,COMMAND$," "+INP$+" ")+2%)/3% &
\		SEARCH$=SEARCH$+INP$+" " IF &
			LOOP%<>0% AND (INSTR(1%,SEARCH$," "+INP$+" ")+2%)/3%=0% &
\		GOSUB 6010 &
\		GOTO 1010 IF INP$="%^C" &
\		SEARCH$(LOOP%)=INP$ &
\		GOTO 2710 &

2730	INP$='' &
\	GOSUB 6030 FOR LOOP%=1% TO LOOP.DATA% &
\	OPT$="C" &
\	FLAG%,CHNG.FLAG%=-1% &

2740	GOTO 1010 IF SEARCH$="" &
\	PRINT #CH%, CLRBOT$;"COMMAND: Delete Replace "; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",1%),32%) &
\	GOTO 1010 IF INP$="%^C" &
\	GOTO 2760 IF INP$="D" &
\	GOTO 2800 IF INP$="R" &
\	GOTO 2740 &

2750	GOTO 18910 IF FNN%(FILE.CH%) &
\	KEY.POINTER%=FNR(FILE.CH%) &

2760	PRINT #CH%, CLRBOT$;"Searching. . . "; &
\	LSET CHACVT.BUF$=FNL$ &
\	SEARCH.FLAG%=0% &
\	FOR I%=1% TO LEN(SEARCH$)/3% &
\		LOOP%=(INSTR(1%,COMMAND$,MID(SEARCH$,I%*3%-2%,3%))+2%)/3% &
\		GOSUB 6040 &
\		GOTO 2770 IF SEARCH.FLAG%=-1% &
\	NEXT I% &
\	GOTO 2750 &

2770	GOSUB 6000 &
\	PRINT #CH%, CLRBOT$;"Confirm deletion (Yes/No) "; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",1%),32%) &
\	GOTO 1030 IF INP$="%^C" &
\	IF INP$<>"Y" &
	THEN	PRINT #CH%, FNP$(24%,1%);CLRLIN$;"This record has "; B.ON$; &
			BLINK.ON$;"NOT ";G.OFF$; "been deleted."; &
			FNP$(24%,55%);"Hit any key to continue "; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		GOTO 1010 IF INP$="%^C" &
\		GOTO 2750 &

2790	IF FND%(FILE.CH%,"") &
	THEN	PRINT #CH%, FNP$(24%,1%);"Error";FNS%;"while deleting this "; &
			"record";FNP$(24%,55%);"Hit any key to continue"; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		GOTO 1010 &

2795	PRINT #CH%, FNP$(24%,1%);CLRLIN$;"This record has been deleted."; &
		FNP$(24%,55%);"Hit any key to continue "; &
\	INP$=FNINP$(CH%,128%," ",1%) &
\	GOTO 1010 IF INP$="%^C" &
\	GOTO 2750 &

2800	OPTION$="REPLACE " &
\	GOTO 2820 &

2810	GOTO 18910 IF FNN%(FILE.CH%) &
\	KEY.POINTER%=FNR(FILE.CH%) &

2820	PRINT #CH%, CLRBOT$;"Searching. . . "; &
\	LSET CHACVT.BUF$=FNL$ &
\	SEARCH.FLAG%=0% &
\	FOR I%=1% TO LEN(SEARCH$)/3% &
\		LOOP%=(INSTR(1%,COMMAND$,MID(SEARCH$,I%*3%-2%,3%))+2%)/3% &
\		GOSUB 6040 &
\		GOTO 2825 IF SEARCH.FLAG% &
\	NEXT I% &
\	GOTO 2810 &

2825	GOSUB 6000 &

2830	PRINT #CH%, CLRBOT$; "Change:"; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",2%),32%) &
\	GOTO 1010 IF INP$="%^C" &
\	GOTO 2810 IF INP$="" &
\	LOOP%=(INSTR(1%,COMMAND$," "+INP$+" ")+2%)/3% &
\	GOTO 2830 IF LOOP%=0% &
\	GOSUB 6010 &
\	GOTO 1010 IF INP$="%^C" &
\	GOTO 2830 IF INP$="%ABORT" &

2840	IF INP$<>"" AND INP$<>"%ABORT" &
	THEN	V%=FNG%(-FILE.CH%,NUM1$(KEY.POINTER%)) &
			IF KEY.POINTER%<>0% &
\		V%=FNU%(-FILE.CH%,CHACVT.BUF$) IF FNS%=0% &
\		IF FNS% &
		THEN	PRINT #CH%, FNP$(24%,1%);"Error";FNS%; &
				"while changing to file"; &
			FNP$(24%,55%);"Hit any key to continue"; &
\				INP$=FNINP$(CH%,128%," ",1%) &
\				GOTO 1010 &

2850	GOTO 2830 &

6000	!-----------------------------------------------LOOP GOSUB SECTION &
	RETURN IF KEY.POINTER%=0% &
\	LSET CHACVT.BUF$=FNL$ &
\	FOR LOOP%=1% TO LOOP.DATA% &
\		ON LOOP% GOSUB	6220, 6240 &
\	NEXT LOOP% &
\	RETURN &

6010	ON LOOP% GOSUB 6210, 6230 &
\	RETURN &
		! INPUT DATA &

6020	ON LOOP% GOSUB 6210 &
\	SEARCH.KEY$=N.ACC$ &
\	RETURN &
		! SEARCH KEY &

6030	ON LOOP% GOSUB 6217, 6237 &
\	RETURN &
		! DEFAULT &

6040	SEARCH.FLAG%=-1% IF N.ACC$=SEARCH$(LOOP%) AND LOOP%=1% &
\	SEARCH.FLAG%=-1% IF O.ACC$=SEARCH$(LOOP%) AND LOOP%=2% &
\	RETURN &
		! SEARCH FOR MATCH &

6200	!-----------------------------------------------DATA ENTRY SECTION &

6210	F.LEN%=LEN(N.ACC$) &
\	PRINT #CH%, FNP$(8%,40%);R.ON$;B.ON$; &
\	PRINT #CH%, SPACE$(F.LEN%); IF CHNG.FLAG%=0% &
\	PRINT #CH%, N.ACC$; IF CHNG.FLAG% &
\	PRINT #CH%, G.OFF$;CLRBOT$;OPTION$;"ALPHA: ";B.ON$; &
\	INP$=FNINP$(CH%,0%,"_",F.LEN%) &
\	INP$=DEFAULT$(LOOP%) IF INP$="" AND OPT$<>"F" &
\	GOTO 6210 IF INP$="" AND CHNG.FLAG%=0% AND OPT$<>"F" &
\	RETURN IF INP$="%^C" &

6217	RSET N.ACC$=INP$ IF INP$<>"" OR CHNG.FLAG%=0% &

6220	PRINT #CH%, FNP$(8%,40%);B.ON$;N.ACC$;G.OFF$; &
\	RETURN &

6230	INP$="" &
\	F.LEN%=LEN(O.ACC$) &
\	PRINT #CH%, FNP$(10%,40%);R.ON$;B.ON$; &
\	PRINT #CH%, SPACE$(F.LEN%); IF CHNG.FLAG%=0% &
\	PRINT #CH%, O.ACC$; IF CHNG.FLAG% &
\	PRINT #CH%, G.OFF$;CLRBOT$;OPTION$;"ALPHA: ";B.ON$; &
\	INP$=FNINP$(CH%,0%,"_",F.LEN%) &
\	RETURN IF INP$="%^C" &

6237	RSET O.ACC$=INP$ IF INP$<>"" OR CHNG.FLAG%=0% &

6240	PRINT #CH%, FNP$(10%,40%);B.ON$;O.ACC$;G.OFF$; &
\	RETURN &

17000	!-----------------------------------------------------PRINT ROUTINE &
	TEMP$     = "New/Old Account Equivalence Table" &
\	PRINT #CH%, CLSCN$;R.ON$;LEFT(TEMP$,39%);SPACE$(40%-LEN(TEMP$)); &
			SPACE$(40%-LEN(COMPANY$));COMPANY$; &
\	PRINT #CH%, FNP$(I%,1%);" ";FNP$(I%,80%);" "; FOR I%=2% TO 19% &
\	PRINT #CH%, FNP$(20%,1%);SPACE$(80%);G.OFF$; &
\	PRINT #CH%, G.OFF$;FNP$(2%,2%);'NEW';FNP$(3%,2%);'ACCOUNT'; &
		FNP$(2%,11%);'OLD';FNP$(3%,11%);'ACCOUNT'; &
\	PRINT #CH%, R.ON$;LDS.ON$;FNP$(4%,2%);STRING$(78%,113%);FNP$(4%,10%); &
		CHR$(110%);USASCII$;G.OFF$;SCROLL.REGION$ &
\	PRINT #CH%, R.ON$;LDS.ON$;FNP$(I%,10%);CHR$(120%);USASCII$;G.OFF$; &
			FOR I%=2% TO 3% &
\	DISPLAY.LINE%=0% &
\	PRINT.LINE%=5% &

17010	V%=FNG%(-FILE.CH%,NUM1$(KEY.POINTER%)) &
			IF KEY.POINTER%<>0% &
\	LSET CHACVT.BUF$=FNL$ &
\	GOTO 17105 &

17100	IF FNN%(FILE.CH%) &
	THEN	17200 &
		ELSE	LSET CHACVT.BUF$=FNL$ &

17105	I%=PRINT.LINE% &
\	PRINT #CH%, FNP$(I%,2%);N.ACC$;FNP$(I%,11%);O.ACC$; &
\	PRINT #CH%, R.ON$;LDS.ON$;FNP$(I%,10%);CHR$(120%); &
		G.OFF$;USASCII$; &
\	PRINT.LINE%=PRINT.LINE%+1% IF PRINT.LINE%<=18% &

17110	IF DISPLAY.LINE%>=14% &
	THEN	PRINT #CH%, FNP$(24%,55%);"Hit any key to continue "; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		GOTO 1010 IF INP$="%^C" &
\		DISPLAY.LINE%=0% &
\		PRINT #CH%, CLRBOT$;FNP$(I%+2%,1%); &

17120	PRINT #CH% &
\	DISPLAY.LINE%=DISPLAY.LINE%+1% &
\	IF PRINT.LINE%>=18% &
	THEN	PRINT #CH%, FNP$(I%,1%);R.ON$;" "; &
			FNP$(I%,80%);" ";FNP$(I%,1%);G.OFF$; &

17190	GOTO 17100 &

17200	IF DISPLAY.LINE%>=0% &
	THEN	PRINT #CH%, FNP$(24%,1%);"End of file.  "; &
			FNP$(24%,55%);"Hit any key to continue "; &
\		INP$=FNINP$(CH%,128%," ",1%) &

17210	V%=FNG%(-FILE.CH%,NUM1$(KEY.POINTER%)) &
			IF KEY.POINTER%<>0% &
\	PRINT #CH%, FNP$(1%,1%);SCROLL.ON$; &
\	GOTO 1010 &

18910	!----------------------------------------------END OF FILE &
	PRINT #CH%, CLRBOT$;FNP$(24%,1%);"End of file has been reached."; &
		FNP$(24%,55%);"Hit any key to continue "; &
\	INP$=FNINP$(CH%,128%," ",1%) &
\	GOTO 1000 &

19000	!-------------------------------------------------ERROR TRAPPING &
	IF ERR=28% &
	THEN	JUNK$=SYS(CHR$(6%) + CHR$(-7%)) &
\		IF ERL<1000% &
		THEN	OPT$="Q" &
\			RESUME 1040 &

19010	IF ERR=28% &
	THEN	V%=FNG%(-FILE.CH%,NUM1$(KEY.POINTER%)) &
			IF KEY.POINTER%<>0% &
\		RESUME 1010 &

19020	IF ERR=52% &
	THEN	INP$="" &
\		RESUME &

19030	IF ERR=11% &
	THEN	RESUME 1000 &

19040	IF ERL=400% &
	THEN	RESUME 450 &

19900	ON ERROR GOTO 0 &

30000	DEF FNINP$(CHN%,KYP%,FILLCHAR$,INPUTLEN%) &
\		PRINT #CHN%, STRING$(INPUTLEN%,ASCII(FILLCHAR$)); &
			  STRING$(INPUTLEN%,8%); &
\		PRINT #CHN%, RECORD 256%, CHR$(KYP%+INPUTLEN%)+FILLCHAR$; &
\		GET #CHN% &
\		FIELD #CHN%, RECOUNT AS BUFFER$ &
\		IF ASCII(BUFFER$)=128% OR ASCII(BUFFER$)=8% &
			OR ASCII(BUFFER$)=23% &
		THEN	BUFFER$="%END" IF MID(BUFFER$,2%,4%)="[21~" &
				OR ASCII(BUFFER$)=8% &
\			BUFFER$="%ABORT" IF MID(BUFFER$,2%,4%)="[19~" &
				OR ASCII(BUFFER$)=23% &

30010		BUFFER$="%^C" IF ASCII(BUFFER$)=3% &
\		FNINP$=CVT$$(BUFFER$,4%) &
\	FNEND &

30050	  !------------------------------------------------------------ &
	  ! A N S I   D I R E C T   C U R S O R   A D D R E S S I N G &
	  !------------------------------------------------------------ &
	  DEF*FNP$(ROW%,COL%)=ESC$+"["+NUM1$(ROW%)+";"+NUM1$(COL%)+"H" &

32767	END &

