10	!---------------------------------------------------------------&
	!	Chart of Accounts Maintenance Program			&
	!								&
	!	CHART.B2S	V1.0	February 1985			&
	!								&
	! Author - Randall Beard, Computer Management Center, Inc.	&
	!								&
	! Files-CONTRL.DAT    	-ISAM	Control File			&
	!	CHART.DAT     	-ISAM	Chart of Accounts File		&
	!								&
	!---------------------------------------------------------------&
	! 								&
	! 								&
	!		      Copyright (c) 1984 by 			&
	!	  Computer Management Center, Idaho Falls, Idaho	&
	! 								&
	! This software is furnished under a license and may be used	&
	! and copied only in accordance with the terms of such license 	&
	! and with the inclusion of the above copyright notice.  This 	&
	! software or any other copies therof may not be provided or 	&
	! otherwise made available to any other person.  No title to 	&
	! and ownership of the software is hereby transferred.		&
	! 								&
	! The information in this software is subject to change without &
	! notice and should not be construed as a committment by 	&
	! Computer Management Center. 					&
	! 								&
	! CMC assumes no responsibility for the use or reliability of 	&
	! its software on equipment which is not supported by CMC. 	&
	! 								&
	!---------------------------------------------------------------&

50	ON ERROR GOTO 19000 &
\	DIM DEFAULT$(30%),SEARCH$(30%),REPLCE$(30%), &
		CHART.MON$(13%), MNAME$(12%) &
\	READ MNAME$(M%) FOR M%=1% TO 12% &
\	JUNK$ = SYS(CHR$(6%)+CHR$(-7%)) &

60	OPEN 'KB:' AS FILE #1%, MODE 8%+256% &
					!    8% - Echo Control &
					!   16% - Disable hibernation, Cntl-C &
\	CH%=1%				! Keyboard channel &
\	ESC$=CHR$(155%)			! Escape code for VT100 control &
\	CLSCN$=ESC$+'[H'+ESC$+'[J'	! Clear screen &
\	CLRLIN$=ESC$+'[2K'		! Erase entire line &
\	G.OFF$=ESC$+'[m'		! Select graphic off &
\	B.ON$=ESC$+'[1m'		! Bold face on &
\	BLINK.ON$=ESC$+'[5m'		! Blinking &
\	R.ON$=ESC$+'[7m'		! Reverse video &
\	CLRBOT$=ESC$+'[21;1H'+ESC$+'[J'	! Erase cursor to end of screen &
\	LDS.ON$=ESC$+'(0'		! Line drawing set &
\	USASCII$=ESC$+'(B'		! United States ASCII &

80	!COM(THIS) DROP.DEAD.DATE$ = 8, VERSION.NO$ = 6, SERIAL.NO$ = 10 &
	DROP.DEAD.DATE$='        ' &
\	VERSION.NO$='V1.0' &
\	IF DROP.DEAD.DATE$<>'' &
	THEN	IF DROP.DEAD.DATE$<MID(SYS(CHR$(6%)+CHR$(20%)+CHR$(0%)+CHR$(0%)+ &
				CHR$(1%)+CHR$(SWAP%(1%))),7%,8%) &
		THEN	MESG$=MID(SYS(CHR$(6%)+CHR$(9%)+CHR$(47%)), &
				3%,30%) &
\			PRINT #CH%, MESG$; &
\			V$=SYS(CHR$(9%)) &

100	DEVICE.CH%,CONTRL.CH%,&
	CHART.CH%	= 02% &
\	MENU.CH%   	= 12% &
\	LOOP.DATA% 	= 23% &

200	IF FNO%(DEVICE.CH%,'DEVICE.DAT','/R0','')=0% &
	THEN	CONTRL.DEVICE$=CVT$$(MID(FNL$,7%,20%),-1%) &
			IF FNG%(DEVICE.CH%,'CONTRL')=0% &
\		CHART.DEVICE$=CVT$$(MID(FNL$,7%,20%),-1%) &
			IF FNG%(DEVICE.CH%,'CHART')=0% &
\		V%=FNC%(DEVICE.CH%) &

300	V%=FNO%(CONTRL.CH%,CONTRL.DEVICE$+'CONTRL.DAT','/RW','') &
\	V%=FNO%(CONTRL.CH%,CONTRL.DEVICE$+'CONTRL.DAT','/CR:8,64','') &
								IF FNS%=5% &
\	IF FNS% &
	THEN	V$=FNMESS$(CH%,FNS%,'CONTRL.DAT',0%,-1%) &
\		PRINT #CH%,CLSCN$;B.ON$;R.ON$;BLINK.ON$; &
			FNP$('1;75');'WAIT';G.OFF$;FNP$('24;1'); &
				FNX%('',0%,'') &

305	IF FNG%(CONTRL.CH%,'CHART ') &
	THEN	GOSUB 6800 &
		ELSE	MM%=CVT$%(MID(FNL$,9%,2%)) &
\			YY%=CVT$%(MID(FNL$,21%,2%)) &
\			FF%=CVT$%(MID(FNL$,17%,2%)) &

309	V%=FNC%(CONTRL.CH%) &

310	V%=FNO%(CHART.CH%,CHART.DEVICE$+'CHART.DAT','/RW','') &
\	V%=FNO%(CHART.CH%,CHART.DEVICE$+'CHART.DAT','/CR:10,256','') &
								IF FNS%=5% &
\	IF FNS% &
	THEN	V$=FNMESS$(CH%,FNS%,' CHART.DAT',0%,-1%) &
\		PRINT #CH%,CLSCN$;B.ON$;R.ON$;BLINK.ON$; &
			FNP$('1;75');'WAIT';G.OFF$;FNP$('24;1'); &
				FNX%('',0%,'') &

400	OPEN 'MENU.FIL/RO' FOR INPUT AS FILE MENU.CH% &
\	DIM #12%, A0$(1%)=64% &
\	COMPANY$=CVT$$(RIGHT(A0$(1%),2%),128%) &
\	CLOSE MENU.CH% &

450	OPEN 'NL:' AS FILE 12%, RECORDSIZE 256% &
\	FIELD #12%, 256% AS CHART.BUF$ &
\	FIELD #12%,	08% AS CHART.ACCNO$, 	&
			40% AS CHART.DESC$,	&
			02% AS CHART.ACCTYP$,	&
			02% AS CHART.MONTH$,	&
			02% AS TEMP$,		&
			08% AS CHART.WORK1$,	&
			08% AS CHART.WORK2$,	&
			08% AS CHART.LAST$,	&
			08% AS CHART.CURR$,	&
			08% AS CHART.BACKUP$,	&
			08%*13% AS TEMP$,	&
			08% AS CHART.YR1$,	&
			08% AS CHART.YR2$,	&
			08% AS CHART.YR3$,	&
			08% AS CHART.YR4$,	&
			01% AS CHART.FLAG$,	&
			01% AS CHART.PRTFLAG$,	&
			04% AS CHART.TYP$,	&
			02% AS CHART.DIV$,	&
			02% AS CHART.DEP$,	&
			02% AS CHART.LOC$,	&
			04% AS CHART.CF.CODE$,	&
			04% AS CHART.WC.CODE$	&
\	FIELD #12%,	86%+8%*M% AS TEMP$,	&
			08% AS CHART.MON$(M%) &
				FOR M%=1% TO 13% &

500	FRAME$=FRAME$+FNP$(NUM1$(I%)+';1')+' '+FNP$(NUM1$(I%)+';80')+ &
							' ' FOR I%=2% TO 19% &
\	COMMAND$  = ' ' &
\	COMMAND$  = COMMAND$+RIGHT(NUM1$(I%+100%),2%) + ' ' &
		FOR I%=1% TO LOOP.DATA% &

1000	!--------------------------------------------PROGRAM RESTART POINT &
	IF 	FNG%(CHART.CH%,'')=0% &
	THEN	GOTO 1010 &
	ELSE	PRINT #CH%, G.OFF$; &
\		GOSUB 1060 &
\		GOTO 1030 &

1005	IF FNN%(CHART.CH%) &
	THEN	18910 &
	ELSE	GOTO 1015 &

1010	PRINT #CH%, COLM.OFF$;FNSR$('1;24');G.OFF$; &
\	GOSUB 1060 &

1015	KEY.POINTER%=FNR(CHART.CH%) &
\	DATA.POINTER%=FNR(CHART.CH%+1%) &
\	GOSUB 6000 &

1030	CHNG.FLAG%=0% &
\	PRINT #CH%, CLRBOT$;G.OFF$; &
		'COMMAND: Add Erase Change Blank Initialize Default '; &
		'Global Find Next Restore ';FNP$('22;10'); &
		'View Quit '; &
\	OPT$=CVT$$(FNINP$(CH%,128%,' ',1%,1%),32%) &
\	OPT$='A' IF OPT$='' &
\	OPT$='' IF KEY.POINTER%=0% AND INSTR(1%,'AQ',OPT$)=0% &

1040	IF OPT$='Q' &
	THEN	PRINT #CH%,CLSCN$;B.ON$;R.ON$;BLINK.ON$; &
			FNP$('1;75');'WAIT';G.OFF$;FNP$('24;1'); &
				FNX%('',0%,'') &

1050	GOTO 2000 IF OPT$='A' &
\	GOTO 2400 IF OPT$='E' &
\	GOTO 2200 IF OPT$='C' &
\	GOTO 2300 IF OPT$='B' &
\	GOTO 2000 IF OPT$='F' &
\	GOTO 1005 IF OPT$='N' &
\	GOTO 17000 IF OPT$='V' &
\	GOTO 2500 IF OPT$='D' &
\	GOTO 2600 IF OPT$='I' &
\	GOTO 2700 IF OPT$='G' &
\	GOTO 1000 IF OPT$='R' &
\	GOTO 1030 &

1060	TEMP$     = 'Chart of Accounts Maintenance' &
\	PRINT #CH%, FNSR$('1;24');COLM.OFF$;G.OFF$; &
			CLSCN$+R.ON$+LEFT(TEMP$,39%)+SPACE$(40%-LEN(TEMP$))+ &
			SPACE$(40%-LEN(COMPANY$))+COMPANY$; &
			FRAME$;FNP$('20;1')+SPACE$(80%)+G.OFF$; &
\	PRINT #CH%,  &
		FNP$('2;2');'(01) Account #'; &
		FNP$('3;2');'(02) Account Type'; &
		FNP$('4;2');'(03) Description'; &
		FNP$('6;2');'(04) Period #'; &
		FNP$('8;2');'(05)'; &
		FNP$('9;2');'(06)'; &
		FNP$('10;2');'(07)'; &
		FNP$('11;2');'(08)'; &
		FNP$('12;2');'(09)'; &
		FNP$('13;2');'(10)'; &
		FNP$('8;41');'(11)'; &
		FNP$('9;41');'(12)'; &
		FNP$('10;41');'(13)'; &
		FNP$('11;41');'(14)'; &
		FNP$('12;41');'(15)'; &
\	PRINT #CH%, FNP$('13;41');'(16)'; &
		FNP$('14;41');'(17) Summary by Account'; &
		FNP$('15;10'); &
		'--------------------Financial Codes------------------'; &
		FNP$('17;10');'(18) Cash Flow Code'; &
		FNP$('18;10');'(19) Work Capital Code'; &
		FNP$('19;10');'(20) Type'; &
		FNP$('17;45');'(21) Division'; &
		FNP$('18;45');'(22) Department'; &
		FNP$('19;45');'(23) Location'; &
\	TEMP.MONTH% = 0% &
\	RETURN &

2000	!-------------------------------------------------SEARCH FOR KEYS &
	FOR LOOP%=1% TO LOOP.DATA% &
\		INP$='' &
\		INP$=DEFAULT$(LOOP%) IF OPT$='A' &
\		GOSUB 6030 &
\	NEXT LOOP% &
\	OPTION$='FIND ' IF OPT$='F' &
\	OPTION$='ADD ' IF OPT$='A' &

2010	GOSUB 6020 &

2030	V%=FNG%(CHART.CH%,SEARCH.KEY$) &
\	IF OPT$<>'A' OR FNS%=0% &
	THEN	KEY.POINTER%=FNR(CHART.CH%) &
\		DATA.POINTER%=FNR(CHART.CH%+1%) &
\		GOTO 1015 &

2100	!-----------------------------------------------------ADD RECORD &
	PRINT #CH%, CLRBOT$;'Short or Long form '; &
\	FORM$=CVT$$(FNINP$(CH%,128%,' ',1%,1%),-1%) &
\	FORM$='S' IF FORM$<>'L' &
\	LSET CHART.YR1$,CHART.YR2$,CHART.YR3$,CHART.YR4$,	&
		CHART.WORK1$,CHART.WORK2$ = STRING$(8%,0%) &
\	LSET CHART.MON$(I%)=STRING$(8%,0%) &
		FOR I%=1% TO 13% &

2110	OPTION$='ADD ' &
\	FLAG%=-1% &
\	FOR LOOP%=2% TO LOOP.DATA% &
\	  	GOSUB 6010 IF FORM$='L' OR FORM$='S' AND LOOP%<5% OR &
			FORM$='S' AND LOOP%>17% &
\	NEXT LOOP% &

2120	IF FNA%(CHART.CH%,CHART.BUF$) &
	THEN	V$=FNMESS$(CH%,FNS%,' CHART.DAT',0%,-1%) &
\		GOTO 1000 &

2130	KEY.POINTER%=FNR(CHART.CH%) &
\	DATA.POINTER%=FNR(CHART.CH%+1%) &
\	GOTO 1030 &

2200	!--------------------------------------------------CHANGE RECORD &
	OPTION$ = 'CHANGE ' &
\	FLAG%, CHNG.FLAG% = -1% &
\	PRINT #CH%, CLRBOT$; 'Change:'; &
\	INP$=CVT$$(FNINP$(CH%,128%,' ',2%,1%),32%) &
\	GOTO 1030 IF INP$='' &
\	LOOP%=(INSTR(1%,COMMAND$,' '+INP$+' ')+2%)/3% &
\	GOTO 2200 IF LOOP%=0% &
\	GOSUB 6010 &

2210	V%=FNG%(-CHART.CH%,NUM1$(KEY.POINTER%))+ &
		FNG%(-CHART.CH%-1%,NUM1$(DATA.POINTER%)) &
			IF KEY.POINTER%<>0% AND DATA.POINTER%<>0% &
\	V%=FNU%(CHART.CH%,CHART.BUF$) IF LOOP%>1% AND FNS%=0% &
\	V%=FNU%(-CHART.CH%,CHART.BUF$) IF LOOP%<2% AND FNS%=0% &
\	IF FNS% &
	THEN	V$=FNMESS$(CH%,FNS%,' CHART.DAT',0%,-1%) &
\		GOTO 1010 &

2220	GOTO 2200 &

2300	!--------------------------------------------------CHANGE RECORD &
	OPTION$ = 'BLANK ' &
\	TEMP%=1% &
\	PRINT #CH%, CLRBOT$; 'Blank:'; &
\	INP$=CVT$$(FNINP$(CH%,128%,' ',2%,1%),32%) &
\	GOTO 1030 IF INP$='' &
\	LOOP%=(INSTR(1%,COMMAND$,' '+INP$+' ')+2%)/3% &
\	GOTO 2300 IF LOOP%=0% OR LOOP%<4% &
\	INP$='' &
\	GOSUB 6030 &

2310	V%=FNG%(-CHART.CH%,NUM1$(KEY.POINTER%))+ &
		FNG%(-CHART.CH%-1%,NUM1$(DATA.POINTER%)) &
			IF KEY.POINTER%<>0% AND DATA.POINTER%<>0% &
\	V%=FNU%(CHART.CH%,CHART.BUF$) IF LOOP%>1% AND FNS%=0% &
\	V%=FNU%(-CHART.CH%,CHART.BUF$) IF LOOP%<2% AND FNS%=0% &
\	IF FNS% &
	THEN	V$=FNMESS$(CH%,FNS%,' CHART.DAT',0%,-1%) &
\		GOTO 1010 &

2320	GOTO 2300 &

2400	!-----------------------------------------------DELETE RECORD &
	PRINT #CH%, CLRBOT$;'Confirm deletion (Yes/No) '; &
\	INP$=CVT$$(FNINP$(CH%,128%,' ',1%,1%),32%) &
\	IF INP$<>'Y' &
	THEN	PRINT #CH%, FNP$('24;1');CLRLIN$;'This record has '; B.ON$; &
			BLINK.ON$;'NOT ';G.OFF$; 'been deleted.'; &
			FNP$('24;55');'Hit any key to continue '; &
\		INP$=FNINP$(CH%,128%,' ',1%,1%) &
\		GOTO 1030 &

2420	IF FND%(CHART.CH%,'')=0% &
	THEN	PRINT #CH%, FNP$('24;1');CLRLIN$; &
			'This record has been deleted.'; &
			FNP$('24;55');'Hit any key to continue '; &
\		INP$=FNINP$(CH%,128%,' ',1%,0%) &
\		GOTO 1005 &

2430	V$=FNMESS$(CH%,FNS%,' CHART.DAT',0%,-1%) &
\	GOTO 1005 &

2500	!-----------------------------------------------SET DEFAULT VALUES &
	FOR LOOP%=1% TO LOOP.DATA% &
\		INP$=DEFAULT$(LOOP%) &
\		GOSUB 6030 &
\	NEXT LOOP% &

2510	OPTION$='DEFAULT ' &
\	PRINT #CH%, CLRBOT$;'Default:'; &
\	INP$=CVT$$(FNINP$(CH%,128%,' ',2%,1%),32%) &
\	LOOP%=(INSTR(1%,COMMAND$,' '+INP$+' ')+2%)/3% &
\	GOTO 2510 IF LOOP%=0% &
\	GOSUB 6010 &
\	DEFAULT$(LOOP%)=INP$ &
\	GOTO 2510 &

2600	!-----------------------------------------------INITIALIZATION &
	INP$='' &
\	GOSUB 6030 FOR LOOP%=1% TO LOOP.DATA% &
\	INITIALIZE$=' ' &

2610	PRINT #CH%, CLRBOT$;FNP$('22;1');INITIALIZE$; &
		FNP$('21;1');'Field to initialize:'; &
\	INP$=CVT$$(FNINP$(CH%,128%,' ',2%,1%),32%) &
\	IF INP$<>'' &
	THEN	LOOP%=(INSTR(1%,COMMAND$,' '+INP$+' ')+2%)/3% &
\		INITIALIZE$=INITIALIZE$+INP$+' ' IF &
			LOOP%<>0% AND (INSTR(1%,INITIALIZE$,' '+INP$+' ') &
				+2%)/3%=0% &
\		GOTO 2610 &

2620	GOTO 1010 IF INITIALIZE$='' &
\	INP$='' &
\	GOSUB 6030 FOR LOOP%=1% TO LOOP.DATA% &
\	OPT$='C' &
\	OPTION$='INITIALIZE ' &
\	FLAG%,CHNG.FLAG%=-1% &
\	GOTO 2660 &

2650	GOTO 18910 IF FNN%(CHART.CH%) &
\	KEY.POINTER%=FNR(CHART.CH%) &
\	DATA.POINTER%=FNR(CHART.CH%+1%) &

2660	GOSUB 6000 &
\	FOR I%=1% TO LEN(INITIALIZE$)/3% &
\		LOOP%=(INSTR(1%,COMMAND$,MID(INITIALIZE$,I%*3%-2%,3%))+2%)/3% &
\		GOSUB 6010 &

2680		IF INP$<>'' &
		THEN &
		V%=FNG%(-CHART.CH%,NUM1$(KEY.POINTER%))+ &
				FNG%(-CHART.CH%-1%,NUM1$(DATA.POINTER%)) &
				IF KEY.POINTER%<>0% AND DATA.POINTER%<>0% &
\			V%=FNU%(CHART.CH%,CHART.BUF$) IF LOOP%>1% AND FNS%=0% &
\			V%=FNU%(-CHART.CH%,CHART.BUF$) IF LOOP%<2% AND FNS%=0% &
\			IF FNS% &
			THEN	V$=FNMESS$(CH%,FNS%,'CHART.DAT',0%,-1%) &
\				GOTO 1010 &

2690	NEXT I% &
\	GOTO 2650 &

2700	!--------------------------------GLOBAL SEARCH AND REPLACE/DELETE &
	INP$='' &
\	GOSUB 6030 FOR LOOP%=1% TO LOOP.DATA% &
\	OPT$='F' &
\	OPTION$='SEARCH FOR ' &
\	SEARCH$=' ' &
\	SEARCH$(LOOP%),REPLCE$(LOOP%)='' FOR LOOP%=1% to LOOP.DATA% &

2710	PRINT #CH%, CLRBOT$;'Field to search for '; &
\	INP$=CVT$$(FNINP$(CH%,128%,' ',2%,1%),32%) &
\	IF INP$<>'' &
	THEN	LOOP%=(INSTR(1%,COMMAND$,' '+INP$+' ')+2%)/3% &
\		SEARCH$=SEARCH$+INP$+' ' IF &
			LOOP%<>0% AND (INSTR(1%,SEARCH$,' '+INP$+' ')+2%)/3%=0% &
\		GOSUB 6010 &
\		SEARCH$(LOOP%)=INP$ &
\		GOTO 2710 &

2730	INP$='' &
\	GOSUB 6030 FOR LOOP%=1% TO LOOP.DATA% &
\	OPT$='C' &
\	FLAG%,CHNG.FLAG%=-1% &

2740	GOTO 1010 IF SEARCH$='' &
\	PRINT #CH%, CLRBOT$;'COMMAND: Delete Replace '; &
\	INP$=CVT$$(FNINP$(CH%,128%,' ',1%,1%),32%) &
\	GOTO 2760 IF INP$='D' &
\	GOTO 2800 IF INP$='R' &
\	GOTO 2740 &

2750	GOTO 18910 IF FNN%(CHART.CH%) &
\	KEY.POINTER%=FNR(CHART.CH%) &
\	DATA.POINTER%=FNR(CHART.CH%+1%) &

2760	PRINT #CH%, CLRBOT$;'Searching. . . '; &
\	LSET CHART.BUF$=FNL$ &
\	SEARCH.FLAG%=0% &
\	FOR I%=1% TO LEN(SEARCH$)/3% &
\		LOOP%=(INSTR(1%,COMMAND$,MID(SEARCH$,I%*3%-2%,3%))+2%)/3% &
\		GOSUB 6040 &
\		GOTO 2770 IF SEARCH.FLAG%=-1% &
\	NEXT I% &
\	GOTO 2750 &

2770	GOSUB 6000 &
\	PRINT #CH%, CLRBOT$;'Confirm deletion (Yes/No) '; &
\	INP$=CVT$$(FNINP$(CH%,128%,' ',1%,1%),32%) &
\	IF INP$<>'Y' &
	THEN	PRINT #CH%, FNP$('24;1');CLRLIN$;'This record has '; B.ON$; &
			BLINK.ON$;'NOT ';G.OFF$; 'been deleted.'; &
			FNP$('24;55');'Hit any key to continue '; &
\		INP$=FNINP$(CH%,128%,' ',1%,1%) &
\		GOTO 2750 &

2790	IF FND%(CHART.CH%,'') &
	THEN	V$=FNMESS$(CH%,FNS%,' CHART.DAT',0%,-1%) &
\		GOTO 1010 &

2795	PRINT #CH%, FNP$('24;1');CLRLIN$;'This record has been deleted.'; &
		FNP$('24;55');'Hit any key to continue '; &
\	INP$=FNINP$(CH%,128%,' ',1%,1%) &
\	GOTO 2750 &

2800	OPTION$='REPLACE ' &
\	GOTO 2820 &

2810	GOTO 18910 IF FNN%(CHART.CH%) &
\	KEY.POINTER%=FNR(CHART.CH%) &
\	DATA.POINTER%=FNR(CHART.CH%+1%) &

2820	PRINT #CH%, CLRBOT$;'Searching. . . '; &
\	LSET CHART.BUF$=FNL$ &
\	SEARCH.FLAG%=0% &
\	FOR I%=1% TO LEN(SEARCH$)/3% &
\		LOOP%=(INSTR(1%,COMMAND$,MID(SEARCH$,I%*3%-2%,3%))+2%)/3% &
\		GOSUB 6040 &
\		GOTO 2825 IF SEARCH.FLAG% &
\	NEXT I% &
\	GOTO 2810 &

2825	GOSUB 6000 &

2830	PRINT #CH%, CLRBOT$; 'Change:'; &
\	INP$=CVT$$(FNINP$(CH%,128%,' ',2%,1%),32%) &
\	GOTO 2810 IF INP$='' &
\	LOOP%=(INSTR(1%,COMMAND$,' '+INP$+' ')+2%)/3% &
\	GOTO 2830 IF LOOP%=0% &
\	GOSUB 6010 &

2840	IF INP$<>'' &
	THEN	V%=FNG%(-CHART.CH%,NUM1$(KEY.POINTER%))+ &
		FNG%(-CHART.CH%-1%,NUM1$(DATA.POINTER%)) &
			IF KEY.POINTER%<>0% AND DATA.POINTER%<>0% &
\		V%=FNU%(CHART.CH%,CHART.BUF$) IF LOOP%>1% AND FNS%=0% &
\		V%=FNU%(-CHART.CH%,CHART.BUF$) IF LOOP%<2% AND FNS%=0% &
\		IF FNS% &
		THEN	V$=FNMESS$(CH%,FNS%,' CHART.DAT',0%,-1%) &
\			GOTO 1010 &

2850	GOTO 2830 &

6000	!-----------------------------------------------LOOP GOSUB SECTION &
	RETURN IF DATA.POINTER%<=0% &
\	V%=FNG%(-CHART.CH%,NUM1$(KEY.POINTER%))+ &
		FNG%(-CHART.CH%-1%,NUM1$(DATA.POINTER%)) &
\	LSET CHART.BUF$=FNL$ &

6005	FOR LOOP%=1% TO LOOP.DATA% &
\		ON LOOP% GOSUB	6220,6240,6260,6280,6300,6300,6300,6300,6300, &
			6300,6300,6300,6300,6300,6300,6300,6340,6360, &
			6380,6400,6420,6440,6460 &
\	NEXT LOOP% &
\	RETURN &

6010	ON LOOP% GOSUB 6210,6230,6250,6270,6290,6290,6290,6290,6290,6290, &
		6290,6290,6290,6290,6290,6290,6330,6350,6370,6390,6410, &
		6430,6450 &

6017	RETURN &

6020	GOSUB 6210 &
\	SEARCH.KEY$=CHART.ACCNO$ &
\	RETURN &
		! SEARCH KEY &

6030	ON LOOP% GOSUB 6217,6237,6257,6277,6297,6297,6297,6297,6297,6297, &
		6297,6297,6297,6297,6297,6297,6337,6357,6377,6397,6417, &
		6437,6457 &
\	RETURN &
		! DEFAULT &

6040	SEARCH.FLAG%=-1% IF CHART.ACCNO$=SEARCH$(LOOP%) AND LOOP%=1% &
\	SEARCH.FLAG%=-1% IF CHART.DESC$=SEARCH$(LOOP%) AND LOOP%=2% &
\	SEARCH.FLAG%=-1% IF CHART.ACCTYP$=SEARCH$(LOOP%) AND LOOP%=3% &
\	SEARCH.FLAG%=-1% IF CVT$%(CHART.MONTH$)=VAL(SEARCH$(LOOP%)) &
								AND LOOP%=4% &
\	SEARCH.FLAG%=-1% IF ASCII(CHART.PRTFLAG$)=VAL(SEARCH$(LOOP%)) &
								AND LOOP%=17% &
\	SEARCH.FLAG%=-1% IF CHART.CF.CODE$=SEARCH$(LOOP%) AND LOOP%=18% &
\	SEARCH.FLAG%=-1% IF CHART.WC.CODE$=SEARCH$(LOOP%) AND LOOP%=19% &
\	SEARCH.FLAG%=-1% IF CHART.TYP$=SEARCH$(LOOP%) AND LOOP%=20% &
\	SEARCH.FLAG%=-1% IF CHART.DIV$=SEARCH$(LOOP%) AND LOOP%=21% &
\	SEARCH.FLAG%=-1% IF CHART.DEP$=SEARCH$(LOOP%) AND LOOP%=22% &
\	SEARCH.FLAG%=-1% IF CHART.LOC$=SEARCH$(LOOP%) AND LOOP%=23% &
\	RETURN &
		! SEARCH FOR MATCH &

6200	!-----------------------------------------------DATA ENTRY SECTION &

6210	INP$=FNALPHA$('2;21',CHART.ACCNO$,DEFAULT$(LOOP%),CHR$(CHNG.FLAG%)+ &
			CHR$(CH%)+CHR$(LEN(CHART.ACCNO$))+OPTION$) &
\	GOTO 6210 IF INP$='' AND CHNG.FLAG%=0% &

6212	IF CHNG.FLAG% AND INP$<>'' &
	THEN	IF FNG%(CHART.CH%,SPACE$(F.LEN%-LEN(INP$))+INP$)=0% &
		THEN	PRINT #CH%, CLRBOT$;FNP$('24;1');INP$;' is a duplicate key'; &
				FNP$('21;1');'COMMAND: Resume Exit '; &
\			INP$=FNINP$(CH%,128%,' ',1%,1%) &
\			PRINT #CH%, FNP$('2;21');B.ON$;CHART.ACCNO$;G.OFF$; &
\			GOTO 6210 IF INP$='R' &
\			RETURN &

6217	RSET CHART.ACCNO$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6220	PRINT #CH%, FNP$('2;21');B.ON$;CHART.ACCNO$;G.OFF$; &
\	RETURN &

6230	INP$=FNALPHA$('3;21',CHART.ACCTYP$,DEFAULT$(LOOP%),CHR$(CHNG.FLAG%)+ &
			CHR$(CH%)+CHR$(LEN(CHART.ACCTYP$))+OPTION$) &
\	INP$=CVT$$(INP$,-1%) &
\	IF INSTR(1%,'ALORE',LEFT(INP$,1%))=0% OR INP$='' AND CHNG.FLAG%=0% &
	THEN	PRINT #CH%, FNP$('24;1');CLRLIN$; &
		'Valid account type A? L? O? R? E? '; &
		FNP$('24;55');'Hit any key to continue '; &
\		INP$=FNINP$(CH%,128%,' ',1%,1%) &
\		PRINT #CH%, FNP$('24;1');CLRLIN$; &
\		GOTO 6230 &

6237	LSET CHART.ACCTYP$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6240	PRINT #CH%, FNP$('3;21');B.ON$;CHART.ACCTYP$;G.OFF$; &
\	RETURN &

6250	INP$=FNALPHA$('4;21',CHART.DESC$,DEFAULT$(LOOP%),CHR$(CHNG.FLAG%)+ &
			CHR$(CH%)+CHR$(LEN(CHART.DESC$))+OPTION$) &

6257	LSET CHART.DESC$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6260	PRINT #CH%, FNP$('4;21');B.ON$;CHART.DESC$;G.OFF$; &
\	RETURN &

6270	F.LEN%=2% &
\	INP$=NUM1$(MM%) IF OPT$='A' &
\	IF OPT$<>'A' THEN &
		PRINT #CH%, FNP$('6;21');R.ON$;B.ON$; &
\		PRINT #CH% USING '##',CVT$%(CHART.MONTH$); IF CHNG.FLAG% &
\		PRINT #CH%, SPACE$(F.LEN%); IF CHNG.FLAG%=0% &
\		PRINT #CH%, G.OFF$;CLRBOT$;OPTION$;'VALUE: ';B.ON$; &
\		INP$=FNINP$(CH%,0%,'_',F.LEN%,1%) &
\		INP$=DEFAULT$(LOOP%) IF INP$='' &
\		GOTO 6280 IF INP$='' AND CHNG.FLAG%=-1% &
\		INP$=NUM1$(VAL(INP$)) &

6277	LSET CHART.MONTH$=CVT%$(VAL(INP$)) IF INP$<>'' OR CHNG.FLAG%=0% &
\	M.OFF%=CVT$%(CHART.MONTH$) &
\	M.OFF%=TEMP.MONTH% IF M.OFF%=0% &
\	IF TEMP.MONTH%<>M.OFF% &
	THEN	GOSUB 6500 FOR TEMP.LOOP%=5% TO 16% &

6280	PRINT #CH%, FNP$('6;21');B.ON$; &
\	PRINT #CH% USING '##', CVT$%(CHART.MONTH$) &
\	PRINT #CH%, G.OFF$; &
\	M.OFF%=CVT$%(CHART.MONTH$) &
\	M.OFF%=TEMP.MONTH% IF M.OFF%=0% &
\	RETURN IF TEMP.MONTH%=M.OFF% &
\	FOR M%=1% TO 12% &
\		M1%=M.OFF%+1%-M% &
\		M1%=M1%+12% IF M1%<1% &
\		PRINT #CH%, FNP$(NUM1$(M%+7%+6%*(M%>6%))+';'+ &
			NUM1$(7%-39%*(M%>6%)));MNAME$(M1%); &
			SPACE$(10%-LEN(MNAME$(M1%))); &
\	NEXT M% &
\	TEMP.MONTH%=M.OFF% &
\	RETURN &

6290	CC%=21%-39%*(LOOP%>10%) &
\	CR%=LOOP%+3%+6%*(LOOP%>10%) &
\	MM1%=M.OFF%+5%-LOOP% &
\	MM1%=MM1%+12% IF MM1%<1% &
\	TEMP%=M.OFF%-MM1% &
\	TEMP%=1% IF M.OFF%=1% AND MM1%=12% &
\	AMOUNT=CVT$F(CHART.MON$(MM1%)) &
\	AMOUNT=CVT$F(CHART.CURR$) IF TEMP%=0% &
\	AMOUNT=CVT$F(CHART.LAST$) IF TEMP%=1% &
\	F.LEN%=12% &
\	PRINT #CH%, FNP$(NUM1$(CR%)+';'+NUM1$(CC%));R.ON$;B.ON$; &
\	PRINT #CH% USING '###,###,###.##', AMOUNT; IF CHNG.FLAG% &
\	PRINT #CH%, SPACE$(F.LEN%+2%); IF CHNG.FLAG%=0% &
\	PRINT #CH%, G.OFF$;CLRBOT$;OPTION$;'VALUE: ';B.ON$; &
\	INP$=FNINP$(CH%,0%,'_',F.LEN%,1%) &
\	INP$=DEFAULT$(LOOP%) IF INP$='' &
\	GOTO 6300 IF INP$='' AND CHNG.FLAG%=-1% &
\	TEMP%=1% &
\	TEMP%=100% IF INSTR(1%,INP$,'.')=0% &
\	INP$=NUM1$(VAL(INP$)/TEMP%) &
\	INP$=INP$+'.' IF INSTR(1%,INP$,'.')=0% &

6297	IF INP$<>'' OR CHNG.FLAG%=0% &
	THEN	MM1%=M.OFF%+5%-LOOP% &
\		MM1%=MM1%+12% IF MM1%<1% &
\		TEMP%=M.OFF%-MM1% &
\		TEMP%=1% IF M.OFF%=1% AND MM1%=12% &
\		IF TEMP%=0% &
		THEN	LSET CHART.CURR$=CVTF$(VAL(INP$)) &
			ELSE	IF TEMP%=1% &
				THEN	LSET CHART.LAST$=CVTF$(VAL(INP$)) &
					ELSE	LSET CHART.MON$(MM1%)=CVTF$(VAL(INP$)) &

6300	TEMP.LOOP%=LOOP% &
\	GOSUB 6500 &
\	RETURN &

6330	INP$=FNALPHA$('14;73',CHART.PRTFLAG$,DEFAULT$(LOOP%),CHR$(CHNG.FLAG%)+ &
			CHR$(CH%)+CHR$(LEN(CHART.PRTFLAG$))+OPTION$) &
\	IF INP$<>'' &
	THEN	IF INSTR(1%,'0123 ',INP$)=0% THEN &
			PRINT #CH%, CLRBOT$;FNP$('24;1');G.OFF$; &
			'This field requires a '; &
			'number in the range 1-3...Hit a key to continue '; &
\			M$=FNINP$(CH%,128%,'',1%,1%) &
\			PRINT #CH%, CLRBOT$; &
\			GOTO 6330 &

6337	LSET  CHART.PRTFLAG$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6340	PRINT #CH%, FNP$('14;73');B.ON$;CHART.PRTFLAG$; G.OFF$; &
\	RETURN &

6350	INP$=FNALPHA$('17;37',CHART.CF.CODE$,DEFAULT$(LOOP%),CHR$(CHNG.FLAG%)+ &
			CHR$(CH%)+CHR$(LEN(CHART.CF.CODE$))+OPTION$) &

6357	LSET CHART.CF.CODE$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6360	PRINT #CH%, FNP$('17;37');B.ON$;CHART.CF.CODE$;G.OFF$; &
\	RETURN &

6370	INP$=FNALPHA$('18;37',CHART.WC.CODE$,DEFAULT$(LOOP%),CHR$(CHNG.FLAG%)+ &
			CHR$(CH%)+CHR$(LEN(CHART.WC.CODE$))+OPTION$) &

6377	LSET CHART.WC.CODE$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6380	PRINT #CH%, FNP$('18;37');B.ON$;CHART.WC.CODE$;G.OFF$; &
\	RETURN &

6390	INP$=FNALPHA$('19;37',CHART.TYP$,DEFAULT$(LOOP%),CHR$(CHNG.FLAG%)+ &
			CHR$(CH%)+CHR$(LEN(CHART.TYP$))+OPTION$) &

6397	LSET CHART.TYP$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6400	PRINT #CH%, FNP$('19;37');B.ON$;CHART.TYP$;G.OFF$; &
\	RETURN &

6410	INP$=FNALPHA$('17;61',CHART.DIV$,DEFAULT$(LOOP%),CHR$(CHNG.FLAG%)+ &
			CHR$(CH%)+CHR$(LEN(CHART.DIV$))+OPTION$) &

6417	LSET CHART.DIV$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6420	PRINT #CH%, FNP$('17;61');B.ON$;CHART.DIV$;G.OFF$; &
\	RETURN &

6430	INP$=FNALPHA$('18;61',CHART.DEP$,DEFAULT$(LOOP%),CHR$(CHNG.FLAG%)+ &
			CHR$(CH%)+CHR$(LEN(CHART.DEP$))+OPTION$) &

6437	LSET CHART.DEP$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6440	PRINT #CH%, FNP$('18;61');B.ON$;CHART.DEP$;G.OFF$; &
\	RETURN &

6450	INP$=FNALPHA$('19;61',CHART.LOC$,DEFAULT$(LOOP%),CHR$(CHNG.FLAG%)+ &
			CHR$(CH%)+CHR$(LEN(CHART.LOC$))+OPTION$) &

6457	LSET CHART.LOC$=INP$ IF INP$<>'' OR CHNG.FLAG%=0% &

6460	PRINT #CH%, FNP$('19;61');B.ON$;CHART.LOC$;G.OFF$; &
\	RETURN &

6500	CC%=21%-39%*(TEMP.LOOP%>10%) &
\	CR%=TEMP.LOOP%+3%+6%*(TEMP.LOOP%>10%) &
\	MM1%=M.OFF%+5%-TEMP.LOOP% &
\	MM1%=MM1%+12% IF MM1%<1% &
\	TEMP%=M.OFF%-MM1% &
\	TEMP%=1% IF M.OFF%=1% AND MM1%=12% &
\	AMOUNT=CVT$F(CHART.MON$(MM1%)) &
\	AMOUNT=CVT$F(CHART.CURR$) IF TEMP%=0% &
\	AMOUNT=CVT$F(CHART.LAST$) IF TEMP%=1% &
\	PRINT #CH%, FNP$(NUM1$(CR%)+';'+NUM1$(CC%));B.ON$; &
\	PRINT #CH% USING '###,###,###.##', AMOUNT; &
\	PRINT #CH%, G.OFF$; &
\	RETURN &

6800	!----------------------------SET UP OR CHANGE THE CONTROL RECORD &
	PRINT #CH%, CLSCN$;FNP$('4;1'); &
				'Please answer the following questions:'; &
\	FF%,MM%=1% &
\	YY%=VAL(RIGHT(DATE$(0%),8%)) &

6810	PRINT #CH%, FNP$('6;1'); 'The current month # is <'; &
		RIGHT(NUM1$(MM%+100%),2%);'>:';FNP$('6;29'); &
\	INP$=FNINP$(CH%,0%,'_',2%,0%) &
\	IF INP$='%^C' &
	THEN	OPT$='Q' &
\		GOTO 1040 &

6815	INP$=NUM1$(MM%) IF INP$='' &
\	MM%=VAL(INP$) &
\	PRINT #CH%, FNP$('6;29'); &
\	PRINT #CH%  USING '##',MM%; &

6820	PRINT #CH%, FNP$('8;1');'The current year is <'; &
		RIGHT(NUM1$(YY%+100%),2%);'>:';FNP$('8;29'); &
\	INP$=FNINP$(CH%,0%,'_',2%,0%) &
\	IF INP$='%^C' &
	THEN	OPT$='Q' &
\		GOTO 1040 &

6825	INP$=NUM1$(YY%) IF INP$='' &
\	YY%=VAL(INP$) &
\	PRINT #CH%, FNP$('8;29'); &
\	PRINT #CH%  USING '##',YY%; &

6830	PRINT #CH%, FNP$('10;1'); &
		'The month # for the start of the fiscal year <'; &
		RIGHT(NUM1$(FF%+100%),2%);'>:';FNP$('10;52'); &
\	INP$=FNINP$(CH%,0%,'_',2%,0%) &
\	IF INP$='%^C' &
	THEN	OPT$='Q' &
\		GOTO 1040 &

6835	INP$=NUM1$(FF%) IF INP$='' &
\	FF%=VAL(INP$) &
\	PRINT #CH%, FNP$('10;52'); &
\	PRINT #CH%  USING '##',FF%; &

6840	IF FNA%(2%,'CHART   '+CVT%$(MM%)+STRING$(6%,0%)+CVT%$(FF%)+ &
			STRING$(2%,0%)+CVT%$(YY%)+CVT%$(YY%)+STRING$(24%,0%)+ &
			STRING$(8%,0%)+SPACE$(8%)) &
	THEN	V$=FNMESS$(CH%,FNS%,' CONTRL.DAT',0%,-1%) &
\		V%=FNX%('',0%,'') &

6850	PRINT #CH%, CLSCN$;B.ON$;R.ON$;BLINK.ON$;FNP$('1;75');'WAIT';G.OFF$; &
\	RETURN &

17000	!-----------------------------------------------VIEW ROUTINE &
	PRINT #CH%, CLSCN$;R.ON$;FNP$('1;1');SPACE$(80%); &
		FNP$('20;1');SPACE$(80%);FNSR$('5;19'); &
\	PRINT #CH%, FNP$(NUM1$(I%)+';'+'1');' ';FNP$(NUM1$(I%)+';'+'80');' '; &
		FOR I%=2% TO 19% &
\	PRINT #CH%, G.OFF$;FNP$('2;2');'Account';FNP$('3;2');'Number'; &
		FNP$('3;11');'Description'; &
		FNP$('3;52');'Type';FNP$('3;57');'Div'; &
		FNP$('3;61');'Dep';FNP$('3;65');'Loc'; &
		FNP$('3;69');'Cash';FNP$('3,73');'WC'; &
\	PRINT #CH%, R.ON$;LDS.ON$;FNP$('4;2');STRING$(78%,113%); &
		FNP$('4;10');CHR$(110%);FNP$('4;51');CHR$(110%); &
		FNP$('4;56');CHR$(110%);FNP$('4;60');CHR$(110%); &
		FNP$('4;64');CHR$(110%);FNP$('4;68');CHR$(110%); &
		FNP$('4;73');CHR$(110%); &
		USASCII$;G.OFF$; &
\	PRINT #CH%, R.ON$;LDS.ON$;FNP$(NUM1$(I%)+';10');CHR$(120%); &
		FNP$(NUM1$(I%)+';51');CHR$(120%); &
		FNP$(NUM1$(I%)+';56');CHR$(120%); &
		FNP$(NUM1$(I%)+';60');CHR$(120%); &
		FNP$(NUM1$(I%)+';64');CHR$(120%); &
		FNP$(NUM1$(I%)+';68');CHR$(120%); &
		FNP$(NUM1$(I%)+';72');CHR$(120%); &
			USASCII$;G.OFF$; &
				FOR I%=2% TO 3% &
\	DISPLAY.LINE%=0% &
\	PRINT.LINE%=5% &

17010	V%=FNG%(-CHART.CH%,NUM1$(KEY.POINTER%))+ &
		FNG%(-CHART.CH%-1%,NUM1$(DATA.POINTER%)) &
\	LSET CHART.BUF$=FNL$ &
\	GOTO 17105 &

17100	IF FNN%(CHART.CH%) &
	THEN	17200 &
		ELSE	LSET CHART.BUF$=FNL$ &

17105	I%=PRINT.LINE% &
\	PRINT #CH%, FNP$(NUM1$(I%)+';2');CHART.ACCNO$; &
		FNP$(NUM1$(I%)+';11');CHART.DESC$; &
		FNP$(NUM1$(I%)+';52');CHART.TYP$; &
		FNP$(NUM1$(I%)+';57');CHART.DIV$; &
		FNP$(NUM1$(I%)+';61');CHART.DEP$; &
		FNP$(NUM1$(I%)+';65');CHART.LOC$; &
		FNP$(NUM1$(I%)+';69');CHART.CF.CODE$; &
		FNP$(NUM1$(I%)+';73');CAHRT.WC.CODE$; &
\	PRINT #CH%, R.ON$;LDS.ON$;FNP$(NUM1$(I%)+';10');CHR$(120%); &
		FNP$(NUM1$(I%)+';51');CHR$(120%); &
		FNP$(NUM1$(I%)+';56');CHR$(120%); &
		FNP$(NUM1$(I%)+';60');CHR$(120%); &
		FNP$(NUM1$(I%)+';64');CHR$(120%); &
		FNP$(NUM1$(I%)+';68');CHR$(120%); &
		FNP$(NUM1$(I%)+';72');CHR$(120%); &
			USASCII$;G.OFF$; &
\	PRINT.LINE%=PRINT.LINE%+1% IF PRINT.LINE%<19% &

17110	IF DISPLAY.LINE%>13% &
	THEN	PRINT #CH%, FNP$('24;55');'Hit any key to continue '; &
\		INP$=FNINP$(CH%,128%,' ',1%,1%) &
\		DISPLAY.LINE%=0% &
\		PRINT #CH%, CLRBOT$;FNP$(NUM1$(PRINT.LINE%)+';1'); &

17120	PRINT #CH% &
\	DISPLAY.LINE%=DISPLAY.LINE%+1% &
\	IF PRINT.LINE%=19% &
	THEN	PRINT #CH%, FNP$(NUM1$(PRINT.LINE%)+';1');R.ON$;' '; &
			FNP$(NUM1$(PRINT.LINE%)+';80');' ';G.OFF$; &

17190	GOTO 17100 &

17200	IF DISPLAY.LINE%>0% &
	THEN	PRINT #CH%, FNP$('24;1');'End of file.  '; &
			FNP$('24;55');'Hit any key to continue '; &
\		INP$=FNINP$(CH%,128%,' ',1%,0%) &

17210	V%=FNG%(-CHART.CH%,NUM1$(KEY.POINTER%))+ &
		FNG%(-CHART.CH%-1%,NUM1$(DATA.POINTER%)) &
\	PRINT #CH%, FNP$('1;1');FNSR$('1;24');COLM.OFF$; &
\	GOTO 1010 &

18910	!----------------------------------------------END OF FILE &
	PRINT #CH%, CLRBOT$;FNP$('24;1');'End of file has been reached.'; &
		FNP$('24;55');'Hit any key to continue '; &
\	INP$=FNINP$(CH%,128%,' ',1%,0%) &
\	GOTO 1000 &

19000	!------------------------------------------------ERROR TRAPPING &
	RESUME IF ERR=52% OR ERR=51% 					&
\	RESUME 450 IF ERL=400 						&
\	RESUME 1010 IF ERR=54% 						&

19999	ON ERROR GOTO 0 						&

30000	!----------------------PROGRAM FUNCTIONS------------------------&
	!------------------------------------------------TERMINAL INPUT &
	DEF FNINP$(CHN%,KYP%,FILLCHAR$,INPUTLEN%,TO.ERR%)		&
\		PRINT #CHN%, STRING$(INPUTLEN%,ASCII(FILLCHAR$)); 	&
			  STRING$(INPUTLEN%,8%); 			&
\		PRINT #CHN%, RECORD 256%,CHR$(KYP%+INPUTLEN%)+FILLCHAR$;&
\		GET #CHN% 						&
\		FIELD #CHN%, RECOUNT AS BUFFER$ 			&
\		BUFFER$='%^C' IF INSTR(1%,BUFFER$,CHR$(3%)) 		&
\		FNINP$=CVT$$(BUFFER$,4%) 				&
\		V=SQR(-1) IF BUFFER$='%^C' AND TO.ERR% !^C Trappping	&
\	FNEND 								&

30200	DEF*FNP$(ROWCOL$)=ESC$+'['+ROWCOL$+'H' 	! Direct Cursor Address &

30210	DEF*FNSR$(BEGEND$)=CHR$(155%)+'['+BEGEND$+'r' ! Scroll control &

30250	!-----------------------------------------------SYSTEM MESSAGES	&
	DEF*FNMESS$(CHN%,ERRNUM%,DESC$,TO.ERR%,PRINT.TEST%)		&
\		MESG$=MID(SYS(CHR$(6%)+CHR$(9%)+CHR$(ERRNUM%)),3%,30%)+	&
			DESC$						&
\		IF PRINT.TEST%						&
		THEN	PRINT #CHN%, FNP$('24;1');CLRLIN$;MESG$;	&
				FNP$('24;55');				&
				'Hit any key to continue.';		&
\			NW$=FNINP$(CH%,128%,' ',1%,TO.ERR%)		&

30260		FNMESS$=MESG$						&
\	FNEND 								&

30500	DATA 'January','February','March','April','May','June','July', &
		'August','September','October','November','December' &

30700	!---------------------DATA INPUT FUNCTIONS----------------------&
	!								&
	DEF FNALPHA$(ROWCOL$,VARABLE$,DEFAL$,ITEMS$)			&
\		CHANGING%=ASCII(LEFT(ITEMS$,1%))			&
\		CHN%=ASCII(MID(ITEMS$,2%,1%))				&
\		LENG%=ASCII(MID(ITEMS$,3%,1%))				&
\		PRINT #CHN%, FNP$(ROWCOL$);R.ON$;B.ON$;			&
\		PRINT #CHN%, VARABLE$; IF CHANGING%			&
\		PRINT #CHN%, SPACE$(LENG%) IF CHANGING%=0%		&
\		PRINT #CHN%, G.OFF$;CLRBOT$;RIGHT(ITEMS$,4%);		&
			' ALPHA: ';B.ON$;				&
\		FIN$ = FNINP$(CHN%,0%,'_',LENG%,1%)			&
\		FIN$ = DEFAL$ IF FIN$ = '' AND MID(ITEMS$,4%,1%)<>'F'	&
\		FNALPHA$ = FIN$+''					&
\	FNEND								&

32767	END &

