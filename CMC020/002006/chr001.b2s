10	!---------------------------------------------------------------&
	!	Financial Codes Listing					&
	!								&
	!	CHR001.B2S	V1.0	February 1985			&
	!								&
	! Author - Randall Beard, Computer Management Center, Inc.	&
	!								&
	! Files-PPRINT.DAT	-ISAM	Printing Info File		&
	!	CHART.DAT     	-ISAM	Chart of Accounts File		&
	!								&
	!---------------------------------------------------------------&
	! 								&
	! 								&
	!		      Copyright (c) 1984 by 			&
	!	  Computer Management Center, Idaho Falls, Idaho	&
	! 								&
	! This software is furnished under a license and may be used	&
	! and copied only in accordance with the terms of such license 	&
	! and with the inclusion of the above copyright notice.  This 	&
	! software or any other copies therof may not be provided or 	&
	! otherwise made available to any other person.  No title to 	&
	! and ownership of the software is hereby transferred.		&
	! 								&
	! The information in this software is subject to change without &
	! notice and should not be construed as a committment by 	&
	! Computer Management Center. 					&
	! 								&
	! CMC assumes no responsibility for the use or reliability of 	&
	! its software on equipment which is not supported by CMC. 	&
	! 								&
	!---------------------------------------------------------------&

20	!RUN entry &
	PRINT '?Please run REPORT' &
\	M$=SYS(CHR$(9%)) &

30	ON ERROR GOTO 19000 &
\	DIM MON$(30%) &
\	JUNK$ = SYS(CHR$(6%) + CHR$(-7%)) &

60	CH%,OUTPUT.CH%=1%		! Keyboard channel &
\	OPEN 'KB:' AS FILE 1%, MODE 8%+256% &
					!    8% - Echo Control &
					!   16% - Disable hibernation, Cntl-C &

70	ESC$=CHR$(155%)			! Escape code for VT100 control &
\	CLSCN$=ESC$+'[H'+ESC$+'[J'	! Clear screen &
\	COLM.ON$=ESC$+'[?3h'		! 132 Column mode &
\	COLM.OFF$=ESC$+'[?3l'		! 80 Column mode &
\	CLRLIN$=ESC$+'[2K'		! Erase entire line &
\	ENTER.COPY$=ESC$+'[5i'		! Enter media copy &
\	EXIT.COPY$=ESC$+'[4i'		! Exit media copy &

80	!COM(THIS) DROP.DEAD.DATE$ = 8, VERSION.NO$ = 6, SERIAL.NO$ = 10 &
	DROP.DEAD.DATE$='        ' &
\	VERSION.NO$='V1.0' &
\	IF DROP.DEAD.DATE$<>'' &
	THEN	IF DROP.DEAD.DATE$<MID(SYS(CHR$(6%)+CHR$(20%)+CHR$(0%)+CHR$(0%)+ &
				CHR$(1%)+CHR$(SWAP%(1%))),7%,8%) &
		THEN	MESG$=MID(SYS(CHR$(6%)+CHR$(9%)+CHR$(47%)), &
				3%,30%) &
\			PRINT #CH%, MESG$; &
\			V$=SYS(CHR$(9%)) &

100	DEVICE.CH%,PPRINT.CH%	= 02% &
\	CHART.CH%		= 04% &
\	MENU.CH%		= 12% &

150	IF FNO%(PPRINT.CH%,'PPRINT.DAT','/RO','') &
	THEN	V$=FNMESS$(FNS%,'PPRINT.DAT',0%) &
\		GOTO 17540 &

160	IF FNG%(PPRINT.CH%,REP.NUM$) &
	THEN	V$=FNMESS$(FNS%,'PPRINT.DAT '+REP.NUM$,0%) &
\		GOTO 17540 &

170	FIELD #PPRINT.CH%+1%, FNL%+6% AS PPRINT.MUNG$,	&
			20% AS PPRINT.REPDESC$,		&
			30% AS PPRINT.MUNG$,		&
			01% AS PPRINT.HPITCH$,		&
			01% AS PPRINT.VPITCH$,		&
			01% AS PPRINT.LPAGE$,		&
			20% AS PPRINT.OUTDEV$,		&
			06% AS PPRINT.SPOOLR$,		!(not used)&
			02% AS PPRINT.SPAGE$,		&
			02% AS PPRINT.EPAGE$,		&
			02% AS PPRINT.COPIES$,		&
			20% AS PPRINT.REPDATE$,		&
			44% AS JUNK$,			&
			20% AS PPRINT.UDF$(1%),		&
			44% AS JUNK$,			&
			20% AS PPRINT.UDF$(2%),		&
			44% AS JUNK$,			&
			20% AS PPRINT.UDF$(3%),		&
			44% AS JUNK$,			&
			20% AS PPRINT.UDF$(4%),		&
			44% AS JUNK$,			&
			20% AS PPRINT.UDF$(5%)		&
\	FIELD #PPRINT.CH%+1%, FNL%+441% AS JUNK$,	&
			03% AS B.SYSNAM$,	&
			08% AS MUNG$,		&
			01% AS B.DISPLAY$	&
\	REPDESC$=PPRINT.REPDESC$+'' &
\	HPITCH%=ASCII(PPRINT.HPITCH$) &
\	VPITCH%=ASCII(PPRINT.VPITCH$) &
\	LIN.PER.PAGE%=ASCII(PPRINT.LPAGE$) &
\	OUTDEV$=PPRINT.OUTDEV$+'' &
\	SPAGE%=CVT$%(PPRINT.SPAGE$) &
\	EPAGE%=CVT$%(PPRINT.EPAGE$) &
\	COPIES%=CVT$%(PPRINT.COPIES$) &
\	REPDATE$=CVT$$(PPRINT.REPDATE$,132%) &
\	DISPLAY$  = B.DISPLAY$+'' &

180	FROM.ITEM$= 'ALL' &
\	FROM.ITEM$= CVT$$(PPRINT.UDF$(1%),132%) &
				 IF PPRINT.UDF$(1%)<>'' &
\	TO.ITEM$= CVT$$(PPRINT.UDF$(2%),132%) &
\	ZERO.BALANCE$ = CVT$$(PPRINT.UDF$(3%),132%) &
\	WLDCRD$ = CVT$$(PPRINT.UDF$(4%),-1%) &
\	V%=FNC%(PPRINT.CH%) &
		! Get report parameters from PPRINT.DAT &

190	V%=FNC%(PPRINT.CH%) &

200	IF FNO%(DEVICE.CH%,'DEVICE.DAT','/RO','')=0% &
	THEN	CHART.DEVICE$=MID(FNL$,7%,20%)+'' &
			IF FNG%(DEVICE.CH%,'CHART')=0% &
\		V%=FNC%(DEVICE.CH%) &

300	IF FNO%(CHART.CH%,CHART.DEVICE$+'CHART.DAT','/RO','') &
	THEN	V$=FNMESS$(FNS%,'CHART.DAT',0%) &
\		GOTO 17540 &

400	OPEN 'MENU.FIL/RO' FOR INPUT AS FILE MENU.CH% &
\	DIM #12%, A0$(1%)=64% &
\	COMPANY$=CVT$$(RIGHT(A0$(1%),2%),128%) &
\	CLOSE MENU.CH% &

450	! RESUME IF ERROR &

510	TITLE$='CHART OF ACCOUNTS FINANCIAL CODES' &
\	PAGE.TOP$=STRING$(3%,10%)+CHR$(13%)+ &
		'Date: '+DATE$(0%)+SPACE$(24-LEN(COMPANY$)/2%)+ &
		COMPANY$+SPACE$(30%-LEN(COMPANY$)/2%)+'Page <<###>>'+ &
		CHR$(10%)+CHR$(13%)+ &
		'Time: '+TIME$(0%)+SPACE$(25%-LEN(TITLE$)/2%)+ &
		TITLE$+SPACE$(29%-LEN(TITLE$)/2%)+'     V1.0'+ &
		CHR$(10%)+CHR$(13%)+SPACE$(40%-LEN(REPDATE$)/2%)+REPDATE$+ &
		STRING$(2%,10%)+CHR$(13%)+ &
		STRING$(80%,61%)+CHR$(10%)+CHR$(13%) &
\	PAGE.TOP1$= &
	'Account   Description                               Type  Div Dep '+ &
			'Loc Cash Wc' + &
			CHR$(13%)+CHR$(10%) &
\	USE.1$=	&
	'\      \  \                                      \  \  \  \\  \\  '+ &
			'\\  \  \ \  \' &
\	PAGE.BOT$=STRING$(6%,10%) &
\	BOT.MARGIN%=7% &
\	MORE.LINES%=2% &

10000	IF DISPLAY$<>'Y' &
	THEN	CLOSE CH% &
\		OUTPUT.CH%,RESET.CH%=1% &
\		TEMP.CH% =12% &
\		OPEN OUTDEV$ AS FILE OUTPUT.CH%, MODE 2% &
\		IF SPAGE%>1% &
		THEN	OPEN 'NL:' AS FILE TEMP.CH% &
\			OUTPUT.CH%=TEMP.CH% &

17000	!-------------------------------------------------OUTPUT REPORT &
	PAGE.BREAK%=LIN.PER.PAGE%-BOT.MARGIN% &
\	START.TEMP$='' &
\	START.TEMP$=SPACE$(8%-LEN(FROM.ITEM$))+FROM.ITEM$ &
		IF FROM.ITEM$<>'ALL' &
\	V% = FNG%(CHART.CH%,START.TEMP$) &
\	IF DISPLAY$='Y' &
	THEN	PRINT #OUTPUT.CH%, CLSCN$;COLM.ON$;FNSR$('2;19');PAGE.TOP1$; &
								FNP$('19;1'); &
		ELSE	PRINT #OUTPUT.CH%, ENTER.COPY$; &
				FNPAGE$(LIN.PER.PAGE%,0%,1%,PAGE.TOP$+ &
				PAGE.TOP1$+CHR$(10%)+CHR$(13%),''); &
\			LINE.COUNT%=FNLINE% &

17020	IF V% AND V%<>88% &
	THEN	17500 &

17030	FIELD #CHART.CH%+1%, FNL% AS TEMP$,	&
		  	08% AS CHART.ACCNO$,   &
			40% AS CHART.DESC$,     &
			02% AS CHART.ACCTYP$,   &
			02% AS CHART.MONTH$,    &
			02% AS CHART.OVHDAL$,   &
			08% AS CHART.MONBUD$,   &
			08% AS CHART.ANNBUD$,   &
			08% AS CHART.LAST$,     &
			08% AS CHART.CURR$,     &
			08% AS CHART.BACKUP$,   &
			13%*8% AS MUNG$,        &
			08% AS CHART.YR1$,      &
			08% AS CHART.YR2$,      &
			08% AS CHART.YR3$,      &
			08% AS CHART.YR4$,      &
			01% AS CHART.FLAG$,     &
			01% AS CHART.PRTFLG$,   &
			04% AS CHART.TYPE$,     &
			02% AS CHART.DIV$,      &
			02% AS CHART.DEPT$,     &
			02% AS CHART.LOC$,	&
			04% AS CHART.CF.CODE$,	&
			04% AS CHART.WC.CODE$	&
\	FIELD #CHART.CH%+1%, F%+86%+8%*M% AS MUNG$, &
			8% AS MON$(M%) FOR M%=1% TO 13% &
\	GOTO 17400 IF SPACE$(8%-LEN(TO.ITEM$))+TO.ITEM$<CHART.ACCNO$ &
							AND TO.ITEM$<>'' &
\	GOTO 17350 IF FNCOMP%(CVT$$(CHART.ACCNO$,-1%), &
		WLDCRD$)=0% AND WLDCRD$<>'' &

17300	PRINT #OUTPUT.CH% USING USE.1$,	CVT$$(CHART.ACCNO$,-1%),   &
					CHART.DESC$,    &
					CHART.TYPE$,    &
					CHART.DIV$,     &
					CHART.DEPT$,    &
					CHART.LOC$,     &
					CHART.CF.CODE$,	&
					CHART.WC.CODE$	&
\	LINE.COUNT%=LINE.COUNT%+1% &
\	GOSUB 17600	! Check the pagination &
\	GOTO 17400 IF END.FLAG% &

17350	IF FNN%(CHART.CH%)=0% &
 	THEN	GOTO 17030 &

17400	! TOTAL LINE &

17500	IF DISPLAY$<>'Y' &
	THEN	PRINT #OUTPUT.CH%, FNPAGE$(LIN.PER.PAGE%,LINE.COUNT%,FNPAGE%, &
			'',PAGE.BOT$); &
	ELSE	PRINT #OUTPUT.CH%, FNP$('24;1');'End of report'; &
			FNP$('24;105');'Hit any key to continue'; &
\		INP$=FNINP$(CH%,128%,' ',1%,0%) &

17540	PRINT #OUTPUT.CH%, EXIT.COPY$; IF DISPLAY$<>'Y' &
\	PRINT #OUTPUT.CH%, FNSR$('1;24');COLM.OFF$; IF DISPLAY$='Y' &
\	CLOSE OUTPUT.CH% &
\	V%=FNX%('',0%,'') &

17600	!Perform the pagination &
	IF DISPLAY$<>'Y' AND LINE.COUNT%+MORE.LINES%>PAGE.BREAK% &
	THEN	END.FLAG%=-1% IF PAGE%>=EPAGE% AND EPAGE%<>0% &
\		RETURN IF END.FLAG% &
\		PRINT #OUTPUT.CH%, FNPAGE$(LIN.PER.PAGE%,LINE.COUNT%, &
			FNPAGE%,PAGE.TOP$+PAGE.TOP1$+CHR$(10%)+CHR$(13%), &
			PAGE.BOT$); &
\		LINE.COUNT%=FNLINE% &
\		IF SPAGE%>1% &
		THEN	IF SPAGE%<=PAGE% &
			THEN	OUTPUT.CH%=RESET.CH% &
\				PRINT #OUTPUT.CH%, ENTER.COPY$;FNPAGE$(LIN.PER.PAGE%, &
					0%,PAGE%,PAGE.TOP$+PAGE.TOP1$+ &
					CHR$(10%)+CHR$(13%),''); &
\				LINE.COUNT%=FNLINE% &
\				SPAGE%=0% &

17650	IF DISPLAY$='Y' AND LINE.COUNT%>15% &
	THEN	PRINT #OUTPUT.CH%, FNP$('24;105');'Hit any key to continue'; &
\		INP$=FNINP$(CH%,128%,' ',1%,1%) &
\		LINE.COUNT%=0% &
\		PRINT #OUTPUT.CH%, FNP$('24;1');CLRLIN$;FNP$('18;132') &

17670	RETURN &

19000	!------------------------------------------------ERROR TRAPPING &
	RESUME IF ERR=52% 						&
\	RESUME 450 IF ERL=400% 						&
\	RESUME 70 IF ERL=60% AND ERR=27% 				&
\	RESUME 17540 IF ERR=54% 					&

19010	IF ERR=28% 							&
	THEN	JUNK$ = SYS(CHR$(6%) + CHR$(-7%)) 			&
\		RESUME 17500 IF OUTPUT.CH%=CH% 				&
\		RESUME 17540 						&

19900	ON ERROR GOTO 0 						&

30000	!----------------------PROGRAM FUNCTIONS------------------------&
	!------------------------------------------------TERMINAL INPUT &
	DEF FNINP$(CHN%,KYP%,FILLCHAR$,INPUTLEN%,TO.ERR%)		&
\		PRINT #CHN%, STRING$(INPUTLEN%,ASCII(FILLCHAR$)); 	&
			  STRING$(INPUTLEN%,8%); 			&
\		PRINT #CHN%, RECORD 256%,CHR$(KYP%+INPUTLEN%)+FILLCHAR$;&
\		GET #CHN% 						&
\		FIELD #CHN%, RECOUNT AS BUFFER$ 			&
\		BUFFER$='%^C' IF INSTR(1%,BUFFER$,CHR$(3%)) 		&
\		FNINP$=CVT$$(BUFFER$,4%) 				&
\		V=SQR(-1) IF BUFFER$='%^C' AND TO.ERR% !^C Trappping	&
\	FNEND 								&

30200	DEF*FNP$(ROWCOL$)=ESC$+"["+ROWCOL$+"H"	! Direct Cursor Address &

30250	!-----------------------------------------------SYSTEM MESSAGES	&
	DEF*FNMESS$(ERRNUM%,DESC$,TO.ERR%)				&
\		MESG$=MID(SYS(CHR$(6%)+CHR$(9%)+CHR$(ERRNUM%)),3%,30%)+	&
			DESC$						&
\		PRINT #CH%, FNP$('24;1');MESG$;FNP$('24;55');		&
			'Hit any key to continue.';			&
\		NW$=FNINP$(CH%,128%,' ',1%,TO.ERR%)			&
\	FNEND 								&

30300	DEF*FNSR$(BEGEND$)=ESC$+"["+BEGEND$+"r"	! Scroll control 	&

30600	!	********************************************************&
	!	Print top and bottom of page 				&
	!	Format : 						&
	!	PRINT FNPAGE$(<LINES/PAGE>,<CURRENT LINE COUNT>,	&
	!		<PAGE COUNT>,<TOP OF PAGE>,<BOTTOM OF PAGE>) 	&
	!	FNPAGE% = PAGE COUNT 					&
	!	FNLINE% = LINE COUNT 					&
	! 								&
	!	Written by Robert Peterson - July 1981 			&
	!	Version 1 Edition 0 					&
	!	********************************************************&

30610	DEF FNPAGE$(Y0%,Y1%,Y2%,Y0$,Y1$) 				&
\		Y2$='' 							&
\		Y2$=STRING$(Y0%-(Y1%+ 					&
			LEN(XLATE(Y1$,STRING$(10%,0%)+CHR$(10%)))),10%) &
							IF Y1$<>'' 	&
\		PAGE.LINE%=LEN(XLATE(Y0$,STRING$(10%,0%)+CHR$(10%))) 	&
\		Y%=INSTR(1%,Y1$+Y0$,'<<#') 				&
\		Y3%=INSTR(1%,Y1$+Y0$,'#>>') 				&
\		Y$=RIGHT(NUM1$(100000+Y2%),8%-(Y3%-Y%)) 		&
\		Y3%=-3% IF Y%=0% 					&
\		PRINT #OUTPUT.CH%, Y2$;LEFT(Y1$+Y0$,Y%-1%);Y$; 		&
			RIGHT(Y1$+Y0$,Y3%+3%); 				&
\		PAGE%=Y2% 						&
\	FNEND 								&

30650	DEF FNPAGE%=PAGE%+1% 						&

30660	DEF FNLINE%=PAGE.LINE% 						&

30900	!-------------------------------------------------------COMPARE &
	DEF FNCOMP%(Y$,Y2$) 						&
\		Y9%=0% 							&
\		Y9%=-1% IF Y2$='*' 					&
\		Y2$=Y2$+',' 						&

30920		IF Y9%=0% 						&
		THEN	Y1$=LEFT(Y2$,INSTR(1%,Y2$,',')-1%) 		&
\			Y2$=RIGHT(Y2$,LEN(Y1$)+2%) 			&
\			Y1%=INSTR(1%,Y1$,'/') 				&
\			IF Y1%+INSTR(1%,Y1$,'?')=0% 			&
			THEN	Y9%=Y$=Y1$ 				&
			ELSE	IF Y1% 					&
				THEN	Y9%=LEFT(Y1$,Y1%-1%)<=Y$ AND 	&
							Y$<=RIGHT(Y1$, 	&
						Y1%+1%) 		&
				ELSE	CHANGE CVT$$(LEFT(Y$,30%),-1%) 	&
								TO Y% 	&
\					CHANGE CVT$$(LEFT(Y1$,30%),-1%) &
								TO Y1% 	&
\					GOTO 30930 			&
					IF (Y%(Y3%)<>Y1%(Y3%))-(Y1%(Y3%)&
					=63%) FOR Y3%=1% TO Y1%(0%) 	&
\					Y9%=-1% 			&

30930		IF Y2$<>'' AND Y9%=0% 					&
		THEN	GOTO 30920 					&
		ELSE	FNCOMP%=Y9% 					&

30940	FNEND 								&

31000	! Chain entry 							&
	M$=SYS(CHR$(7%)) 						&
\	REP.NUM$=MID(M$,INSTR(1%,M$,'~')+1%,6%) 			&
\	GOTO 30 							&

32767	END &

