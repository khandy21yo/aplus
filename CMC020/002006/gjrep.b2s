10	!---------------------------------------------------------------&
	!	GENERAL LEDGER SYSTEM		-General Journal	&
	!	General	Journal Writer					&
	!								&
	!	GJREP.B2S	V1.0	January 1985			&
	!								&
	! Author - Randall Beard, Computer Management Center, Inc.	&
	!								&
	! Files-PPRINT.DAT 	-ISAM	Printing Info File		&
	!	GJ(G/R/C).DAT 	-ISAM	Journal Header File		&
	! 	GJL(G/R/C).DAT	-ISAM	Journal Detail File		&
	!	CHART.DAT     	-ISAM   Chart of Accounts File  	&
	!---------------------------------------------------------------&
	!		      Copyright (c) 1984 by 			&
	!	  Computer Management Center, Idaho Falls, Idaho	&
	! 								&
	! This software is furnished under a license and may be used	&
	! and copied only in accordance with the terms of such license 	&
	! and with the inclusion of the above copyright notice.  This 	&
	! software or any other copies therof may not be provided or 	&
	! otherwise made available to any other person.  No title to 	&
	! and ownership of the software is hereby transferred.		&
	! 								&
	! The information in this software is subject to change without &
	! notice and should not be construed as a committment by 	&
	! Computer Management Center. 					&
	! 								&
	! CMC assumes no responsibility for the use or reliability of 	&
	! its software on equipment which is not supported by CMC. 	&
	!---------------------------------------------------------------&

30	ON ERROR GOTO 19000 &
\	DIM Y%(32%),Y1%(32%) &
\	JUNK$ = SYS(CHR$(6%) + CHR$(-7%)) &
\	JUNK$ = SYS(CHR$(6%) + CHR$(9%)) &

60	CH%=1%				! Keyboard channel &
\	OPEN 'KB:' AS FILE 1%, MODE 8%+256% &
					!    8% - Echo Control &
					!   16% - Disable hibernation, Cntl-C &

70	ESC$=CHR$(155%)			! Escape code for VT100 control &
\	CLSCN$=ESC$+'[H'+ESC$+'[J'	! Clear screen &
\	COLM.ON$=ESC$+'[?3h'		! 132 Column mode &
\	COLM.OFF$=ESC$+'[?3l'		! 80 Column mode &
\	CLRLIN$=ESC$+'[2K'		! Erase entire line &
\	ENTER.COPY$=ESC$+'[5i'		! Enter media copy &
\	EXIT.COPY$=ESC$+'[4i'		! Exit media copy &

80	!COM(THIS) DROP.DEAD.DATE$ = 8, VERSION.NO$ = 6, SERIAL.NO$ = 10 &
	DROP.DEAD.DATE$='        ' &
\	VERSION.NO$='V1.0' &
\	IF 	DROP.DEAD.DATE$<>'' &
	THEN	IF 	DROP.DEAD.DATE$<MID(SYS(CHR$(6%)+CHR$(20%)+CHR$(0%)+ &
				CHR$(0%)+CHR$(1%)+CHR$(SWAP%(1%))),7%,8%) &
		THEN	MESG$=MID(SYS(CHR$(6%)+CHR$(9%)+CHR$(47%)), &
				3%,30%) &
\			PRINT #CH%, MESG$; &
\			V$=SYS(CHR$(9%)) &

100	PPRINT.CH%,DEVICE.CH%	= 02% &
\	GJH.CH% 		= 04% &
\	GJL.CH%			= 06% &
\	CHART.CH%		= 08% &
\	MENU.CH% 		= 12% &

150	IF 	FNO%(PPRINT.CH%,'PPRINT.DAT','/RO','') &
	THEN	V$=FNMESS$(CH%,FNS%,'PPRINT.DAT',0%,-1%) &
\		GOTO 17540 &

160	IF 	FNG%(PPRINT.CH%,REP.NUM$) &
	THEN	V$=FNMESS$(CH%,FNS%,'PPRINT.DAT  '+REP.NUM$,0%,-1%) &
\		GOTO 17540 &

170	FIELD #PPRINT.CH%+1%, FNL%+6% AS PPRINT.MUNG$,	&
			20% AS PPRINT.REPDESC$,		&
			30% AS PPRINT.MUNG$,		&
			01% AS PPRINT.HPITCH$,		&
			01% AS PPRINT.VPITCH$,		&
			01% AS PPRINT.LPAGE$,		&
			20% AS PPRINT.OUTDEV$,		&
			06% AS PPRINT.SPOOLR$,		&
			02% AS PPRINT.SPAGE$,		&
			02% AS PPRINT.EPAGE$,		&
			02% AS PPRINT.COPIES$,		&
			20% AS PPRINT.REPDATE$,		&
			44% AS JUNK$,			&
			20% AS PPRINT.UDF$(1%),		&
			44% AS JUNK$,			&
			20% AS PPRINT.UDF$(2%),		&
			44% AS JUNK$,			&
			20% AS PPRINT.UDF$(3%),		&
			44% AS JUNK$,			&
			20% AS PPRINT.UDF$(4%),		&
			44% AS JUNK$,			&
			20% AS PPRINT.UDF$(5%)		&
\	FIELD #PPRINT.CH%+1%, FNL%+441% AS JUNK$,	&
			03% AS B.SYSNAM$,	&
			08% AS MUNG$,		&
			01% AS B.DISPLAY$	&
\	REPDESC$=PPRINT.REPDESC$+'' &
\	HPITCH%=ASCII(PPRINT.HPITCH$) &
\	VPITCH%=ASCII(PPRINT.VPITCH$) &
\	LIN.PER.PAGE%=ASCII(PPRINT.LPAGE$) &
\	OUTDEV$=PPRINT.OUTDEV$+'' &
\	SPAGE%=CVT$%(PPRINT.SPAGE$) &
\	EPAGE%=CVT$%(PPRINT.EPAGE$) &
\	COPIES%=CVT$%(PPRINT.COPIES$) &
\	REPDATE$=CVT$$(PPRINT.REPDATE$,132%) &
\	DISPLAY$  = B.DISPLAY$+'' &

180	FROM.ITEM$= 'ALL' &
\	FROM.ITEM$= CVT$$(PPRINT.UDF$(1%),132%) &
				 IF PPRINT.UDF$(1%)<>'' &
\	TO.ITEM$= CVT$$(PPRINT.UDF$(2%),132%) &
\	JOU.NAME$=CVT$$(PPRINT.UDF$(3%),132%) &
\	JOURNAL.TITLE$='G E N E R A L ' IF JOU.NAME$='G' &
\	JOURNAL.TITLE$='R E C U R R I N G ' IF JOU.NAME$='R' &
\	JOURNAL.TITLE$='C L O S I N G ' IF JOU.NAME$='C' &
\	REVERSING%=1% &
\	PRINT.ITEMS$=CVT$$(PPRINT.UDF$(4%),132%) &

190	V%=FNC%(PPRINT.CH%) &

200	IF FNO%(DEVICE.CH%,'DEVICE.DAT','/RO','')=0% &
	THEN	CHART.DEVICE$=MID(FNL$,7%,20%)+'' &
			IF FNG%(DEVICE.CH%,'CHART ')=0% &
\		GJ.DEVICE$=MID(FNL$,7%,20%)+'' &
			IF FNG%(DEVICE.CH%,'GJ')=0% &
\		GJL.DEVICE$=MID(FNL$,7%,20%)+'' &
			IF FNG%(DEVICE.CH%,'GJL')=0% &
\		V%=FNC%(DEVICE.CH%) &

300	IF FNO%(GJH.CH%,GJ.DEVICE$+'GJ'+JOU.NAME$+'.DAT','/RO','') &
	THEN	V$=FNMESS$(CH%,FNS%,'GJ'+JOU.NAME$+'.DAT',0%,-1%) &
\		GOTO 17540 &

310	IF FNO%(GJL.CH%,GJL.DEVICE$+'GJL'+JOU.NAME$+'.DAT','/RO','') &
	THEN	V$=FNMESS$(CH%,FNS%,'GJL'+JOU.NAME$+'.DAT',0%,-1%) &
\		GOTO 17540 &

320	IF FNO%(CHART.CH%,CHART.DEVICE$+'CHART.DAT','/RO','') &
	THEN	V$=FNMESS$(CH%,FNS%,'CHART.DAT',0%,-1%) &
\		GOTO 17540 &

400	OPEN 'MENU.FIL/RO' FOR INPUT AS FILE MENU.CH% &
\	DIM #12%, A0$(1%)=64% &
\	COMPANY$=CVT$$(RIGHT(A0$(1%),2%),128%) &
\	CLOSE MENU.CH% &

510	TITLE$=JOURNAL.TITLE$+' J O U R N A L' &
\	PAGE.TOP$=STRING$(2%,10%)+CHR$(13%)+ &
		'Date: '+DATE$(0%)+SPACE$(52-LEN(COMPANY$)/2%)+ &
		COMPANY$+SPACE$(57%-LEN(COMPANY$)/2%)+'Page <<###>>'+ &
		CHR$(10%)+CHR$(13%)+ &
		'Time: '+TIME$(0%)+SPACE$(52%-LEN(TITLE$)/2%)+ &
		TITLE$+SPACE$(58%-LEN(TITLE$)/2%)+VERSION.NO$+ &
		CHR$(10%)+CHR$(13%)+SPACE$(66%-LEN(REPDATE$)/2%)+REPDATE$+ &
		STRING$(2%,10%)+CHR$(13%) &
\	HEADINGS.1$='Item Acc #     Description       Check #  Cross'+ &
		'   Bank   Vouch  Sub-   Phase'+ &
		'           -------Amount-------'+ &
		'     Reg Qty'+'     Pre Qty'+CHR$(10%)+CHR$(13%) &
\	HEADINGS.2$=SPACE$(42%)+'Ref     Code     #    Acc #'+ &
		SPACE$(18%)+'Debit         '+'Credit'+CHR$(10%)+CHR$(13%) &
\	USE.1$= '#### \       \ \               \ \      \ \     \ \    \'+ &
		' \    \ \    \ \    \' &
\	USE.2$= ' ###,###,###.##' &
\	USE.3$=	' ###,###.###' &
\	J.TOTAL.USE$='*=Undefined Account No.      JOURNAL ! \    \   TOTALS'+ &
		'                        ###,###,###.## ###,###,###.##' &
\	GRAND.TOTAL.USE$= SPACE$(43%)+ &
	'GRAND TOTALS                       ###,###,###.## ###,###,###.##' &
\	PAGE.BOT$=STRING$(6%,10%) &
\	BOT.MARGIN%=6% &
\	MORE.LINES%=3% &

10000	REP.NUM$='000000' IF REP.NUM$='' &
\	IF DISPLAY$<>'Y' &
	THEN	CLOSE CH% &
\		OUTPUT.CH%,RESET.CH%=1% &
\		TEMP.CH%=12% &
\		OPEN OUTDEV$ AS FILE OUTPUT.CH%, MODE 2% &
\		IF SPAGE%>1% &
		THEN	OPEN 'NL:' AS FILE TEMP.CH% &
\			OUTPUT.CH%=TEMP.CH% &

17000	!-------------------------------------------------OUTPUT REPORT &
	PAGE.BREAK%=LIN.PER.PAGE%-BOT.MARGIN% &
\	START.TEMP$='' &
\	START.TEMP$=FROM.ITEM$ IF FROM.ITEM$<>'ALL' &
\	DEBIT.GRAND.TOT,CREDIT.GRAND.TOT,J.DEBIT.TOT,J.CREDIT.TOT=0. &
\	V%=FNG%(GJH.CH%,START.TEMP$) &

17010	IF DISPLAY$='Y' &
	THEN	PRINT #OUTPUT.CH%, CLSCN$;COLM.ON$;FNSR$('1;19');FNP$('19;1');&
\		GOTO 17030 &

17020	PRINT #OUTPUT.CH%,ENTER.COPY$; &
			FNPAGE$(LIN.PER.PAGE%,0%,1%,PAGE.TOP$,''); &
\	LINE.COUNT%=FNLINE% &

17030	FIELD #GJH.CH%+1%,     FNL% AS JUNK$, 		&
				06% AS GJH.JOUNUM$,	&
				02% AS GJH.SOURCE$,	&
				02% AS GJH.ITEMS.REC$	&
\	GOTO 17400 IF TO.ITEM$<GJH.JOUNUM$ AND TO.ITEM$<>'' &
\	GOTO 17350 IF JOU.NAME$='R' AND PRINT.ITEMS$<>'' &
			AND FNCOMP%(GJH.JOUNUM$,PRINT.ITEMS$)=0% &
\	PREV.JOUNUM$=GJH.JOUNUM$+'' &
\	GOSUB 18300 IF LINE.COUNT%+6%+MORE.LINES%<PAGE.BREAK% &
\	IF DISPLAY$<>'Y' AND LINE.COUNT%+6%+MORE.LINES%>PAGE.BREAK% &
	THEN	PAGE.EARLY%=-1% &
\		GOSUB 17600 &	
\		GOTO 17400 IF END.FLAG% &

17100	IF FNG%(GJL.CH%,GJH.JOUNUM$)=0% &
	THEN GOTO 17130 &
	ELSE	PRINT #OUTPUT.CH%,SPACE$(50%);'No items for this Journal Entry' &
\	     	PRINT #OUTPUT.CH% &
\		LINE.COUNT%=LINE.COUNT%+2% &
\	   	GOSUB 17600 &
\		GOTO 17400 IF END.FLAG% &
\	  	GOTO 17350 &

17130	FIELD #GJL.CH%+1%,FNL% AS JUNK$,	&
			06% AS GJL.JOUNUM$,	&
			02% AS GJL.ITEMNO$,	&
			08% AS GJL.ACCNO$,	&
			30% AS GJL.DESC$,	&
			02% AS GJL.DATE$,	&
			08% AS GJL.CKNO$,	&
			06% AS GJL.F$(1%),	! XREFNO$  &
			06% AS GJL.F$(2%),	! BNKCDE$  &
			06% AS GJL.F$(3%),	! VCHRNO$  &
			06% AS GJL.F$(4%),	! SUBACC$ &
			06% AS GJL.F$(5%),	! PHASE$   &
			08% AS GJL.AMNT$,	&
			08% AS GJL.QTY$(1%),	! REGQTY$  &
			08% AS GJL.QTY$(2%)	! PREQTY$  &
\	ACCOUNT.NO$=SPACE$(8%-LEN(GJL.ACCNO$))+GJL.ACCNO$ &
\	ACCOUNT.NO$=GJL.ACCNO$+'*' IF FNG%(CHART.CH%,ACCOUNT.NO$) AND &
						GJL.DESC$<>'ERASED RECORD' &
\	AMOUNT=CVT$F(GJL.AMNT$)*REVERSING% &
\	REG.QTY=CVT$F(GJL.QTY$(1%))*REVERSING% &
\	PRE.QTY=CVT$F(GJL.QTY$(2%))*REVERSING% &
\	CREDIT.AMOUNT,DEBIT.AMOUNT=0. &
\	AMOUNT,REG.QTY,PRE.QTY=0. IF GJL.DESC$='ERASED RECORD' &
\	IF AMOUNT>0. &
	THEN	J.DEBIT.TOT=J.DEBIT.TOT+AMOUNT &
\		DEBIT.AMOUNT=AMOUNT &

17140	IF AMOUNT<0. &
	THEN	J.CREDIT.TOT=J.CREDIT.TOT+AMOUNT &
\		CREDIT.AMOUNT=AMOUNT &

17150	PRINT #OUTPUT.CH% USING USE.1$, CVT$%(GJL.ITEMNO$), &
					ACCOUNT.NO$, &
					GJL.DESC$, &
					GJL.CKNO$, &
					GJL.F$(1%), &
					GJL.F$(2%), &
					GJL.F$(3%), &
					GJL.F$(4%), &
					GJL.F$(5%); &

17160	IF DEBIT.AMOUNT<>0. &
	THEN	PRINT #OUTPUT.CH% USING USE.2$,	DEBIT.AMOUNT; &
		ELSE	PRINT #OUTPUT.CH%, SPACE$(LEN(USE.2$)); &

17170	IF CREDIT.AMOUNT<>0. &
	THEN	PRINT #OUTPUT.CH% USING USE.2$,	-CREDIT.AMOUNT; &
		ELSE	PRINT #OUTPUT.CH%, SPACE$(LEN(USE.2$)); &

17180	IF REG.QTY<>0. &
	THEN	PRINT #OUTPUT.CH% USING USE.2$,	REG.QTY; &
		ELSE	PRINT #OUTPUT.CH%, SPACE$(LEN(USE.3$)); &

17190	IF PRE.QTY<>0. &
	THEN	PRINT #OUTPUT.CH% USING USE.2$,	PRE.QTY; &
		ELSE	PRINT #OUTPUT.CH%, SPACE$(LEN(USE.3$)); &

17200	PRINT #OUTPUT.CH% &
\	LINE.COUNT%=LINE.COUNT%+1% &
\	GOSUB 17600 &
\	GOTO 17400 IF END.FLAG% &

17250	IF FNN%(GJL.CH%) &
	THEN	GOTO 18200 &
	ELSE 	IF GJH.JOUNUM$=LEFT(FNL$,6%) &
		THEN 	GOTO 17130 &
		ELSE	GOSUB 18100 &

17350	IF FNN%(GJH.CH%) &
	THEN	GOTO 17400 &
	ELSE	GOTO 17030 &

17400	IF END.FLAG%=0% &
	THEN	PRINT #OUTPUT.CH% &
\		PRINT #OUTPUT.CH% USING GRAND.TOTAL.USE$,DEBIT.GRAND.TOT, &
						-CREDIT.GRAND.TOT; &
\		PRINT #OUTPUT.CH%,SPACE$(8%)+'Out of Balance  **' &
				IF FNZ(DEBIT.GRAND.TOT+CREDIT.GRAND.TOT)<>0. &
\		LINE.COUNT%=LINE.COUNT%+2% &

17500	IF DISPLAY$<>'Y' &
	THEN	PRINT #OUTPUT.CH%, FNPAGE$(LIN.PER.PAGE%,LINE.COUNT%,FNPAGE%, &
			'',PAGE.BOT$);CHR$(13%); &
	ELSE	PRINT #OUTPUT.CH%, FNP$('24;1');'End of report'; &
			FNP$('24;105');'Hit any key to continue'; &
\		INP$=FNINP$(CH%,128%,' ',1%,1%) &

17520	IF JOU.NAME$='C' AND SECOND.PASS%=0% &
	THEN	REVERSING%=-1% &
\		JOURNAL.TITLE$='R E V E R S I N G ' &
\		SECOND.PASS%=-1% &
\		LINE.COUNT%=0% &
\		GOTO 510 &

17540	PRINT #OUTPUT.CH%, EXIT.COPY$; IF DISPLAY$<>'Y' &
\	PRINT #OUTPUT.CH%, FNSR$('1;24');COLM.OFF$; IF DISPLAY$='Y' &
\	CLOSE OUTPUT.CH% &
\	V%=FNX%('',0%,'') &

17600	!Perform the pagination &
	IF DISPLAY$<>'Y' AND LINE.COUNT%+MORE.LINES%>PAGE.BREAK% OR &
				DISPLAY$<>'Y' AND PAGE.EARLY% &
	THEN	END.FLAG%=-1% IF PAGE%>=EPAGE% AND EPAGE%<>0% &
\		RETURN IF END.FLAG% &
\		PRINT #OUTPUT.CH%, FNPAGE$(LIN.PER.PAGE%,LINE.COUNT%, &
			FNPAGE%,PAGE.TOP$+CHR$(124%)+STRING$(130%,45%)+ &
			CHR$(124%)+CHR$(10%)+CHR$(13%)+'  JOURNAL #  '+ &
			GJH.JOUNUM$+SPACE$(42%)+'SOURCE:  '+GJH.SOURCE$+ &
			SPACE$(45%)+NUM1$(CVT$%(GJH.ITEMS.REC$))+' ENTRIES'+ &
			STRING$(2%,10%)+CHR$(13%)+ &
			HEADINGS.1$+HEADINGS.2$,PAGE.BOT$); &
\		PAGE.EARLY%=0% IF PAGE.EARLY% &
\		LINE.COUNT%=FNLINE% &
\		IF SPAGE%>1% &
		THEN	IF SPAGE%<=PAGE% &
			THEN	OUTPUT.CH%=RESET.CH% &
\			PRINT #OUTPUT.CH%, FNPAGE$(LIN.PER.PAGE%,0%, &
				PAGE%,PAGE.TOP$+CHR$(124%)+STRING$(130%,45%)+ &
				CHR$(124%)+CHR$(10%)+CHR$(13%)+ &
				'  JOURNAL #  '+GJH.JOUNUM$+SPACE$(42%)+ &
				'SOURCE:  '+GJH.SOURCE$+SPACE$(45%)+ &
				NUM1$(CVT$%(GJH.ITEMS.REC$))+' ENTRIES'+ &
				STRING$(2%,10%)+CHR$(13%)+ &
				HEADINGS.1$+HEADINGS.2$,''); &
\				LINE.COUNT%=FNLINE% &
\				SPAGE%=0% &

17650	IF DISPLAY$='Y' AND LINE.COUNT%>15% &
	THEN	PRINT #OUTPUT.CH%, FNP$('24;105');'Hit any key to continue'; &
\		INP$=FNINP$(CH%,128%,' ',1%,1%) &
\		LINE.COUNT%=0% &
\		PRINT #OUTPUT.CH%, FNP$('24;1');CLRLIN$;FNP$('18;132') &

17670	RETURN &

18000	!---------------------------PROGRAM SUBROUTINES---------------------- &
	
18100	!-----------------------------------PRINT JOURNAL TOTALS &
	PRINT #OUTPUT.CH% &
\	PRINT #OUTPUT.CH% USING J.TOTAL.USE$,'#', &
						PREV.JOUNUM$, &
						J.DEBIT.TOT, &
						-J.CREDIT.TOT; &
\	PRINT #OUTPUT.CH%,SPACE$(8%)+'Out of Balance  **'; &
			IF FNZ(J.DEBIT.TOT+J.CREDIT.TOT)<>0. &
\	PRINT #OUTPUT.CH% &
\	PRINT #OUTPUT.CH% &
\	DEBIT.GRAND.TOT=DEBIT.GRAND.TOT+J.DEBIT.TOT &
\	CREDIT.GRAND.TOT=CREDIT.GRAND.TOT+J.CREDIT.TOT &
\	J.DEBIT.TOT,J.CREDIT.TOT=0. &
\	PREV.JOUNUM$=GJH.JOUNUM$+'' &
\	LINE.COUNT%=LINE.COUNT%+3% &
\	RETURN &

18200	GOSUB 18100 &
\	GOTO 17350 &

18300	!---------------------------------------PRINT JOURNAL HEADINGS &
	PRINT #OUTPUT.CH%, CHR$(124%);STRING$(130%,45%);CHR$(124%); &
\	PRINT #OUTPUT.CH% &
\	PRINT #OUTPUT.CH%,'  JOURNAL #  ';GJH.JOUNUM$;SPACE$(42%); &
	'SOURCE:  ';GJH.SOURCE$;SPACE$(45%); &
	NUM1$(CVT$%(GJH.ITEMS.REC$));' ENTRIES'; &
	STRING$(2%,10%);CHR$(13%); &
	HEADINGS.1$;HEADINGS.2$; &
\	LINE.COUNT%=LINE.COUNT%+5% &
\	GOSUB 17600 &
\	GOTO 17400 IF END.FLAG% &
\	RETURN &

19000	!------------------------------------------------ERROR TRAPPING &
	RESUME IF ERR=52% 						&
\	RESUME 510 IF ERL=400% 						&
\	RESUME 70 IF ERL=60% AND ERR=27% 				&
\	RESUME 17540 IF ERR=54% 					&

19010	IF 	ERR=28% 						&
	THEN	JUNK$ = SYS(CHR$(6%) + CHR$(-7%)) 			&
\		RESUME 17500 IF OUTPUT.CH%=CH% 				&
\		RESUME 17540 						&

19900	ON ERROR GOTO 0 						&

30000	!----------------------PROGRAM FUNCTIONS------------------------&
	!------------------------------------------------TERMINAL INPUT &
	DEF FNINP$(CHN%,KYP%,FILLCHAR$,INPUTLEN%,TO.ERR%)		&
\		PRINT #CHN%, STRING$(INPUTLEN%,ASCII(FILLCHAR$)); 	&
			  STRING$(INPUTLEN%,8%); 			&
\		PRINT #CHN%, RECORD 256%,CHR$(KYP%+INPUTLEN%)+FILLCHAR$;&
\		GET #CHN% 						&
\		FIELD #CHN%, RECOUNT AS BUFFER$ 			&
\		BUFFER$='%^C' IF INSTR(1%,BUFFER$,CHR$(3%)) 		&
\		FNINP$=CVT$$(BUFFER$,4%) 				&
\		V=SQR(-1) IF BUFFER$='%^C' AND TO.ERR% !^C Trappping	&
\	FNEND 								&

30200	DEF*FNP$(ROWCOL$)=ESC$+"["+ROWCOL$+"H"	! Direct Cursor Address &

30210	DEF*FNZ(ROUND.A)=INT(ROUND.A*100.+.51)/100. &

30250	!-----------------------------------------------MESSAGE HANDLER	&
	DEF*FNMESS$(CHN%,ERRNUM%,DESC$,TO.ERR%,PRINT.TEST%)		&
\		MESG$=MID(SYS(CHR$(6%)+CHR$(9%)+CHR$(ERRNUM%)),3%,30%)+	&
			DESC$						&
\		IF PRINT.TEST%						&
		THEN	PRINT #CHN%, FNP$('24;1');CLRLIN$;MESG$;	&
				FNP$('24;55');				&
				'Hit any key to continue.';		&
\			NW$=FNINP$(CHN%,128%,' ',1%,TO.ERR%)		&

30260	FNMESS$=MESG$							&
\	FNEND 								&

30300	DEF*FNSR$(BEGEND$)=ESC$+"["+BEGEND$+"r"	! Scroll control 	&

30400	!--------------------------------------------------DATE HANDLER &

30420	DEF FND6$(D9%)=RIGHT(NUM1$((D9% AND 15%*32%)/32%+100%),2%) 	&
		+ '/'+RIGHT(NUM1$((D9% AND 31%)+100%),2%) 		&
		+ '/'+RIGHT(NUM1$(((SWAP%(D9%) AND 254%)/2%)+100%),2%) 	&

30600	!	********************************************************&
	!	Print top and bottom of page 				&
	!	Format : 						&
	!	PRINT FNPAGE$(<LINES/PAGE>,<CURRENT LINE COUNT>,	&
	!		<PAGE COUNT>,<TOP OF PAGE>,<BOTTOM OF PAGE>) 	&
	!	FNPAGE% = PAGE COUNT 					&
	!	FNLINE% = LINE COUNT 					&
	! 								&
	!	Written by Robert Peterson - July 1981 			&
	!	Version 1 Edition 0 					&
	!	********************************************************&

30610	DEF FNPAGE$(Y0%,Y1%,Y2%,Y0$,Y1$) 				&
\		Y2$='' 							&
\		Y2$=STRING$(Y0%-(Y1%+ 					&
			LEN(XLATE(Y1$,STRING$(10%,0%)+CHR$(10%)))),10%) &
							IF Y1$<>'' 	&
\		PAGE.LINE%=LEN(XLATE(Y0$,STRING$(10%,0%)+CHR$(10%))) 	&
\		Y%=INSTR(1%,Y1$+Y0$,'<<#') 				&
\		Y3%=INSTR(1%,Y1$+Y0$,'#>>') 				&
\		Y$=RIGHT(NUM1$(100000+Y2%),8%-(Y3%-Y%)) 		&
\		Y3%=-3% IF Y%=0% 					&
\		PRINT #OUTPUT.CH%, Y2$;LEFT(Y1$+Y0$,Y%-1%);Y$; 		&
			RIGHT(Y1$+Y0$,Y3%+3%); 				&
\		PAGE%=Y2% 						&
\	FNEND 								&

30650	DEF FNPAGE%=PAGE%+1% 						&

30660	DEF FNLINE%=PAGE.LINE% 						&

30900	!-------------------------------------------------------COMPARE &
	DEF FNCOMP%(Y$,Y2$) 						&
\		Y9%=0% 							&
\		Y9%=-1% IF Y2$='*' 					&
\		Y2$=Y2$+',' 						&

30920		IF Y9%=0% 						&
		THEN	Y1$=LEFT(Y2$,INSTR(1%,Y2$,',')-1%) 		&
\			Y2$=RIGHT(Y2$,LEN(Y1$)+2%) 			&
\			Y1%=INSTR(1%,Y1$,'/') 				&
\			IF Y1%+INSTR(1%,Y1$,'?')=0% 			&
			THEN	Y9%=Y$=Y1$ 				&
			ELSE	IF Y1% 					&
				THEN	Y9%=LEFT(Y1$,Y1%-1%)<=Y$ AND 	&
							Y$<=RIGHT(Y1$, 	&
						Y1%+1%) 		&
				ELSE	CHANGE CVT$$(LEFT(Y$,30%),-1%) 	&
								TO Y% 	&
\					CHANGE CVT$$(LEFT(Y1$,30%),-1%) &
								TO Y1% 	&
\					GOTO 30930 			&
					IF (Y%(Y3%)<>Y1%(Y3%))-(Y1%(Y3%)&
					=63%) FOR Y3%=1% TO Y1%(0%) 	&
\					Y9%=-1% 			&

30930		IF Y2$<>'' AND Y9%=0% 					&
		THEN	GOTO 30920 					&
		ELSE	FNCOMP%=Y9% 					&

30940	FNEND 								&

31000	! Chain entry 							&
	M$=SYS(CHR$(7%)) 						&
\	REP.NUM$=MID(M$,INSTR(1%,M$,'~')+1%,6%) 			&
\	GOTO 30 							&

32767	END &

