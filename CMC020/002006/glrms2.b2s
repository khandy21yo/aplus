10	!--------------------------------------------------------------- &
	!	CONVERT GL FILES TO ASCII TEXT FILES &
	!--------------------------------------------------------------- &
	! &
	! &
	!		      Copyright (c) 1984 by &
	!	  Computer Management Center, Idaho Falls, Idaho &
	! &
	! This software is furnished under a license and may be used &
	! and copied only in accordance with the terms of such license &
	! and with the inclusion of the above copyright notice.  This &
	! software or any other copies therof may not be provided or &
	! otherwise made available to any other person.  No title to &
	! and ownership of the software is hereby transferred. &
	! &
	! The information in this software is subject to change without &
	! notice and should not be construed as a committment by &
	! Computer Management Center. &
	! &
	! CMC assumes no responsibility for the use or reliability of &
	! its software on equipment which is not supported by CMC. &
	! &
	!--------------------------------------------------------------- &

50	ON ERROR GOTO 19000 &
\	JUNK$ = SYS(CHR$(6%)+CHR$(-7%)) &

60	OPEN 'KB:' AS FILE #1%, MODE 8%+256% &
					!    8% - Echo Control &
					!   16% - Disable hibernation, Cntl-C &
\	CH%=1%				! Keyboard channel &
\	ESC$=CHR$(155%)			! Escape code for VT100 control &
\	CLSCN$=ESC$+'[H'+ESC$+'[J'	! Clear screen &
\	CLRLIN$=ESC$+'[2K'		! Erase entire line &
\	G.OFF$=ESC$+'[m'		! Select graphic off &
\	B.ON$=ESC$+'[1m'		! Bold face on &
\	BLINK.ON$=ESC$+'[5m'		! Blinking &
\	R.ON$=ESC$+'[7m'		! Reverse video &
\	COLM.ON$=ESC$+'[?3h'		! 132 Column mode &
\	COLM.OFF$=ESC$+'[?3l'		! 80 Column mode &
\	CLRBOT$=ESC$+'[21;1H'+ESC$+'[J'	! Erase cursor to end of screen &
\	LDS.ON$=ESC$+'(0'		! Line drawing set &
\	USASCII$=ESC$+'(B'		! United States ASCII &

80	!COM(THIS) DROP.DEAD.DATE$ = 8, VERSION.NO$ = 6, SERIAL.NO$ = 10 &
	DROP.DEAD.DATE$='        ' &
\	VERSION.NO$='V1.0' &
\	IF DROP.DEAD.DATE$<>'' &
	THEN	IF DROP.DEAD.DATE$<MID(SYS(CHR$(6%)+CHR$(20%)+CHR$(0%)+CHR$(0%)+ &
				CHR$(1%)+CHR$(SWAP%(1%))),7%,8%) &
		THEN	MESG$=MID(SYS(CHR$(6%)+CHR$(9%)+CHR$(47%)), &
				3%,30%) &
\			PRINT #CH%, MESG$; &
\			V$=SYS(CHR$(9%)) &

100	DIM AMOUNT(15%), CHART.MON$(15%), BDGT.PER$(13%) &
\	DIM FILE.NAME$(100%), SYS.CALL%(30%), FIL.NAM%(30%), &
		Y%(32%), Y1%(32%) &
\	CHART.CH%	= 02% &
\	CONTRL.CH%	= 02% &
\	GJH.CH%		= 02% &
\	GJL.CH%		= 04% &
\	GL.CH%		= 02% &
\	BUDGET.CH%	= 02% &
\	FINSTA.CH%	= 02% &
\	CMD.CH%		= 02% &
\	GJL.CH% 	= 02% &
\	ASCII.CH%	= 10% &
\	MENU.CH%   	= 12% &

200	IF FNO%(DEVICE.CH%,'DEVICE.DAT','/R0','')=0% &
	THEN	CONTRL.DEVICE$=CVT$$(MID(FNL$,7%,20%),-1%) &
			IF FNG%(DEVICE.CH%,'CONTRL')=0% &
\		CHART.DEVICE$=CVT$$(MID(FNL$,7%,20%),-1%) &
			IF FNG%(DEVICE.CH%,'CHART')=0% &
\		GLMMM.DEVICE$=CVT$$(MID(FNL$,7%,20%),-1%) &
			IF FNG%(DEVICE.CH%,'GLMMM')=0% &
\		BUDGET.DEVICE$=CVT$$(MID(FNL$,7%,20%),-1%) &
			IF FNG%(DEVICE.CH%,'BUDGET')=0% &
\		GJ.DEVICE$=CVT$$(MID(FNL$,7%,20%),-1%) &
			IF FNG%(DEVICE.CH%,'GJ')=0% &
\		FINSTA.DEVICE$=CVT$$(MID(FNL$,7%,20%),-1%) &
			IF FNG%(DEVICE.CH%,'FINSTA')=0% &
\		V%=FNC%(DEVICE.CH%) &

300	!------------------------------------------------------------- &
	! OPEN ASCII TEXT FILE &
	!------------------------------------------------------------- &
	OPEN 'GLSYS.ASC' FOR OUTPUT AS FILE ASCII.CH% &

1000	!-----------------------------------------------PROGRAM RESTART POINT &
	PRINT #CH%, CLSCN$;'Convert gl to ascii file' &

1030	PRINT #CH%, CLRBOT$;G.OFF$; &
		'COMMAND: Convert Quit'; &
\	OPT$=CVT$$(FNINP$(CH%,128%,' ',1%,0%),32%) &
\	GOTO 1000 IF OPT$='%^C' &

1040	IF OPT$='Q' &
	THEN	CLOSE ASCII.CH% &
\		PRINT #CH%, &
			CLSCN$;B.ON$;R.ON$;BLINK.ON$;FNP$('1;75');'WAIT'; &
			G.OFF$;FNP$('24;1');FNX%('',0%,''); &

1050	GOTO 2000 IF OPT$='C' &
\	GOTO 1030 &

2000	!-------------------------------------------------------------- &
	! OPEN CHART AND CONTROL FILE &
	!-------------------------------------------------------------- &

3000	!------------------------------------------------------------- &
	! CONVERT GL FILES &
	!------------------------------------------------------------- &
	FILES%=FNFILE%('GL????.88S') &
\	PRINT #CH%,	FNP$('06;1');CLRLIN$; &
			FNP$('08;1');CLRLIN$; &
			FNP$('10;1');CLRLIN$; &

3010	FOR LOOP%=1% TO FILES% &
\		V% = FNC%(GL.CH%) &
\		GOTO 3099 IF INSTR(1%,FILE.NAME$(LOOP%),'GL    .DAS') &
\		GOTO 3099 IF INSTR(1%,FILE.NAME$(LOOP%),'.B') &
\		STOP IF FNO%(GL.CH%,FILE.NAME$(LOOP%),"/RO/SF/NS","") &
\		STOP IF FNG%(GL.CH%,'') &
\		FILE.NAME$ = MID(FILE.NAME$(LOOP%),3%,3%) + &
			MID(FILE.NAME$(LOOP%),8%,2%) &
\		PRINT #ASCII.CH%, '<STARTFILE> = GL' + FILE.NAME$ &
\		COUNTER% = 0% &
\		PRINT #CH%, FNP$('06;1');CLRLIN$;"Converting "; &
			FILE.NAME$; &

3030		FIELD #GL.CH%, FNL% AS TEMP$, &
			08% AS GL.ACCNO$, &
			02% AS GL.SOURCE$, &
			16% AS GL.REFNO$, &
			02% AS GL.TRANDAT$, &
			26% AS GL.DESC$, &
			08% AS GL.AMOUNT$, &
			06% AS GL.XREFNO$, &
			02% AS GL.POSTIM$, &
			02% AS GL.POSDAT$, &
			06% AS GL.BNKCDE$, &
			08% AS GL.CKNO$, &
			06% AS GL.VCHRNO$, &
			06% AS GL.SUBACC$, &
			06% AS GL.PHASE$, &
			08% AS GL.REGQTY$, &
			08% AS GL.PREQTY$, &
			02% AS GL.UPDATE$, &
			04% AS TEMP$, &
			02% AS POINTER$ &
\		GOTO 3090 IF CVT$%(POINTER$)<=0% OR GL.ACCNO$='DDDDDDDD' &
\		GL.WORK$=CVT$$(GL.ACCNO$,-1%) &
\		GL.WORK$=SPACE$(8%-LEN(GL.WORK$))+GL.WORK$ &
\		WORK.TRANDAT$ = FND6$(CVT$%(GL.TRANDAT$)) &
\		WORK.TRANDAT$ = RIGHT(WORK.TRANDAT$,7%) + &
				LEFT(WORK.TRANDAT$,2%) + &
				MID(WORK.TRANDAT$,4%,2%) &
\		WORK.TRANDAT$ = LEFT(WORK.TRANDAT$+'000000',6%) &
\		WORK.POSDAT$ = FND6$(CVT$%(GL.POSDAT$)) &
\		WORK.POSDAT$ = RIGHT(WORK.POSDAT$,7%) + &
				LEFT(WORK.POSDAT$,2%) + &
				MID(WORK.POSDAT$,4%,2%) &
\		WORK.POSDAT$ = LEFT(WORK.POSDAT$+'000000',6%) &
\		BATCH$ = RIGHT(NUM1$(VAL(BATCH$)+1000001.),2%) &
			IF GL.SOURCE$<>WORK.SOURCE$ &
\		WORK.SOURCE$ = GL.SOURCE$+'' &
\		WORK.STRG$ = &
			'<' + GL.ACCNO$+ &
			'><' + GL.SOURCE$+ &
			'><' + GL.REFNO$+ &
			'><' + WORK.TRANDAT$+ &
			'><' + GL.DESC$+ &
			'><' + FNREFORM$(CVT$F(GL.AMOUNT$),11%)+ &
			'><' + GL.XREFNO$+ &
			'><' + LEFT(FNT24P$(GL.POSTIM$)+'000000',6%)+ &
			'><' + WORK.POSDAT$+ &
			'><' + GL.BNKCDE$+ &
			'><' + GL.CKNO$+ &
			'><' + GL.VCHRNO$+ &
			'><' + GL.SUBACC$+ &
			'><' + GL.PHASE$+ &
			'><' + FNREFORM$(CVT$F(GL.REGQTY$),11%)+ &
			'><' + FNREFORM$(CVT$F(GL.PREQTY$),11%)+ &
			'><' + GL.UPDATE$+ &
			'><' + BATCH$ + '>' &
\		PRINT #ASCII.CH%, WORK.STRG$ &
\		IF LEN(WORK.STRG$)<>WORK.STRG% AND WORK.STRG%<>0% &
		THEN	STOP &

3090		WORK.STRG% = LEN(WORK.STRG$) &
\		GOTO 3030 IF FNN%(GL.CH%)=0% &
\		PRINT #ASCII.CH%, '<ENDFILE> = ' + FILE.NAME$ &

3099	NEXT LOOP% &
\	V% = FNC%(GL.CH%) &

7000	!--------------------------------------------------------------- &
	! compete process &
	!--------------------------------------------------------------- &
	PRINT #CH%, CLSCN$; "Finished"; &
\	CLOSE #ASCII.CH% &
\	V% = FNX%('',0%,'') &

19000	!------------------------------------------------ERROR TRAPPING &
	RESUME 30130 IF ERL=30110 &
\	RESUME 2230 IF ERL=2220% &

19900	ON ERROR GOTO 0 &

30000	!----------------------PROGRAM FUNCTIONS------------------------ &
	!------------------------------------------------TERMINAL INPUT &
	DEF FNINP$(CHN%,KYP%,FILLCHAR$,INPUTLEN%,TO.ERR%) &
\		PRINT #CHN%, STRING$(INPUTLEN%,ASCII(FILLCHAR$)); &
			  STRING$(INPUTLEN%,8%); &
\		PRINT #CHN%, RECORD 256%,CHR$(KYP%+INPUTLEN%)+FILLCHAR$; &
\		GET #CHN% &
\		FIELD #CHN%, RECOUNT AS BUFFER$ &
\		BUFFER$='%^C' IF INSTR(1%,BUFFER$,CHR$(3%)) &
\		FNINP$=CVT$$(BUFFER$,4%) &
\		V=SQR(-1) IF BUFFER$='%^C' AND TO.ERR% !^C Trappping &
\	FNEND &

30020	DEF*FNP$(ROWCOL$)=CHR$(155%)+"["+ROWCOL$+"H" ! Direct Cursor Address &

30030	! &
	!	FNREFORM - Converts a floating point value to the form: &
	!		9999999999V99 &
	!		where: &
	!			9999999999 = dollar amount zero filled &
	!			V          = implied decimal point &
	!			99	   = cents &
	! &
	!	NOTE:  AMOUNT is converted to a positive value. &
	! &
	DEF FNREFORM$(AMOUNT,STR.LEN%) &
\		WORK$ = "+" &
\		WORK$ = "-" IF AMOUNT<0.0 &
\		AMOUNT = 100.0 * ABS(AMOUNT) &
\		DOLLARS$ = CVT$$(NUM1$(INT(AMOUNT + .5001)),128%) &
\		DOLLARS$ = "0" + DOLLARS$ UNTIL LEN(DOLLARS$) >= STR.LEN% &
\		FNREFORM$ = WORK$+DOLLARS$ &
\	FNEND &

30040	DEF FNZ(Y)=INT(ABS(Y)*100.+.5001)/100.*SGN(Y) &

30100	!-----------------------------------------FILE LOOKUP FUNCTION &
	DEF FNFILE%(FILE.SPEC$) &
\		WLDCNT%=0% &
\		WLDCRD.FLAG%=0% &

30110	!-----------------WILD CARD LOOKUP------------------------------ &
		NEXXT%=0% &
\		CHANGE SYS(CHR$(6%)+CHR$(-10%)+FILE.SPEC$) TO SYS.CALL% &
\		SYS.CALL%(0%)=30% &
\		SYS.CALL%(1%)=6% &
\		SYS.CALL%(2%)=17% &
\		SYS.CALL%(3%)=WLDCRD.FLAG% !JUMP &
\		SYS.CALL%(4%)=SWAP%(WLDCRD.FLAG%) &
\		WLDCRD.FLAG%=WLDCRD.FLAG%+1% &
\		CHANGE SYS.CALL% TO SYS.CALL$ &
\		CHANGE SYS(SYS.CALL$) TO FIL.NAM% &
\		FIL.NAM%(23%)=ASCII('S') IF FIL.NAM%(23%)=0% &
\		FIL.NAM%(24%)=ASCII('Y') IF FIL.NAM%(24%)=0% &
\		WILD.FILE$= &
				RAD$(FIL.NAM%(07%)+SWAP%(FIL.NAM%(08%)))+ &
				RAD$(FIL.NAM%(09%)+SWAP%(FIL.NAM%(10%)))+'.'+ &
				RAD$(FIL.NAM%(11%)+SWAP%(FIL.NAM%(12%))) &

30120		WLDCNT% = WLDCNT% + 1% &
\		FILE.NAME$(WLDCNT%)=WILD.FILE$ &

30125		GOTO 30110 &

30130		FNFILE%=WLDCNT% &
\	FNEND &

30140	DEF*FNZER$(Z%)=RIGHT(NUM1$(100%+Z%),2%) &

30400	!-------------------------------------------------DATE HANDLERS &
	DEF FND8%(D8)=D8 &

30410	DEF FND6%(D9$)=VAL(MID(D9$,3%,2%))+VAL(LEFT(D9$,2%))*32% &
		+ FND8%(VAL(RIGHT(D9$,5%)))*512% &

30420	DEF FND6$(D9%)=RIGHT(NUM1$((D9% AND 15%*32%)/32%+100%),2%) &
		+ '/'+RIGHT(NUM1$((D9% AND 31%)+100%),2%) &
		+ '/'+RIGHT(NUM1$(((SWAP%(D9%) AND 254%)/2%)+100%),2%) &

30300	!----------------------------------SYSTEM 24 HOUR TIME HANDLERS &
	DEF FNT24S$(TYME$)=CHR$(VAL(MID(TYME$,21%,2%)))+ &
				CHR$(VAL(MID(TYME$,24%,2%))) &

30310	DEF FNT24P$(TYME$)=NUM1$(ASCII(LEFT(TYME$,1%)))+ &
				NUM1$(ASCII(RIGHT(TYME$,2%))) &

30900	!-------------------------------------------------------COMPARE &
	DEF FNCOMP%(Y$,Y2$) &
\		Y9%=0% &
\		Y9%=-1% IF Y2$='*' &
\		Y2$=Y2$+',' &

30920		IF Y9%=0% &
		THEN	Y1$=LEFT(Y2$,INSTR(1%,Y2$,',')-1%) &
\			Y2$=RIGHT(Y2$,LEN(Y1$)+2%) &
\			Y1%=INSTR(1%,Y1$,'/') &
\			IF Y1%+INSTR(1%,Y1$,'?')=0% &
			THEN	Y9%=Y$=Y1$ &
			ELSE	IF Y1% &
				THEN	Y9%=LEFT(Y1$,Y1%-1%)<=Y$ AND &
							Y$<=RIGHT(Y1$, &
						Y1%+1%) &
				ELSE	CHANGE CVT$$(LEFT(Y$,30%),-1%) &
								TO Y% &
\					CHANGE CVT$$(LEFT(Y1$,30%),-1%) &
								TO Y1% &
\					GOTO 30930 &
					IF (Y%(Y3%)<>Y1%(Y3%))-(Y1%(Y3%) &
					=63%) FOR Y3%=1% TO Y1%(0%) &
\					Y9%=-1% &

30930		IF Y2$<>'' AND Y9%=0% &
		THEN	GOTO 30920 &
		ELSE	FNCOMP%=Y9% &

30940	FNEND &

32767	END &

