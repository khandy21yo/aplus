10	! MAINTAIN THE BANK BALANCE FILE &
	!-------------------------------------------------------------------- &
	! &
	! &
	!		COPYRIGHT (c) 1984 BY &
	!		COMPUTER MANAGEMENT CENTER, IDAHO FALLS, IDAHO &
	! &
	! THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND &
	! COPIED ONLY IN ACCORDANCE WITH THE TERMS OF SUCH LINCENSE AND WITH &
	! THE INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR ANY &
	! OTHER COPIES THEROF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAIL- &
	! ABLE TO ANY OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF THE SOFT- &
	! WARE IS HEREBY TRANSFERRED. &
	! &
	! THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE WITHOUT NO- &
	! TICE AND SHOULD NOT BE CONSTRUED AS A COMMITTMENT BY COMPUTER &
	! MANAGEMENT CENTER. &
	! &
	! CMC ASSUMES NO RESPONSIBILITY FOR THE USE OR RELIABILITY OF &
	! ITS SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPORTED BY CMC. &
	! &
	!----------------------------------------------------------------- &

50	ON ERROR GOTO 19000 &
\	JUNK$ = SYS(CHR$(6%) + CHR$(-7%)) &

60	OPEN "KB:" AS FILE #1%, MODE 8%+256% &
					!    8% - Echo Control &
					!   16% - Disable hibernation, Cntl-C &
\	CH%=1%				! Keyboard channel &
\	ESC$=CHR$(155%)			! Escape code for VT100 control &
\	CLSCN$=ESC$+"[H"+ESC$+"[J"	! Clear screen &
\	CLRLIN$=ESC$+"[2K"		! Erase entire line &
\	CLREL$=ESC$+"[K"		! Cursor to end of line &
\	INSERT.LIN$=ESC$+"[1L"		! Insert a line &
\	G.OFF$=ESC$+"[m"		! Select graphic off &
\	B.ON$=ESC$+"[1m"		! Bold face on &
\	BLINK.ON$=ESC$+"[5m"		! Blinking &
\	R.ON$=ESC$+"[7m"		! Reverse video &
\	SCROLL.ON$=ESC$+"[1;24r"	! Scroll control &
\	SCROLL.REGION$=ESC$+"[4;19r"	! Scroll region &
\	S.RESET$=ESC$+"c"		! Reset Screen &
\	COLM.ON$=ESC$+"[?3h"		! 132 Column mode &
\	COLM.OFF$=ESC$+"[?3l"		! 80 Column mode &
\	CLRBOT$=ESC$+"[21;1H"+ESC$+"[J"	! Erace cursor to end of screen &
\	LDS.ON$=ESC$+"(0"		! Line drawing set &
\	USASCII$=ESC$+"(B"		! United States ASCII &
\	HID.CURSOR$=ESC$+"[24;80H"	! Hid the cursor in the corner &
\	PRINT #CH%, ESC$;"<";		! Enter ANSI mode &

100	FILE.CH%   = 2% &
\	MENU.CH%   = 12% &
\	LOOP.DATA% = 8% &
\	LOOP.KEY%  = 1% &
\	KEY.LEN%   = 16% &
\	DATA.LEN%  = 64% &

110	DIM DEFAULT$(30%),SEARCH$(30%),REPLCE$(30%) &

200	BANKBL.DEVICE$="SY:" &
\	IF FNO%(FILE.CH%,'DEVICE.DAT','/RO','')=0% &
	THEN	BANKBL.DEVICE$=CVT$$(MID(FNL$,7%,20%),-1%) &
			IF FNG%(FILE.CH%,"BANKBL")=0% &
\		V%=FNC%(FILE.CH%) &

300	V%=FNO%(FILE.CH%,BANKBL.DEVICE$+"BANKBL.DAT","/RW","") &
\	V%=FNO%(FILE.CH%,BANKBL.DEVICE$+"BANKBL.DAT","/CR:"+NUM1$(KEY.LEN%)+","+ &
			NUM1$(DATA.LEN%),"") IF FNS%=5% &
\	IF FNS% &
	THEN	PRINT #CH%, FNP$(24%,1%);CLRLIN$; &
			"Error";FNS%;"While open BANKBL.DAT";FNP$(24%,55%); &
			"Hit any key to continue"; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		OPT$='Q' &
\		GOTO 1040 &

400	OPEN "MENU.FIL/RO" FOR INPUT AS FILE MENU.CH% &
\	DIM #12%, A0$(255%)=64% &
\	COMPANY$=CVT$$(RIGHT(A0$(1%),2%),128%) &
\	CLOSE MENU.CH% &

450	!---------------RESUME LINE FOR ERROR ON LINE 400 &
	OPEN "NL:" AS FILE 12%, RECORDSIZE 64% &
\	FIELD #12%, 64% AS BANKBL.BUF$ &
\	FIELD #12%,	08% AS BANKBL.CHECKNUM$,	&
			06% AS BANKBL.BANKACC$,		&
			02% AS BANKBL.FLAG$,		&
			02% AS BANKBL.CHECKDATE$,	&
			08% AS BANKBL.CHECKAMT$,	&
			02% AS BANKBL.CANDATE$,		&
			08% AS BANKBL.BANKAMT$,		&
			02% AS BANKBL.GLDATE$		&

500	FRAME$    = FRAME$+FNP$(I%,1%)+" "+FNP$(I%,80%)+" " FOR I%=2% TO 19% &
\	COMMAND$  = " " &
\	COMMAND$  = COMMAND$+RIGHT(NUM1$(I%+100%),2%) + " " &
		FOR I%=1% TO LOOP.DATA% &

1000	!--------------------------------------------PROGRAM RESTART POINT &
	IF FNG%(FILE.CH%,"")=0% &
	THEN	1010 &
		ELSE	PRINT #CH%, G.OFF$; &
\			GOSUB 1060 &
\			GOTO 1030 &

1005	IF FNN%(FILE.CH%) &
	THEN	18910 &
		ELSE	1015 &

1010	PRINT #CH%, COLM.OFF$;SCROLL.ON$;G.OFF$; &
\	GOSUB 1060 &

1015	KEY.POINTER%=FNR(FILE.CH%) &
\	DATA.POINTER%=FNR(FILE.CH%+1%) &
\	GOSUB 6000 &

1030	FLAG%,CHNG.FLAG%=0% &
\	PRINT #CH%, CLRBOT$;G.OFF$; &
		"COMMAND: Add Erase Change Blank Initialize Default "; &
		"Global Find Next Restore ";FNP$(22%,10%); &
		"View Quit "; &
\	OPT$=CVT$$(FNINP$(CH%,128%," ",1%),32%) &
\	GOTO 1010 IF OPT$="%^C" &
\	OPT$='' IF KEY.POINTER%=0% AND INSTR(1%,"AQ",OPT$)=0% &

1040	IF OPT$="Q" &
	THEN	PRINT #CH%, CLSCN$; &
			"Returning to previous menu."; &
			FNP$(1%,64%);"Please wait. . . "; &
\		V%=FNX%("",0%,"") &

1050	GOTO 2000 IF OPT$="A" &
\	GOTO 2400 IF OPT$="E" &
\	GOTO 2300 IF OPT$="B" &
\	GOTO 2200 IF OPT$="C" &
\	GOTO 2000 IF OPT$="F" &
\	GOTO 1005 IF OPT$="N" &
\	GOTO 17000 IF OPT$="V" &
\	GOTO 2500 IF OPT$="D" &
\	GOTO 2600 IF OPT$="I" &
\	GOTO 2700 IF OPT$="G" &
\	GOTO 1000 IF OPT$="R" &
\	GOTO 1030 &

1060	TEMP$     = "BANK RECONCILATION FILE MAINTENANCE" &
\	PRINT #CH%, CLSCN$;R.ON$;LEFT(TEMP$,39%);SPACE$(40%-LEN(TEMP$)); &
			SPACE$(40%-LEN(COMPANY$));COMPANY$; &
			FRAME$;FNP$(20%,1%);SPACE$(80%);G.OFF$; &
\	PRINT #CH%, FNP$(4%,20%);"(01) Check #"; &
			FNP$(6%,20%);"(02) Bank Code"; &
			FNP$(8%,20%);"(03) Flag (D/C/A)"; &
			FNP$(10%,20%);"(04) Check Date"; &
			FNP$(12%,20%);"(05) Check Amount"; &
			FNP$(14%,20%);"(06) Cancel Date"; &
			FNP$(16%,20%);"(07) Bank Amount"; &
			FNP$(18%,20%)"(08) G/L Date"; &
\	RETURN &

2000	!-------------------------------------------------SEARCH FOR KEYS &
	FOR LOOP%=1% TO LOOP.DATA% &
\		INP$='' &
\		INP$=DEFAULT$(LOOP%) IF OPT$='A' &
\		GOSUB 6030 &
\	NEXT LOOP% &
\	OPTION$="FIND " IF OPT$="F" &
\	OPTION$="ADD " IF OPT$="A" &

2010	FOR LOOP%=1% TO 2% &
\		GOSUB 6020 &
\		GOTO 1010 IF INP$="%^C" OR INP$='' AND LOOP%=1% &
\	NEXT LOOP% &

2020	IF OPT$="F" &
	THEN	V%=FNG%(FILE.CH%,CVT$$(SEARCH.KEY$,128%)) &
\		GOTO 1015 &

2100	!-----------------------------------------------------ADD RECORD &
	OPTION$="ADD " &
\	FLAG%=-1% &
\	FOR LOOP%=3% TO LOOP.DATA% &
\	  	GOSUB 6010 &
\		GOTO 1010 IF INP$="%^C" OR INP$="%ABORT" &
\	NEXT LOOP% &

2110	IF FNA%(FILE.CH%,BANKBL.BUF$) &
	THEN	PRINT #CH%, FNP$(24%,1%);"Error";FNS%;"while adding to file"; &
			FNP$(24%,55%);"Hit any key to continue"; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		GOTO 1000 &

2120	KEY.POINTER%=FNR(FILE.CH%) &
\	DATA.POINTER%=FNR(FILE.CH%+1%) &
\	GOTO 1030 &

2200	!--------------------------------------------------CHANGE RECORD &
	OPTION$ = "CHANGE " &
\	FLAG%, CHNG.FLAG% = -1% &
\	PRINT #CH%, CLRBOT$; "Change:"; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",2%),32%) &
\	GOTO 1010 IF INP$="%^C" &
\	GOTO 1030 IF INP$="" &
\	LOOP%=(INSTR(1%,COMMAND$," "+INP$+" ")+2%)/3% &
\	GOTO 2200 IF LOOP%=0% &
\	GOSUB 6010 &
\	GOTO 1010 IF INP$="%^C" &
\	GOTO 2200 IF INP$="%ABORT" &

2210	V%=FNG%(-FILE.CH%,NUM1$(KEY.POINTER%))+ &
		FNG%(-FILE.CH%-1%,NUM1$(DATA.POINTER%)) &
\	V%=FNU%(FILE.CH%,BANKBL.BUF$) IF LOOP%>3% AND FNS%=0% &
\	V%=FNU%(-FILE.CH%,BANKBL.BUF$) IF LOOP%<4% AND FNS%=0% &
\	IF FNS% &
	THEN	PRINT #CH%, FNP$(24%,1%);"Error";FNS%;"while changing to file"; &
			FNP$(24%,55%);"Hit any key to continue"; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		GOTO 1010 &

2220	GOTO 2200 &

2300	!--------------------------------------------------CHANGE RECORD &
	OPTION$ = "BLANK " &
\	TEMP%=1% &
\	PRINT #CH%, CLRBOT$; "Blank:"; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",2%),32%) &
\	GOTO 1010 IF INP$="%^C" &
\	GOTO 1030 IF INP$="" &
\	LOOP%=(INSTR(1%,COMMAND$," "+INP$+" ")+2%)/3% &
\	GOTO 2300 IF LOOP%=0% OR LOOP%<4% &
\	INP$="" &
\	GOSUB 6030 &

2310	V%=FNG%(-FILE.CH%,NUM1$(KEY.POINTER%))+ &
		FNG%(-FILE.CH%-1%,NUM1$(DATA.POINTER%)) &
\	V%=FNU%(FILE.CH%,BANKBL.BUF$) IF LOOP%>4% AND FNS%=0% &
\	IF FNS% &
	THEN	PRINT #CH%, FNP$(24%,1%);"Error";FNS%;"while changing to file"; &
			FNP$(24%,55%);"Hit any key to continue"; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		GOTO 1010 &

2320	GOTO 2300 &

2400	!-----------------------------------------------DELETE RECORD &

2410	PRINT #CH%, CLRBOT$;"Confirm deletion (Yes/No) "; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",1%),32%) &
\	GOTO 1010 IF INP$="%^C" &
\	IF INP$<>"Y" &
	THEN	PRINT #CH%, FNP$(24%,1%);CLRLIN$;"This record has "; B.ON$; &
			BLINK.ON$;"NOT ";G.OFF$; "been deleted."; &
			FNP$(24%,55%);"Hit any key to continue "; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		GOTO 1030 &

2420	IF FND%(FILE.CH%,"")=0% &
	THEN	PRINT #CH%, FNP$(24%,1%);CLRLIN$;"This record has been deleted."; &
			FNP$(24%,55%);"Hit any key to continue "; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		GOTO 1005 &

2430	PRINT #CH%, FNP$(24%,1%);"Error";FNS%;"while deleting to file"; &
		FNP$(24%,55%);"Hit any key to continue"; &
\	INP$=FNINP$(CH%,128%," ",1%) &
\	GOTO 1005 &

2500	!-----------------------------------------------SET DEFAULT VALUES &
	FOR LOOP%=1% TO LOOP.DATA% &
\		INP$=DEFAULT$(LOOP%) &
\		GOSUB 6030 &
\	NEXT LOOP% &

2510	OPTION$="DEFAULT " &
\	PRINT #CH%, CLRBOT$;"Default:"; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",2%),32%) &
\	GOTO 1010 IF INP$="%^C" OR INP$="" &
\	LOOP%=(INSTR(1%,COMMAND$," "+INP$+" ")+2%)/3% &
\	GOTO 2510 IF LOOP%=0% &
\	GOSUB 6010 &
\	GOTO 1010 IF INP$="%^C" &
\	DEFAULT$(LOOP%)=INP$ &
\	GOTO 2510 &

2600	!-----------------------------------------------INITIALIZATION &
	INP$='' &
\	GOSUB 6030 FOR LOOP%=1% TO LOOP.DATA% &
\	INITIALIZE$=" " &

2610	PRINT #CH%, CLRBOT$;FNP$(22%,1%);INITIALIZE$; &
		FNP$(21%,1%);"Field to initialize:"; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",2%),32%) &
\	GOTO 1010 IF INP$="%^C" &
\	IF INP$<>"" &
	THEN	LOOP%=(INSTR(1%,COMMAND$," "+INP$+" ")+2%)/3% &
\		INITIALIZE$=INITIALIZE$+INP$+" " IF &
			LOOP%<>0% AND (INSTR(1%,INITIALIZE$," "+INP$+" ") &
				+2%)/3%=0% &
\		GOTO 2610 &

2620	GOTO 1010 IF INITIALIZE$="" &
\	OPT$="C" &
\	OPTION$="INITIALIZE " &
\	FLAG%,CHNG.FLAG%=-1% &
\	GOTO 2660 &

2650	GOTO 18910 IF FNN%(FILE.CH%) &
\	KEY.POINTER%=FNR(FILE.CH%) &
\	DATA.POINTER%=FNR(FILE.CH%+1%) &

2660	GOSUB 6000 &
\	FOR I%=1% TO LEN(INITIALIZE$)/3% &
\		LOOP%=(INSTR(1%,COMMAND$,MID(INITIALIZE$,I%*3%-2%,3%))+2%)/3% &
\		GOSUB 6010 &
\		GOTO 1030 IF INP$="%^C" &

2680		IF INP$<>"" AND INP$<>"%ABORT" &
		THEN	V%=FNG%(-FILE.CH%,NUM1$(KEY.POINTER%))+ &
				FNG%(-FILE.CH%-1%,NUM1$(DATA.POINTER%)) &
\			V%=FNU%(FILE.CH%,BANKBL.BUF$) IF LOOP%>3% AND FNS%=0% &
\			V%=FNU%(-FILE.CH%,BANKBL.BUF$) IF LOOP%<4% AND FNS%=0% &
\			IF FNS% &
			THEN	PRINT #CH%, FNP$(24%,1%);"Error";FNS%; &
					"while changing to file"; &
				FNP$(24%,55%);"Hit any key to continue"; &
\				INP$=FNINP$(CH%,128%," ",1%) &
\				GOTO 1010 &

2690	NEXT I% &
\	GOTO 2650 &

2700	!--------------------------------GLOBAL SEARCH AND REPLACE/DELETE &
	INP$='' &
\	GOSUB 6030 FOR LOOP%=1% TO LOOP.DATA% &
\	OPT$="F" &
\	OPTION$="SEARCH FOR " &
\	SEARCH$=" " &
\	SEARCH$(LOOP%),REPLCE$(LOOP%)="" FOR LOOP%=1% to LOOP.DATA% &

2710	PRINT #CH%, CLRBOT$;"Field to search for "; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",2%),32%) &
\	GOTO 1010 IF INP$="%^C" &
\	IF INP$<>"" &
	THEN	LOOP%=(INSTR(1%,COMMAND$," "+INP$+" ")+2%)/3% &
\		SEARCH$=SEARCH$+INP$+" " IF &
			LOOP%<>0% AND (INSTR(1%,SEARCH$," "+INP$+" ")+2%)/3%=0% &
\		GOTO 2710 IF LOOP%<1% OR LOOP%>LOOP.DATA% &
\		GOSUB 6010 &
\		GOTO 1010 IF INP$="%^C" &
\		SEARCH$(LOOP%)=INP$ &
\		GOTO 2710 &

2730	OPT$="C" &
\	FLAG%,CHNG.FLAG%=-1% &

2740	GOTO 1010 IF SEARCH$="" &
\	PRINT #CH%, CLRBOT$;"COMMAND: Delete Replace "; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",1%),32%) &
\	GOTO 1010 IF INP$="%^C" &
\	GOTO 2760 IF INP$="D" &
\	GOTO 2800 IF INP$="R" &
\	GOTO 2740 &

2750	GOTO 18910 IF FNN%(FILE.CH%) &
\	KEY.POINTER%=FNR(FILE.CH%) &
\	DATA.POINTER%=FNR(FILE.CH%+1%) &

2760	PRINT #CH%, CLRBOT$;"Searching. . . "; &
\	LSET BANKBL.BUF$=FNL$ &
\	SEARCH.FLAG%=0% &
\	FOR I%=1% TO LEN(SEARCH$)/3% &
\		LOOP%=(INSTR(1%,COMMAND$,MID(SEARCH$,I%*3%-2%,3%))+2%)/3% &
\		GOSUB 6040 &
\		GOTO 2770 IF SEARCH.FLAG%=-1% &
\	NEXT I% &
\	GOTO 2750 &

2770	GOSUB 6000 &

2780	PRINT #CH%, CLRBOT$;"Confirm deletion (Yes/No) "; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",1%),32%) &
\	GOTO 1030 IF INP$="%^C" &
\	IF INP$<>"Y" &
	THEN	PRINT #CH%, FNP$(24%,1%);CLRLIN$;"This record has "; B.ON$; &
			BLINK.ON$;"NOT ";G.OFF$; "been deleted."; &
			FNP$(24%,55%);"Hit any key to continue "; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		GOTO 1010 IF INP$="%^C" &
\		GOTO 2750 &

2790	IF FND%(FILE.CH%,"") &
	THEN	PRINT #CH%, FNP$(24%,1%);"Error";FNS%;"while deleting this "; &
			"record";FNP$(24%,55%);"Hit any key to continue"; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		GOTO 1010 &

2795	PRINT #CH%, FNP$(24%,1%);CLRLIN$;"This record has been deleted."; &
		FNP$(24%,55%);"Hit any key to continue "; &
\	INP$=FNINP$(CH%,128%," ",1%) &
\	GOTO 1010 IF INP$="%^C" &
\	GOTO 2750 &

2800	OPTION$="REPLACE " &
\	GOTO 2820 &

2810	GOTO 18910 IF FNN%(FILE.CH%) &
\	KEY.POINTER%=FNR(FILE.CH%) &
\	DATA.POINTER%=FNR(FILE.CH%+1%) &

2820	PRINT #CH%, CLRBOT$;"Searching. . . "; &
\	LSET BANKBL.BUF$=FNL$ &
\	SEARCH.FLAG%=0% &
\	FOR I%=1% TO LEN(SEARCH$)/3% &
\		LOOP%=(INSTR(1%,COMMAND$,MID(SEARCH$,I%*3%-2%,3%))+2%)/3% &
\		GOSUB 6040 &
\		GOTO 2825 IF SEARCH.FLAG% &
\	NEXT I% &
\	GOTO 2810 &

2825	GOSUB 6000 &

2830	PRINT #CH%, CLRBOT$; "Change:"; &
\	INP$=CVT$$(FNINP$(CH%,128%," ",2%),32%) &
\	GOTO 1010 IF INP$="%^C" &
\	GOTO 2810 IF INP$="" &
\	LOOP%=(INSTR(1%,COMMAND$," "+INP$+" ")+2%)/3% &
\	GOTO 2830 IF LOOP%=0% &
\	GOSUB 6010 &
\	GOTO 1010 IF INP$="%^C" &
\	GOTO 2830 IF INP$="%ABORT" &

2840	IF INP$<>"" AND INP$<>"%ABORT" &
	THEN	V%=FNG%(-FILE.CH%,NUM1$(KEY.POINTER%))+ &
				FNG%(-FILE.CH%-1%,NUM1$(DATA.POINTER%)) &
\			V%=FNU%(FILE.CH%,BANKBL.BUF$) IF LOOP%>3% AND FNS%=0% &
\			V%=FNU%(-FILE.CH%,BANKBL.BUF$) IF LOOP%<4% AND FNS%=0% &
\			IF FNS% &
			THEN	PRINT #CH%, FNP$(24%,1%);"Error";FNS%; &
					"while changing to file"; &
				FNP$(24%,55%);"Hit any key to continue"; &
\				INP$=FNINP$(CH%,128%," ",1%) &
\				GOTO 1010
2850	GOTO 2830 &

6000	!-----------------------------------------------LOOP GOSUB SECTION &
	RETURN IF KEY.POINTER%=0% &
\	LSET BANKBL.BUF$=FNL$ &
\	FOR LOOP%=1% TO LOOP.DATA% &
\		ON LOOP% GOSUB	6220, 6240, 6260, 6280, 6300, 6320, 6340, &
				6360 &
\	NEXT LOOP% &
\	RETURN &

6010	ON LOOP% GOSUB 6210, 6230, 6250, 6270, 6290, 6310, 6330,6350 &
\	RETURN &
		! INPUT DATA &

6020	ON LOOP% GOSUB 6210,6230 &
\	SEARCH.KEY$=BANKBL.CHECKNUM$ IF LOOP%=1% &
\	SEARCH.KEY$=SEARCH.KEY$+BANKBL.BANKACC$ IF LOOP%=2% &
\	RETURN &
		! SEARCH KEY &

6030	ON LOOP% GOSUB 6217, 6237, 6257, 6277, 6297, 6317, 6337,6357 &
\	RETURN &
		! DEFAULT &

6040	SEARCH.FLAG%=-1% IF BANKBL.CHECKNUM$=SEARCH$(LOOP%) AND LOOP%=1% &
\	SEARCH.FLAG%=-1% IF BANKBL.BANKACC$=SEARCH$(LOOP%) AND LOOP%=2% &
\	SEARCH.FLAG%=-1% IF BANKBL.FLAG$=SEARCH$(LOOP%) AND LOOP%=3% &
\	SEARCH.FLAG%=-1% IF BANKBL.CHECKDATE$=CVT%$(FND6%(SEARCH$(LOOP%))) AND &
		LOOP%=4% &
\	SEARCH.FLAG%=-1% IF BANKBL.CHECKAMT$=CVTF$(VAL(SEARCH$(LOOP%))) AND &
		LOOP%=5% &
\	SEARCH.FLAG%=-1% IF BANKBL.CANDATE$=CVT%$(FND6%(SEARCH$(LOOP%))) AND &
		LOOP%=6% &
\	SEARCH.FLAG%=-1% IF BANKBL.BANKAMT$=CVTF$(VAL(SEARCH$(LOOP%))) AND &
		LOOP%=7% &
\	SEARCH.FLAG%=-1% IF BANKBL.GLDATE$=CVT%$(FND6%(SEARCH$(LOOP%))) AND &
		LOOP%=8% &
\	RETURN &
		! SEARCH FOR MATCH &

6200	!-----------------------------------------------DATA ENTRY SECTION &

6210	F.LEN%=LEN(BANKBL.CHECKNUM$) &
\	PRINT #CH%, FNP$(4%,40%);R.ON$;B.ON$; &
\	PRINT #CH%, SPACE$(F.LEN%); IF CHNG.FLAG%=0% &
\	PRINT #CH%, BANKBL.CHECKNUM$; IF CHNG.FLAG% &
\	PRINT #CH%, G.OFF$;CLRBOT$;OPTION$;"ALPHA: ";B.ON$; &
\	INP$=FNINP$(CH%,0%,"_",F.LEN%) &
\	INP$=DEFAULT$(LOOP%) IF INP$="" &
\	GOTO 6210 IF INP$="" AND CHNG.FLAG%=0% AND OPT$<>"F" &
\	GOTO 6220 IF INP$="%^C" &

6217	RSET BANKBL.CHECKNUM$=INP$ IF INP$<>"" OR CHNG.FLAG%=0% &

6220	PRINT #CH%, FNP$(4%,40%);B.ON$;BANKBL.CHECKNUM$;G.OFF$; &
\	RETURN &

6230	F.LEN%=LEN(BANKBL.BANKACC$) &
\	PRINT #CH%, FNP$(6%,40%);R.ON$;B.ON$; &
\	PRINT #CH%, SPACE$(F.LEN%); IF CHNG.FLAG%=0% &
\	PRINT #CH%, BANKBL.BANKACC$; IF CHNG.FLAG% &
\	PRINT #CH%, G.OFF$;CLRBOT$;OPTION$;"ALPHA: ";B.ON$; &
\	INP$=FNINP$(CH%,0%,"_",F.LEN%) &
\	INP$=DEFAULT$(LOOP%) IF INP$="" AND OPT$<>"F" &
\	GOTO 6230 IF INP$="" AND CHNG.FLAG%=0% AND OPT$<>"F" &
\	GOTO 6240 IF INP$="%^C" &

6237	LSET BANKBL.BANKACC$=INP$ IF INP$<>"" OR CHNG.FLAG%=0% &

6240	PRINT #CH%, FNP$(6%,40%);B.ON$;BANKBL.BANKACC$;G.OFF$; &
\	RETURN &

6250	F.LEN%=1% &
\	PRINT #CH%, FNP$(8%,40%);R.ON$;B.ON$; &
\	PRINT #CH%, SPACE$(F.LEN%); IF CHNG.FLAG%=0% &
\	PRINT #CH%, BANKBL.FLAG$; IF CHNG.FLAG% &
\	PRINT #CH%, G.OFF$;CLRBOT$;OPTION$;"ALPHA: ";B.ON$; &
\	INP$=FNINP$(CH%,0%,"_",F.LEN%) &
\	INP$=DEFAULT$(LOOP%) IF INP$="" &
\	GOTO 6260 IF INP$="%^C" &
\	IF INP$<>'' OR CHNG.FLAG%=0% &
	THEN	IF INSTR(1%,'ACD',INP$)=0% &
		THEN	PRINT #CH%, FNP$(23%,1%);CLRLIN$; &
				"'C' Check or Disbursement; "; &
				FNP$(24%,1%);CLRLIN$;"'D'=Deposit; "; &
				"'A'=Adjustment"; &
				FNP$(24%,55%);"Hit any key to continue"; &
\			INP$=FNINP$(CH%,128%," ",1%) &
\			GOTO 6250 &

6257	LSET BANKBL.FLAG$=INP$ IF INP$<>"" OR CHNG.FLAG%=0% &

6260	TEMP$='Adjustment            ' &
\	TEMP$='Check or Disbursement ' IF BANKBL.FLAG$='C' &
\	TEMP$='Deposit               ' IF BANKBL.FLAG$='D' &
\	PRINT #CH%, FNP$(8%,40%);B.ON$;BANKBL.FLAG$;G.OFF$;' ';TEMP$; &
\	RETURN &

6270	F.LEN%=6% &
\	PRINT #CH%, FNP$(10%,40%);R.ON$;B.ON$; &
\	PRINT #CH%, SPACE$(F.LEN%+2%); IF CHNG.FLAG%=0% &
\	PRINT #CH%, FND6$(CVT$%(BANKBL.CHECKDATE$)); IF CHNG.FLAG% &
\	PRINT #CH%, G.OFF$;CLRBOT$;OPTION$;"DATE (MMDDYY) : ";B.ON$; &
\	INP$=FNINP$(CH%,0%,"_",F.LEN%) &
\	INP$=DEFAULT$(LOOP%) IF INP$="" &
\	GOTO 6280 IF INP$="%^C" &
\	TEMP=VAL(INP$) &

6277	LSET BANKBL.CHECKDATE$=CVT%$(FND6%(INP$)) IF INP$<>"" OR CHNG.FLAG%=0% &

6280	PRINT #CH%, FNP$(10%,40%);B.ON$;FND6$(CVT$%(BANKBL.CHECKDATE$));G.OFF$; &
\	RETURN &

6290	F.LEN%=12% &
\	PRINT #CH%, FNP$(12%,40%);R.ON$;B.ON$; &
\	PRINT #CH%, SPACE$(F.LEN%+2%); IF CHNG.FLAG%=0% &
\	PRINT #CH% USING '###,###,###.##', CVT$F(BANKBL.CHECKAMT$); IF CHNG.FLAG% &
\	PRINT #CH%, G.OFF$;CLRBOT$;OPTION$;"VALUE: ";B.ON$; &
\	INP$=FNINP$(CH%,0%,"_",F.LEN%) &
\	INP$=DEFAULT$(LOOP%) IF INP$="" &
\	GOTO 6300 IF INP$='' AND CHNG.FLAG% OR INP$="%^C" &
\	TEMP%=1% &
\	TEMP%=100% IF INSTR(1%,INP$,'.')=0% &
\	INP$=NUM1$(VAL(INP$)/TEMP%) &
\	INP$=INP$+"." IF INSTR(1%,INP$,".")=0% &

6297	LSET BANKBL.CHECKAMT$=CVTF$(VAL(INP$)) IF INP$<>"" OR CHNG.FLAG%=0% &

6300	PRINT #CH%, FNP$(12%,40%);B.ON$; &
\	PRINT #CH% USING '###,###,###.##\  \', CVT$F(BANKBL.CHECKAMT$),G.OFF$; &
\	RETURN &

6310	F.LEN%=6% &
\	PRINT #CH%, FNP$(14%,40%);R.ON$;B.ON$; &
\	PRINT #CH%, SPACE$(F.LEN%+2%); IF CHNG.FLAG%=0% &
\	PRINT #CH%, FND6$(CVT$%(BANKBL.CANDATE$)); IF CHNG.FLAG% &
\	PRINT #CH%, G.OFF$;CLRBOT$;OPTION$;"DATE (MMDDYY) : ";B.ON$; &
\	INP$=FNINP$(CH%,0%,"_",F.LEN%) &
\	INP$=DEFAULT$(LOOP%) IF INP$="" &
\	GOTO 6320 IF INP$="%^C" &
\	TEMP=VAL(INP$) &

6317	LSET BANKBL.CANDATE$=CVT%$(FND6%(INP$)) IF INP$<>"" OR CHNG.FLAG%=0% &

6320	PRINT #CH%, FNP$(14%,40%);B.ON$;FND6$(CVT$%(BANKBL.CANDATE$));G.OFF$; &
\	RETURN &

6330	F.LEN%=12% &
\	PRINT #CH%, FNP$(16%,40%);R.ON$;B.ON$; &
\	PRINT #CH%, SPACE$(F.LEN%+2%); IF CHNG.FLAG%=0% &
\	PRINT #CH%  USING '###,###,###.##',CVT$F(BANKBL.BANKAMT$); IF CHNG.FLAG% &
\	PRINT #CH%, G.OFF$;CLRBOT$;OPTION$;"VALUE: ";B.ON$; &
\	INP$=FNINP$(CH%,0%,"_",F.LEN%) &
\	INP$=DEFAULT$(LOOP%) IF INP$="" AND OPT$<>"F" &
\	GOTO 6340 IF INP$="" AND CHNG.FLAG% OR INP$="%^C" &
\	TEMP%=1% &
\	TEMP%=100% IF INSTR(1%,INP$,'.')=0% &
\	INP$=NUM1$(VAL(INP$)/TEMP%) &
\	INP$=INP$+"." IF INSTR(1%,INP$,".")=0% &

6337	LSET BANKBL.BANKAMT$=CVTF$(VAL(INP$)) IF INP$<>"" OR CHNG.FLAG%=0% &

6340	PRINT #CH%, FNP$(16%,40%);B.ON$; &
\	PRINT #CH%  USING '###,###,###.##\  \',CVT$F(BANKBL.BANKAMT$),G.OFF$; &
\	RETURN &

6350	F.LEN%=6% &
\	PRINT #CH%, FNP$(18%,40%);R.ON$;B.ON$; &
\	PRINT #CH%, SPACE$(F.LEN%+2%); IF CHNG.FLAG%=0% &
\	PRINT #CH%, FND6$(CVT$%(BANKBL.GLDATE$)); IF CHNG.FLAG% &
\	PRINT #CH%, G.OFF$;CLRBOT$;OPTION$;"DATE (MMDDYY) : ";B.ON$; &
\	INP$=FNINP$(CH%,0%,"_",F.LEN%) &
\	INP$=DEFAULT$(LOOP%) IF INP$="" &
\	GOTO 6360 IF INP$="%^C" &
\	TEMP=VAL(INP$) &

6357	LSET BANKBL.GLDATE$=CVT%$(FND6%(INP$)) IF INP$<>"" OR CHNG.FLAG%=0% &

6360	PRINT #CH%, FNP$(18%,40%);B.ON$;FND6$(CVT$%(BANKBL.GLDATE$));G.OFF$; &
\	RETURN &

17000	!-----------------------------------------------VIEW ROUTINE &
	TEMP$     = "BANK RECONCILATION FILE MAINTENANCE" &
\	PRINT #CH%, CLSCN$;R.ON$;LEFT(TEMP$,39%);SPACE$(40%-LEN(TEMP$)); &
			SPACE$(40%-LEN(COMPANY$));COMPANY$; &
			FRAME$;FNP$(20%,1%);SPACE$(80%);G.OFF$;SCROLL.REGION$; &
\	PRINT #CH%, G.OFF$;FNP$(2%,2%);'CHECK #';FNP$(2%,11%); &
		'BANK';FNP$(2%,18%);'FL';FNP$(2%,21%);'CK DATE'; &
		FNP$(2%,31%);'CK AMOUNT';FNP$(2%,41%);'BNK DATE'; &
		FNP$(2%,50%);'BNK AMOUNT';FNP$(2%,61%);'GL DATE'; &
\	PRINT #CH%, R.ON$;LDS.ON$;FNP$(3%,2%);STRING$(78%,113%); &
		FNP$(3%,10%);CHR$(110%);FNP$(3%,17%);CHR$(110%); &
		FNP$(3%,20%);CHR$(110%);FNP$(3%,29%);CHR$(110%); &
		FNP$(3%,40%);CHR$(110%);FNP$(3%,49%);CHR$(110%); &
		FNP$(3%,60%);CHR$(110%); &
		USASCII$;G.OFF$; &
\	PRINT #CH%, R.ON$;LDS.ON$;FNP$(I%,10%);CHR$(120%); &
		FNP$(I%,17%);CHR$(120%);FNP$(I%,20%);CHR$(120%); &
		FNP$(I%,29%);CHR$(120%);FNP$(I%,40%);CHR$(120%); &
		FNP$(I%,49%);CHR$(120%);FNP$(I%,60%);CHR$(120%); &
		USASCII$;G.OFF$; &
			FOR I%=2% TO 2% & 
\	DISPLAY.LINE%=0% &
\	PRINT.LINE%=4% &

17010	V%=FNG%(-FILE.CH%,NUM1$(KEY.POINTER%))+ &
		FNG%(-FILE.CH%-1%,NUM1$(DATA.POINTER%)) &
\	LSET BANKBL.BUF$=FNL$ &
\	GOTO 17105 &

17100	IF FNN%(FILE.CH%) &
	THEN	17200 &
		ELSE	LSET BANKBL.BUF$=FNL$ &

17105	PRINT #CH%, FNP$(PRINT.LINE%,2%);BANKBL.CHECKNUM$; &
		FNP$(PRINT.LINE%,11%);BANKBL.BANKACC$; &
		FNP$(PRINT.LINE%,18%);BANKBL.FLAG$; &
		FNP$(PRINT.LINE%,21%);FND6$(CVT$%(BANKBL.CHECKDATE$)); &
		FNP$(PRINT.LINE%,41%);FND6$(CVT$%(BANKBL.CANDATE$)); &
		FNP$(PRINT.LINE%,61%);FND6$(CVT$%(BANKBL.GLDATE$)); &
		FNP$(PRINT.LINE%,30%); &
\	PRINT #CH% USING "###,###.##",CVT$F(BANKBL.CHECKAMT$); &
\	PRINT #CH%, FNP$(PRINT.LINE%,50%); &
\	PRINT #CH% USING "###,###.##",CVT$F(BANKBL.BANKAMT$); &
\	I%=PRINT.LINE% &
\	PRINT #CH%, R.ON$;LDS.ON$;FNP$(I%,10%);CHR$(120%); &
		FNP$(I%,17%);CHR$(120%);FNP$(I%,20%);CHR$(120%); &
		FNP$(I%,29%);CHR$(120%);FNP$(I%,40%);CHR$(120%); &
		FNP$(I%,49%);CHR$(120%);FNP$(I%,60%);CHR$(120%); &
		USASCII$;G.OFF$; &
\	PRINT.LINE%=PRINT.LINE%+1% IF PRINT.LINE%<19% &

17110	IF DISPLAY.LINE%>14% &
	THEN	PRINT #CH%, FNP$(24%,55%);"Hit any key to continue "; &
\		INP$=FNINP$(CH%,128%," ",1%) &
\		GOTO 17210 IF INP$="%^C" &
\		DISPLAY.LINE%=0% &
\		PRINT #CH%, CLRBOT$;FNP$(PRINT.LINE%,1%); &

17120	PRINT #CH% &
\	DISPLAY.LINE%=DISPLAY.LINE%+1% &
\	IF PRINT.LINE%=19% &
	THEN	PRINT #CH%, FNP$(PRINT.LINE%,1%);R.ON$;" "; &
			FNP$(PRINT.LINE%,132%);" ";G.OFF$; &

17190	GOTO 17100 &

17200	IF DISPLAY.LINE%>0% &
	THEN	PRINT #CH%, FNP$(24%,1%);"End of file.  "; &
			FNP$(24%,55%);"Hit any key to continue "; &
\		INP$=FNINP$(CH%,128%," ",1%) &

17210	V%=FNG%(-FILE.CH%,NUM1$(KEY.POINTER%))+ &
		FNG%(-FILE.CH%-1%,NUM1$(DATA.POINTER%)) &
\	PRINT #CH%, FNP$(1%,1%);SCROLL.ON$;COLM.OFF$; &
\	GOTO 1010 &

18910	!----------------------------------------------END OF FILE &
	PRINT #CH%, CLRBOT$;FNP$(24%,1%);"End of file has been reached."; &
		FNP$(24%,55%);"Hit any key to continue "; &
\	INP$=FNINP$(CH%,128%," ",1%) &
\	GOTO 1000 &

19000	!-------------------------------------------------ERROR TRAPPING &
	IF ERR=28% &
	THEN	JUNK%=CTRLC &
\		IF ERL<1000% &
		THEN	OPT$="Q" &
\			RESUME 1040 &

19010	IF ERR=28% &
	THEN	V%=FNG%(-FILE.CH%,NUM1$(KEY.POINTER%))+ &
			FNG%(-FILE.CH%-1%,NUM1$(DATA.POINTER%)) &
\		RESUME 1010 &
	
19020	IF ERR=52% &
	THEN	INP$,D9$='' &
\		RESUME &

19030	IF ERR=11% &
	THEN	RESUME 1000 &

19900	ON ERROR GOTO 0 &

30000	DEF FNINP$(CHN%,KYP%,FILLCHAR$,INPUTLEN%) &
\		PRINT #CHN%, STRING$(INPUTLEN%,ASCII(FILLCHAR$)); &
			  STRING$(INPUTLEN%,8%); &
\		PRINT #CHN%, RECORD 256%, CHR$(KYP%+INPUTLEN%)+FILLCHAR$; &
\		GET #CHN% &
\		FIELD #CHN%, RECOUNT AS BUFFER$ &
\		IF ASCII(BUFFER$)=128% OR ASCII(BUFFER$)=8% &
			OR ASCII(BUFFER$)=23% &
		THEN	BUFFER$="%END" IF MID(BUFFER$,2%,4%)="[21~" &
				OR ASCII(BUFFER$)=8% &
\			BUFFER$="%ABORT" IF MID(BUFFER$,2%,4%)="[19~" &
				OR ASCII(BUFFER$)=23% &

30010		BUFFER$="%^C" IF ASCII(BUFFER$)=3% &
\		FNINP$=CVT$$(BUFFER$,4%) &
\	FNEND &

30050	  !------------------------------------------------------------ &
	  ! A N S I   D I R E C T   C U R S O R   A D D R E S S I N G &
	  !------------------------------------------------------------ &
	  DEF*FNP$(ROW%,COL%)=ESC$+"["+NUM1$(ROW%)+";"+NUM1$(COL%)+"H" &

30060	DEF FND8%(D8)=D8 &

30080	DEF FND6%(D9$)=VAL(MID(D9$,3%,2%))+VAL(LEFT(D9$,2%))*32% &
		+ FND8%(VAL(RIGHT(D9$,5%)))*512% &

30090	  DEF FND6$(D9%)=RIGHT(NUM1$((D9% AND 15%*32%)/32%+100%),2%) &
		+ "/"+RIGHT(NUM1$((D9% AND 31%)+100%),2%) &
		+ "/"+RIGHT(NUM1$(((SWAP%(D9%) AND 254%)/2%)+100%),2%) &

32767	END &

