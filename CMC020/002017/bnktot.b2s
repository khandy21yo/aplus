1	EXTEND &

10	!-------------------------------------------------------------------- &
	! &
	! &
	!		Copyright (c) 1984 by &
	!		Computer Management Center, Idaho Falls, Idaho &
	! &
	! This software is furnished under a license and may be used and &
	! copied only in accordance with the terms of such license and with &
	! the inclusion of the above copyright notice.  This software or any &
	! other copies thereof may not be provided or otherwise made avail- &
	! able to any other person.  No title to and ownership of the soft- &
	! ware is hereby transferred. &
	! &
	! The information in this software is subject to change without no- &
	! tice and should not be construed as a committment by Computer &
	! Management Center. &
	! &
	! CMC assumes no responsibility for the use or reliability of &
	! its software on equipment which is not supported by CMC. &
	! &
	! ++ &
	! &
	! Author: Dave Kochan &
	! &
	!----------------------------------------------------------------- &

60	OPEN "KB:MAYNRD.CMD" AS FILE #1%, MODE 256% &
\	CH%=1% &
\	ESC$=CHR$(155%) &
\	CLSCN$=ESC$+"[H"+ESC$+"[J" &
\	CLRLIN$=ESC$+"[2K" &
\	CLREL$=ESC$+"[K" &
\	INSERT.LIN$=ESC$+"[1L" &
\	G.OFF$=ESC$+"[m" &
\	B.ON$=ESC$+"[1m" &
\	BLINK.ON$=ESC$+"[5m" &
\	R.ON$=ESC$+"[7m" &
\	SCROLL.ON$=ESC$+"[1;24r" &
\	SCROLL.REGION$=ESC$+"[5;19r" &
\	S.RESET$=ESC$+"c" &
\	COLM.ON$=ESC$+"[?3h" &
\	COLM.OFF$=ESC$+"[?3l" &
\	ENTER.COPY$=ESC$+"[5i" &
\	EXIT.COPY$=ESC$+"[4i" &
\	CLRBOT$=ESC$+"[21;1H"+ESC$+"[J" &
\	LDS.ON$=ESC$+"(0" &
\	USASCII$=ESC$+"(B" &
\	HID.CURSOR$=ESC$+"[24;80H" &
\	PRINT #CH%, ESC$;"<"; &
\	TEMP$     = "Check Register Totaling" &
\	FRAME$    = CLSCN$+R.ON$+LEFT(TEMP$,39%)+SPACE$(40%-LEN(TEMP$))+ &
			SPACE$(40%-LEN(COMPANY$))+COMPANY$ &
\	FRAME$    = FRAME$+FNP$(I%,1%)+" "+FNP$(I%,80%)+" " FOR I%=2% TO 19% &
\	FRAME$    = FRAME$+FNP$(20%,1%)+SPACE$(80%)+G.OFF$ &
\	PER$=CVT$$(MID(DATE$(0%),4%,3%),-1%) &
\	GL.CH%=2% &
\	CH.CH%=4% &
\	PRINT #CH%, FRAME$; &

120	OPEN 'NL:NULL.BUF' AS FILE 12%, RECORDSIZE 64% &
\	FIELD #12%, 64% AS CNULL$ &
\	FIELD #12%, 8% AS CHK$, 6% AS BACC$, 2% AS FLAG$, 2% AS CDATE$, &
		8% AS CAMT$, 2% AS CANDATE$, 8% AS BAMT$, 2% AS GLDATE$ &
\	DIM TOTC(4%),TOTB(4%) &
\	TOTC(M%),TOTB(M%)=0. FOR M%=0% TO 4% &

200	M%=FNO%(2%,'BANKBL.DAT','/RO','') &
\	IF M% THEN &
		PRINT #CH%, FNP$(22%,1%);'Error';M%;'opening BANKBL.DAT'; &
			' - Aborting...' &
\		GOTO 32767 &

210	IF FNG%(2%,'') THEN &
		PRINT #CH%, FNP$(22%,1%);'No entries in BANKBL file' &
\		GOTO 32767 &

220	PRINT #CH%, FNP$(14%,6%);'Totaling - Please be patient'; &
		FNP$(24%,80%); &
\	GOTO 240 &

230	GOTO 1000 IF FNN%(2%) &

240	LSET CNULL$=FNL$+'' &
\	CA=INT(CVT$F(CAMT$)*100%+.51)/100. &
\	BA=INT(CVT$F(BAMT$)*100%+.51)/100. &
\	STAT%=4% &
\	STAT%=3% IF CA=0. AND BA=0. &
\	STAT%=2% IF CA=BA AND CA<>0. &
\	STAT%=1% IF CA<>0. AND BA=0. &
\	TOTC(STAT%)=TOTC(STAT%)+CA &
\	TOTB(STAT%)=TOTB(STAT%)+BA &
\	GOTO 230 &

1000	PRINT #CH%, FRAME$;FNP$(8%,6%);'Total Cancelled'; &
		FNP$(10%,6%);'Total Outstanding'; &
		FNP$(12%,6%);'Total Void'; &
		FNP$(14%,6%);'Total Error'; FNP$(6%,30%); &
		'Check Amount';FNP$(6%,50%);'Bank Amount' &
\	FOR STAT%=1% TO 4% &
\		PRINT #CH%, FNP$(2%*STAT%+6%,27%); &
\		PRINT #CH% USING ' ###,###,###.##     ###,###,###.##', &
			TOTC(STAT%),TOTB(STAT%) &
\	NEXT STAT% &

13999	GOTO 18999 &
		! Try real hard not to die horribly. &

14000	! &
	! STORE AND RETRIEVE DATE IN INTEGER (D9% <=> MM.DD.YY) &
	! &
	! THIS FUNCTION DOES NOT SORT DATE CORRECTLY. &
	! IT IS BETTER TO USE FND6% FOR SORTING &
	! FND9% IS USED ONLY FOR OLDER PROGRAM COMPATIBILITY &
	! &
	&

14010	DEF FND9%(D9$) &
	\ D9$="0"+D9$ IF INSTR(1%,D9$,"/")=2% &
	\ D9$=LEFT(D9$,3%)+"0"+RIGHT(D9$,4%) IF INSTR(4%,D9$,"/")=5% &
	\ FND9%=VAL(LEFT(D9$,2%))+VAL(MID(D9$,4%,2%))*16% &
		+ FND8%(VAL(RIGHT(D9$,7%)))*512% &
	\ FNEND &

14020	DEF FND9$(D9%)=RIGHT(NUM1$((D9% AND 15%)+100%),2%) &
		+ "/"+RIGHT(NUM1$((D9% AND 31%*16%)/16%+100%),2%) &
		+"/"+RIGHT(NUM1$(((SWAP%(D9%) AND 254%)/2%)+100%),2%) &

14030	! &
	! CONVERT FLOATING POINT TO INTEGER &
	! &

14040	DEF FND8%(D8)=D8 &

14050	! &
	! NEW DATE INTEGER (DD/MM/YY) FOR CORRECT SORTING &
	! &
	! THIS FUNCTION (FND6%) REPLACES OLD DATE FUNCTION (FND9%) &
	! MUST USE FND5% TO CONVERT DATA FROM FND9% TO FND6% &
	! &
	&

14060	DEF FND6%(D9$)=VAL(MID(D9$,4%,2%))+VAL(LEFT(D9$,2%))*32% &
		+ FND8%(VAL(RIGHT(D9$,7%)))*512% &
	&

14070	  DEF FND6$(D9%)=RIGHT(NUM1$((D9% AND 15%*32%)/32%+100%),2%) &
		+ "/"+RIGHT(NUM1$((D9% AND 31%)+100%),2%) &
		+ "/"+RIGHT(NUM1$(((SWAP%(D9%) AND 254%)/2%)+100%),2%) &

14080	! &
	!  CONVERT MM/DD/YY INTEGER TO DD/MM/YY INTEGER &
	! &
	! USE TO CONVERT FND9% TO FND6% DATA FOR CORRECT SORTING BY DATE &
	! &
	&

14090	  DEF FND5%(D5%)=FND6%(FND9$(D5%)) &
	&

14130	! &
	! CHECK FOR VALID DATE &
	! &

14140	  DEF FND7%(D7$) &
		\ ON ERROR GOTO 14150 &
		\ IF INSTR(1%,D7$,"/")=3% AND INSTR(4%,D7$,"/")=6% &
			AND INSTR(7%,D7$,"/")=0% AND LEN(D7$)=8% &
			THEN D7%=VAL(LEFT(D7$,2%)) &
		\ IF D7%>0% AND D7%<13% THEN D7%=VAL(MID(D7$,4%,2%)) &
		\ IF D7%>0% AND D7%<32% THEN D7%=VAL(RIGHT(D7$,7%)) &
		\ IF D7%>=0% THEN FND7%=0% &
		\ GOTO 14160 &

14150	FND7%=-1% &
	\ RESUME 14160 &

14160	  ON ERROR GOTO 0 &
	\ FNEND &
	&

14170	! &
	! FORMAT DATE TO MM/DD/YY , FILL WITH ZEROS &
	! &

14180	  DEF FND7$(D7$) &
		\ D7$=D7$+"/"+RIGHT(DATE$(0%),8%) IF LEN(D7$)<6% &
		\ D7$="0"+D7$ IF INSTR(1%,D7$,"/")=2% &
		\ D7$=LEFT(D7$,3%)+"0"+RIGHT(D7$,4%) IF INSTR(4%,D7$,"/")=5% &
	\ FND7$=D7$ &
	\ FNEND &
	&

14190	! &
	!  PUT DATE$(0%) INTO MM/DD/YY FORMAT &
	! &

14200	! ****  NOTE THIS FUNCTION MAY HAVE A PROBLEM! **** ! &
	  DEF FND0$(I%)=NUM1$((INSTR(1%, &
		"JAN FEB MAR APR MAY JUN JUL AUG SEP OCT NOV DEC", &
		CVT$$(MID(DATE$(0%),4%,3%),32%))+3%)/4%) &
		+ "/" + LEFT(DATE$(0%),2%)+"/"+RIGHT(DATE$(0%),8%) &
	&


18999	GOTO 32767 &
		! Prevent a nasty fall-through to whatever &

19000	! E R R O R   T R A P P I N G &
	&
	ON ERROR GOTO &
		! Pretty tricky, eh? &

30050	!------------------------------------------------------------ &
	! A N S I   D I R E C T   C U R S O R   A D D R E S S I N G &
	!------------------------------------------------------------ &
	DEF*FNP$(ROW%,COL%)=ESC$+"["+NUM1$(ROW%)+";"+NUM1$(COL%)+"H" &

32767	M%=FNX%('',0%,'') &
\	END &

