1	%TITLE "Legal Billing Accounts Receivable Journal"
	%SBTTL "AR_JOUR_LBSJ"
	%IDENT "V3.3"

	!
	! COPYRIGHT (C) 1987, 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! Abstract:HELP
	!	.b
	!	.lm +5
	!	The ^*Maintain Sales Journal\* program provides the means to maintain the
	!	Sales Journal File for the Accounts Receivable.
	!	.lm -5
	!
	! Index:
	!
	! Option:
	!
	!
	! Environment:
	!
	!	VAX/VMS BASIC+2 V4.4
	!
	! Compile:
	!
	!	$ BAS AR_SOURCE:AR_JOUR_LBSJ/LINE
	!	$ LINK/EXECUTABLE=AR_EXE: AR_JOUR_LBSJ,FUNC_LIB:CMCLINK/OPTION
	!	$ DELETE AR_JOUR_LBSJ.OBJ;*
	!
	! Author:
	!
	!	08/03/88 - Kevin Handy
	!
	! Modification history:
	!
	!	03/11/88 - Kevin Handy
	!		Modified to get customer/client/patient from
	!		control file.
	!
	!	05/14/88 - Lance Williams
	!		Modified the header.
	!
	!	02/09/89 - Kevin Handy
	!		Modified for changes in ENTR_ENTER.
	!
	!	03/22/93 - Kevin Handy
	!		Clean up (Check)
	!--
	%PAGE

	!
	! Set up compiling options
	!
	OPTION SIZE	= (REAL GFLOAT, INTEGER LONG)

	!
	! Include files
	!
	%INCLUDE "FUNC_INCLUDE:SCOPE.COM"
	%INCLUDE "FUNC_INCLUDE:AR_WINDOW.INC"

	!
	! Maps
	!
	%INCLUDE %FROM %CDD "CDD$TOP.AR.AR_CONTROL"
	MAP	(AR_CONTROL)	AR_CONTROL_CDD	AR_CONTROL

	!
	! Common Statements
	!
	COM (CH_AR_CONTROL) &
		AR_CONTROL.CH%

	COM (CH_AR_SJH) &
		AR_SJH.CH%

	COM (CH_AR_SJL) &
		AR_SJL.CH%

	COM (TT_AR_SJ) &
		BATCH_NO$ = 2%

	!
	! External functions
	!
	EXTERNAL LONG    FUNCTION MAIN_WINDOW
	EXTERNAL INTEGER FUNCTION ENTR_3CHOICE
	EXTERNAL LONG    FUNCTION ENTR_3ENTER

	!
	! Dimension statements
	!
	DIM AR_SJH_FILE$(100%)

	%PAGE

	ON ERROR GOTO 19000

	!*******************************************************************
	! Initilize maintainence
	!*******************************************************************

	CALL READ_INITIALIZE

200	%INCLUDE "SOURCE:[AR.OPEN]AR_CONTROL.OPN"

	GET #AR_CONTROL.CH%, RECORD 1%, REGARDLESS

300	!******************************************************************
	! Get batch number
	!******************************************************************

	!
	! Look up device
	!
	CALL  READ_DEVICE("AR_SJH", AR_SJH.DEV$, STAT%)

	!
	! Find out what is already there
	!
	CALL FIND_FILE(AR_SJH.DEV$ + "AR_SJH_*.JRL", AR_SJH_FILE$(), &
		'16'L,"","")

	SELECT SCOPE.EXIT%
		CASE 3%, SMG$K_TRM_F10, SMG$K_TRM_CTRLZ, SMG$K_TRM_F8
			GOTO ExitProgram
	END SELECT

	!
	! If any files are in the list, then do a query screen
	!
	AR_SJH_FILE% = VAL(AR_SJH_FILE$(0%))

	IF AR_SJH_FILE%
	THEN
		!
		! Get ONLY the batch number of the file
		!
		FOR LOOP% = 1% TO AR_SJH_FILE%
			TEMP$ = RIGHT(AR_SJH_FILE$(LOOP%), 8%)
			I% = INSTR(1%, TEMP$, ".")
			I% = LEN(TEMP$) + 1% IF I% = 0%
			AR_SJH_FILE$(LOOP%) = LEFT(TEMP$, I% - 1%)
		NEXT LOOP%

		!
		! Query the user
		!
		X% = ENTR_3CHOICE(SCOPE, "", "", AR_SJH_FILE$(), "", &
			'0'L, "AR Sales Journal Files", "", "" )

		IF X% > 0%
		THEN
			BATCH_NO$ = EDIT$(AR_SJH_FILE$(X%),-1%)
			GOTO 390
		END IF
	END IF

	SELECT SCOPE.EXIT%
		!
		! Exit key ?
		!
		CASE 3%, SMG$K_TRM_F10, SMG$K_TRM_CTRLZ
			GOTO ExitProgram
	END SELECT

	!
	! Ask user for period
	!
	SMG_STATUS% = SMG$CREATE_VIRTUAL_DISPLAY &
	( &
		'18'L, &
		'80'L, &
		SMG_SCREEN_DATA% &
	)

310	!
	! Print background
	!
	SMG_STATUS% = SMG$PUT_CHARS(SMG_SCREEN_DATA%, &
		"AR Sales Journal Maintenance", '10'L, '24'L)
	SMG_STATUS% = SMG$PUT_CHARS(SMG_SCREEN_DATA%, &
		"Batch:   " + BATCH_NO$, &
		'12'L, '31'L)

	SMG_STATUS% = SMG$PASTE_VIRTUAL_DISPLAY &
	( &
		SMG_SCREEN_DATA%, &
		SMG_PBID, &
		'1'L, &
		'1'L &
	)

320	!
	! Query user for batch number
	!
	BATCH_NO$ = ""
	PRG_ITEM = "BATCH"

	SELECT ENTR_3ENTER(SCOPE, SMG_SCREEN_DATA%, 12%, 39%, BATCH_NO$, -1%, 16%)

		CASE 3%, SMG$K_TRM_F10, SMG$K_TRM_CTRLZ, SMG$K_TRM_F8
			GOTO ExitProgram

		CASE SMG$K_TRM_UP
			GOTO 320

		CASE SMG$K_TRM_DOWN
			GOTO 320

		CASE 0%, 10%, 12%, 13%, SMG$K_TRM_DO
			! Good key

		CASE ELSE
			CALL ENTR_3BADKEY(SCOPE,SCOPE::SCOPE_EXIT)
			GOTO 320
	END SELECT

	IF LEN(TRM$(BATCH_NO$)) <> 2%
	THEN
		CALL ENTR_3MESSAGE(SCOPE, "Please enter the BATCH number in XX format", 0%)
		GOTO 320
	END IF

390	SMG_STATUS% = SMG$DELETE_VIRTUAL_DISPLAY(SMG_SCREEN_DATA%)


	!*******************************************************************
	! Handle main program
	!*******************************************************************

	V% = MAIN_WINDOW(AR_MAIN_LBSJ.ID, "")

	!******************************************************************
	! End of the program
	!******************************************************************

 ExitProgram:
	CALL SUBR_3EXITPROGRAM(SCOPE, "", "")

19000	!*******************************************************************
	! Trap errors
	!*******************************************************************

	FILENAME$ = ""
	SELECT ERL

		CASE 200%
			FILENAME$ = "AR_CONTROL"
			RESUME HelpFile
	END SELECT

	RESUME HelpError

 HelpFile:
	CALL HELP_34MESSAGE(SCOPE, NUM1$(ERL) + " " + ERT$(ERR), &
			"E", ERN$, FILENAME$, NUM1$(ERR))
	RESUME ExitProgram

 HelpError:
	CALL HELP_34MESSAGE(SCOPE, NUM1$(ERL) + " " + ERT$(ERR), &
			"E", ERN$, FILENAME$, NUM1$(ERR))
	RESUME ExitProgram

19990	END



20000	FUNCTION LONG MAINT_GROUP(CDD_WINDOW_CDD SMG_WINDOW, &
		LONG MOPTION, LONG MLOOP, LONG MFLAG, STRING MVALUE)

	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	%INCLUDE %FROM %CDD "CDD$TOP.CDD.CDD_WINDOW"
	%INCLUDE "FUNC_INCLUDE:AR_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:GL_WINDOW.INC"

	EXTERNAL LONG FUNCTION AR_MAIN_35CUSTOM
	EXTERNAL LONG FUNCTION AR_MAIN_LBSJ
	EXTERNAL LONG FUNCTION AR_MAIN_LBSJ_LINE
	EXTERNAL LONG FUNCTION GL_MAIN_CHART

	%PAGE

	!
	! Process the proper window
	!
	SELECT SMG_WINDOW::IDENT

		CASE GL_MAIN_CHART.ID
			MAINT_GROUP = GL_MAIN_CHART(SMG_WINDOW, &
				MOPTION, MLOOP, MFLAG, MVALUE)

		CASE AR_MAIN_35CUSTOM.ID
			MAINT_GROUP = AR_MAIN_35CUSTOM(SMG_WINDOW, &
				MOPTION, MLOOP, MFLAG, MVALUE)

		CASE AR_MAIN_LBSJ.ID
			MAINT_GROUP = AR_MAIN_LBSJ(SMG_WINDOW, &
				MOPTION, MLOOP, MFLAG, MVALUE)

		CASE AR_MAIN_LBSJ_LINE.ID
			MAINT_GROUP = AR_MAIN_LBSJ_LINE(SMG_WINDOW, &
				MOPTION, MLOOP, MFLAG, MVALUE)

	END SELECT

32767	END FUNCTION
