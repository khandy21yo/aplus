1	%TITLE "AR Ledger Maintenance"
	%SBTTL "AR_MAIN_LBCLOSEMAINT"
	%IDENT "V3.3"

	FUNCTION LONG AR_MAIN_LBCLOSEMAINT(CDD_WINDOW_CDD SMG_WINDOW, &
		LONG MOPTION, LONG MLOOP, LONG MFLAG, STRING MVALUE)

	!
	! COPYRIGHT (C) 1987, 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! Abstract:HELP
	!	.p
	!	This program maintains the ledger file.
	!
	! Index:
	! Option:
	!
	!
	! Environment:
	!
	!	VAX/VMS BASIC+2 V4.4
	!
	! Compile:
	!
	!	$ BAS AR_SOURCE:AR_MAIN_LBCLOSEMAINT/LINE
	!	$ LIB FUNC_LIB:CMCFUN/REP AR_MAIN_LBCLOSEMAINT
	!	$ DELETE AR_MAIN_LBCLOSEMAINT.OBJ;*
	!
	!
	! Author:
	!
	!	02/23/88 - Aaron Redd
	!
	! Modification history:
	!
	!	05/16/88 - Aaron Redd
	!		Modified to allow R/O open of file if R/W open fails.
	!
	!	04/15/92 - Dan Perkins
	!		Use FUNC_TESTENTRY to test input.
	!
	!	04/21/92 - Kevin Handy
	!		Clean up (check)
	!
	!	02/22/93 - Dan Perkins
	!		Changed "V0" to "VX" on chart of accounts to be
	!		able to list accounts starting at a particular
	!		account.
	!--
	%PAGE

	!
	! Set up compiling options
	!
	OPTION SIZE	= (REAL GFLOAT, INTEGER LONG)

	%INCLUDE "FUNC_INCLUDE:SCOPE.COM"

	%INCLUDE "FUNC_INCLUDE:MAIN_WINDOW.COM"
	%INCLUDE "FUNC_INCLUDE:AR_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:GL_WINDOW.INC"

	%INCLUDE %FROM %CDD "CDD$TOP.CDD.CDD_WINDOW"

	!
	! Map's
	!
	%INCLUDE %FROM %CDD "CDD$TOP.AR.AR_CONTROL"
	MAP (AR_CONTROL)	AR_CONTROL_CDD		AR_CONTROL

	%INCLUDE %FROM %CDD "CDD$TOP.AR.AR_CLOSED"
	MAP (AR_CLOSED)	AR_CLOSED_CDD	AR_CLOSED
	MAP (AR_CLOSED_OLD)	AR_CLOSED_CDD		AR_CLOSED_OLD, &
							AR_CLOSED2

	%INCLUDE %FROM %CDD "CDD$TOP.GL.GL_CHART"
	MAP (GL_CHART)		GL_CHART_CDD		GL_CHART

	%INCLUDE %FROM %CDD "CDD$TOP.AR.AR_35CUSTOM"
	MAP (AR_35CUSTOM)	AR_35CUSTOM_CDD		AR_35CUSTOM

	COM (TT_AR_LBCLOSEMAINT) &
		ARTITLE$ = 20%, &
		ARTYPE$(7%) = 20%

	!
	! Common Areas
	!
	! These areas store information that is re-used between
	! calls to these functions.
	!
	COM (CH_AR_CLOSED)		&
		AR_CLOSED.CH%,		&
		AR_CLOSED.READONLY%

	!
	! External functions
	!
	EXTERNAL STRING FUNCTION PRNT_DATE
	EXTERNAL STRING  FUNCTION ENTR_3STRING
	EXTERNAL STRING  FUNCTION ENTR_3STRINGLIST
	EXTERNAL STRING  FUNCTION ENTR_3DATE
	EXTERNAL REAL    FUNCTION ENTR_3NUMBER
	EXTERNAL LONG    FUNCTION MAIN_WINDOW
	EXTERNAL LONG    FUNCTION FUNC_TESTENTRY

	%PAGE

	ON ERROR GOTO 29000

	SELECT MOPTION

	!
	! Initialization
	!
	! This option is used to initialize the window structure,
	! set up the default values for add, and open all files
	! necessary that have not already been opened.
	!
	CASE OPT_INIT

		!
		! Define window
		!
		SMG_WINDOW::DESCR = "AR Close Ledger Maintenance"
		SMG_WINDOW::NHELP = "AR_MAIN_LBCLOSEMAINT"
		SMG_WINDOW::HSIZE = 78%
		SMG_WINDOW::VSIZE = 18%
		SMG_WINDOW::HVIEW = 130%
		SMG_WINDOW::VVIEW = 18%
		SMG_WINDOW::HPOS  = 2%
		SMG_WINDOW::VPOS  = 2%
		SMG_WINDOW::NITEMS= 12%
		SMG_WINDOW::FLAGS = 0%

		SMG_WINDOW::NKEYS = 1%
		SMG_WINDOW::KNAME(0%) = TRM$(AR_CONTROL::CTITLE)
			SMG_WINDOW::KFIELD(0%, 0%) = 2%
			SMG_WINDOW::KFIELD(0%, 1%) = 1%
			SMG_WINDOW::KFIELD(0%, 2%) = 2%

		!
		! AR type
		!
		ARTITLE$	= "Type   Description"
		ARTYPE$(0%)	= "7"
		ARTYPE$(1%)	= "01   Receivable"
		ARTYPE$(2%)	= "02   Cash Sale"
		ARTYPE$(3%)	= "03   Debit Memo"
		ARTYPE$(4%)	= "04   Service Charge"
		ARTYPE$(5%)	= "08   Credit Memo"
		ARTYPE$(6%)	= "09   ROA"
		ARTYPE$(7%)	= "10   Cash Receipt"

		!
		! Load in defaults
		!
		AR_CLOSED::DISAMT = 0.0
		AR_CLOSED::OTHCHG = 0.0
		AR_CLOSED::SALNUM = ""
		AR_CLOSED::BATCH = ""

		CALL READ_DEFAULTS(SMG_WINDOW) &
			IF INSTR(1%, " QV", MVALUE) <= 1%

700		!
		! Declare channels
		!
		IF AR_CLOSED.CH% > 0%
		THEN
			!
			! Already open, set flag to read-only if was
			! that way from last time.
			!
			SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%) &
				IF AR_CLOSED.READONLY%
			GOTO 790
		END IF

		!
		! Open main file (existing) for modification
		!
750		%INCLUDE "SOURCE:[AR.OPEN]AR_CLOSED.CRE"
		AR_CLOSED.READONLY% = 0%
		GOTO 790

760		!
		! If unable to open for modify, try to open with read
		! access only.
		!
		%INCLUDE "SOURCE:[AR.OPEN]AR_CLOSED.OPN"
		SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%)
		AR_CLOSED.READONLY% = -1%

		GOTO 790

770		!
		! File not open, so reset channel
		!
		CALL ASSG_FREECHANNEL(AR_CLOSED.CH%)

		EXIT FUNCTION

790		SMG_WINDOW::CHAN  = AR_CLOSED.CH%
		RESET #AR_CLOSED.CH%
		GET #AR_CLOSED.CH%, REGARDLESS

	!
	! Display the background
	!
	! This option is used to display the background information on the
	! screen.  It must first clear any junk on the screen, and then
	! write the background onto it.
	!
20100	CASE OPT_BACKGROUND

		SMG_STATUS% = SMG$BEGIN_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

		SMG_STATUS% = SMG$ERASE_DISPLAY(SMG_WINDOW::WNUMBER)

		DATA	3,  3, "(01)", &
			4,  3, "(02) Invoice", &
			5,  3, "(03) Type", &
			6,  3, "(04) Date", &
			7,  3, "(05) Amount", &
			8,  3, "(06) Receipt", &
			9,  3, "(07) Check No", &
			10,  3, "(08) Account", &
			11,  3, "(09) Matter #", &
			12,  3, "(10) Description", &
			13,  3, "(11) Update", &
			14,  3, "(12) Close", &
			0,  0, ""

		RESTORE

		READ XPOS%, YPOS%, XSTR$
		I%=0%
		WHILE (XPOS% <> 0)
			I% = I% + 1%
			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				XSTR$, XPOS%, YPOS%) IF (SMG_WINDOW::HFLAG(I%) AND 2%)=0%
		READ XPOS%, YPOS%, XSTR$
		NEXT

		SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
			LEFT(AR_CONTROL::CTITLE, 14%), 3%, 8%)

		SMG_STATUS% = SMG$END_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

	!
	! Enter/Display/Default
	!
	! This option is used to enter the data from the user, display data,
	! set defaults, and return the data back according to MFLAG.
	!
20200	CASE OPT_ENTRY

		TEMP$, TEMP1$ = TRM$(PRG_ITEM)
		TEMP$ = "View starting at" IF TEMP$ = "View"

		PRG_ITEM = "FLD" + FORMAT$(MLOOP, "<0>##")

		SCOPE.EXIT% = 0%

 E0loop:	SELECT MLOOP

		CASE 1%

	!++
	! Abstract:FLD001
	!	^*Customer _#\*
	!	.b
	!	.lm +5
	!	The ^*Customer _#\* refers to the number assigned to a customer
	!	as recorded in the Customer Master File.
	!	.b
	!	^*Note:\*  It is not recommended that Customer Numbers be changed, nor
	!	that records be added or deleted in this option. Nevertheless,
	!	the ability to accomplish these kinds of edit functions is
	!	provided, but should be used in very unusual circumstances only.
	!	.lm -5
	!
	! Index:
	!
	!--

			AR_CLOSED::CUSNUM = ENTR_3STRING(SCOPE, SMG_WINDOW::WNUMBER, &
				"3;21", TEMP$, &
				AR_CLOSED::CUSNUM, MFLAG, "'E", MVALUE)

				IF (SCOPE.EXIT% = SMG$K_TRM_F14)
				THEN
					IF (MAIN_WINDOW(AR_MAIN_35CUSTOM.ID, "VX  ") = 1%)
					THEN
						AR_CLOSED::CUSNUM = &
							AR_35CUSTOM::CUSNUM
					END IF
					GOTO E0Loop
				END IF

		CASE 2%

	!++
	! Abstract:FLD002
	!	^*(02) Invoice _#\*
	!	.b
	!	.lm +5
	!	The ^*Invoice _#\* refers to a reference number as to whether the
	!	record represents an invoice or credit memo.
	!	.b
	!	^*Note:\* It is not recommended the Invoice Numbers be changed in this
	!	option. Nevertheless, the ability to accomplish this function
	!	is provided, but should be used in very unusual circumstances only.
	!	.lm -5
	!
	! Index:
	!
	!--

			AR_CLOSED::INVNUM = ENTR_3STRING(SCOPE, SMG_WINDOW::WNUMBER, &
				"4;21", TEMP$, &
				AR_CLOSED::INVNUM, MFLAG, "'E", MVALUE)

		CASE 3%

	!++
	! Abstract:FLD003
	!	^*(03) Type\*
	!	.b
	!	.lm +5
	!	The ^*Type\* field contains a code which indicates the type
	!	of transaction this screen represents.
	!	.b
	!	Valid types are:
	!	.table 3,25
	!	.te
	!	^*01\* = Receivable
	!	.te
	!	^*02\* = Cash Sale
	!	.te
	!	^*03\* = Debit Memo
	!	.te
	!	^*04\* = Service Charge
	!	.te
	!	^*08\* = Credit Memo
	!	.te
	!	^*09\* = ROA
	!	.te
	!	^*10\* = Cash Receipt
	!	.end table
	!	^*Note:\* It is not recommended that the type field be changed in this
	!	screen, even though the ability to accomplish this edit function
	!	is provided.
	!	.b
	!	Valid Type codes may be viewed by pressing ^*List Choices\*.
	!	.lm -5
	!
	! Index:
	!
	!--

			AR_CLOSED::TRATYP = ENTR_3STRINGLIST(SCOPE, SMG_WINDOW::WNUMBER, &
				"5;21", TEMP$, &
				AR_CLOSED::TRATYP, MFLAG, "'E", MVALUE, &
				ARTYPE$(),ARTITLE$,"005")

		CASE 4%

	!++
	! Abstract:FLD004
	!	.ts 55
	!	^*(04) Date	MMDDYYYY or MMDDYY\*
	!	.b
	!	.lm +5
	!	The ^*Date\* field represents the date of a particular
	!	transaction.
	!	.lm -5
	!
	! Index:
	!
	!--

			AR_CLOSED::TRADAT = ENTR_3DATE(SCOPE, SMG_WINDOW::WNUMBER, &
				"6;21", TEMP$, &
				AR_CLOSED::TRADAT, MFLAG, "'E", MVALUE)

		CASE 5%

	!++
	! Abstract:FLD005
	!	^*(05) Amount\*
	!	.b
	!	.lm +5
	!	The ^*Amount\* field will display the gross amount of the
	!	transaction.
	!	.b
	!	^*Note:\* It is recommended that the amount field ^*not\* be changed
	!	in this screen, even though the ability to do so exists.
	!	.lm -5
	!
	! Index:
	!
	!--

			AR_CLOSED::SALAMT = ENTR_3NUMBER(SCOPE, SMG_WINDOW::WNUMBER, &
				"7;21", TEMP$, &
				AR_CLOSED::SALAMT, MFLAG, "######.##", MVALUE)

		CASE 6%

	!++
	! Abstract:FLD006
	!	^*(06) Receipt\*
	!	.b
	!	.lm +5
	!	The ^*Receipt\* field contains the receipt number when the
	!	transaction is a cash receipt type.
	!	.b
	!	^*Note:\* It is ^*not\* recommended that changes be made in this screen,
	!	even though the ability to do so exists.
	!	.lm -5
	!
	! Index:
	!
	!--

			AR_CLOSED::RECNUM = ENTR_3STRING(SCOPE, SMG_WINDOW::WNUMBER, &
				"8;21", TEMP$, &
				AR_CLOSED::RECNUM, MFLAG, "'E", MVALUE)

		CASE 7%

	!++
	! Abstract:FLD007
	!	^*(07) Check No.\*
	!	.b
	!	.lm +5
	!	The ^*Check Number\* field contains the customers check number
	!	when the type of transaction is a cash receipt.
	!	.lm -5
	!
	! Index:
	!
	!--

			AR_CLOSED::CHKNUM = ENTR_3STRING(SCOPE, SMG_WINDOW::WNUMBER, &
				"9;21", TEMP$, &
				AR_CLOSED::CHKNUM, MFLAG, "'E", MVALUE)

		CASE 8%

	!++
	! Abstract:FLD008
	!	^*(08) Account
	!	.b
	!	.lm +5
	!	The ^*Account\* number field contains the General Ledger Cash
	!	account number whenever the transaction is a cash sale, or the
	!	Accounts Receivable General Ledger number if the transaction
	!	relates to an on account transaction.
	!	.b
	!	^*Note:\* It is ^*not\* recommended that changes be made in this
	!	screen, even though the ability to do so exists.
	!	.b
	!	Valid account numbers may be viewed by pressing ^*List Choices\*.
	!	.lm -5
	!
	! Index:
	!
	!--

			AR_CLOSED::ARACCT = ENTR_3STRING(SCOPE, SMG_WINDOW::WNUMBER, &
				"10;21", TEMP$, &
				AR_CLOSED::ARACCT, MFLAG, "'E", MVALUE)

			IF (SCOPE.EXIT% = SMG$K_TRM_F14)
			THEN
				IF (MAIN_WINDOW(GL_MAIN_CHART.ID, "VX  ") = 1%)
				THEN
					AR_CLOSED::ARACCT = &
						GL_CHART::ACCT
				END IF
				GOTO E0Loop
			END IF

		CASE 9%

	!++
	! Abstract:FLD009
	!
	!	^*(09) Matter No.\*
	!	.p
	!
	! Index:
	!--

			AR_CLOSED::SUBACC = ENTR_3STRING(SCOPE, SMG_WINDOW::WNUMBER, &
				"11;21", TEMP$, &
				AR_CLOSED::SUBACC, MFLAG, "~L0'E", MVALUE)

		CASE 10%

	!++
	! Abstract:FLD010
	!	^*(10) Description\*
	!	.b
	!	.lm +5
	!	The ^*Description\* field contains the description for a
	!	transaction if one were entered in the Receipts Journal.
	!	.b
	!	^*Note:\* It is ^*not\* recommended that changes be made in this
	!	screen, even though the ability to do so exists.
	!	.lm -5
	!
	! Index:
	!
	!--

			AR_CLOSED::DESCR = ENTR_3STRING(SCOPE, SMG_WINDOW::WNUMBER, &
				"12;21", TEMP$, &
				AR_CLOSED::DESCR, MFLAG, "'E", MVALUE)

		CASE 11%

	!++
	! Abstract:FLD011
	!	^*(11) Update\*
	!	.b
	!	.lm +5
	!	The ^*Update\* field contains the year and month in which
	!	a transaction is updated.
	!	.b
	!	^*Note:\* It is ^*not\* recommended that changes be made in this screen
	!	in the update field, even though the ability to do so exists.
	!	.lm -5
	!
	! Index:
	!
	!--

			AR_CLOSED::UPDATED = ENTR_3STRING(SCOPE, SMG_WINDOW::WNUMBER, &
				"13;21", TEMP$, &
				AR_CLOSED::UPDATED, MFLAG, "'E", MVALUE)

		CASE 12%

	!++
	! Abstract:FLD012
	!	^*(12) Close\*
	!	.b
	!	.lm +5
	!	The ^*Close\* field contains the year and the month a
	!	particular record was closed.
	!	.b
	!	^*Note:\* It is recommended that changes ^*not\* be made in this
	!	field even though the ability to do so exists.
	!	.lm -5
	!
	! Index:
	!
	!--

			AR_CLOSED::CLOSEDATE = ENTR_3STRING(SCOPE, SMG_WINDOW::WNUMBER, &
				"14;21", TEMP$, &
				AR_CLOSED::CLOSEDATE, MFLAG, "'E", MVALUE)

		END SELECT

		PRG_ITEM = TEMP1$

20300	CASE OPT_TESTENTRY

		AR_MAIN_LBCLOSEMAINT = 0%

		SELECT MLOOP

		CASE 1%
			AR_MAIN_LBCLOSEMAINT = FUNC_TESTENTRY( SMG_WINDOW, &
				AR_CLOSED::CUSNUM, AR_35CUSTOM::CUSNAM, &
				"AR", MLOOP, "CUST", &
				"Customer number", AR_MAIN_35CUSTOM.ID)

			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				LEFT(AR_35CUSTOM::CUSNAM, 30%), &
				3%, 45%, , SMG$M_BOLD)

		CASE 8%
	!++
	! Abstract:FLD008
	!	^*(08) Account
	!	.b
	!	.lm +5
	!	The ^*Account\* number field contains the General Ledger Cash
	!	account number whenever the transaction is a cash sale, or the
	!	Accounts Receivable General Ledger number if the transaction
	!	relates to an on account transaction.
	!	.b
	!	^*Note:\* It is ^*not\* recommended that changes be made in this
	!	screen, even though the ability to do so exists.
	!	.b
	!	Valid account numbers may be viewed by pressing ^*List Choices\*.
	!	.lm -5
	!
	! Index:
	!
	!--
			AR_MAIN_LBCLOSEMAINT = FUNC_TESTENTRY( SMG_WINDOW, &
				AR_CLOSED::ARACCT, GL_CHART::DESCR, &
				"AR", MLOOP, "ACCT", &
				"Account number", GL_MAIN_CHART.ID)

			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				LEFT(GL_CHART::DESCR, 30%), &
				10%, 45%, , SMG$M_BOLD)

		END SELECT

	CASE OPT_DISPLAY
		IF MAIN_WINDOW(AR_MAIN_35CUSTOM.ID, 'Q0' + AR_CLOSED::CUSNUM) <> 1%
		THEN
			AR_35CUSTOM::CUSNAM = "????????????????????"
		END IF

		IF (SMG_WINDOW::HFLAG(1%) AND 2%)=0%
		THEN
			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				LEFT(AR_35CUSTOM::CUSNAM, 30%), &
				3%, 45%, , SMG$M_BOLD)
		END IF

		IF MAIN_WINDOW(GL_MAIN_CHART.ID, 'Q0' + AR_CLOSED::ARACCT) <> 1%
		THEN
			GL_CHART::DESCR = "????????????????????"
		END IF

		IF (SMG_WINDOW::HFLAG(8%) AND 2%) = 0%
		THEN
			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				LEFT(GL_CHART::DESCR,30%), &
				10%, 45%, , SMG$M_BOLD)
		END IF

20500	CASE OPT_SETOLD
		AR_CLOSED_OLD = AR_CLOSED

	CASE OPT_RESETOLD
		AR_CLOSED = AR_CLOSED_OLD

	CASE OPT_SETDEFAULT
		AR_CLOSED2 = AR_CLOSED

	CASE OPT_RESETDEFAULT
		AR_CLOSED = AR_CLOSED2

	CASE OPT_VIEW
		SELECT MLOOP

			CASE 1%

				MVALUE = "  Cust #     Inv Num    Date      " + &
					" Amount Sales Tx Description"

			CASE 2%

				MVALUE = "013,022,033,042,051"

			CASE 3%

				MVALUE = AR_CLOSED::CUSNUM + " " + &
					AR_CLOSED::INVNUM + " " + &
					PRNT_DATE(AR_CLOSED::TRADAT, 8%) + " " + &
					FORMAT$(AR_CLOSED::SALAMT, "#####.##") + " " + &
					FORMAT$(AR_CLOSED::OTHCHG, "#####.##") + " " + &
					AR_CLOSED::DESCR

		END SELECT
	CASE OPT_FIND
		SELECT MLOOP

		CASE 0%
			FIND #AR_CLOSED.CH%, KEY #0% GE AR_CLOSED::CUSNUM + &
				AR_CLOSED::INVNUM, &
				REGARDLESS

		END SELECT

	END SELECT

	EXIT FUNCTION

29000	!***************************************************************
	! Trap Errors
	!***************************************************************
	SELECT ERL
		CASE 750%
			RESUME 760 IF ERR = 10%
			AR_MAIN_LBCLOSEMAINT = ERR
			RESUME 770

		CASE 760%
			AR_MAIN_LBCLOSEMAINT = ERR
			RESUME 770

		CASE 790%
			RESUME 32767

		CASE 20300%
			RESUME 32767 IF ERR = 155%

	END SELECT

	ON ERROR GO BACK

32767	END FUNCTION
