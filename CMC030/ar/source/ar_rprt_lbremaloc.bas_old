1	%TITLE "AR LB Remaining Allocations Report"
	%SBTTL "AR_RPRT_LBREMALOC"
	%IDENT "V3.3"

	!
	! COPYRIGHT (C) 1991 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! ID:AR036
	!
	! Abstract:HELP
	!	.b
	!	.lm +5
	!	The ^*Remaining Allocations\* Report prints those billed amounts that
	!	remain to be allocated to costs, service charges and fees.
	!	The report prints in Client/matter sequence with sub-totals on
	!	matter and client and an overall total for the report.
	!	.lm -5
	!
	! Index:
	!	.x Reports>LB Remaining Allocations
	!	.x LB Remaining Allocations>Reports
	!
	! Option:
	!
	!
	! Input:
	!
	!
	! Output:
	!
	!
	! Example:
	!
	!
	! Environment:
	!
	!	VAX/VMS BASIC V4.4
	!
	! Author:
	!
	!	05/10/91 - Craig P. Tanner
	!
	! Compile:
	!
	!	$ BAS AR_SOURCE:AR_RPRT_LBREMALOC
	!	$ LINK/EXE=AR_EXE:*.EXE AR_RPRT_LBREMALOC, -
	!		FUNC_LIB:CMCLINK/OPTION
	!	$ DELETE AR_RPRT_LBREMALOC.OBJ;*
	!
	! Modification history:
	!
	!	06/14/93 - Kevin Handy
	!		Added REGARDLESS to AR_OPEN_DIST.
	!--
	%PAGE

	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	!
	! Include files
	!
	%INCLUDE "FUNC_INCLUDE:SCOPE.COM"

	%INCLUDE %FROM %CDD "CDD$TOP.UTL.UTL_REPORTX"
	DECLARE UTL_REPORTX_CDD UTL_REPORTX

	%INCLUDE %FROM %CDD "CDD$TOP.AR.AR_OPEN_DIST"
	MAP	(AR_OPEN_DIST)	AR_OPEN_DIST_CDD	AR_OPEN_DIST

	%INCLUDE %FROM %CDD "CDD$TOP.AR.AR_35CUSTOM"
	MAP	(AR_35CUSTOM)	AR_35CUSTOM_CDD	AR_35CUSTOM

	MAP	(AR_TEMP)	AR_TEMP.CLIENT$		= 10%,	&
				AR_TEMP.DATE$		= 8%, &
				AR_TEMP.MATTER$		= 10%, &
				AR_TEMP.LTYPE$		= 1%, &
				AR_TEMP.TYPE$		= 1%, &
				AR_TEMP.AMOUNT

	!
	! External functions
	!
	EXTERNAL STRING  FUNCTION PRNT_DATE

	%PAGE

	ON ERROR GOTO 19000


	!

	CALL ASSG_CHANNEL(AR_TEMP.CH%,STAT%)

 Init:	!
	! Initilize report
	!
	CALL OUTP_INITFROMFILE(UTL_REPORTX, 132%)

	GOTO ExitProgram IF UTL_REPORTX::STAT

	PERIOD$		= EDIT$(UTL_REPORTX::OPTDEF(0%), 132%)

	!++
	! Abstract:FLD01
	!	^*(01) Period\*
	!	.b
	!	.lm +5
	!	The ^*Period\* field in the Non-Allocated Billed Fees/Costs in
	!	the Report Setting Screens provides the means to enter an
	!	accounting period for which the report will print.
	!	.b
	!	The format for entry is YYYYPP.
	!	.b
	!	An entry is required in this field.
	!	.lm -5
	!
	! Index:
	!	.x Period>Report>Detail Payments
	!	.x Report>Period>Detail Payments
	!
	!--

	CALL  READ_DEVICE("UTL_WORK", UTL_WORK.DEV$, STAT%)

300	!
	%INCLUDE "SOURCE:[AR.OPEN]AR_OPEN_DIST.OPN"

330	!
	! Open file
	!
	%INCLUDE "SOURCE:[AR.OPEN]AR_35CUSTOM.OPN"

350	CALL ENTR_3MESSAGE(SCOPE, "Creating temporary file.  Reading work files.", 1%)

	OPEN UTL_WORK.DEV$ + "AR_TEMP.TMP" FOR OUTPUT AS FILE #AR_TEMP.CH%, &
		ORGANIZATION INDEXED FIXED, &
		MAP AR_TEMP, &
		PRIMARY KEY (AR_TEMP.CLIENT$, &
			AR_TEMP.MATTER$, &
			AR_TEMP.TYPE$, &
			AR_TEMP.LTYPE$) &
			DUPLICATES, &
		TEMPORARY, &
		BUFFER 32%, &
		ACCESS MODIFY, ALLOW NONE

400	!*******************************************************************
	! Start putting stuff into AR_TEMP file
	!*******************************************************************

	FIND #AR_OPEN_DIST.CH%, KEY#1% GE PERIOD$, REGARDLESS
410
 GetFromHistory:
	GET #AR_OPEN_DIST.CH%, REGARDLESS

	GOTO ReportTitle IF AR_OPEN_DIST::UPDATED > PERIOD$

	GOTO GetFromHistory IF AR_OPEN_DIST::LTYPE <> "1" AND &
		AR_OPEN_DIST::LTYPE <> "2"

	!
	! Build Temporary File now
	!

	AR_OPEN_DIST::TAXTYP = "S" IF AR_OPEN_DIST::TAXTYP = "E"
	AR_TEMP.MATTER$		= AR_OPEN_DIST::SUBACCT
	AR_TEMP.AMOUNT		= AR_OPEN_DIST::AMOUNT
	AR_TEMP.CLIENT$		= AR_OPEN_DIST::CUSNUM
	AR_TEMP.DATE$		= AR_OPEN_DIST::POST_DATE
	AR_TEMP.TYPE$		= AR_OPEN_DIST::TAXTYP
	AR_TEMP.LTYPE$		= AR_OPEN_DIST::LTYPE

	GOTO GetFromHistory IF AR_TEMP.AMOUNT = 0.0

	PUT #AR_TEMP.CH%
	AR_TEMP.AMOUNT = 0.0

	GOTO GetFromHistory

 ReportTitle:
	!
	! Title
	!
	TITLE$(1%) =	"LEGAL BILLING REMAINING ALLOCATIONS REPORT"
	TITLE$(2%) =	"For Period " + PERIOD$
	TITLE$(3%) = ""

	!
	! Heading
	!		 1234567890123456789012345678901234567890
	TITLE$(4%) =    "Type Client#    ClientName              " + &
			"                           Matter#    Date           Amount"

	TITLE$(5%) =	"."

	%PAGE

17000	!******************************************************************
	! OUTPUT REPORT
	!******************************************************************

	RESET #AR_TEMP.CH%, KEY#0%

	LAST_MATTER$, LAST_CUST$ = ""

	LAST_TYPE$ = ""

 GetNextRec:
17020	GET #AR_TEMP.CH%, REGARDLESS



	GOSUB CliTotal1 IF LAST_CUST$ <> AR_TEMP.CLIENT$
	GOSUB MatTotal IF LAST_MATTER$ <> AR_TEMP.MATTER$
	GOSUB Print_Line IF LAST_TYPE$ <> AR_TEMP.TYPE$

	MATTER_TOTAL = MATTER_TOTAL + AR_TEMP.AMOUNT
	CLIENT_TOTAL = CLIENT_TOTAL + AR_TEMP.AMOUNT
	TYPE_TOTAL = TYPE_TOTAL + AR_TEMP.AMOUNT
	OVER_TOTAL = OVER_TOTAL + AR_TEMP.AMOUNT

	LAST_DATE$ = AR_TEMP.DATE$
	LAST_NAME$ = AR_35CUSTOM::CUSNAM

	GOTO GetNextRec

 ExitTotal:
	!
	! Handle end of report
	!
	!
	GOSUB CliTotal1

	!
	! Do grand total here
	!
	TEXT$ = SPACE$(73%) + "Over All Total: " + FORMAT$(-OVER_TOTAL, "###,###.##")
	CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, 0%)

	TEXT$ = SPACE$(89%)  + "=========="
	CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, 0%)
	CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), "", 0%)


 ExitProgram:
17510	CALL OUTP_FINISH(UTL_REPORTX)

	!
	! Exit to next program or menu
	!
	IF TRM$(UTL_REPORTX::NEXTRUN) = ""
	THEN
		CALL SUBR_3EXITPROGRAM(SCOPE, "", "")
	ELSE
		CALL SUBR_3EXITPROGRAM(SCOPE, "RUN " + UTL_REPORTX::NEXTRUN, "")
	END IF

	%PAGE



18000
 Print_line:

	GOTO 18010 IF TYPE_TOTAL = 0.0

	TEXT$ = LAST_TYPE$ + "    " + &
		LAST_CUST$ + " " + &
		LAST_NAME$ + " " + &
		LAST_MATTER$ + " " + &
		PRNT_DATE(LAST_DATE$, 8%) + " " + &
		FORMAT$(-TYPE_TOTAL, "###,###.##")
	CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, 0%)
18010
	TYPE_TOTAL = 0.0

	LAST_TYPE$ = AR_TEMP.TYPE$

	RETURN
 Client:
	AR_35CUSTOM::CUSNAM = STRING$(LEN(AR_35CUSTOM::CUSNAM), ASCII('?'))

	GET #AR_35CUSTOM.CH%, KEY#0% EQ AR_TEMP.CLIENT$, REGARDLESS

	RETURN

 CliTotal1:

	!
	!Sub-total Client here
	!

	GOSUB MatTotal

	GOTO 18100 IF CLIENT_TOTAL = 0.0

	TEXT$ = SPACE$(89%)  + "=========="
	CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, 0%)

	TEXT$ = SPACE$(75%) + "Client Total: " + FORMAT$(-CLIENT_TOTAL, "###,###.##")
	CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, 0%)

	TEXT$ = SPACE$(89%)  + "=========="
	CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, 0%)
	CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), "", 0%)

18100
	CLIENT_TOTAL = 0.0

	LAST_CUST$ = AR_TEMP.CLIENT$

	GOSUB Client

	RETURN

 MatTotal:
	!
	! Sub-total the matter here
	!
	GOSUB Print_Line

	GOTO 18200 IF MATTER_TOTAL = 0.0

	TEXT$ = SPACE$(89%)  + "----------"
	CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, 0%)

	TEXT$ = SPACE$(75%) + "Matter Total: " + FORMAT$(-MATTER_TOTAL, "###,###.##")
	CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, 0%)
	CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), "", 0%)

18200	MATTER_TOTAL = 0.0

	LAST_MATTER$ = AR_TEMP.MATTER$

	RETURN

 HelpError:
	!******************************************************************
	! Help Message for an error
	!******************************************************************
	CALL HELP_34MESSAGE(SCOPE, NUM1$(ERL) + " " + ERT$(ERR), &
			"E", ERN$, FILENAME$, NUM1$(ERR))
	UTL_REPORTX::STAT = -1%
	GOTO ExitProgram

	%Page

19000	!******************************************************************
	! ERROR TRAPPING
	!******************************************************************


	SELECT ERR
		CASE 154%	! Locked record
			SLEEP 1%
			RESUME
	END SELECT

	FILENAME$ = ""
	SELECT ERL
		CASE 300%
			FILENAME$ = "AR_OPEN_DIST"

		CASE 330%
			RESUME 350 IF ERR =  5%
			FILENAME$ = "AR_35CUSTOM"

		CASE 350%
			FILENAME$ = "AR_TEMP"

		CASE 400%
			RESUME ExitProgram IF ERR = 155%
			FILENAME$ = "AR_OPEN_DIST"

		CASE 410%  ! End of file
			RESUME ReportTitle IF ERR = 11%
			FILENAME$ = "AR_OPEN_DIST"

		CASE 17020%
			RESUME ExitTotal IF ERR = 11%
			FILENAME$ = "AR_TEMP"


		CASE 18000%	! Can't find record
			RESUME CliTotal1 IF ERR = 155% OR ERR = 9%
			FILENAME$ = "AR_35CUSTOM"

	END SELECT

	!
	! Untrapped error
	!
	RESUME HelpError

32767	END
