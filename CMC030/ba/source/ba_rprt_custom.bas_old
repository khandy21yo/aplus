1	%TITLE "Billing Agency Customer Master File"
	%SBTTL "BA_RPRT_CUSTOM"
	%IDENT "V3.6a Calico"

	!
	! COPYRIGHT (C) 1987 BY
	!
	! Computer Management Center
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! ID:BA50
	!
	! Abstract:HELP
	!	.p
	!	The ^*List Name/Address File\* provides the means to print the customer
	!	master file. The option of printing on a long or short form is available.
	!	If the long form option is chosen, the following fields are included:
	!	.b
	!	.lm 15
	!	.list 0,"*"
	!	.le
	!	Agency
	!	.le
	!	Name
	!	.le
	!	Address
	!	.le
	!	Type
	!	.le
	!	Method
	!	.le
	!	Monthly Statement Indicator
	!	.le
	!	Service Charge Indicator
	!	.le
	!	Alpha Sort Key
	!	.els
	!	.lm -10
	!	.p
	!	Included in the sort form are the following fields:
	!	.lm 15
	!	.b
	!	.list 0,"*"
	!	.le
	!	Agency
	!	.le
	!	Name
	!	.le
	!	Type
	!	.els
	!
	! Index:
	!
	! Option:
	!
	!
	! Input:
	!
	!
	! Output:
	!
	!
	! Example:
	!
	!
	! Author:
	!
	!	05/04/88 - Lance Williams
	!
	! Compile:
	!
	!	$ BAS BA_SOURCE:BA_RPRT_CUSTOM/LINE
	!	$ LINK/EXECUTABLE=BA_EXE:*.EXE BA_RPRT_CUSTOM, -
	!		FUNC_LIB:CMCLINK/OPTION
	!	$ DELETE BA_RPRT_CUSTOM.OBJ;*
	!
	! Modification history:
	!
	!	08/04/88 - Kevin Handy
	!		Added customer type (from BA_CUSTOMEX file) to
	!		the output.
	!
	!	06/22/90 - Aaron Redd
	!		Added line layout for the short form so that a report
	!		of that form could be sent to more destinations.
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	08/30/96 - Kevin Handy
	!		Reformat source code.
	!
	!	05/22/97 - Kevin Handy
	!		Use integer for #key
	!
	!	08/17/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--
	%PAGE

	!
	! Set up compiling options
	!
	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	!
	! Include files
	!
	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "SOURCE:[UTL.OPEN]UTL_REPORTX.HB"
	DECLARE UTL_REPORTX_CDD UTL_REPORTX

	%INCLUDE "SOURCE:[AR.OPEN]AR_35CUSTOM.HB"
	MAP	(AR_35CUSTOM)	AR_35CUSTOM_CDD	AR_35CUSTOM

	%INCLUDE "SOURCE:[BA.OPEN]BA_CUSTOMEX.HB"
	MAP	(BA_CUSTOMEX)	BA_CUSTOMEX_CDD	BA_CUSTOMEX

	!
	! Declare variables and constants
	!
	DECLARE	STRING	LYT_LINE

	%PAGE

	!
	! Set up compiling options
	!
	ON ERROR GOTO 19000

 Init:	!
	! Initilize report
	!
	CALL OUTP_INITFROMFILE(UTL_REPORTX, 80%)
	GOTO ExitProgram IF UTL_REPORTX::STAT

	FROM.ITEM$ = EDIT$(UTL_REPORTX::OPTDEF(0%), 132%)
	!++
	! Abstract:FLD01
	!	^*(01) From Item\*
	!	.p
	!	The ^*From Item\* entered in this field causes the printing of the report
	!	to begin with the particular item. The value must be in agreement with the
	!	value entered in field four (04).
	!	.p
	!	A blank field causes the report to start with the first item in the file.
	!
	! Index:
	!	.x From Item
	!	.x Item>From
	!
	!--

	TO.ITEM$ = EDIT$(UTL_REPORTX::OPTDEF(1%), 132%)
	!++
	! Abstract:FLD02
	!	^*(02) To Item\*
	!	.p
	!	The ^*To Item\* number entered in this field causes the report to end printing
	!	with a particular item. The value must be in agreement with the value entered
	!	in field four (04).
	!	.p
	!	A blank field causes the report to end with the last item in the file.
	!
	! Index:
	!	.x To Item
	!	.x Item>To
	!
	!--

	SORTBY$ = EDIT$(UTL_REPORTX::OPTDEF(2%), -1%)
	!++
	! Abstract:FLD03
	!	^*(03) Sort By\*
	!	.p
	!	The ^*Sort By\* code entered in this field causes the report to print in a
	!	particular order. The valid sort codes are:
	!	.b
	!	.lm 15
	!	.list 0,"*"
	!	.le
	!	N = Number
	!	.le
	!	T = Type
	!	.le
	!	C = Category
	!	.le
	!	A = Alphabetical
	!	.els
	!	.lm -10
	!	.p
	!	An entry is required in this field and only the above codes are valid.
	!
	! Index:
	!	.x Sort By
	!
	!--

	FORMTYPE$ = EDIT$(UTL_REPORTX::OPTDEF(3%), -1%)
	!++
	! Abstract:FLD04
	!	^*(04) Form Type (L,S)\*
	!	.p
	!	The ^*From\* field provides the means for the user to choose whether to print
	!	the report in long or short form. If the long form is chosen, the following
	!	fields are included:
	!	.b
	!	.lm 15
	!	.list 0,"*"
	!	.le
	!	Agency
	!	.le
	!	Name
	!	.le
	!	Address
	!	.le
	!	Type
	!	.le
	!	Method
	!	.le
	!	Monthly Statement Indicator
	!	.le
	!	Service Charge Indicator
	!	.le
	!	Alpha Sort Key
	!	.els
	!	.lm -10
	!	.p
	!	Included in the sort form are the following fields:
	!	.lm 15
	!	.b
	!	.list 0,"*"
	!	.le
	!	Agency
	!	.le
	!	Name
	!	.le
	!	Type
	!	.els
	!
	! Index:
	!
	!--

	SELECT SORTBY$
	CASE "N"
		K_NUM% = 0%
	CASE "T"
		K_NUM% = 1%
	CASE "C"
		K_NUM% = 2%
	CASE "A"
		K_NUM% = 3%
	END SELECT

300	!
	%INCLUDE "SOURCE:[AR.OPEN]AR_35CUSTOM.OPN"

320	!
	! Open customer extra file
	!
	%INCLUDE "SOURCE:[BA.OPEN]BA_CUSTOMEX.OPN"

 ReportTitle:
330	!
	! Title
	!
	TITLE$(1%) = "Agency Master File List"
	TITLE$(2%) = ""

	!
	! Heading
	!
	IF FORMTYPE$ = "L"
	THEN
		TITLE1$ = "Name                                  " + &
			"              Agency        Type"
	ELSE
		TITLE$(3%) = "Agency     Name                    " + &
			"                           Type"
		TITLE$(4%) = ""
	END IF

	TITLE3$ = "M Statement S Charge Alpha Sort Key"

	!
	! Line layout for short print form
	!
	LYT_LINE = "$CustomerNum:010,$CustomerName:051,$WorkType:056"

	%PAGE

17000	!******************************************************************
	! OUTPUT REPORT
	!******************************************************************
	IF FROM.ITEM$ = ""
	THEN
		RESET #AR_35CUSTOM.CH%, KEY#K_NUM%
	ELSE
		FIND #AR_35CUSTOM.CH%, KEY#K_NUM% GE FROM.ITEM$, REGARDLESS
	END IF

 GetNextRec:
17020	!
	! Main loop starts here
	!
	GOTO ExitProgram IF UTL_REPORTX::STAT

	!
	! Get next record
	!
	GET #AR_35CUSTOM.CH%, REGARDLESS

	!
	! Check current record
	!
	SELECT SORTBY$
	CASE "N"
		GOTO ExitTotal IF (AR_35CUSTOM::CUSNUM > TO.ITEM$) AND &
			TO.ITEM$ <> ""
	CASE "T"
		GOTO ExitTotal IF (AR_35CUSTOM::TTYPE > TO.ITEM$) AND &
			TO.ITEM$ <> ""
	CASE "C"
		GOTO ExitTotal IF (AR_35CUSTOM::CATEGORY > TO.ITEM$) AND &
			TO.ITEM$ <> ""
	CASE "A"
		GOTO ExitTotal IF (AR_35CUSTOM::ALPSRT > TO.ITEM$) AND &
			TO.ITEM$ <> ""
	END SELECT

17100	!
	! Search for any extra information
	!
	WORK_TYPE$ = "----"

	GET #BA_CUSTOMEX.CH%, KEY#0% EQ AR_35CUSTOM::CUSNUM, REGARDLESS

	WORK_TYPE$ = " " + BA_CUSTOMEX::CUSTYP + " "

17200	!
	! Print out one line
	!
	IF FORMTYPE$ = "L"
	THEN
		CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TITLE1$, 0%)
		TEXT$ = AR_35CUSTOM::CUSNAM + "   " + &
			AR_35CUSTOM::CUSNUM + "   " + WORK_TYPE$

		CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, 0%)
		CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), AR_35CUSTOM::ADD1, 0%)
		CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), AR_35CUSTOM::ADD2, 0%)
		GOTO ExitProgram IF UTL_REPORTX::STAT

		TEXT$ = AR_35CUSTOM::CITY   + " " + &
			AR_35CUSTOM::STATE  + " " + &
			AR_35CUSTOM::ZIP    + " " + &
			AR_35CUSTOM::COUNTRY + "     " + &
			TITLE3$

		CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, 0%)
		GOTO ExitProgram IF UTL_REPORTX::STAT

		TEXT$ = PRNT_PHONE( AR_35CUSTOM::PHONE, 0% ) + SPACE$(30%) + &
			AR_35CUSTOM::METHOD   + "     " + &
			AR_35CUSTOM::STMTFLG   + "        " + &
			AR_35CUSTOM::SERCHRG   + "     " + &
			AR_35CUSTOM::ALPSRT

		CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, 0%)
		CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), "", 0%)
		CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), STRING$(80%,61%), 8%)
		GOTO ExitProgram IF UTL_REPORTX::STAT

	ELSE
		TEXT$ = AR_35CUSTOM::CUSNUM + " " + &
			AR_35CUSTOM::CUSNAM + " " + &
			WORK_TYPE$

		CALL OUTP_LINE(LYT_LINE, UTL_REPORTX, TITLE$(), TEXT$, 0%)
		GOTO ExitProgram IF UTL_REPORTX::STAT
	END IF

	!
	! Try for next record
	!
	GOTO GetNextRec

 ExitTotal:
	!
	! Handle end of report
	!

 ExitProgram:
	CALL OUTP_FINISH(UTL_REPORTX)

	!
	! Exit to next program or menu
	!
	IF TRM$(UTL_REPORTX::NEXTRUN) = ""
	THEN
		CALL SUBR_3EXITPROGRAM(SCOPE, "", "")
	ELSE
		CALL SUBR_3EXITPROGRAM(SCOPE, "RUN " + UTL_REPORTX::NEXTRUN, "")
	END IF

	%PAGE

 HelpError:
	!******************************************************************
	! Help Message for an error
	!******************************************************************
	CALL HELP_34MESSAGE(SCOPE, NUM1$(ERL) + " " + ERT$(ERR), &
		"E", ERN$, FILENAME$, NUM1$(ERR))
	UTL_REPORTX::STAT = -1%
	GOTO ExitProgram

	%Page

19000	!******************************************************************
	! ERROR TRAPPING
	!******************************************************************

	SELECT ERR
	CASE 154%	! Locked record
		SLEEP 1%
		RESUME
	END SELECT

	FILENAME$ = ""
	SELECT ERL
	!
	! AR_35CUSTOM file
	!
	CASE 300%, 17000%
		FILENAME$ = "AR_35CUSTOM"

	!
	! BA_CUSTOMEX file
	!
	CASE 320%
		RESUME 330

	!
	! End of file
	!
	CASE 17020%
		RESUME ExitTotal IF ERR = 11%
		FILENAME$ = "AR_35CUSTOM"

	!
	! Extra customer information
	!
	CASE 17100
		RESUME 17200

	END SELECT

	!
	! Untrapped error
	!
	RESUME HelpError

32767	END
