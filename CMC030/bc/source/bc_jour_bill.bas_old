1	%TITLE "BC Bill Maintenance"
	%SBTTL "BC_JOUR_BILL"
	%IDENT "V3.6a Calico"

	!
	! COPYRIGHT (C) 1987, 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! Abstract:HELP
	!	.p
	!	The ^*Billing Journal\* option provides the means
	!	to access a maintenance screen where customer order information
	!	is entered and maintained. The screen also contains a function in
	!	the Command Menu, Line__Items, where Customer billing information
	!	may be entered.
	!
	! Index:
	!	.x Maintain>Billing Journal
	!	.x Billing Journal>Maintenance
	!	.x Customer Billing Journal>Maintain
	!
	! Option:
	!
	!	BC_MAIN_BILL$HELP
	!	BC_MAIN_BILL_LINE$HELP
	!
	! Compile:
	!
	!	$ BAS BC_SOURCE:BC_JOUR_BILL/LINE
	!	$ LINK/EXECUTABLE=BC_EXE: BC_JOUR_BILL,FUNC_LIB:CMCLINK/OPTION
	!	$ DELETE BC_JOUR_BILL.OBJ;*
	!
	! Author:
	!
	!	08/03/88 - Kevin Handy
	!
	! Modification history:
	!
	!	05/14/88 - Lance Williams
	!		Modified the header.
	!
	!	04/13/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standard.
	!		Fix last param of entr_3choice from "" to 0%
	!
	!	10/15/96 - Kevin Handy
	!		Reformat source code.
	!
	!	08/25/97 - Kevin Handy
	!		Change 'val' to 'val%'
	!
	!	08/19/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--
	%PAGE

	!
	! Set up compiling options
	!
	OPTION SIZE = (REAL GFLOAT, INTEGER LONG)

	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "FUNC_INCLUDE:BC_WINDOW.INC"

	!
	! External functions
	!
	EXTERNAL LONG FUNCTION MAIN_WINDOW

	!
	! This common area must be mapped in both the main program and
	! in MAINT_GROUP.
	!
	COM (CH_BC_BILLH) &
		BC_BILLH.CH%, &
		BC_BILLH.READONLY%, &
		BATCH_NO$ = 2%

	COM (CH_BC_BILL) &
		BC_BILL.CH%

	COM (CH_BC_BILL2) &
		BC_BILL2.CH%

	!
	! External functions
	!
	EXTERNAL LONG		FUNCTION MAINT_GROUP

	%PAGE

	!
	! Initilization section - Prepare to do anything
	!
	ON ERROR GOTO 19000

	!
	! Initialize all the standard stuff through an external call
	!
	CALL READ_INITIALIZE

	!
	! Declare channels
	!
	CALL ASSG_CHANNEL(BC_BILL2.CH%,STAT%)

	!
	! Look up device
	!
	CALL  READ_DEVICE("BC_BILLH", BC_BILLH.DEV$, STAT%)

300	!
	! Query user for year of file
	!
	CALL FIND_FILE( BC_BILLH.DEV$ + "BC_BILLH_*.JRL", BC_BILLH_FILE$(), &
		16%, "","")

	BC_BILLH_FILE% = VAL%(BC_BILLH_FILE$(0%))

	IF BC_BILLH_FILE%
	THEN
		BC_BILLH_FILE$(LOOP%) = &
			MID(BC_BILLH_FILE$(LOOP%),10%,2%) &
				FOR LOOP% = 1% TO BC_BILLH_FILE%

		TEMP$ = "BC Bill Files"

		X% = ENTR_3CHOICE(SCOPE, "", "", BC_BILLH_FILE$(), "", &
			0%, TEMP$, "", 0%)

		IF X% > 0%
		THEN
			BATCH_NO$ = EDIT$(BC_BILLH_FILE$(X%),-1%)
			GOTO 400
		END IF
	END IF

	SELECT SCOPE::SCOPE_EXIT
	CASE 3%, SMG$K_TRM_F10, SMG$K_TRM_CTRLZ	! Exit key ?
		GOTO ExitProgram

	END SELECT

	!
	! Ask for year
	!
	SMG_STATUS% = SMG$CREATE_VIRTUAL_DISPLAY &
	( &
		20%, &
		80%, &
		SMG_SCREEN_DATA% &
	)

	SMG_STATUS% = SMG$PASTE_VIRTUAL_DISPLAY &
	( &
		SMG_SCREEN_DATA%, &
		SCOPE::SMG_PBID, &
		1%, &
		1% &
	)

	SMG_STATUS% = SMG$PUT_CHARS(SMG_SCREEN_DATA%, "Batch number:", &
		11%, 30%)

320	!
	! Set up the help message
	!
	SCOPE::PRG_ITEM = "FLD01BATCH"

	!++
	! Abstract:FLD01BATCH
	!	^*Batch Number\*
	!	.p
	!	The ^*Batch _#\* field will be bypassed if a Batch _# is
	!	selected in the Units Journal File screen.  If a new batch
	!	is to be created, the Batch _# would be entered here.
	!
	! Index:
	!	.x Journal>Batch _#
	!	.x Batch _#>Journal
	!
	!--
	BATCH_NO$ = "01"

	SELECT ENTR_3ENTER(SCOPE, SMG_SCREEN_DATA%, 11%, 43%, BATCH_NO$, -1%, 16%)

	CASE 3%, SMG$K_TRM_F10, SMG$K_TRM_CTRLZ	! Exit key ?
		GOTO ExitProgram

	END SELECT

	BATCH_NO$ = EDIT$(BATCH_NO$, -1%)

	IF LEN(TRM$(BATCH_NO$)) <> 2%
	THEN
		CALL ENTR_3MESSAGE(SCOPE, "Please enter the batch number in XX format", 0%)
		GOTO 320
	END IF

400	SMG_STATUS% = SMG$DELETE_VIRTUAL_DISPLAY(SMG_SCREEN_DATA%)

1000	!*******************************************************************
	! Handle main program
	!*******************************************************************

	V% = MAIN_WINDOW(BC_MAIN_BILL.ID, "")

	!******************************************************************
	! End of the program
	!******************************************************************

 ExitProgram:
	CALL SUBR_3EXITPROGRAM(SCOPE, "", "")

19000	!******************************************************************
	! Error trapping
	!******************************************************************

	ON ERROR GOTO 0

19990	END

20000	FUNCTION LONG MAINT_GROUP(CDD_WINDOW_CDD SMG_WINDOW, &
		LONG MOPTION, LONG MLOOP, LONG MFLAG, STRING MVALUE)

	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	%INCLUDE "SOURCE:[CDD.OPEN]CDD_WINDOW.HB"
	%INCLUDE "FUNC_INCLUDE:BC_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:GL_WINDOW.INC"

	EXTERNAL LONG FUNCTION BC_MAIN_CUSTOM
	EXTERNAL LONG FUNCTION BC_MAIN_BILL
	EXTERNAL LONG FUNCTION BC_MAIN_BILL_LINE
	EXTERNAL LONG FUNCTION GL_MAIN_CHART

	%PAGE

	!
	! Process the proper window
	!
	SELECT SMG_WINDOW::IDENT

	CASE GL_MAIN_CHART.ID
		MAINT_GROUP = GL_MAIN_CHART(SMG_WINDOW, &
			MOPTION, MLOOP, MFLAG, MVALUE)

	CASE BC_MAIN_CUSTOM.ID
		MAINT_GROUP = BC_MAIN_CUSTOM(SMG_WINDOW, &
			MOPTION, MLOOP, MFLAG, MVALUE)

	CASE BC_MAIN_BILL.ID
		MAINT_GROUP = BC_MAIN_BILL(SMG_WINDOW, &
			MOPTION, MLOOP, MFLAG, MVALUE)

	CASE BC_MAIN_BILL_LINE.ID
		MAINT_GROUP = BC_MAIN_BILL_LINE(SMG_WINDOW, &
			MOPTION, MLOOP, MFLAG, MVALUE)

	END SELECT

32767	END FUNCTION
