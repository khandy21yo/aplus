1	%TITLE "Customer Type Description"
	%SBTTL "BC_RPRT_CUSTYP"
	%IDENT "V3.6a Calico"

	!
	! COPYRIGHT (C) 1986 BY
	!
	! Computer Management Center
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! ID:BC051
	!
	! Abstract:HELP
	!	.p
	!	The ^*List Accounts Recievable to General Ledger Table\* provides a report
	!	which will contain
	!	the following information:
	!	.b
	!	.lm +10
	!	.list 0,"*"
	!	.le
	!	Customer Type
	!	.le
	!	Account _#
	!	.le
	!	Account Mask
	!	.le
	!	Description
	!	.els
	!	.lm -10
	!
	! Index:
	!	.x AR to GL Table>Report
	!	.x Report>AR to GL
	!
	! Option:
	!
	!
	! Compile:
	!
	!	$ BAS BC_SOURCE:BC_RPRT_CUSTYP/LINE
	!	$ LINK/EXE=BC_EXE: BC_RPRT_CUSTYP, FUNC_LIB:CMCLINK/OPTION
	!	$ DELETE BC_RPRT_CUSTYP.OBJ;*
	!
	! Author:
	!
	!	07/05/88 - Kevin Handy
	!
	! Modification history:
	!
	!	06/22/90 - Aaron Redd
	!		Added line layout information so that the report could
	!		also be sent to either a spreadsheet or a DIF file.
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	08/30/96 - Kevin Handy
	!		Reformat source code.
	!
	!	05/22/97 - Kevin Handy
	!		Use integer for #key
	!
	!	08/19/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--
	%PAGE

	!
	! Set up compiling options
	!
	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	!
	! Include files
	!
	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "SOURCE:[UTL.OPEN]UTL_REPORTX.HB"
	DECLARE UTL_REPORTX_CDD UTL_REPORTX

	%INCLUDE "SOURCE:[BC.OPEN]BC_CUSTYP.HB"
	MAP (BC_CUSTYP) BC_CUSTYP_CDD BC_CUSTYP

	!
	! Declare variables and constants
	!
	DECLARE	STRING	LYT_LINE

	%PAGE

	!
	! Set up error trapping
	!
	ON ERROR GOTO 19000

 Init:
	!
	! Initilize report
	!
	CALL OUTP_INITFROMFILE(UTL_REPORTX, 132%)
	GOTO ExitProgram IF UTL_REPORTX::STAT

	FROM.ITEM$ = EDIT$(UTL_REPORTX::OPTDEF(0%), 132%)

	!++
	! Abstract:FLD01
	!
	!	^*(01) From Type\*
	!	.p
	!	The ^*From Type\* value entered in this field will cause the
	!	report to begin with the selected Customer Type.
	!	.p
	!	A blank setting will cause the report to begin with the first
	!	Customer Type in the file.
	!
	! Index:
	! Datatype:TEXT
	! Size:2
	!--

	TO.ITEM$ = EDIT$(UTL_REPORTX::OPTDEF(1%), 132%)

	!++
	! Abstract:FLD02
	!
	!	^*(02) To Type\*
	!	.p
	!	The ^*To Type\* value entered in this field will cause the report
	!	to end with the selected Customer Type.
	!	.p
	!	A blank setting will cause the report to end with the last Customer
	!	Type in the file.
	!
	! Index:
	! Datatype:TEXT
	! Size:2
	!--

	WLDCRD$ = EDIT$(UTL_REPORTX::OPTDEF(2%), -1%)

	!++
	! Abstract:FLD03
	!
	!	^*(03) Wildcard\*
	!	.p
	!	The ^*Wildcard\* field provides the means to select designated
	!	customer types to be printed by entering a "wildcard" value in this
	!	field.
	!
	! Index:
	! Datatype:TEXT
	! Size:18
	!--


300	%INCLUDE "SOURCE:[BC.OPEN]BC_CUSTYP.OPN"

 ReportTitle:
	!
	! Title
	!
	TITLE$(1%) = "CUSTOMER TYPE REPORT"
	TITLE$(2%) = "Customer Billing System"
	TITLE$(3%) = ""

	!
	! Heading
	!
	TITLE$(4%) = "AgcyType Account#             " + &
		"Description"
	TITLE$(5%) = "."

	!
	! Line layouts
	!
	LYT_LINE = "$CustomerType:002,$AR_Account:027,$Description:060"

	%PAGE

17000	!******************************************************************
	! OUTPUT REPORT
	!******************************************************************
	IF FROM.ITEM$ = ""
	THEN
		RESET #BC_CUSTYP.CH%
	ELSE
		FIND #BC_CUSTYP.CH%, KEY#0% GE FROM.ITEM$,REGARDLESS
	END IF

 GetNextRec:
17020	!
	! Main loop starts here
	!
	GOTO ExitProgram IF UTL_REPORTX::STAT

	!
	! Get next record
	!
	GET #BC_CUSTYP.CH%, REGARDLESS

	!
	! Check current record
	!
	GOTO ExitTotal IF (BC_CUSTYP::CUSTYP > TO.ITEM$) AND TO.ITEM$ <> ""

	GOTO GetNextRec &
		IF COMP_STRING(EDIT$(BC_CUSTYP::CUSTYP,-1%), WLDCRD$) = 0% &
		AND WLDCRD$ <> ""

17300	!
	! Print out one line
	!
	TEXT$ = BC_CUSTYP::CUSTYP + "       " + &
		BC_CUSTYP::AR_ACCT + "   " + &
		BC_CUSTYP::DESCR

	CALL OUTP_LINE(LYT_LINE, UTL_REPORTX, TITLE$(), TEXT$, 0%)
	GOTO ExitProgram IF UTL_REPORTX::STAT

17350	!
	! Try for next record
	!
	GOTO GetNextRec

 ExitTotal:
17400	!
	! Handle end of report
	!

 ExitProgram:
	CALL OUTP_FINISH(UTL_REPORTX)

	!
	! Exit to next program or menu
	!
	IF TRM$(UTL_REPORTX::NEXTRUN) = ""
	THEN
		CALL SUBR_3EXITPROGRAM(SCOPE, "", "")
	ELSE
		CALL SUBR_3EXITPROGRAM(SCOPE, "RUN " + UTL_REPORTX::NEXTRUN, "")
	END IF

	%PAGE

 HelpError:
	!******************************************************************
	! Help Message for an error
	!******************************************************************
	CALL HELP_34MESSAGE(SCOPE, NUM1$(ERL) + " " + ERT$(ERR), &
		"E", ERN$, FILENAME$, NUM1$(ERR))
	UTL_REPORTX::STAT = -1%
	GOTO ExitProgram

	%Page

19000	!******************************************************************
	! ERROR TRAPPING
	!******************************************************************

	SELECT ERR
	CASE 154%	! Locked record
		SLEEP 5%
		RESUME
	END SELECT

	FILENAME$ = ""
	SELECT ERL

	CASE 300%
		FILENAME$ = "BC_CUSTYP"

	CASE 17000%
		CALL ENTR_3MESSAGE(SCOPE, &
			"Unable to find beginning record!", 0%)
		RESUME ExitProgram

	CASE 17020%	! End of file
		RESUME ExitTotal IF ERR = 11%
		FILENAME$ = "BC_CUSTYP"

	END SELECT

	!
	! Untrapped error
	!
	RESUME HelpError

32767	END
