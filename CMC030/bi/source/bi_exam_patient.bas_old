1	%TITLE "Examination of Patient Number"
	%SBTTL "BI_EXAM_PATIENT"
	%IDENT "V3.6a Calico"

	FUNCTION LONG BI_EXAM_PATIENT(STRING INSURNUM, &
		STRING PATIENT, &
		BI_PATIENT_CDD BI_PATIENT_EXAM )

	!
	! COPYRIGHT (C) 1989 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	!
	! Abstract:HELP
	!	.p
	!	This function, given an insurance number and a
	!	patient number, will return the record from the Insurance
	!	Billing Patient file associated with this patient number.
	!
	! Index:
	! Option:
	!
	!
	! Inputs:
	!
	!	INSURNUM	is the given insurance number
	!	PATIENT		is the given patient number
	!
	! Outputs:
	!
	!	BI_PATIENT	is the record associated with the given
	!		patient number.
	!	Returned Value	is CMC$_NORMAL if the search for the
	!		record was successful, CMC$_UNDEFINED if the
	!		record wasn't found, or CMC$_UNTERROR if there
	!		was an error of some sort.
	!
	! Compile:
	!
	!	$ BAS BI_SOURCE:BI_EXAM_PATIENT
	!	$ LIB FUNC_LIB:CMCFUN/REP BI_EXAM_PATIENT
	!	$ DELETE BI_EXAM_PATIENT.OBJ;*
	!
	! Author:
	!
	!	07/13/89 - Aaron Redd
	!
	! Modification History:
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	10/16/96 - Kevin Handy
	!		Reformat source code
	!
	!	08/19/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--
	%PAGE

	!
	! Set up compiling options
	!
	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	!
	! Include special CMC codes
	!
	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	!
	! CDD inclusions
	!
	%INCLUDE "SOURCE:[BI.OPEN]BI_PATIENT.HB"
	MAP	(BI_PATIENT)	BI_PATIENT_CDD	BI_PATIENT
	COM	(BI_PATIENT_INI)	BI_PATIENT_CDD	BI_PATIENT_INI

	!
	! Common memory areas
	!
	COM	(BI_EXAM_PATIENT.CH)	BI_PATIENT.CH%

	DECLARE LONG EXIT_STATUS

	%PAGE

	!
	! Set up error trapping
	!
	ON ERROR GOTO 19000

	!
	! Assume undefined
	!
	EXIT_STATUS = CMC$_UNDEFINED

	%PAGE

	!
	! Open the BI Patient file if not already open
	!
100	IF BI_PATIENT.CH% <= 0%
	THEN
		BI_PATIENT_INI::INSURED = &
			STRING$(LEN(BI_PATIENT_INI::INSURED),ASCII("?"))
		BI_PATIENT_INI::PATIENT = &
			STRING$(LEN(BI_PATIENT_INI::PATIENT),ASCII("?"))
		BI_PATIENT_INI::FAMRELAT = &
			STRING$(LEN(BI_PATIENT_INI::FAMRELAT),ASCII("?"))
		BI_PATIENT_INI::INSURANCE = &
			STRING$(LEN(BI_PATIENT_INI::INSURANCE),ASCII("?"))
		BI_PATIENT_INI::GROUPNO = &
			STRING$(LEN(BI_PATIENT_INI::GROUPNO),ASCII("?"))

		%INCLUDE "SOURCE:[BI.OPEN]BI_PATIENT.OPN"
	END IF

	!
	! Get the BI_PATIENT record and set it to be returned to the main program
	!
200	BI_PATIENT_EXAM = BI_PATIENT_INI
	GET #BI_PATIENT.CH%, KEY #0% EQ (INSURNUM + PATIENT), REGARDLESS

	BI_PATIENT_EXAM = BI_PATIENT
	EXIT_STATUS = CMC$_NORMAL

 ExitFunction:
	BI_EXAM_PATIENT = EXIT_STATUS

	EXIT FUNCTION

	%PAGE

 HelpError:
	!******************************************************************
	! Help Message for an error
	!******************************************************************
	CALL HELP_34MESSAGE(SCOPE, NUM1$(ERL) + " " + ERT$(ERR), &
		"E", ERN$, FILENAME$, NUM1$(ERR))

	EXIT_STATUS = CMC$_UNTERROR
	GOTO ExitFunction

	%PAGE

19000	!******************************************************************
	! Error trapping
	!******************************************************************
	!
	! Trap errors by error number
	!
	SELECT ERR
	!
	! Locked block
	!
	CASE 154%
		SLEEP 1%
		RESUME
	END SELECT

	!
	! Trap errors by line number
	!
	FILENAME$ = ""
	SELECT ERL

	!
	! Can't find BI_PATIENT file
	!
	CASE 100%
		RESUME ExitFunction IF ERR = 5%
		FILENAME$ = "BI_PATIENT"

	!
	! Undefined Patient Number
	!
	CASE 200%
		RESUME ExitFunction IF (ERR = 155%) OR (ERR = 9%)
		FILENAME$ = "BI_PATIENT"

	END SELECT

	!
	! Trap untrapped errors
	!
	RESUME HelpError

	%PAGE

32767	!******************************************************************
	! End of function BI_EXAM_PATIENT
	!******************************************************************
	END FUNCTION
