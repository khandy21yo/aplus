1	%TITLE "CPT Description"
	%SBTTL "BI_MAIN_CPT"
	%IDENT "V3.6a Calico"

	FUNCTION LONG BI_MAIN_CPT(CDD_WINDOW_CDD SMG_WINDOW, &
		LONG MOPTION, LONG MLOOP, LONG MFLAG, STRING MVALUE)

	!
	! COPYRIGHT (C) 1987, 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! Abstract:HELP
	!	.p
	!	The ^*CPT Description\* will provide a screen
	!	where the following information is entered and maintained:
	!	.lm +10
	!	.b
	!	.list 0,"*"
	!	.le
	!	CPT Code
	!	.le
	!	Description
	!	.le
	!	CPT Type
	!	.le
	!	CPT Category
	!	.le
	!	Rate Flag
	!	.els
	!	.lm -10
	!	.p
	!	The screen also contains a function in the COMMAND Menu which
	!	accesses a screen where the following information concerning
	!	rates is entered and maintained:
	!	.lm +10
	!	.b
	!	.list 0,"*"
	!	.le
	!	Effective Date
	!	.le
	!	Rate Amount
	!	.le
	!	Rate Table
	!	.els
	!	.lm -10
	!
	! Index:
	!	.X Add>CPT Master
	!	.X Erase>CPT Master
	!	.X Change>CPT Master
	!	.X Maintain>CPT Master
	!	.X CPT Master>Add
	!	.X CPT Master>Erase
	!	.x CPT Master>Change
	!	.x CPT Master>Maintain
	!	.x Rate>Add
	!	.x Rate>Erase
	!	.x Rate>Change
	!	.x Rate>Maintain
	!	.x Add>Rate
	!	.x Erase>Rate
	!	.x Change>Rate
	!	.x Maintain>Rate
	!
	! Option:
	!
	!
	! Input:
	!
	!
	! Output:
	!
	!
	! Compile:
	!
	!	$ BAS BI_SOURCE:BI_MAIN_CPT/LINE
	!	$ LIB/REP FUNC_LIB:CMCFUN BI_MAIN_CPT
	!	$ DELETE BI_MAIN_CPT.OBJ;*
	!
	! Author:
	!
	!	10/27/88 - Frank F. Starman
	!
	! Modification history:
	!
	!	03/23/93 - Kevin Handy
	!		Clean up (Check)
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	10/16/96 - Kevin Handy
	!		Reformat source code
	!
	!	05/22/97 - Kevin Handy
	!		Use integer for #key
	!
	!	08/19/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!
	!	03/09/99 - Kevin Handy
	!		Fix FIND bug
	!--
	%PAGE

	!
	! Set up compiling options
	!
	OPTION SIZE = (REAL GFLOAT, INTEGER LONG)

	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "FUNC_INCLUDE:BI_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:MAIN_WINDOW.COM"

	%INCLUDE "SOURCE:[CDD.OPEN]CDD_WINDOW.HB"

	%INCLUDE "SOURCE:[BI.OPEN]BI_CPT.HB"
	MAP (BI_CPT)		BI_CPT_CDD		BI_CPT
	MAP (BI_CPT_OLD)	BI_CPT_CDD		BI_CPT_OLD, BI_CPT2

	%INCLUDE "SOURCE:[BI.OPEN]BI_CPTTYPE.HB"
	MAP (BI_CPTTYPE)	BI_CPTTYPE_CDD		BI_CPTTYPE

	%INCLUDE "SOURCE:[BI.OPEN]BI_CATEGORY.HB"
	MAP (BI_CATEGORY)	BI_CATEGORY_CDD		BI_CATEGORY

	!
	! Common Areas
	!
	! These areas store information that is re-used between
	! calls to these functions.
	!
	COM (CH_BI_CPT) &
		BI_CPT.CH%, &
		BI_CPT.READONLY%

	COM (TT_BI_CPT) &
		RATETITLE$ = 20%, &
		RATE$(3%) = 20%

	!
	! External functions
	!
	EXTERNAL LONG   FUNCTION FUNC_TESTENTRY
	EXTERNAL LONG   FUNCTION MAIN_WINDOW

	%PAGE

	ON ERROR GOTO 29000

	SELECT MOPTION

	!
	! Initialization
	!
	! This option is used to initialize the window structure,
	! set up the default values for add, and open all files
	! necessary that have not already been opened.
	!
	CASE OPT_INIT

		!
		! Define window
		!
		SMG_WINDOW::DESCR = "CPT Description"
		SMG_WINDOW::NHELP = "BI_MAIN_CPT"
		SMG_WINDOW::HSIZE = 76%
		SMG_WINDOW::VSIZE = 18%
		SMG_WINDOW::HVIEW = 78%
		SMG_WINDOW::VVIEW = 18%
		SMG_WINDOW::HPOS  = 2%
		SMG_WINDOW::VPOS  = 2%
		SMG_WINDOW::NITEMS= 5%
		SMG_WINDOW::FLAGS = 0%

		SMG_WINDOW::NKEYS = 1%
		SMG_WINDOW::KNAME(0%) = "CPT"
			SMG_WINDOW::KFIELD(0%, 0%) = 1%
			SMG_WINDOW::KFIELD(0%, 1%) = 1%

		!
		! Rate flag
		!
		RATETITLE$= "Flag Description"
		RATE$(0%) = "3"
		RATE$(1%) = "F   Flat Rate"
		RATE$(2%) = "R   Real Time"
		RATE$(3%) = "T   Table Time"

		!
		! Load in defaults
		!
		CALL READ_DEFAULTS(SMG_WINDOW) &
			IF INSTR(1%, " QV", MVALUE) <= 1%

700		!
		! Declare channels
		!
		IF BI_CPT.CH% > 0%
		THEN
			!
			! Already open, set flag to read-only if was
			! that way from last time.
			!
			SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%) &
				IF BI_CPT.READONLY%
			GOTO 790
		END IF

		!
		! Open main file (existing) for modification
		!
750		%INCLUDE "SOURCE:[BI.OPEN]BI_CPT.CRE"
		BI_CPT.READONLY% = 0%
		GOTO 790

760		!
		! If unable to open for modify, try to open with read
		! access only.
		!
		%INCLUDE "SOURCE:[BI.OPEN]BI_CPT.OPN"
		SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%)
		BI_CPT.READONLY% = -1%

		GOTO 790

770		!
		! File not open, so reset channel
		!
		CALL ASSG_FREECHANNEL(BI_CPT.CH%)

		EXIT FUNCTION

790		SMG_WINDOW::CHAN  = BI_CPT.CH%
		RESET #BI_CPT.CH%
		GET #BI_CPT.CH%, REGARDLESS

	!
	! Display the background
	!
	! This option is used to display the background information on the
	! screen.  It must first clear any junk on the screen, and then
	! write the background onto it.
	!
20100	CASE OPT_BACKGROUND

		SMG_STATUS% = SMG$BEGIN_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

		SMG_STATUS% = SMG$ERASE_DISPLAY(SMG_WINDOW::WNUMBER)

		DATA	04, 05, "(01) CPT Code", &
			05, 05, "(02) Description", &
			06, 05, "(03) CPT Type", &
			07, 05, "(04) CPT Category", &
			08, 05, "(05) Rate Flag", &
			00, 00, ""

		RESTORE

		READ XPOS%, YPOS%, XSTR$
		I%=0%
		WHILE (XPOS% <> 0)
			I% = I% + 1%
			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				XSTR$, XPOS%, YPOS%) &
				IF (SMG_WINDOW::HFLAG(I%) AND 2%)=0%
		READ XPOS%, YPOS%, XSTR$
		NEXT

		SMG_STATUS% = SMG$END_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

	!
	! Enter/Display/Default
	!
	! This option is used to enter the data from the user, display data,
	! set defaults, and return the data back according to MFLAG.
	!
20200	CASE OPT_ENTRY

		TEMP$, TEMP1$ = TRM$(SCOPE::PRG_ITEM)
		TEMP$ = "View starting at" IF TEMP$ = "View"

		SCOPE::PRG_ITEM = "FLD" + FORMAT$(MLOOP, "<0>##")

 Reenter:	SCOPE::SCOPE_EXIT = 0%

		SELECT MLOOP

		CASE 1%

	!++
	! Abstract:FLD001
	!	^*(01) Current Procedural Terminology Code\*
	!	.p
	!	The ^*Current Procedural Terminology Code\* field is provided to enter a code
	!	which will identify a particular CPT.
	!	.p
	!	The field will accommodate five (05) alphanumeric characters.
	!
	! Index:
	!	.x CPT>Code
	!
	!--

			BI_CPT::CPT = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, "04;30", TEMP$, &
				BI_CPT::CPT, MFLAG, "'E", MVALUE )

		CASE 2%

	!++
	! Abstract:FLD002
	!	^*(02) Description\*
	!	.p
	!	The ^*Description\* field provides the means to enter a
	!	description of the CPT Code which was entered in
	!	field (01).
	!	.p
	!	The field will accommodate forty (40) alphanumeric
	!	characters.
	!
	! Index:
	!	.x Description>CPT
	!	.x CPT>Description
	!	.x Current Procedural Terminology>Description
	!	.x Description>Current Procedural Terminology
	!
	!--

			BI_CPT::DESCRIPTION = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, "05;30", TEMP$, &
				BI_CPT::DESCRIPTION, MFLAG, "'E", MVALUE )

		CASE 3%

	!++
	! Abstract:FLD003
	!	^*(03) Current Procedural Terminology Type\*
	!	.p
	!	The ^*Current Procedural Terminology Type\* field is provided to enter the code
	!	which
	!	has been identified in the Current Procedural Terminology Type Masterfile for
	!	the Current Procedural Terminology Code description entered in field (02).
	!	.p
	!	Pressing ^*<List Choices>\* while the cursor is located at this
	!	field will display a list of valid CPT Types.
	!
	! Index:
	!	.x Current Procedural Terminology Type
	!	.x Type>Current Procedural Terminology
	!	.x Type>CPT
	!	.x CPT>Type
	!
	!--

			BI_CPT::CPTTYPE = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, "06;30", TEMP$, &
				BI_CPT::CPTTYPE, MFLAG, "'E", MVALUE )

			IF (SCOPE::SCOPE_EXIT = SMG$K_TRM_F14)
			THEN
				IF MAIN_WINDOW(BI_MAIN_CPTTYPE.ID, "V0") = 1%
				THEN
					BI_CPT::CPTTYPE = &
						BI_CPTTYPE::CPTTYPE
				END IF
				GOTO Reenter
			END IF

		CASE 4%

	!++
	! Abstract:FLD004
	!	^*(04) Current Procedural Terminology Category\*
	!	.p
	!	The ^*Current Procedural Terminology Category\* field is provided to enter the
	!	code
	!	which has been identified in the Current Procedural Terminology Category
	!	Masterfile
	!	to which this screen has reference.
	!	.p
	!	Pressing ^*<List Choices>\* while the cursor is located at this
	!	field will display a list of CPT Category codes.
	!
	! Index:
	!	.x Current Procedural Terminology Category
	!	.x Category>Current Procedural Terminology
	!	.x Category>CPT
	!	.x CPT>Category
	!
	!--

			BI_CPT::CATEGORY = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, "07;30", TEMP$, &
				BI_CPT::CATEGORY, MFLAG, "'E", MVALUE )

			IF (SCOPE::SCOPE_EXIT = SMG$K_TRM_F14)
			THEN
				IF MAIN_WINDOW(BI_MAIN_CATEGORY.ID, "V0") = 1%
				THEN
					BI_CPT::CATEGORY = &
					BI_CATEGORY::CATEGORY
				END IF
				GOTO Reenter
			END IF

		CASE 5%

	!++
	! Abstract:FLD005
	!	^*(05) Rate Flag\*
	!	.p
	!	The ^*Rate Flag\* field provides the means to designate the
	!	rate method to be utilized in billing an insurance carrier.
	!	.p
	!	Valid settings are:
	!	.lm +10
	!	.b
	!	.list 0,"*"
	!	.le
	!	F = Flat Rate
	!	.le
	!	R = Real Time
	!	.le
	!	T = Table Time
	!	.els
	!	.lm -10
	!	.p
	!	Pressing ^*<List Choices>\* while the cursor is located at this
	!	field will display a list of valid rate flags.
	!
	! Index:
	!	.x Rate Flag
	!	.x Flag>Rate
	!
	!--

			BI_CPT::RATEFLAG = EDIT$(ENTR_3STRINGLIST(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"08;30", TEMP$, &
				BI_CPT::RATEFLAG, MFLAG, "'", MVALUE, &
				RATE$(), RATETITLE$, "004"), -1%)

		END SELECT

		SCOPE::PRG_ITEM = TEMP1$

	!
	! Test values
	!
20300	CASE OPT_TESTENTRY

		BI_MAIN_CPT = 0%

		SELECT MLOOP

		CASE 1%
			IF BI_CPT::CPT = ""
			THEN
				BI_MAIN_CPT = 1%
			ELSE
				IF (MVALUE = "ADD")
				THEN
					GET #BI_CPT.CH%, &
						KEY#0% EQ BI_CPT::CPT + "", &
						REGARDLESS

					BI_MAIN_CPT = 2%
					CALL ENTR_3MESSAGE(SCOPE, &
						"Record Already Exists", 1%)
				END IF
			END IF

		CASE 3%
			!
			! Is the input defined?
			!
			BI_MAIN_CPT = FUNC_TESTENTRY(SMG_WINDOW, &
				BI_CPT::CPTTYPE, &
				BI_CPTTYPE::DESCRIPTION, &
				"BI", MLOOP, "TYPE", &
				"CPT Type", BI_MAIN_CPTTYPE.ID)

		CASE 4%
			!
			! Is the input defined?
			!
			BI_MAIN_CPT = FUNC_TESTENTRY(SMG_WINDOW, &
				BI_CPT::CATEGORY, &
				BI_CATEGORY::CATEGORY, &
				"BI", MLOOP, "CATEGORY", &
				"CPT Category", BI_MAIN_CATEGORY.ID)

		END SELECT


	CASE OPT_DISPLAY

	!
	! Set BI_CPT_OLD value
	!
20500	CASE OPT_SETOLD

		BI_CPT_OLD = BI_CPT

	!
	! Restore BI_CPT_OLD value
	!
	CASE OPT_RESETOLD

		BI_CPT = BI_CPT_OLD

	!
	! Set default value
	!
	CASE OPT_SETDEFAULT

		BI_CPT2 = BI_CPT

	!
	! Restore default value
	!
	CASE OPT_RESETDEFAULT

		BI_CPT = BI_CPT2

	!
	! View header
	!
	CASE OPT_VIEW

		SELECT MLOOP

		!
		! Title (One line only)
		!
		CASE 1%
			MVALUE = "  CPTCode Description                   "+ &
				"           Ty Category RateFlag"

		!
		! Positions of lines
		!
		CASE 2%
			MVALUE = "010,051,054,063"

		!
		! Convert current record into text
		!
		CASE 3%
			MVALUE = BI_CPT::CPT + "   "   + &
				BI_CPT::DESCRIPTION+" "+ &
				BI_CPT::CPTTYPE + " " + &
				BI_CPT::CATEGORY + "     " + &
				BI_CPT::RATEFLAG

		END SELECT

	!
	! Find
	!
	CASE OPT_FIND
		SELECT MLOOP
		CASE 0%
			FIND #BI_CPT.CH%, &
				KEY#0% GE BI_CPT::CPT + "", &
				REGARDLESS
		END SELECT
	END SELECT

 ExitFunction:
	EXIT FUNCTION

29000	!***************************************************************
	! Trap errors
	!***************************************************************
	SELECT ERL
	!
	! Unable to open Read/Write
	!
	CASE 750%
		RESUME 760 IF ERR = 10%
		BI_MAIN_CPT = ERR
		RESUME 770

	!
	! Unable to open Read/Only
	!
	CASE 760%
		BI_MAIN_CPT = ERR
		RESUME 770

	!
	! Unable to reset
	!
	CASE 790%
		RESUME 32767

	!
	! Record not found, ok to add new one
	!
	CASE 20300%
		RESUME 32767

	END SELECT

	ON ERROR GO BACK

32767	END FUNCTION
	!+-+-+
	! More Menu option hidde in MAST
	!++
	! Abstract:RATE
	!	^*Rate\*
	!	.p
	!	The ^*Rate\* option in the COMMAND menu provides the means to enter and
	!	maintain the specific rates assigned to the Current Procedural Terminology
	!	Codes.
	!
	! Index:
	!	.x Rate
	!
	!--
