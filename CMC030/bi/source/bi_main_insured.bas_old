1	%TITLE "Insured Address File"
	%SBTTL "BI_MAIN_INSURED"
	%IDENT "V3.6a Calico"

	FUNCTION LONG BI_MAIN_INSURED(CDD_WINDOW_CDD SMG_WINDOW, &
		LONG MOPTION, LONG MLOOP, LONG MFLAG, STRING MVALUE)

	!
	! COPYRIGHT (C) 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! Abstract:HELP
	!	.p
	!	The ^*Patient Master\* option provides a means to access
	!	a screen where information regarding both patient and
	!	insured is entered and maintained.
	!
	! Index:
	!	.x Patient>Master
	!	.x Patient>Add
	!	.x Patient>Erase
	!	.x Patient>Change
	!	.x Change>Patient
	!	.x Erase>Patient
	!	.x Add>Patient
	!	.x Master>Patient
	!	.x Insured>Master
	!	.x Insured>Add
	!	.x Insured>Erase
	!	.x Insured>Change
	!	.x Add>Insured
	!	.x Erase>Insured
	!	.x Change>Insured
	!	.x Master>Insured
	!
	! Option:
	!
	! Compile:
	!
	!	$ BAS BI_SOURCE:BI_MAIN_INSURED/LINE
	!	$ LIB FUNC_LIB:CMCFUN/REP BI_MAIN_INSURED
	!	$ DELETE BI_MAIN_INSURED.OBJ;*
	!
	!
	! Author:
	!
	!	11/01/88 - Frank F. Starman
	!
	! Modification history:
	!
	!	06/25/91 - Craig Tanner
	!		Added list choises for Country feild.
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 Calico coding standards
	!
	!	04/19/95 - Kevin Handy
	!		Fix last parameter to entr_3phone.
	!
	!	10/16/96 - Kevin Handy
	!		Reformat source code
	!
	!	05/22/97 - Kevin Handy
	!		Use integer for #key
	!
	!	08/19/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!
	!	03/09/99 - Kevin Handy
	!		Fix FIND bug
	!--
	%PAGE

	!
	! Set up compiling options
	!
	OPTION SIZE = (REAL GFLOAT, INTEGER LONG)

	!
	! Include files
	!
	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "FUNC_INCLUDE:MAIN_WINDOW.COM"
	%INCLUDE "FUNC_INCLUDE:BI_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:UTL_WINDOW.INC"

	!
	! CDD inclusions and MAPs
	!
	%INCLUDE "SOURCE:[CDD.OPEN]CDD_WINDOW.HB"

	%INCLUDE "SOURCE:[BI.OPEN]BI_INSURED.HB"
	MAP (BI_INSURED)	BI_INSURED_CDD		BI_INSURED
	MAP (BI_INSURED_OLD)	BI_INSURED_CDD		BI_INSURED_OLD
	MAP (BI_INSURED_DEF)	BI_INSURED_CDD		BI_INSURED_DEF

	%INCLUDE "SOURCE:[UTL.OPEN]UTL_COUNTRY.HB"
	MAP (UTL_COUNTRY)	UTL_COUNTRY_CDD		UTL_COUNTRY

	!
	! Common areas
	!
	! These areas store information that is re-used between
	! calls to these functions.
	!
	COM (CH_BI_INSURED) &
		BI_INSURED.CH%, &
		BI_INSURED.READONLY

	COM (TT_BI_INSURED) &
		SEXTITLE$ = 20%, &
		SEX$(2%) = 20%

	%PAGE

	ON ERROR GOTO 29000

	SELECT MOPTION

	!******************************************************************
	! Initialization
	!
	! This option is used to initialize the window structure,
	! set up the default values for add, and open all files
	! necessary that have not already been opened.
	!******************************************************************

	CASE OPT_INIT
		!
		! Define window
		!
		SMG_WINDOW::DESCR = "Insured Address File"
		SMG_WINDOW::NHELP = "BI_MAIN_INSURED"
		SMG_WINDOW::HSIZE = 76%
		SMG_WINDOW::VSIZE = 18%
		SMG_WINDOW::HVIEW = 130%
		SMG_WINDOW::VVIEW = 18%
		SMG_WINDOW::HPOS  = 2%
		SMG_WINDOW::VPOS  = 2%
		SMG_WINDOW::NITEMS= 15%
		SMG_WINDOW::FLAGS = 0%

		SMG_WINDOW::NKEYS = 3%
		SMG_WINDOW::KNAME(0%) = "Insured-number"
			SMG_WINDOW::KFIELD(0%, 0%) = 1%
			SMG_WINDOW::KFIELD(0%, 1%) = 1%
		SMG_WINDOW::KNAME(1%) = "Name"
			SMG_WINDOW::KFIELD(1%, 0%) = 1%
			SMG_WINDOW::KFIELD(1%, 1%) = 2%
		SMG_WINDOW::KNAME(2%) = "aLpha-sort"
			SMG_WINDOW::KFIELD(2%, 0%) = 1%
			SMG_WINDOW::KFIELD(2%, 1%) = 3%

		!
		! Sex table
		!
		SEXTITLE$= "Sex Description"
		SEX$(0%) = "2"
		SEX$(1%) = "M   Male"
		SEX$(2%) = "F   Female"

		!
		! Load in defaults for chart
		!
		CALL READ_DEFAULTS(SMG_WINDOW) &
			IF INSTR(1%, " QV", MVALUE) <= 1%

700		!
		! Get info required for main file
		!
		IF BI_INSURED.CH% > 0%
		THEN
			!
			! Already open, set flag to read-only if
			! was that way from last time.
			!
			SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%) &
				IF BI_INSURED.READONLY%
			GOTO 790
		END IF

		!
		! Open main file (existing) for modification
		!
750		%INCLUDE "SOURCE:[BI.OPEN]BI_INSURED.CRE"
		BI_INSURED.READONLY% = 0%
		GOTO 790

760		!
		! If unable to open for modify, try to open
		! with read access only.
		!
		%INCLUDE "SOURCE:[BI.OPEN]BI_INSURED.OPN"
		SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%)
		BI_INSURED.READONLY% = -1%

		GOTO 790

770		!
		! File not able to open, so reset channel
		!
		CALL ASSG_FREECHANNEL(BI_INSURED.CH%)

		EXIT FUNCTION

790		SMG_WINDOW::CHAN  = BI_INSURED.CH%

		RESET #BI_INSURED.CH%
		GET #BI_INSURED.CH%, REGARDLESS


20100	!******************************************************************
	! Display the background
	!
	! This option is used to display the background information
	! on the screen.  It must first clear any junk on the screen,
	! and then write the background onto it.
	!******************************************************************

	CASE OPT_BACKGROUND

		SMG_STATUS% = SMG$BEGIN_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

		SMG_STATUS% = SMG$ERASE_DISPLAY(SMG_WINDOW::WNUMBER)

		DATA	2,  5, "(01) Insured", &
			3,  5, "(02) Name", &
			4,  5, "(03) Alpha", &
			5,  5, "(04) Add1", &
			6,  5, "(05) Add2", &
			7,  5, "(06) City", &
			7, 38, "(07) State", &
			8,  5, "(08) Zip", &
			8, 38, "(09) Country", &
			9,  5, "(10) Phone", &
			9, 38, "(11) County", &
			11,  5, "(12) Reference #", &
			12,  5, "(13) Birthdate", &
			13,  5, "(14) Sex", &
			14,  5, "(15) Onset Date", &
			0,  0, ""

		RESTORE

		READ XPOS%, YPOS%, XSTR$
		I%=0%
		WHILE (XPOS% <> 0)
			I% = I% + 1%
			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				XSTR$, XPOS%, YPOS%) &
				IF (SMG_WINDOW::HFLAG(I%) AND 2%)=0%
		READ XPOS%, YPOS%, XSTR$
		NEXT

		SMG_STATUS% = SMG$END_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

20200	!******************************************************************
	! Enter/Display/Default
	!
	! This option is used to enter the data from the user, display
	! data, set defaults, and return the data back according to
	! MFLAG.
	!******************************************************************

	CASE OPT_ENTRY
		TEMP$, TEMP1$ = TRM$(SCOPE::PRG_ITEM)
		TEMP$ = "View starting at" IF TEMP$ = "View"

		SCOPE::PRG_ITEM = "FLD" + FORMAT$(MLOOP, "<0>##")

 Reenter:	SCOPE::SCOPE_EXIT = 0%

		SELECT MLOOP

		CASE 1%

	!++
	! Abstract:FLD001
	!
	!	^*(01) Insured\*
	!	.p
	!	The ^*Insured\* field is provided to assign an identification
	!	number to be used for referencing a particular insured person.
	!	.p
	!	The field will accommodate up to ten (10) alphanumeric characters,
	!	providing a field large enough to use Social Security numbers as
	!	Insured numbers if desired.  The format is user defined.  This
	!	field is a key and must have a value.  It cannot be null.
	!
	! Index:
	!	.x Insured
	!	.x Insured>Add
	!	.x Insured>Erase
	!	.x Insured>Change
	!	.x Insured>Maintenance
	!	.x Maintain>Insured
	!	.x Add>Insured
	!	.x Erase>Insured
	!	.x Change>Insured
	!--

			BI_INSURED::INSURED = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"2;22", TEMP$, &
				BI_INSURED::INSURED, MFLAG, "'E", MVALUE)

		CASE 2%

	!++
	! Abstract:FLD002
	!	^*(02) Name\*
	!	.p
	!	The ^*Name\* field provides the means to enter the name of the Insured as
	!	it would appear on all reports.
	!	.p
	!	The field may contain up to fifty (50) alphanumeric characters.
	!
	! Index:
	!	.x Name
	!
	!--

			BI_INSURED::INSNAME = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"3;22", TEMP$, &
				BI_INSURED::INSNAME, MFLAG, "'E", MVALUE)

		CASE 3%

	!++
	! Abstract:FLD003
	!	^*(03) Alpha\*
	!	.p
	!	The ^*Alpha\* field contents enable the file to be sorted,
	!	listed, or reported in alphabetical order.
	!	.p
	!	This field will contain up to fifteen (15) alphanumeric
	!	characters.
	!
	! Index:
	!	.x Alpha>Insured Address File
	!	.x Insured Address File>Alpha
	!
	!--

			BI_INSURED::ALPSRT = EDIT$(BI_INSURED::INSNAME, 32%) &
				IF (TEMP1$ = "Add") AND &
					((MFLAG AND 1%) = 0%) AND &
					(BI_INSURED::ALPSRT = "")

			BI_INSURED::ALPSRT = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"4;22", TEMP$, &
				BI_INSURED::ALPSRT, MFLAG, "'E", MVALUE)

		CASE 4%

	!++
	! Abstract:FLD004
	!	^*(04) Address 1\*
	!	.p
	!	The ^*Address 1\* field provides the means to enter the address
	!	of the Insured. It is recommended that in the event of a one line
	!	address, this field be left blank and field (05) be used
	!	for the address.
	!	.p
	!	This field may contain up to twenty-five (25) alphanumeric
	!	characters.
	!
	! Index:
	!	.x Address
	!
	!--

			BI_INSURED::ADD1 = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"5;22", TEMP$, &
				BI_INSURED::ADD1, MFLAG, "'E", MVALUE)

		CASE 5%

	!++
	! Abstract:FLD005
	!	^*(05) Address 2\*
	!	.p
	!	The ^*Address 2\* field provides the means to enter the second
	!	line street address for the Insured. It is recommended that in
	!	the event there is a single line address, this field be used
	!	for the entry.
	!	.p
	!	This field may contain up to twenty-one (21) alphanumeric
	!	characters.
	!
	! Index:
	!	.x Address
	!
	!--

			BI_INSURED::ADD2 = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"6;22", TEMP$, &
				BI_INSURED::ADD2, MFLAG, "'E", MVALUE)

		CASE 6%

	!++
	! Abstract:FLD006
	!	^*(06) City\*
	!	.p
	!	The ^*City\* field is provided to enter the city in which
	!	the insured is located.
	!	.p
	!	The field may contain up to fifteen (15) alphanumeric
	!	characters.
	!
	! Index:
	!	.x City>Insured Address File
	!	.x Insured Address File>City
	!
	!--

			BI_INSURED::CITY = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"7;22", TEMP$, &
				BI_INSURED::CITY, MFLAG, "'E", MVALUE)

		CASE 7%

	!++
	! Abstract:FLD007
	!	^*(07) State\*
	!	.p
	!	The ^*State\* field is provided to enter the State in which
	!	the insured is located.
	!	.p
	!	The field will accommodate a two (2) character state postal
	!	code.
	!
	! Index:
	!	.x State>Insured Address File
	!	.x Insured Address File>State
	!
	!--

			BI_INSURED::STATE = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"7;52", TEMP$, &
				BI_INSURED::STATE, MFLAG, "'E", MVALUE)

		CASE 8%

	!++
	! Abstract:FLD008
	!	^*(08) Zip\*
	!	.p
	!	The ^*Zip\* field contains the zip or postal code
	!	for the area in which the insured is located.
	!	.p
	!	This field will contain up to ten (10) alphanumeric characters.
	!
	! Index:
	!	.x Zip
	!
	!--

			BI_INSURED::ZIP = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"8;22", TEMP$, &
				BI_INSURED::ZIP, MFLAG, "'E", MVALUE)

		CASE 9%

	!++
	! Abstract:FLD009
	!	^*(09) Country\*
	!	.p
	!	The ^*Country\* field is available to enter the country, if the
	!	insured is located in a foreign country.
	!	.p
	!	This field may contain two (2) alphanumeric characters.
	!
	! Index:
	!	.x Country>Insured Address File
	!	.x Insured Address File>Country
	!
	!--

			BI_INSURED::COUNTRY = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"8;52", TEMP$, &
				BI_INSURED::COUNTRY, MFLAG, "'E", MVALUE)

			IF (SCOPE::SCOPE_EXIT = SMG$K_TRM_F14)
			THEN
				IF MAIN_WINDOW(UTL_MAIN_COUNTRY.ID, "V0" + &
					BI_INSURED::COUNTRY) = 1%
				THEN
					BI_INSURED::COUNTRY = &
						UTL_COUNTRY::COUNTRY
				END IF
				GOTO Reenter
			END IF
		CASE 10%

	!++
	! Abstract:FLD010
	!	^*(10) Phone\*
	!	.p
	!	The ^*Phone\* field is for entry of the insured's telephone
	!	number.
	!	.p
	!	This field may contain ten (10) numeric characters. Special
	!	characters are not required.
	!
	! Index:
	!	.x Phone>Insured Address File
	!	.x Insured Address File>Phone
	!
	!--

			BI_INSURED::PHONE = ENTR_3PHONE(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"9;22", TEMP$, &
				BI_INSURED::PHONE, 2% OR MFLAG, 0%, MVALUE)

		CASE 11%

	!++
	! Abstract:FLD011
	!	^*(11) County\*
	!	.p
	!	The ^*County\* field provides the means to enter the code for the
	!	County in which a particular insured person is located.
	!	.p
	!	The field may contain two (2) characters.
	!
	! Index:
	!	.x County>Insured Address File
	!	.x Insured Address File>County
	!
	!--

			BI_INSURED::COUNTY = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"9;52", TEMP$, &
				BI_INSURED::COUNTY, MFLAG, "'E", MVALUE)

		CASE 12%

	!++
	! Abstract:FLD012
	!	^*(12) Reference _#\*
	!	.p
	!	The ^*Reference _#\* field provides the means to enter an identification
	!	number which will identify a particular insured person.
	!	.p
	!	The field will accommodate up to sixteen (16) alphanumeric characters
	!	providing a field large enough to use Social Security numbers as
	!	insured numbers if desired. The format is user defined.
	!
	! Index:
	!	.x Reference Number>Insured Address File
	!	.x Insured Address File>Reference Number
	!	.x Number>Reference
	!
	!--

			BI_INSURED::REFNO = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"11;22", TEMP$, &
				BI_INSURED::REFNO, MFLAG, "'E", MVALUE)

		CASE 13%

	!++
	! Abstract:FLD013
	!	^*(13) Birthdate\*
	!	.p
	!	The ^*Birthdate\* field is provided to enter the date of
	!	birth for a particular insured person.
	!	.p
	!	The format for entry is MMDDYYYY or MMDDYY.
	!
	! Index:
	!	.x Birthdate>Insured Address File
	!	.x Insured Address File>Birthdate
	!
	!--

			BI_INSURED::BIRTHDATE = ENTR_3DATE(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"12;22", TEMP$, &
				BI_INSURED::BIRTHDATE, MFLAG, "'E", &
				MVALUE)

		CASE 14%

	!++
	! Abstract:FLD014
	!	^*(14) Sex\*
	!	.p
	!	The ^*Sex\* field provides for entry of the sex of the
	!	insured person.
	!	.p
	!	The field will accommodate one (1) character.
	!	.p
	!	Valid characters are:
	!	.lm +10
	!	.b
	!	.list 0,"*"
	!	.le
	!	M = Male
	!	.le
	!	F = Female
	!	.els
	!	.lm -10
	!	.p
	!	Pressing ^*<List Choices>\* while the cursor is located at
	!	this field will display a list of valid characters.
	!
	! Index:
	!	.x Sex>Insured Address File
	!	.x Insured Address File>Sex
	!
	!--

			BI_INSURED::SEX = EDIT$(ENTR_3STRINGLIST(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"13;22", TEMP$, &
				BI_INSURED::SEX, MFLAG, "'", MVALUE, &
				SEX$(), SEXTITLE$, "004"), -1%)

		CASE 15%

	!++
	! Abstract:FLD015
	!	^*(15) Onset Date\*
	!	.p
	!	The ^*Onset Date\* is provided to enter the beginning date
	!	of services or procedures performed for the insured.
	!	.p
	!	The format for entry is MMDDYYYY or MMDDYY.
	!
	! Index:
	!	.x Onset Date>Insured Address File
	!	.x Insured Address File>Onset Date
	!	.x Date>Onset
	!
	!--

			BI_INSURED::ONSETDATE = ENTR_3DATE(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"14;22", TEMP$, &
				BI_INSURED::ONSETDATE, MFLAG, "'E", &
				MVALUE)

		END SELECT

		SCOPE::PRG_ITEM = TEMP1$

	!
	! Test values
	!
20300	CASE OPT_TESTENTRY
		BI_MAIN_INSURED = 0%

		SELECT MLOOP

		CASE 1%
			IF BI_INSURED::INSURED = ""
			THEN
				BI_MAIN_INSURED = 1%
			ELSE
				IF (MVALUE = "ADD")
				THEN
					GET #BI_INSURED.CH%, &
						KEY#0% EQ BI_INSURED::INSURED + "", &
						REGARDLESS

					BI_MAIN_INSURED = 2%
					CALL ENTR_3MESSAGE(SCOPE, &
						"Record Already Exists", 1%)
				END IF
			END IF

		END SELECT

	!
	! Set BI_INSURED_OLD value
	!
20500	CASE OPT_SETOLD
		BI_INSURED_OLD = BI_INSURED

	!
	! Restore BI_INSURED_OLD value
	!
	CASE OPT_RESETOLD
		BI_INSURED = BI_INSURED_OLD

	!
	! Set default value
	!
	CASE OPT_SETDEFAULT
		BI_INSURED_DEF = BI_INSURED

	!
	! Restore default value
	!
	CASE OPT_RESETDEFAULT
		BI_INSURED = BI_INSURED_DEF

	!
	! View header
	!
	CASE OPT_VIEW
		SELECT MLOOP

		!
		! Title (One line only)
		!
		CASE 1%
			MVALUE = "  Insured#   Name                      Address" + &
				"                                          City, State" + &
				"                  Zip Code"

		!
		! Positions of lines
		!
		CASE 2%
			MVALUE = "013,039,087,116"

		!
		! Convert current record into text
		!
		CASE 3%

			MVALUE = &
				BI_INSURED::INSURED + " " + &
				LEFT(BI_INSURED::INSNAME + SPACE$(25%), &
				25%) + " " + &
				LEFT(BI_INSURED::ADD1 + "/" + &
					BI_INSURED::ADD2 + SPACE$(48%), &
					48%) + " " + &
				LEFT(BI_INSURED::CITY + " " + &
					BI_INSURED::STATE + " " + &
					BI_INSURED::COUNTRY + SPACE$(28%), &
					28%) + " " + &
				TRM$(BI_INSURED::ZIP)

		END SELECT
	!
	! Find
	!
	CASE OPT_FIND
		SELECT MLOOP

		CASE 0%
			FIND #BI_INSURED.CH%, &
				KEY #0% GE BI_INSURED::INSURED + "", &
				REGARDLESS

		CASE 1%
			FIND #BI_INSURED.CH%, &
				KEY #1% GE BI_INSURED::INSNAME + "", &
				REGARDLESS

		CASE 2%
			FIND #BI_INSURED.CH%, &
				KEY #2% GE BI_INSURED::ALPSRT + "", &
				REGARDLESS

		END SELECT

	END SELECT

	EXIT FUNCTION

	%PAGE

29000	!*******************************************************************
	! Trap errors
	!*******************************************************************
	SELECT ERL
	!
	! Unable to open Read/Write
	!
	CASE 750%
		RESUME 760 IF ERR = 10%
		BI_MAIN_INSURED = ERR
		RESUME 770

	!
	! Unable to open Read/Only
	!
	CASE 760%
		BI_MAIN_INSURED = ERR
		RESUME 770

	!
	! Unable to reset
	!
	CASE 790%
		RESUME 32767

	!
	! Record does not already exist
	!
	CASE 20300%
		RESUME 32767 IF ERR = 155%

	END SELECT

	ON ERROR GO BACK

32767	END FUNCTION
	!+-+-+
	!++
	! Abstract:PATIENT
	!	^*Patient\*
	!	.p
	!	The ^*Patient\* option provides the means to enter and maintain all information
	!	pertaining to each patient.
	!
	! Index:
	!	.x Patient
	!
	!--
