1	%TITLE "CPT Rate Maintenance"
	%SBTTL "BI_MAIN_RATE"
	%IDENT "V3.6a Calico"

	FUNCTION LONG BI_MAIN_RATE(CDD_WINDOW_CDD SMG_WINDOW, LONG MOPTION, &
		LONG MLOOP, LONG MFLAG, STRING MVALUE)

	!
	! COPYRIGHT (C) 1987, 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! Abstract:HELP
	!	.p
	!	The ^*Rate\* program maintains Current Procedural Terminology rate file.
	!
	! Index:
	!	.x Rate Maintenance
	!
	! Option:
	!
	!
	! Compile:
	!
	!	$ BAS BI_SOURCE:BI_MAIN_RATE/LINE
	!	$ LIB FUNC_LIB:CMCFUN/REP BI_MAIN_RATE
	!	$ DELETE BI_MAIN_RATE.OBJ;*
	!
	! Author:
	!
	!	10/28/88 - Frank Starman
	!
	! Modification history:
	!
	!	03/23/93 - Kevin Handy
	!		Clean up (Check)
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	10/16/96 - Kevin Handy
	!		Reformat source code
	!
	!	05/22/97 - Kevin Handy
	!		Use integer for #key
	!
	!	08/19/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--

	!
	! Set up compiling options
	!
	OPTION SIZE = (REAL GFLOAT, INTEGER LONG)

	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "FUNC_INCLUDE:BI_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:MAIN_WINDOW.COM"

	%INCLUDE "SOURCE:[CDD.OPEN]CDD_WINDOW.HB"

	%INCLUDE "SOURCE:[BI.OPEN]BI_RATE.HB"
	MAP (BI_RATE)		BI_RATE_CDD		BI_RATE
	MAP (BI_RATE_OLD)	BI_RATE_CDD		BI_RATE_OLD, BI_RATE2

	%INCLUDE "SOURCE:[BI.OPEN]BI_CPTTYPE.HB"
	MAP (BI_CPTTYPE)	BI_CPTTYPE_CDD		BI_CPTTYPE

	%INCLUDE "SOURCE:[BI.OPEN]BI_CPT.HB"
	MAP (BI_CPT)		BI_CPT_CDD		BI_CPT

	%INCLUDE "SOURCE:[BI.OPEN]BI_CATEGORY.HB"
	MAP (BI_CATEGORY)	BI_CATEGORY_CDD		BI_CATEGORY

	!
	! Common Areas
	!
	! These areas store information that is re-used between
	! calls to these functions.
	!
	COM (CH_BI_RATE) &
		BI_RATE.CH%, &
		BI_RATE.READONLY%

	!
	! External functions
	!
	EXTERNAL LONG    FUNCTION FUNC_TESTENTRY

	%PAGE

	ON ERROR GOTO 29000

	SELECT MOPTION

	!
	! Initialization
	!
	! This option is used to initialize the window structure,
	! set up the default values for add, and open all files
	! necessary that have not already been opened.
	!
	CASE OPT_INIT

		!*************************************************************
		! Set up information
		!*************************************************************

		!
		! Define window
		!
		SMG_WINDOW::DESCR = "CPT Rate"
		SMG_WINDOW::CURREC = -2%
		SMG_WINDOW::NHELP = "BI_MAIN_RATE"
		SMG_WINDOW::HSIZE = 76%
		SMG_WINDOW::VSIZE = 11%
		SMG_WINDOW::HPOS  = 3%
		SMG_WINDOW::VPOS  = 8%
		SMG_WINDOW::FLAGS = 0%
		SMG_WINDOW::NITEMS= 3%

		SMG_WINDOW::HVIEW = 76%
		SMG_WINDOW::VVIEW = 11%
		SMG_WINDOW::VVPOS = 8%
		SMG_WINDOW::VHPOS = 3%

		CALL READ_DEFAULTS(SMG_WINDOW) &
			IF INSTR(1%, " QV", MVALUE) <= 1%

700		!
		! Declare channels
		!
		IF BI_RATE.CH% > 0%
		THEN
			!
			! Already open, set flag to read-only if was
			! that way from last time.
			!
			SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%) &
				IF BI_RATE.READONLY%
			GOTO 790
		END IF

		!
		! Open main file (existing) for modification
		!
750		%INCLUDE "SOURCE:[BI.OPEN]BI_RATE.CRE"
		BI_RATE.READONLY% = 0%
		GOTO 790

760		!
		! If unable to open for modify, try to open with read
		! access only.
		!
		%INCLUDE "SOURCE:[BI.OPEN]BI_RATE.OPN"
		SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%)
		BI_RATE.READONLY% = -1%

		GOTO 790

770		!
		! File not open, so reset channel
		!
		CALL ASSG_FREECHANNEL(BI_RATE.CH%)

		EXIT FUNCTION

790		SMG_WINDOW::CHAN  = BI_RATE.CH%
		RESET #BI_RATE.CH%
		GET #BI_RATE.CH%, REGARDLESS

	!
	! Display the background
	!
	! This option is used to display the background information on the
	! screen.  It must first clear any junk on the screen, and then
	! write the background onto it.
	!
20100	CASE OPT_BACKGROUND

		SMG_STATUS% = SMG$BEGIN_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

		SMG_STATUS% = SMG$ERASE_DISPLAY(SMG_WINDOW::WNUMBER)


		DATA	04,05, "(01) Eff Date", &
			05,05, "(02) Rate Amount", &
			06,05, "(03) Rate Table", &
			0, 0, ""

		RESTORE

		READ XPOS%, YPOS%, XSTR$
		I%=0%
		WHILE (XPOS% <> 0)
			I% = I% + 1%
			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				XSTR$, XPOS%, YPOS%) &
				IF (SMG_WINDOW::HFLAG(I%) AND 2%)=0%
		READ XPOS%, YPOS%, XSTR$
		NEXT

		SMG_STATUS% = SMG$END_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

	!
	! Enter/Display/Default
	!
	! This option is used to enter the data from the user, display data,
	! set defaults, and return the data back according to MFLAG.
	!
20200	CASE OPT_ENTRY

		TEMP$, TEMP1$ = TRM$(SCOPE::PRG_ITEM)
		TEMP$ = "View starting at" IF TEMP$ = "View"

		SCOPE::PRG_ITEM = "FLD" + FORMAT$(MLOOP, "<0>##")

 ReEnter:	SCOPE::SCOPE_EXIT = 0%

		SELECT MLOOP

		CASE 1%

	!++
	! Abstract:FLD001
	!	^*(01) Effective Date\*
	!	.p
	!	The ^*Effective date\* field contains the date
	!	a particular rate is to be effective.
	!	.p
	!	The format for entry is MMDDYYYY or MMDDYY.
	!
	! Index:
	!	.x Effective Date>Rate Maintenance
	!	.x Rate Maintenance>Effective Date
	!	.x Date>Effective
	!
	!--

			BI_RATE::EFFDATE = ENTR_3DATE(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"04;28", TEMP$, &
				BI_RATE::EFFDATE, MFLAG, "'E", &
				MVALUE)

		CASE 2%

	!++
	! Abstract:FLD002
	!	^*(02) Rate Amount\*
	!	.p
	!	The ^*Rate Amount\* field contains the
	!	rate for this particular service or procedure.
	!
	! Index:
	!	.x Rate Amount
	!
	!--

			BI_RATE::RATE = ENTR_3NUMBER(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"05;28",TEMP$, BI_RATE::RATE, MFLAG, &
				"###.##", MVALUE )

		CASE 3%

	!++
	! Abstract:FLD003
	!	^*(03) Rate Table\*
	!	.p
	!	The ^*Rate Table\* field is utilized only if field (05) Rate Flag
	!	in the Current Procedural Terminology Description screen contains a "T"
	!	(Table Time).
	!	.p
	!	The field provides for entry of a particular rate table as defined in the CPT
	!	Rate Table screen.
	!
	! Index:
	!	.x Rate Table
	!	.x Table>Rate
	!
	!--

			BI_RATE::RATETABLE = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"06;28", TEMP$, &
				BI_RATE::RATETABLE, MFLAG, "'E", &
				MVALUE)

		END SELECT

		SCOPE::PRG_ITEM = TEMP1$

	!
	! Test values
	!
	CASE OPT_TESTENTRY
		BI_MAIN_RATE = 0%

		SELECT MLOOP

		CASE 3%
			IF BI_RATE::RATETABLE <> ""
			THEN
				!
				! Is the table defined?
				!
				BI_MAIN_RATE = FUNC_TESTENTRY(SMG_WINDOW, &
					BI_RATE::RATETABLE, "", &
					"BI", MLOOP, "RATE", &
					"Rate Table", BI_MAIN_RATETABLE.ID)

			END IF

		END SELECT

	!
	! Set BI_RATE_OLD value
	!
20500	CASE OPT_SETOLD
		BI_RATE_OLD = BI_RATE

	!
	! Restore BI_RATE_OLD value
	!
	CASE OPT_RESETOLD
		BI_RATE = BI_RATE_OLD

	!
	! Set default value
	!
	CASE OPT_SETDEFAULT
		BI_RATE2 = BI_RATE

	!
	! Restore default value
	!
	CASE OPT_RESETDEFAULT
		BI_RATE = BI_RATE2
		BI_RATE::CPT = MVALUE

	!
	! View header
	!
	CASE OPT_VIEW
		SELECT MLOOP

		!
		! Title (One line only)
		!
		CASE 1%
			MVALUE = "  Date       Cost RateTable"

		!
		! Positions of lines
		!
		CASE 2%
			MVALUE = "011,018"

		!
		! Convert current record into text
		!
		CASE 3%
			MVALUE = PRNT_DATE(BI_RATE::EFFDATE, 0%) + " " + &
				FORMAT$(BI_RATE::RATE,"###.##")+" "+ &
				BI_RATE::RATETABLE


		END SELECT

	!
	! Find
	!
	CASE OPT_FIND
		SELECT MLOOP
		CASE 0%
			FIND #SMG_WINDOW::CHAN, &
				KEY#0% GE BI_RATE::CPT+ &
					BI_RATE::EFFDATE, &
				REGARDLESS

		END SELECT


	!
	! Handle array of records
	!
	CASE OPT_SUBWIND

		SELECT MLOOP

		!
		! Find first record (if there is any)
		!
		CASE 1%

			!
			! Set init value
			!
			SMG_WINDOW::CURREC = -1%

			!
27110			!
			! Search for first record
			!
			FIND #SMG_WINDOW::CHAN,	KEY #0% EQ MVALUE, REGARDLESS

			!
			! Get a record
			!
			GET #SMG_WINDOW::CHAN
			SMG_WINDOW::CURREC = 0%

		!
		! Check if still right key
		!
		CASE 3%
			SMG_WINDOW::CURREC = -1%
			IF BI_RATE::CPT = MVALUE
			THEN
				SMG_WINDOW::CURREC = 0%
			END IF

		!
		! Set the new key
		CASE 6%
			BI_RATE::CPT = MVALUE

		END SELECT

	END SELECT

28000	EXIT FUNCTION

29000	!***************************************************************
	! Trap errors
	!***************************************************************
	SELECT ERL

	!
	! Unable to open Read/Write
	!
	CASE 750%
		RESUME 760 IF ERR = 10%
		BI_MAIN_RATE = ERR
		RESUME 770

	!
	! Unable to open Read/Only
	!
	CASE 760%
		BI_MAIN_RATE = ERR
		RESUME 770

	!
	! Unable to reset
	!
	CASE 790%
		RESUME 32767

	!
	! Error while searching for the first record
	!
	CASE 27110%
		RESUME 28000 IF ERR = 155%

	END SELECT

	ON ERROR GO BACK

32767	END FUNCTION
