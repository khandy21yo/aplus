1	%TITLE "Current Procedural Terminology Rate File List"
	%SBTTL "BI_RPRT_CPTRATE"
	%IDENT "V3.6a Calico"

	!
	! COPYRIGHT (C) 1986, 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! ID:BI010
	!
	! Abstract:HELP
	!	.p
	!	Accessing the ^*Current Procedural Terminology Rate\* option in the Master List
	!	Menu will provide the means to print a report which will include the following
	!	information:
	!	.lm +10
	!	.b
	!	.list 0,"*"
	!	.le
	!	Current Procedural Terminology Code
	!	.le
	!	Effective Date
	!	.le
	!	Rate
	!	.le
	!	Rate Table
	!	.els
	!
	! Index:
	!	.x Report>Current Procedural Terminology Rate
	!	.x Current Procedural Terminology Rate>Report
	!
	! Option:
	!
	!
	! Compile:
	!
	!	$ BAS BI_SOURCE:BI_RPRT_CPTRATE/LINE
	!	$ LINK/EXE=BI_EXE: BI_RPRT_CPTRATE, FUNC_LIB:CMCLINK/OPTION
	!	$ DELETE BI_RPRT_CPTRATE.OBJ;*
	!
	! Author:
	!
	!	10/08/88 - J. Shad Rydalch
	!
	! Modification History:
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	09/03/96 - Kevin Handy
	!		Reformat source code.
	!
	!	05/22/97 - Kevin Handy
	!		Use integer for #key
	!
	!	08/19/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--
	%PAGE

	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	!
	! Include files
	!
	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "SOURCE:[UTL.OPEN]UTL_REPORTX.HB"
	DECLARE			UTL_REPORTX_CDD	UTL_REPORTX

	%INCLUDE "SOURCE:[BI.OPEN]BI_RATE.HB"
	MAP	(BI_RATE)	BI_RATE_CDD	BI_RATE

	%PAGE

	ON ERROR GOTO 19000

	!
	! Initilize report
	!
	CALL OUTP_INITFROMFILE(UTL_REPORTX, 132%)

	GOTO ExitProgram IF UTL_REPORTX::STAT

	FROM.ITEM$ = EDIT$(UTL_REPORTX::OPTDEF(0%), 132%)
	!++
	! Abstract:FLD02
	!	^*(02) From Current Procedural Terminology Code\*
	!	.p
	!	The value entered in the ^*From Current Procedural Terminology Code\* field
	!	will cause the report to begin with a selected Current Procedural Terminology
	!	Code by entering that selection in this field.
	!	.p
	!	A blank setting will cause the report to begin with the first
	!	Current Procedural Terminology Code in the file.
	!
	! Index:
	!	.x From Current Procedural Terminology Code>Rate List
	!	.x Current Procedural Terminology Code>From
	!	.x Rate List>From Current Procedural Terminology Code
	!
	!--

	TO.ITEM$ = EDIT$(UTL_REPORTX::OPTDEF(1%), 132%)
	!++
	! Abstract:FLD03
	!	^*(03) To Current Procedural Terminology Code\*
	!	.p
	!	The value entered in the ^*To Current Procedural Terminology Code\* field
	!	causes the report to end with a selected Current Procedural Terminology Code by
	!	entering that selection in this field.
	!	.p
	!	A blank setting will cause the report to end with the last Current Procedural
	!	Terminology Code in the file.
	!
	! Index:
	!	.x To Current Procedural Terminology Code>Rate List
	!	.x Current Procedural Terminology Code>To
	!	.x To>CPT Code
	!	.x CPT Code>To
	!	.x Rate List>To Current Procedural Terminology Code
	!
	!--

	WLDCRD$ = EDIT$(UTL_REPORTX::OPTDEF(2%), -1%)
	!++
	! Abstract:FLD04
	!	^*(04) Wildcard\*
	!	.p
	!	The ^*Wildcard\* field in the CPT Rate file report setting screen
	!	provides the means to select designated rates to be printed in the
	!	report by entering a "wildcard" value in this field.
	!
	! Index:
	!	.x Wildcard>Rate List
	!	.x Rate List>Wildcard
	!
	!--



300	%INCLUDE "SOURCE:[BI.OPEN]BI_RATE.OPN"

 ReportTitle:
	!
	! Title
	!
	TITLE$(1%) = "CPT  RATE  LIST"
	TITLE$(2%) = "Billing Insurance System"
	TITLE$(3%) = ""

	!
	! Heading
	!		12345...YYYYMMDD.#,###.##.123456...
	TITLE$(4%) = "CPTCode EffDate      Rate RateTable"
	TITLE$(5%) = "."

	lyt_line$ = "$CPTCode:008,DEffDate:021,VRate:026,$RateTable:036"

	%PAGE

17000	!******************************************************************
	! OUTPUT REPORT
	!******************************************************************

	IF FROM.ITEM$ = ""
	THEN
		RESET #BI_RATE.CH%
	ELSE
		FIND #BI_RATE.CH%, KEY#0% GE FROM.ITEM$,REGARDLESS
	END IF

 GetNextRec:
17020	!
	! Main loop starts here
	!
	GOTO ExitProgram IF UTL_REPORTX::STAT

	!
	! Get next record
	!
	GET #BI_RATE.CH%, REGARDLESS

	!
	! Check current record
	!
	GOTO ExitTotal IF (BI_RATE::CPT > TO.ITEM$) AND &
		TO.ITEM$ <> ""

	GOTO GetNextRec IF WLDCRD$ <> "" AND &
		COMP_STRING(EDIT$(BI_RATE::CPT,-1%), WLDCRD$) = 0%

17300	!
	! Print out one line
	!
	TEXT$ = BI_RATE::CPT + " " + &
		PRNT_DATE(BI_RATE::EFFDATE,8%) + " " + &
		FORMAT$(BI_RATE::RATE, "#,###.##") + " " + &
		BI_RATE::RATETABLE

	CALL OUTP_LINE(lyt_line$, UTL_REPORTX, TITLE$(), TEXT$, 0%)

	GOTO ExitProgram IF UTL_REPORTX::STAT

17350	!
	! Try for next record
	!
	GOTO GetNextRec

 ExitTotal:
17400	!
	! Handle end of report
	!

 ExitProgram:
	CALL OUTP_FINISH(UTL_REPORTX)

	!
	! Exit to next program or menu
	!
	IF TRM$(UTL_REPORTX::NEXTRUN) = ""
	THEN
		CALL SUBR_3EXITPROGRAM(SCOPE, "", "")
	ELSE
		CALL SUBR_3EXITPROGRAM(SCOPE, "RUN " + UTL_REPORTX::NEXTRUN, "")
	END IF

	%PAGE

 HelpError:
	!******************************************************************
	! Help Message for an error
	!******************************************************************
	CALL HELP_34MESSAGE(SCOPE, NUM1$(ERL) + " " + ERT$(ERR), &
		"E", ERN$, FILENAME$, NUM1$(ERR))
	UTL_REPORTX::STAT = -1%
	GOTO ExitProgram

	%Page

19000	!******************************************************************
	! ERROR TRAPPING
	!******************************************************************

	SELECT ERR
	CASE 154%	! Locked record
		SLEEP 5%
		RESUME
	END SELECT

	FILENAME$ = ""
	SELECT ERL

	CASE 300%
		FILENAME$ = "BI_CPTRATE"

	CASE 17000%
		CALL ENTR_3MESSAGE(SCOPE, &
			"Unable to find beginning record!", 0%)
		RESUME ExitProgram

	CASE 17020%	! End of file
		RESUME ExitTotal IF ERR = 11%
		FILENAME$ = "BI_RATE"

	END SELECT

	!
	! Untrapped error
	!
	RESUME HelpError

32767	END
