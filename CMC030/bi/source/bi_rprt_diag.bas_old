1	%TITLE "Diagnosis Code Description File List"
	%SBTTL "BI_RPRT_DIAG"
	%IDENT "V3.6a Calico"

	!
	! COPYRIGHT (C) 1986, 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! ID:BI009
	!
	! Abstract:HELP
	!	.p
	!	Accessing the ^*Diagnosis Codes\* option in the Master List Menu
	!	provides the means to print a report which will provide the following
	!	information:
	!	.lm +10
	!	.b
	!	.list 0,"*"
	!	.le
	!	Diagnosis
	!	.le
	!	Description
	!	.els
	!
	! Index:
	!	.x Report>Diagnosis Codes
	!	.x Diagnosis Codes>Report
	!
	! Option:
	!
	!
	! Compile:
	!
	!	$ BAS BI_SOURCE:BI_RPRT_DIAG/LINE
	!	$ LINK/EXE=BI_EXE: BI_RPRT_DIAG, FUNC_LIB:CMCLINK/OPTION
	!	$ DELETE BI_RPRT_DIAG.OBJ;*
	!
	! Author:
	!
	!	11/08/88 - J. Shad Rydalch
	!
	! Modification History:
	!
	!	09/25/90 - Kevin Handy
	!		Spelling.  Diagnosis.
	!
	!	03/23/93 - Kevin Handy
	!		Clean up (Check)
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	09/03/96 - Kevin Handy
	!		Reformat source code.
	!
	!	08/19/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--
	%PAGE

	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	!
	! Include files
	!
	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "SOURCE:[UTL.OPEN]UTL_REPORTX.HB"
	DECLARE			UTL_REPORTX_CDD	UTL_REPORTX

	%INCLUDE "SOURCE:[BI.OPEN]BI_DIAG.HB"
	MAP	(BI_DIAG)	BI_DIAG_CDD	BI_DIAG

	%PAGE

	ON ERROR GOTO 19000


	!
 Init:	! Initilize report
	!
	CALL OUTP_INITFROMFILE(UTL_REPORTX, 132%)

	GOTO ExitProgram IF UTL_REPORTX::STAT

	SORT.BY$ = EDIT$(UTL_REPORTX::OPTDEF(0%), 132%)

	!++
	! Abstract:FLD01
	!	^*(01) Sort (D,S)\*
	!	.p
	!	The value entered in the ^*Sort\* field determines the
	!	order in which the report will print.
	!	.p
	!	Valid settings are:
	!	.lm +10
	!	.b
	!	.list 0,"*"
	!	.le
	!	D = Description
	!	.le
	!	S = Diagnosis Code
	!	.els
	!
	! Index:
	!	.x Sort
	!
	!--

	FROM.ITEM$ = EDIT$(UTL_REPORTX::OPTDEF(1%), 132%)

	!++
	! Abstract:FLD02
	!	^*(02) From Item\*
	!	.p
	!	The value entered in the ^*From Item\* field causes the
	!	report to begin with a selected item by entering that selection
	!	in this field. The value of the selection must be in agreement
	!	with the Sort value in field (01).
	!	.p
	!	A blank field will cause the report to begin with the first
	!	item in the file.
	!
	! Index:
	!	.x From Item
	!
	!--

	TO.ITEM$ = EDIT$(UTL_REPORTX::OPTDEF(2%), 132%)

	!++
	! Abstract:FLD03
	!	^*(03) To Item\*
	!	.p
	!	The ^*To Item\* field is provided to enter an item with which
	!	the report will end by entering that selection in this field.
	!	The value of the selection must be in agreement with the
	!	Sort value in field (01).
	!	.p
	!	A blank field will cause the report to end with the last item
	!	in the file.
	!
	! Index:
	!	.x To Item>Code Description List
	!	.x Code Description List>To Item
	!	.x Item>To
	!
	!--

	WLDCRD$ = EDIT$(UTL_REPORTX::OPTDEF(3%), -1%)

	!++
	! Abstract:FLD04
	!	^*(04) Wildcard\*
	!	.p
	!	The ^*Wildcard\* field in the Diagnosis Codes report setting screen
	!	provides the means to select designated Diagnostic Codes to be printed
	!	in the report by entering a "wildcard" value in this field.
	!
	! Index:
	!	.x Wildcard>Code Description List
	!	.x Code Description List>Wildcard
	!
	!--


	SELECT SORT.BY$
	CASE "D"
		SORT.KEY% = 0%
		ADD.TITLE$ = " By  Diagnosis"
	CASE "S"
		SORT.KEY% = 1%
		ADD.TITLE$ = " By  Description"
	END SELECT


300	%INCLUDE "SOURCE:[BI.OPEN]BI_DIAG.OPN"

 ReportTitle:
	!
	! Title
	!
	TITLE$(1%) = "DIAGNOSIS  CATEGORIES"+ &
		ADD.TITLE$
	TITLE$(2%) = "Billing Insurance System"
	TITLE$(3%) = ""

	!
	! Heading
	!		 123456..1234567890123456789012345678901234567890
	TITLE$(4%) = "Diag    Description                             "
	TITLE$(5%) = "."

	lyt_line$ = "$Diag:008,$Description:048"

	%PAGE

17000	!******************************************************************
	! OUTPUT REPORT
	!******************************************************************

	IF FROM.ITEM$ = ""
	THEN
		RESET #BI_DIAG.CH%, KEY#SORT.KEY%
	ELSE
		FIND #BI_DIAG.CH%, KEY #SORT.KEY% GE FROM.ITEM$,REGARDLESS
	END IF

 GetNextRec:
17020	!
	! Main loop starts here
	!
	GOTO ExitProgram IF UTL_REPORTX::STAT

	!
	! Get next record
	!
	GET #BI_DIAG.CH%, REGARDLESS

	!
	! Check current record
	!
	SELECT SORT.BY$

	CASE "D"
		GOTO ExitTotal IF (BI_DIAG::DIAGNOSIS > TO.ITEM$) AND &
			TO.ITEM$ <> ""

		GOTO GetNextRec IF WLDCRD$ <> "" AND &
			COMP_STRING(EDIT$(BI_DIAG::DIAGNOSIS,-1%), WLDCRD$) = 0%

	CASE "S"
		GOTO ExitTotal IF (BI_DIAG::DESCRIPTION > TO.ITEM$) AND &
			TO.ITEM$ <> ""

		GOTO GetNextRec IF WLDCRD$ <> "" AND &
			COMP_STRING(EDIT$(BI_DIAG::DESCRIPTION,-1%), WLDCRD$) = 0%

	END SELECT

17300	!
	! Print out one line
	!
	TEXT$ = BI_DIAG::DIAGNOSIS	+ "    " + &
		BI_DIAG::DESCRIPTION

	CALL OUTP_LINE(lyt_line$, UTL_REPORTX, TITLE$(), TEXT$, 0%)

	GOTO ExitProgram IF UTL_REPORTX::STAT

17350	!
	! Try for next record
	!
	GOTO GetNextRec

 ExitTotal:
17400	!

	!

 ExitProgram:
	CALL OUTP_FINISH(UTL_REPORTX)

	!
	! Exit to next program or menu
	!
	IF TRM$(UTL_REPORTX::NEXTRUN) = ""
	THEN
		CALL SUBR_3EXITPROGRAM(SCOPE, "", "")
	ELSE
		CALL SUBR_3EXITPROGRAM(SCOPE, "RUN " + UTL_REPORTX::NEXTRUN, "")
	END IF

	%PAGE

 HelpError:
	!******************************************************************
	! Help Message for an error
	!******************************************************************
	CALL HELP_34MESSAGE(SCOPE, NUM1$(ERL) + " " + ERT$(ERR), &
		"E", ERN$, FILENAME$, NUM1$(ERR))
	UTL_REPORTX::STAT = -1%
	GOTO ExitProgram

	%Page

19000	!******************************************************************
	! ERROR TRAPPING
	!******************************************************************

	SELECT ERR
	CASE 154%	! Locked record
		SLEEP 5%
		RESUME
	END SELECT

	FILENAME$ = ""
	SELECT ERL

	CASE 300%
		FILENAME$ = "BI_DIAG"

	CASE 17000%
		CALL ENTR_3MESSAGE(SCOPE, "Unable to find beginning record!", 0%)
		RESUME ExitProgram

	CASE 17020%	! End of file
		RESUME ExitTotal IF ERR = 11%
		FILENAME$ = "BI_DIAG"

	END SELECT

	!
	! Untrapped error
	!
	RESUME HelpError

32767	END

