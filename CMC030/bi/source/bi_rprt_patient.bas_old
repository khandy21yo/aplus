1	%TITLE "Current Procedural Terminology Patient File List"
	%SBTTL "BI_RPRT_PATIENT"
	%IDENT "V3.6a Calico"

	!
	! COPYRIGHT (C) 1986, 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! ID:BI004
	!
	! Abstract:HELP
	!	.p
	!	Accessing the ^*Insured File\* option in the Master List Menu
	!	provides the means to print a report which will contain the
	!	following information:
	!	.lm +10
	!	.b
	!	.list 0,"*"
	!	.le
	!	Insured
	!	.le
	!	Patient Number
	!	.le
	!	Family Relationship
	!	.le
	!	Insurance
	!	.le
	!	Group Number
	!	.els
	!
	! Index:
	!	.x Report>Insured File
	!	.x Insured File>Report
	!	.x Report>Insured File
	!
	! Option:
	!
	!
	! Compile:
	!
	!	$ BAS BI_SOURCE:BI_RPRT_PATIENT/LINE
	!	$ LINK/EXE=BI_EXE: BI_RPRT_PATIENT, FUNC_LIB:CMCLINK/OPTION
	!	$ DELETE BI_RPRT_PATIENT.OBJ;*
	!
	! Author:
	!
	!	11/08/88 - J. Shad Rydalch
	!
	! Modification History:
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	09/03/96 - Kevin Handy
	!		Reformat source code
	!
	!	05/22/97 - Kevin Handy
	!		Use integer for #key
	!
	!	08/19/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--
	%PAGE

	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	!
	! Include files
	!
	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "SOURCE:[UTL.OPEN]UTL_REPORTX.HB"
	DECLARE			UTL_REPORTX_CDD	UTL_REPORTX

	%INCLUDE "SOURCE:[BI.OPEN]BI_PATIENT.HB"
	MAP	(BI_PATIENT)	BI_PATIENT_CDD	BI_PATIENT

	%PAGE

	ON ERROR GOTO 19000

	!
	! Initilize report
	!
	CALL OUTP_INITFROMFILE(UTL_REPORTX, 132%)

	GOTO ExitProgram IF UTL_REPORTX::STAT

	SORT.BY$ = EDIT$(UTL_REPORTX::OPTDEF(0%), 132%)

	!++
	! Abstract:FLD01
	!	^*(01) Sort (I,B)\*
	!	.p
	!	The value entered in the ^*Sort\* field causes the report
	!	to print in a selected order.
	!	.p
	!	Valid values are:
	!	.lm +10
	!	.b
	!	.list "*"
	!	.le
	!	I = Insured
	!	.le
	!	B = Birthdate
	!	.els
	!
	! Index:
	!	.x Sort
	!
	!--

	FROM.ITEM$ = EDIT$(UTL_REPORTX::OPTDEF(1%), 132%)

	!++
	! Abstract:FLD02
	!	^*(02) From Item\*
	!	.p
	!	The ^*From Item\* value entered in this field will cause the
	!	report to begin with a selected item by entering that
	!	selection in this field. The value of the selection must
	!	be in agreement with the Sort value in field (01).
	!	.p
	!	A blank field will cause the report to begin with the first
	!	item in the file.
	!
	! Index:
	!	.x From Item>Insured File
	!	.x Insrued File>From Item
	!	.x Item>From
	!
	!--

	TO.ITEM$ = EDIT$(UTL_REPORTX::OPTDEF(2%), 132%)

	!++
	! Abstract:FLD03
	!	^*(03) To Item\*
	!	.p
	!	The ^*To Item\* value entered in this field causes the report
	!	to end with a selected item by entering that selection in this
	!	field. The value of the selection must be in agreement with the
	!	Sort value in field (01).
	!	.p
	!	A blank value will cause the report to end with the last item
	!	in the file.
	!
	! Index:
	!	.x To Item
	!	.x Item>To
	!
	!--

	WLDCRD$ = EDIT$(UTL_REPORTX::OPTDEF(3%), -1%)

	!++
	! Abstract:FLD04
	!	^*(04) Wildcard\*
	!	.p
	!	The ^*Wildcard\* field in the Patient File List report setting screen
	!	provides the means to select designated patients to be printed in the
	!	report by entering a "wildcard" value in this field.
	!
	! Index:
	!	.x Wildcard>Insured File
	!	.x Insured File>Wildcard
	!
	!--


	SELECT SORT.BY$
	CASE "I"
		SORT.KEY% = 0%
		ADD.TITLE$ = "BY  INSURANCE"

	CASE "P"
		SORT.KEY% = 0%
		ADD.TITLE$ = "BY  PATIENT  NUMBER"
	END SELECT

300	%INCLUDE "SOURCE:[BI.OPEN]BI_PATIENT.OPN"

 ReportTitle:
	!
	! Title
	!
	TITLE$(1%) = "CPT  PATIENT  FILE  LIST  " + ADD.TITLE$
	TITLE$(2%) = "Billing Insurance System"
	TITLE$(3%) = ""

	!
	! Heading
	!
	TITLE$(4%) = "Insured     PatientNum  FamRelat  Insurance   GroupNumber"
	TITLE$(5%) = "."

	%PAGE

17000	!******************************************************************
	! OUTPUT REPORT
	!******************************************************************

	IF FROM.ITEM$ = ""
	THEN
		RESET #BI_PATIENT.CH%, KEY#SORT.KEY%
	ELSE
		FIND #BI_PATIENT.CH%, KEY#0% GE FROM.ITEM$,REGARDLESS
	END IF

 GetNextRec:
17020	!
	! Main loop starts here
	!
	GOTO ExitProgram IF UTL_REPORTX::STAT

	!
	! Get next record
	!
	GET #BI_PATIENT.CH%, REGARDLESS

	!
	! Check current record
	!
	SELECT SORT.BY$

	CASE "I"
		GOTO ExitProgram IF (BI_PATIENT::INSURED > TO.ITEM$) AND &
			TO.ITEM$ <> ""

		GOTO GetNextRec IF WLDCRD$ <> "" AND &
			COMP_STRING(EDIT$(BI_PATIENT::INSURED,-1%), &
			WLDCRD$) = 0%

	CASE "P"
		GOTO ExitProgram IF (BI_PATIENT::PATIENT > TO.ITEM$) AND &
			TO.ITEM$ <> ""

		GOTO GetNextRec IF WLDCRD$ <> "" AND &
			COMP_STRING(EDIT$(BI_PATIENT::PATIENT,-1%), &
			WLDCRD$) = 0%

	END SELECT

17300	!
	! Print out one line
	!
	TEXT$ = BI_PATIENT::INSURED + "  " + &
		BI_PATIENT::PATIENT + "  " + &
		BI_PATIENT::FAMRELAT + "         " + &
		BI_PATIENT::INSURANCE + "  " + &
		BI_PATIENT::GROUPNO

	CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, 0%)

	GOTO ExitProgram IF UTL_REPORTX::STAT

17350	!
	! Try for next record
	!
	GOTO GetNextRec

 ExitProgram:
	CALL OUTP_FINISH(UTL_REPORTX)

	!
	! Exit to next program or menu
	!
	IF TRM$(UTL_REPORTX::NEXTRUN) = ""
	THEN
		CALL SUBR_3EXITPROGRAM(SCOPE, "", "")
	ELSE
		CALL SUBR_3EXITPROGRAM(SCOPE, "RUN " + UTL_REPORTX::NEXTRUN, "")
	END IF

	%PAGE

 HelpError:
	!******************************************************************
	! Help Message for an error
	!******************************************************************
	CALL HELP_34MESSAGE(SCOPE, NUM1$(ERL) + " " + ERT$(ERR), &
		"E", ERN$, FILENAME$, NUM1$(ERR))
	UTL_REPORTX::STAT = -1%
	GOTO ExitProgram

	%Page

19000	!******************************************************************
	! ERROR TRAPPING
	!******************************************************************

	SELECT ERR
	CASE 154%	! Locked record
		SLEEP 5%
		RESUME
	END SELECT

	FILENAME$ = ""
	SELECT ERL

	CASE 300%
		FILENAME$ = "BI_PATIENT"

	CASE 17000%
		CALL ENTR_3MESSAGE(SCOPE, &
			"Unable to find beginning record!", 0%)
		RESUME ExitProgram

	CASE 17020%	! End of file
		RESUME ExitProgram IF ERR = 11%
		FILENAME$ = "BI_PATIENT"

	END SELECT

	!
	! Untrapped error
	!
	RESUME HelpError

32767	END
