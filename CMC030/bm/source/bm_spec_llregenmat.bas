1	!
	! Regenerate material from indented BM listing
	!
	! History:
	!
	!	05/26/2004 - Kevin Handy
	!

	%INCLUDE "SOURCE:[BM.OPEN]BM_RELATION.HB"
	MAP (BM_RELATION)	BM_RELATION_CDD		BM_RELATION

100	LINPUT "Text file name: "; FINAME$

	OPEN FINAME$ FOR INPUT AS FILE 1%, &
		RECORDSIZE 511%, &
		ACCESS READ, &
		ALLOW MODIFY

300	%INCLUDE "SOURCE:[BM.OPEN]BM_RELATION.CRE"

1000	!
	! Grap one line, and split it up
	!
	LINPUT #1%, INLINE$

	PARENT$ = TRM$(MID$(INLINE$, 1%, 14%))
	GOTO 1000 IF PARENT$ = ""
	LEVEL$ = MID$(INLINE$, 17%, 4%)
	GOTO 1000 IF LEVEL$ <> "   1"
	ITEM$ = MID$(INLINE$, 23%, 4%)
	COMPONENT$ = MID$(INLINE$, 32%, 14%)
	OPERATION$ = MID$(INLINE$, 106%, 8%)
	QTY$ = MID$(INLINE$, 96%, 9%)
	SCRAP$ = MID$(INLINE$, 115%, 6%)

	WHEN ERROR IN
		QTY = VAL(QTY$)
	USE
		CONTINUE 1000
	END WHEN

	WHEN ERROR IN
		SCRAP = VAL(SCRAP$)
	USE
		CONTINUE 1000
	END WHEN

1100	BM_RELATION::PRODUCT = PARENT$
	BM_RELATION::ITEMNUM = ITEM$

	WHEN ERROR IN
		GET #BM_RELATION.CH%, &
			KEY #0% EQ BM_RELATION::PRODUCT + BM_RELATION::ITEMNUM
	USE
		CONTINUE 1200
	END WHEN

	IF BM_RELATION::COMPONENT = COMPONENT$ AND &
		BM_RELATION::QUANTITY = QTY AND &
		BM_RELATION::OPERATION = OPERATION$ AND &
		BM_RELATION::SCRAP = SCRAP
	THEN
		PRINT ".";
		PRINT IF CCPOS(0%) >= 50%
		GOTO 1000
	END IF

	BM_RELATION::COMPONENT = COMPONENT$
	BM_RELATION::QUANTITY = QTY
	BM_RELATION::OPERATION = OPERATION$
	BM_RELATION::SCRAP = SCRAP

	WHEN ERROR IN
		DELETE #BM_RELATION.CH%
		PUT #BM_RELATION.CH%
		PRINT "U";
		PRINT IF CCPOS(0%) >= 50%
	USE
		PRINT "-";
		PRINT IF CCPOS(0%) >= 50%
		CONTINUE 1000
	END WHEN

	GOTO 1000

1200	BM_RELATION::PRODUCT = PARENT$
	BM_RELATION::ITEMNUM = ITEM$
	BM_RELATION::COMPONENT = COMPONENT$
	BM_RELATION::QUANTITY = QTY
	BM_RELATION::OPERATION = OPERATION$
	BM_RELATION::SCRAP = SCRAP

	WHEN ERROR IN
		PUT #BM_RELATION.CH%
		PRINT "*";
		PRINT IF CCPOS(0%) >= 50%
	USE
		PRINT "?";
		PRINT IF CCPOS(0%) >= 50%
		CONTINUE 1000
	END WHEN

	GOTO 1000

32767	END
