1	%TITLE "Client Address File"
	%SBTTL "BS_MAIN_CLIENT"
	%IDENT "V3.6a Calico"

	FUNCTION LONG BS_MAIN_CLIENT(CDD_WINDOW_CDD SMG_WINDOW, &
		LONG MOPTION, LONG MLOOP, LONG MFLAG, STRING MVALUE)

	!
	! COPYRIGHT (C) 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! Abstract:HELP
	!	.p
	!	This program maintains the client file in BS system
	!
	! Compile:
	!
	!	$ BAS BS_SOURCE:BS_MAIN_CLIENT/LINE
	!	$ LIB FUNC_LIB:CMCFUN/REP BS_MAIN_CLIENT
	!	$ DELETE BS_MAIN_CLIENT.OBJ;*
	!
	!
	! Author:
	!
	!	09/26/89 - Frank F. Starman
	!
	! Modification history:
	!
	!	07/3/91 - Craig Tanner
	!		Added list of choises to country feild.
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 Calico coding standards
	!
	!	04/19/95 - Kevin Handy
	!		Fix format parameter to entr_3phone.
	!
	!	10/17/96 - Kevin Handy
	!		Reformat source code
	!
	!	05/23/97 - Kevin Handy
	!		Use integer for #key
	!
	!	08/19/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!
	!	03/10/99 - Kevin Handy
	!		Fix FIND bug
	!--
	%PAGE

	!
	! Set up compiling options
	!
	OPTION SIZE = (REAL GFLOAT, INTEGER LONG)

	!
	! Include files
	!
	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "FUNC_INCLUDE:MAIN_WINDOW.COM"
	%INCLUDE "FUNC_INCLUDE:UTL_WINDOW.INC"

	!
	! CDD inclusions and MAPs
	!
	%INCLUDE "SOURCE:[CDD.OPEN]CDD_WINDOW.HB"

	%INCLUDE "SOURCE:[BS.OPEN]BS_CLIENT.HB"
	MAP (BS_CLIENT)		BS_CLIENT_CDD	BS_CLIENT
	MAP (BS_CLIENT_OLD)	BS_CLIENT_CDD	BS_CLIENT_OLD
	MAP (BS_CLIENT_DEF)	BS_CLIENT_CDD	BS_CLIENT_DEF

	%INCLUDE "SOURCE:[UTL.OPEN]UTL_COUNTRY.HB"
	MAP (UTL_COUNTRY)		UTL_COUNTRY_CDD		UTL_COUNTRY

	!
	! Common areas
	!
	! These areas store information that is re-used between
	! calls to these functions.
	!
	COM (CH_BS_CLIENT) &
		BS_CLIENT.CH%, &
		BS_CLIENT.READONLY

	COM (TT_BS_SEX) &
		SEXTITLE$ = 20%, &
		SEX$(2%) = 20%

	COM (TT_BS_STAT) &
		STITLE$ = 20%, &
		SSTAT$(5%) = 20%

	%PAGE

	ON ERROR GOTO 29000

	SELECT MOPTION

	!******************************************************************
	! Initialization
	!
	! This option is used to initialize the window structure,
	! set up the default values for add, and open all files
	! necessary that have not already been opened.
	!******************************************************************

	CASE OPT_INIT
		!
		! Define window
		!
		SMG_WINDOW::DESCR = "Client Address File"
		SMG_WINDOW::NHELP = "BS_MAIN_CLIENT"
		SMG_WINDOW::HSIZE = 76%
		SMG_WINDOW::VSIZE = 18%
		SMG_WINDOW::HVIEW = 130%
		SMG_WINDOW::VVIEW = 18%
		SMG_WINDOW::HPOS  = 2%
		SMG_WINDOW::VPOS  = 2%
		SMG_WINDOW::NITEMS= 17%
		SMG_WINDOW::FLAGS = 0%

		SMG_WINDOW::NKEYS = 3%
		SMG_WINDOW::KNAME(0%) = "Client_number"
			SMG_WINDOW::KFIELD(0%, 0%) = 1%
			SMG_WINDOW::KFIELD(0%, 1%) = 1%
		SMG_WINDOW::KNAME(1%) = "Name"
			SMG_WINDOW::KFIELD(1%, 0%) = 1%
			SMG_WINDOW::KFIELD(1%, 1%) = 2%
		SMG_WINDOW::KNAME(2%) = "aLpha-sort"
			SMG_WINDOW::KFIELD(2%, 0%) = 1%
			SMG_WINDOW::KFIELD(2%, 1%) = 3%

		!
		! Sex table
		!
		SEXTITLE$= "Sex Description"
		SEX$(0%) = "2"
		SEX$(1%) = "M   Male"
		SEX$(2%) = "F   Female"

		STITLE$ = "Status   Description"
		SSTAT$(0%) = "3"
		SSTAT$(1%) = "A      Active"
		SSTAT$(2%) = "I      Inactive"
		SSTAT$(3%) = "T      Terminated"

		!
		! Load in defaults for chart
		!
		CALL READ_DEFAULTS(SMG_WINDOW) &
			IF INSTR(1%, " QV", MVALUE) <= 1%

700		!
		! Get info required for main file
		!
		IF BS_CLIENT.CH% > 0%
		THEN
			!
			! Already open, set flag to read-only if
			! was that way from last time.
			!
			SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%) &
				IF BS_CLIENT.READONLY%
			GOTO 790
		END IF

		!
		! Open main file (existing) for modification
		!
750		%INCLUDE "SOURCE:[BS.OPEN]BS_CLIENT.CRE"
		BS_CLIENT.READONLY% = 0%
		GOTO 790

760		!
		! If unable to open for modify, try to open
		! with read access only.
		!
		%INCLUDE "SOURCE:[BS.OPEN]BS_CLIENT.OPN"
		SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%)
		BS_CLIENT.READONLY% = -1%

		GOTO 790

770		!
		! File not able to open, so reset channel
		!
		CALL ASSG_FREECHANNEL(BS_CLIENT.CH%)

		EXIT FUNCTION

790		SMG_WINDOW::CHAN  = BS_CLIENT.CH%

		RESET #BS_CLIENT.CH%
		GET #BS_CLIENT.CH%, REGARDLESS


20100	!******************************************************************
	! Display the background
	!
	! This option is used to display the background information
	! on the screen.  It must first clear any junk on the screen,
	! and then write the background onto it.
	!******************************************************************

	CASE OPT_BACKGROUND

		SMG_STATUS% = SMG$BEGIN_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

		SMG_STATUS% = SMG$ERASE_DISPLAY(SMG_WINDOW::WNUMBER)

		DATA	2,  5, "(01) Client #", &
			3,  5, "(02) Name", &
			4,  5, "(03) Alpha", &
			5,  5, "(04) Add1", &
			6,  5, "(05) Add2", &
			7,  5, "(06) City", &
			7, 38, "(07) State", &
			8,  5, "(08) Zip", &
			8, 38, "(09) Country", &
			9,  5, "(10) Phone", &
			9, 38, "(11) County", &
			11,  5, "(12) Reference #", &
			12,  5, "(13) Birthdate", &
			13,  5, "(14) Sex", &
			14,  5, "(15) Onset Date", &
			15,  5, "(16) Status", &
			16,  5, "(17) Term Date", &
			0,  0, ""

		RESTORE

		READ XPOS%, YPOS%, XSTR$
		I%=0%
		WHILE (XPOS% <> 0)
			I% = I% + 1%
			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				XSTR$, XPOS%, YPOS%) &
				IF (SMG_WINDOW::HFLAG(I%) AND 2%)=0%
		READ XPOS%, YPOS%, XSTR$
		NEXT

		SMG_STATUS% = SMG$END_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

20200	!******************************************************************
	! Enter/Display/Default
	!
	! This option is used to enter the data from the user, display
	! data, set defaults, and return the data back according to
	! MFLAG.
	!******************************************************************

	CASE OPT_ENTRY
		TEMP$, TEMP1$ = TRM$(SCOPE::PRG_ITEM)
		TEMP$ = "View starting at" IF TEMP$ = "View"

		SCOPE::PRG_ITEM = "FLD" + FORMAT$(MLOOP, "<0>##")

 Reenter:	SCOPE::SCOPE_EXIT = 0%

		SELECT MLOOP

		CASE 1%
			BS_CLIENT::CLIENT = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"2;22", TEMP$, &
				BS_CLIENT::CLIENT, MFLAG, "'E", MVALUE)

		CASE 2%
			BS_CLIENT::CLIENTNAME = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"3;22", TEMP$, &
				BS_CLIENT::CLIENTNAME, MFLAG, "'E", MVALUE)

		CASE 3%
			BS_CLIENT::ALPSRT = EDIT$(BS_CLIENT::CLIENTNAME, 32%) &
				IF (TEMP1$ = "Add") AND &
					((MFLAG AND 1%) = 0%) AND &
					(BS_CLIENT::ALPSRT = "")

			BS_CLIENT::ALPSRT = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"4;22", TEMP$, &
				BS_CLIENT::ALPSRT, MFLAG, "'E", MVALUE)

		CASE 4%
			BS_CLIENT::ADD1 = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"5;22", TEMP$, &
				BS_CLIENT::ADD1, MFLAG, "'E", MVALUE)

		CASE 5%
			BS_CLIENT::ADD2 = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"6;22", TEMP$, &
				BS_CLIENT::ADD2, MFLAG, "'E", MVALUE)

		CASE 6%
			BS_CLIENT::CITY = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"7;22", TEMP$, &
				BS_CLIENT::CITY, MFLAG, "'E", MVALUE)

		CASE 7%
			BS_CLIENT::STATE = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"7;52", TEMP$, &
				BS_CLIENT::STATE, MFLAG, "'E", MVALUE)

		CASE 8%
			BS_CLIENT::ZIP = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"8;22", TEMP$, &
				BS_CLIENT::ZIP, MFLAG, "'E", MVALUE)

		CASE 9%
			BS_CLIENT::COUNTRY = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"8;52", TEMP$, &
				BS_CLIENT::COUNTRY, MFLAG, "'E", MVALUE)

			IF (SCOPE::SCOPE_EXIT = SMG$K_TRM_F14)
			THEN
				IF MAIN_WINDOW(UTL_MAIN_COUNTRY.ID, "V0" + &
					BS_CLIENT::COUNTRY) = 1%
				THEN
					BS_CLIENT::COUNTRY = &
						UTL_COUNTRY::COUNTRY
				END IF
				GOTO Reenter
			END IF



		CASE 10%
			BS_CLIENT::PHONE = ENTR_3PHONE(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"9;22", TEMP$, &
				BS_CLIENT::PHONE, 2% OR MFLAG, 0%, MVALUE)

		CASE 11%
			BS_CLIENT::COUNTY = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"9;52", TEMP$, &
				BS_CLIENT::COUNTY, MFLAG, "'E", MVALUE)

		CASE 12%
			BS_CLIENT::REFNO = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"11;22", TEMP$, &
				BS_CLIENT::REFNO, MFLAG, "'E", MVALUE)

		CASE 13%
			BS_CLIENT::BIRTHDATE = ENTR_3DATE(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"12;22", TEMP$, &
				BS_CLIENT::BIRTHDATE, MFLAG, "'E", &
				MVALUE)

		CASE 14%
			BS_CLIENT::SEX = EDIT$(ENTR_3STRINGLIST(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"13;22", TEMP$, &
				BS_CLIENT::SEX, MFLAG, "'", MVALUE, &
				SEX$(), SEXTITLE$, "004"), -1%)

		CASE 15%
			BS_CLIENT::ONSETDATE = ENTR_3DATE(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"14;22", TEMP$, &
				BS_CLIENT::ONSETDATE, MFLAG, "'E", &
				MVALUE)

		CASE 16%
			BS_CLIENT::SSTATUS = EDIT$(ENTR_3STRINGLIST(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"15;22", TEMP$, &
				BS_CLIENT::SSTATUS, MFLAG, "'", MVALUE, &
				SSTAT$(), STITLE$, "008"), -1%)

		CASE 17%
			BS_CLIENT::TERMDATE= ENTR_3DATE(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"16;22", TEMP$, &
				BS_CLIENT::TERMDATE, MFLAG, "'E", &
				MVALUE)

		END SELECT

		SCOPE::PRG_ITEM = TEMP1$

	!
	! Test values
	!
20300	CASE OPT_TESTENTRY
		BS_MAIN_CLIENT = 0%

		SELECT MLOOP

		CASE 1%
			IF BS_CLIENT::CLIENT = ""
			THEN
				BS_MAIN_CLIENT = 1%
			ELSE
				IF (MVALUE = "ADD")
				THEN
					GET #BS_CLIENT.CH%, &
						KEY#0% EQ BS_CLIENT::CLIENT + "", &
						REGARDLESS
					BS_MAIN_CLIENT = 2%
					CALL ENTR_3MESSAGE(SCOPE, &
						"Record Already Exists", 1%)
				END IF
			END IF

		END SELECT

	!
	! Set BS_CLIENT_OLD value
	!
20500	CASE OPT_SETOLD
		BS_CLIENT_OLD = BS_CLIENT

	!
	! Restore BS_CLIENT_OLD value
	!
	CASE OPT_RESETOLD
		BS_CLIENT = BS_CLIENT_OLD

	!
	! Set default value
	!
	CASE OPT_SETDEFAULT
		BS_CLIENT_DEF = BS_CLIENT

	!
	! Restore default value
	!
	CASE OPT_RESETDEFAULT
		BS_CLIENT = BS_CLIENT_DEF

	!
	! View header
	!
	CASE OPT_VIEW
		SELECT MLOOP

		!
		! Title (One line only)
		!
		CASE 1%
			MVALUE = "  Client#    Name                      Address" + &
				"                                          City, State" + &
				"                  Zip Code"

		!
		! Positions of lines
		!
		CASE 2%
			MVALUE = "013,039,087,116"

		!
		! Convert current record into text
		!
		CASE 3%

			MVALUE = &
				BS_CLIENT::CLIENT + " " + &
				LEFT(BS_CLIENT::CLIENTNAME + SPACE$(25%), &
				25%) + " " + &
				LEFT(BS_CLIENT::ADD1 + "/" + &
					BS_CLIENT::ADD2 + SPACE$(48%), &
					48%) + " " + &
				LEFT(BS_CLIENT::CITY + " " + &
					BS_CLIENT::STATE + " " + &
					BS_CLIENT::COUNTRY + SPACE$(28%), &
					28%) + " " + &
				TRM$(BS_CLIENT::ZIP)

		END SELECT
	!
	! Find
	!
	CASE OPT_FIND
		SELECT MLOOP

		CASE 0%
			FIND #BS_CLIENT.CH%, &
				KEY #0% GE BS_CLIENT::CLIENT + "", &
				REGARDLESS

		CASE 1%
			FIND #BS_CLIENT.CH%, &
				KEY #1% GE BS_CLIENT::CLIENTNAME + "", &
				REGARDLESS

		CASE 2%
			FIND #BS_CLIENT.CH%, &
				KEY #2% GE BS_CLIENT::ALPSRT + "", &
				REGARDLESS

		END SELECT

	END SELECT

	EXIT FUNCTION

	%PAGE

29000	!*******************************************************************
	! Trap errors
	!*******************************************************************
	SELECT ERL
	!
	! Unable to open Read/Write
	!
	CASE 750%
		RESUME 760 IF ERR = 10%
		BS_MAIN_CLIENT = ERR
		RESUME 770

	!
	! Unable to open Read/Only
	!
	CASE 760%
		BS_MAIN_CLIENT = ERR
		RESUME 770

	!
	! Unable to reset
	!
	CASE 790%
		RESUME 32767

	!
	! Record does not already exist
	!
	CASE 20300%
		RESUME 32767 IF ERR = 155%

	END SELECT

	ON ERROR GO BACK

32767	END FUNCTION
