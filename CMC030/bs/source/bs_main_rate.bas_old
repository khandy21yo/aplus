1	%TITLE "Program Rate Maintenance"
	%SBTTL "BS_MAIN_RATE"
	%IDENT "V3.6a Calico"

	FUNCTION LONG BS_MAIN_RATE(CDD_WINDOW_CDD SMG_WINDOW, LONG MOPTION, &
		LONG MLOOP, LONG MFLAG, STRING MVALUE)

	!
	! COPYRIGHT (C) 1987, 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! Abstract:HELP
	!	.p
	!	This program maintains Program rate file
	!
	! Compile:
	!
	!	$ BAS BS_SOURCE:BS_MAIN_RATE/LINE
	!	$ LIB FUNC_LIB:CMCFUN/REP BS_MAIN_RATE
	!	$ DELETE BS_MAIN_RATE.OBJ;*
	!
	! Author:
	!
	!	09/25/89 - Frank F. Starman
	!
	! Modification history:
	!
	!	03/23/93 - Kevin Handy
	!		Clean up (Check)
	!
	!	04/12/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standard.
	!		Add SCOPE to ENTR_3STRING call.
	!
	!	10/17/96 - Kevin Handy
	!		Reformat source code
	!
	!	05/23/97 - Kevin Handy
	!		Use integer for #key
	!
	!	08/19/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--

	!
	! Set up compiling options
	!
	OPTION SIZE = (REAL GFLOAT, INTEGER LONG)

	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "FUNC_INCLUDE:BS_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:UTL_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:MAIN_WINDOW.COM"

	%INCLUDE "SOURCE:[CDD.OPEN]CDD_WINDOW.HB"

	%INCLUDE "SOURCE:[BS.OPEN]BS_RATE.HB"
	MAP (BS_RATE)		BS_RATE_CDD	BS_RATE
	MAP (BS_RATE_OLD)	BS_RATE_CDD	BS_RATE_OLD
	MAP (BS_RATE_DEF)	BS_RATE_CDD	BS_RATE_DEF

	%INCLUDE "SOURCE:[UTL.OPEN]UTL_MEASURE.HB"
	MAP	(UTL_MEASURE)		UTL_MEASURE_CDD	UTL_MEASURE

	!
	! Common Areas
	!
	! These areas store information that is re-used between
	! calls to these functions.
	!
	COM (CH_BS_RATE) &
		BS_RATE.CH%, &
		BS_RATE.READONLY%

	!
	! External functions
	!
	EXTERNAL LONG    FUNCTION MAIN_WINDOW

	%PAGE

	ON ERROR GOTO 29000

	SELECT MOPTION

	!
	! Initialization
	!
	! This option is used to initialize the window structure,
	! set up the default values for add, and open all files
	! necessary that have not already been opened.
	!
	CASE OPT_INIT

		!*************************************************************
		! Set up information
		!*************************************************************

		!
		! Define window
		!
		SMG_WINDOW::DESCR = "Program Rate"
		SMG_WINDOW::CURREC = -2%
		SMG_WINDOW::NHELP = "BS_MAIN_RATE"
		SMG_WINDOW::HSIZE = 76%
		SMG_WINDOW::VSIZE = 11%
		SMG_WINDOW::HPOS  = 3%
		SMG_WINDOW::VPOS  = 8%
		SMG_WINDOW::FLAGS = 0%
		SMG_WINDOW::NITEMS= 3%

		SMG_WINDOW::NKEYS = 1%
		SMG_WINDOW::KNAME(0%) = "Rate_UOM"
			SMG_WINDOW::KFIELD(0%, 0%) = 2%
			SMG_WINDOW::KFIELD(0%, 1%) = 1%
			SMG_WINDOW::KFIELD(0%, 2%) = 2%

		SMG_WINDOW::HVIEW = 76%
		SMG_WINDOW::VVIEW = 11%
		SMG_WINDOW::VVPOS = 8%
		SMG_WINDOW::VHPOS = 3%

		CALL READ_DEFAULTS(SMG_WINDOW) &
			IF INSTR(1%, " QV", MVALUE) <= 1%

700		!
		! Declare channels
		!
		IF BS_RATE.CH% > 0%
		THEN
			!
			! Already open, set flag to read-only if was
			! that way from last time.
			!
			SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%) &
				IF BS_RATE.READONLY%
			GOTO 790
		END IF

		!
		! Open main file (existing) for modification
		!
750		%INCLUDE "SOURCE:[BS.OPEN]BS_RATE.CRE"
		BS_RATE.READONLY% = 0%
		GOTO 790

760		!
		! If unable to open for modify, try to open with read
		! access only.
		!
		%INCLUDE "SOURCE:[BS.OPEN]BS_RATE.OPN"
		SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%)
		BS_RATE.READONLY% = -1%

		GOTO 790

770		!
		! File not open, so reset channel
		!
		CALL ASSG_FREECHANNEL(BS_RATE.CH%)

		EXIT FUNCTION

790		SMG_WINDOW::CHAN  = BS_RATE.CH%
		RESET #BS_RATE.CH%
		GET #BS_RATE.CH%, REGARDLESS

	!
	! Display the background
	!
	! This option is used to display the background information on the
	! screen.  It must first clear any junk on the screen, and then
	! write the background onto it.
	!
20100	CASE OPT_BACKGROUND

		SMG_STATUS% = SMG$BEGIN_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

		SMG_STATUS% = SMG$ERASE_DISPLAY(SMG_WINDOW::WNUMBER)


		DATA	04,05, "(01) Rate UOM", &
			05,05, "(02) Eff Date", &
			06,05, "(03) Rate Amount", &
			0, 0, ""

		RESTORE

		READ XPOS%, YPOS%, XSTR$
		I%=0%
		WHILE (XPOS% <> 0)
			I% = I% + 1%
			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				XSTR$, XPOS%, YPOS%) &
				IF (SMG_WINDOW::HFLAG(I%) AND 2%)=0%
		READ XPOS%, YPOS%, XSTR$
		NEXT

		SMG_STATUS% = SMG$END_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

	!
	! Enter/Display/Default
	!
	! This option is used to enter the data from the user, display data,
	! set defaults, and return the data back according to MFLAG.
	!
20200	CASE OPT_ENTRY

		TEMP$, TEMP1$ = TRM$(SCOPE::PRG_ITEM)
		TEMP$ = "View starting at" IF TEMP$ = "View"

		SCOPE::PRG_ITEM = "FLD" + FORMAT$(MLOOP, "<0>##")

 ReEnter:	SCOPE::SCOPE_EXIT = 0%

		SELECT MLOOP

		CASE 1%
			BS_RATE::RATEUOM = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, "04;28", TEMP$, &
				BS_RATE::RATEUOM, MFLAG, "'E", &
				MVALUE)

			IF (SCOPE::SCOPE_EXIT = SMG$K_TRM_F14)
			THEN
				IF (MAIN_WINDOW(UTL_MAIN_MEASURE.ID, "V0  ") = 1%)
				THEN
					BS_RATE::RATEUOM = &
						UTL_MEASURE::CODE
				END IF
				GOTO Reenter
			END IF

		CASE 2%
			BS_RATE::EFFDATE = ENTR_3DATE(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"05;28", TEMP$, &
				BS_RATE::EFFDATE, MFLAG, "'E", &
				MVALUE)

		CASE 3%
			BS_RATE::RATE = ENTR_3NUMBER(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"06;28",TEMP$, BS_RATE::RATE, MFLAG, &
				"#,###.##", MVALUE )

		END SELECT

		SCOPE::PRG_ITEM = TEMP1$

	!
	! Set BS_RATE_OLD value
	!
20500	CASE OPT_SETOLD
		BS_RATE_OLD = BS_RATE

	!
	! Restore BS_RATE_OLD value
	!
	CASE OPT_RESETOLD
		BS_RATE = BS_RATE_OLD

	!
	! Set default value
	!
	CASE OPT_SETDEFAULT
		BS_RATE_DEF = BS_RATE

	!
	! Restore default value
	!
	CASE OPT_RESETDEFAULT
		BS_RATE = BS_RATE_DEF
		BS_RATE::PRG = MVALUE

	!
	! View header
	!
	CASE OPT_VIEW
		SELECT MLOOP

		!
		! Title (One line only)
		!
		CASE 1%
			MVALUE = "  UOM  Date         Rate"

		!
		! Positions of lines
		!
		CASE 2%
			MVALUE = "007,016"

		!
		! Convert current record into text
		!
		CASE 3%
			MVALUE = BS_RATE::RATEUOM + "   "+ &
				PRNT_DATE(BS_RATE::EFFDATE, 0%) + " " + &
				FORMAT$(BS_RATE::RATE,"#,###.##")



		END SELECT

	!
	! Find
	!
	CASE OPT_FIND
		SELECT MLOOP
		CASE 0%
			FIND #SMG_WINDOW::CHAN, &
				KEY#0% GE BS_RATE::PRG + &
				BS_RATE::RATEUOM + &
				BS_RATE::EFFDATE, &
				REGARDLESS

		END SELECT


	!
	! Handle array of records
	!
	CASE OPT_SUBWIND

		SELECT MLOOP

		!
		! Find first record (if there is any)
		!
		CASE 1%

			!
			! Set init value
			!
			SMG_WINDOW::CURREC = -1%

			!
27110			!
			! Search for first record
			!
			FIND #SMG_WINDOW::CHAN,	KEY #0% EQ MVALUE, REGARDLESS

			!
			! Get a record
			!
			GET #SMG_WINDOW::CHAN
			SMG_WINDOW::CURREC = 0%

		!
		! Check if still right key
		!
		CASE 3%
			SMG_WINDOW::CURREC = -1%
			IF BS_RATE::PRG = MVALUE
			THEN
				SMG_WINDOW::CURREC = 0%
			END IF

		!
		! Set the new key
		!
		CASE 6%
			BS_RATE::PRG = MVALUE

		END SELECT

	END SELECT

28000	EXIT FUNCTION

29000	!***************************************************************
	! Trap errors
	!***************************************************************
	SELECT ERL

	!
	! Unable to open Read/Write
	!
	CASE 750%
		RESUME 760 IF ERR = 10%
		BS_MAIN_RATE = ERR
		RESUME 770

	!
	! Unable to open Read/Only
	!
	CASE 760%
		BS_MAIN_RATE = ERR
		RESUME 770

	!
	! Unable to reset
	!
	CASE 790%
		RESUME 32767

	!
	! Error while searching for the first record
	!
	CASE 27110%
		RESUME 28000 IF ERR = 155%

	END SELECT

	ON ERROR GO BACK

32767	END FUNCTION
