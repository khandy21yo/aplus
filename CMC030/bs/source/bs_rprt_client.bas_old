1	%TITLE "BS Client File List"
	%SBTTL "BS_RPRT_CLIENT"
	%IDENT "V3.6a Calico"

	!
	! COPYRIGHT (C) 1986, 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! ID:BS004
	!
	! Abstract:HELP
	!	.p
	!	This program prints out a list of the BS client file
	!
	! Compile:
	!
	!	$ BAS BS_SOURCE:BS_RPRT_CLIENT/LINE
	!	$ LINK/EXE=BS_EXE: BS_RPRT_CLIENT, FUNC_LIB:CMCLINK/OPTION
	!	$ DELETE BS_RPRT_CLIENT.OBJ;*
	!
	! Author:
	!
	!	09/29/89 - Frank F. Starman
	!
	! Modification History:
	!
	!	04/22/92 - Kevin Handy
	!		Clean up (check)
	!
	!	03/24/93 - Kevin Handy
	!		Clean up (Check)
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	09/03/96 - Kevin Handy
	!		Reformat source code.
	!
	!	05/23/97 - Kevin Handy
	!		Use integer for #key
	!
	!	08/19/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--
	%PAGE

	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	!
	! Include files
	!
	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "SOURCE:[UTL.OPEN]UTL_REPORTX.HB"
	DECLARE			UTL_REPORTX_CDD	UTL_REPORTX

	%INCLUDE "SOURCE:[BS.OPEN]BS_CLIENT.HB"
	MAP	(BS_CLIENT)	BS_CLIENT_CDD	BS_CLIENT

	%PAGE

	ON ERROR GOTO 19000

	!
	! Initilize report
	!
	CALL OUTP_INITFROMFILE(UTL_REPORTX, 132%)

	GOTO ExitProgram IF UTL_REPORTX::STAT

	SORT.BY$ = EDIT$(UTL_REPORTX::OPTDEF(0%), 132%)
	FROM.ITEM$ = EDIT$(UTL_REPORTX::OPTDEF(1%), 132%)
	TO.ITEM$ = EDIT$(UTL_REPORTX::OPTDEF(2%), 132%)
	WLDCRD$ = EDIT$(UTL_REPORTX::OPTDEF(3%), -1%)

	SELECT SORT.BY$
	CASE "C"
		SORT.KEY% = 0%
		ADD.TITLE$ = "BY  CLIENT  NUMBER"

	CASE "N"
		SORT.KEY% = 1%
		ADD.TITLE$ = "BY  NAME"
	END SELECT

300	%INCLUDE "SOURCE:[BS.OPEN]BS_CLIENT.OPN"

 ReportTitle:
	!
	! Title
	!
	TITLE$(1%) = "BS  CLIENT  FILE  LIST  "+ADD.TITLE$
	TITLE$(2%) = "Billing Services System"
	TITLE$(3%) = ""

	!
	! Heading
	TITLE$(4%) = "ClientNum  ClientName                             "+ &
		"            AlhpaSortField  ReferenceNumber  Birthdate  Sex"+ &
		" OnsetDate "
	TITLE$(5%) = "          Address1                  Address2      "+ &
			"        City            State ZipCode    Country Phone"
	TITLE$(6%) = "."

	%PAGE

17000	!******************************************************************
	! OUTPUT REPORT
	!******************************************************************

	IF FROM.ITEM$ = ""
	THEN
		RESET #BS_CLIENT.CH%, KEY#SORT.KEY%
	ELSE
		FIND #BS_CLIENT.CH%, KEY#0% GE FROM.ITEM$,REGARDLESS
	END IF

 GetNextRec:
17020	!
	! Main loop starts here
	!
	GOTO ExitProgram IF UTL_REPORTX::STAT

	!
	! Get next record
	!
	GET #BS_CLIENT.CH%, REGARDLESS

	!
	! Check current record
	!
	SELECT SORT.BY$

	CASE "C"
		GOTO ExitTotal IF (BS_CLIENT::CLIENT > TO.ITEM$) AND &
			TO.ITEM$ <> ""

		GOTO GetNextRec IF WLDCRD$ <> "" AND &
			COMP_STRING(EDIT$(BS_CLIENT::CLIENT,-1%), WLDCRD$) = 0%

	CASE "N"
		GOTO ExitTotal IF (BS_CLIENT::CLIENTNAME > TO.ITEM$) AND &
			TO.ITEM$ <> ""

		GOTO GetNextRec IF WLDCRD$ <> "" AND &
			COMP_STRING(EDIT$(BS_CLIENT::CLIENTNAME,-1%), &
			WLDCRD$) = 0%

	END SELECT

17300	!
	! Print 1st line
	!
	TEXT$ = BS_CLIENT::CLIENT	+ " " + &
		BS_CLIENT::CLIENTNAME	+ " " + &
		BS_CLIENT::ALPSRT	+ " " + &
		BS_CLIENT::REFNO	+ " " + &
		PRNT_DATE(BS_CLIENT::BIRTHDATE,8%) + " " + &
		BS_CLIENT::SEX		+ "   " + &
		PRNT_DATE(BS_CLIENT::ONSETDATE,8%)

	CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, 0%)

	!
	! Print 2nd line
	!
	TEXT$ = SPACE$(10%)+ &
		BS_CLIENT::ADD1	+ " " + &
		BS_CLIENT::ADD2	+ " " + &
		BS_CLIENT::CITY	+ " " + &
		BS_CLIENT::STATE	+ "    " + &
		BS_CLIENT::ZIP		+ " " + &
		BS_CLIENT::COUNTRY	+ "      " + &
		BS_CLIENT::PHONE

	CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, 0%)

	GOTO ExitProgram IF UTL_REPORTX::STAT

17350	!
	! Try for next record
	!
	GOTO GetNextRec

 ExitTotal:
17400	!
	! Handle end of report
	!

 ExitProgram:
	CALL OUTP_FINISH(UTL_REPORTX)

	!
	! Exit to next program or menu
	!
	IF TRM$(UTL_REPORTX::NEXTRUN) = ""
	THEN
		CALL SUBR_3EXITPROGRAM(SCOPE, "", "")
	ELSE
		CALL SUBR_3EXITPROGRAM(SCOPE, "RUN " + UTL_REPORTX::NEXTRUN, "")
	END IF

	%PAGE

 HelpError:
	!******************************************************************
	! Help Message for an error
	!******************************************************************
	CALL HELP_34MESSAGE(SCOPE, NUM1$(ERL) + " " + ERT$(ERR), &
		"E", ERN$, FILENAME$, NUM1$(ERR))
	UTL_REPORTX::STAT = -1%
	GOTO ExitProgram

	%Page

19000	!******************************************************************
	! ERROR TRAPPING
	!******************************************************************

	SELECT ERR
	CASE 154%	! Locked record
		SLEEP 5%
		RESUME
	END SELECT

	FILENAME$ = ""
	SELECT ERL

	CASE 300%
		FILENAME$ = "BS_CLIENT"

	CASE 17000%
		CALL ENTR_3MESSAGE(SCOPE, &
			"Unable to find beginning record!", 0%)
		RESUME ExitProgram

	CASE 17020%	! End of file
		RESUME ExitTotal IF ERR = 11%
		FILENAME$ = "BS_CLIENT"

	END SELECT

	!
	! Untrapped error
	!
	RESUME HelpError

32767	END
