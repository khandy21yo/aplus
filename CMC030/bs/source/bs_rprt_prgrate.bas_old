1	%TITLE "Program Rate File List"
	%SBTTL "BS_RPRT_PRGRATE"
	%IDENT "V3.6a Calico"

	!
	! COPYRIGHT (C) 1986, 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! ID:BS005
	!
	! Abstract:HELP
	!	.p
	!	This program prints out a list of the Program rate file
	!
	! Compile:
	!
	!	$ BAS BS_SOURCE:BS_RPRT_PRGRATE/LINE
	!	$ LINK/EXE=BS_EXE: BS_RPRT_PRGRATE, FUNC_LIB:CMCLINK/OPTION
	!	$ DELETE BS_RPRT_PRGRATE.OBJ;*
	!
	! Author:
	!
	!	09/09/89 - Frank F. Starman
	!
	! Modification History:
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	09/03/96 - Kevin Handy
	!		Reformat source code.
	!
	!	05/23/97 - Kevin Handy
	!		Use integer for #key
	!
	!	08/19/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--
	%PAGE

	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	!
	! Include files
	!
	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "SOURCE:[UTL.OPEN]UTL_REPORTX.HB"
	DECLARE			UTL_REPORTX_CDD	UTL_REPORTX

	%INCLUDE "SOURCE:[BS.OPEN]BS_RATE.HB"
	MAP	(BS_RATE)	BS_RATE_CDD	BS_RATE

	%PAGE

	ON ERROR GOTO 19000

	!
	! Initilize report
	!
	CALL OUTP_INITFROMFILE(UTL_REPORTX, 132%)

	GOTO ExitProgram IF UTL_REPORTX::STAT

	FROM.ITEM$ = EDIT$(UTL_REPORTX::OPTDEF(0%), 132%)
	TO.ITEM$ = EDIT$(UTL_REPORTX::OPTDEF(1%), 132%)
	WLDCRD$ = EDIT$(UTL_REPORTX::OPTDEF(2%), -1%)

300	%INCLUDE "SOURCE:[BS.OPEN]BS_RATE.OPN"

 ReportTitle:
	!
	! Title
	!
	TITLE$(1%) = "PROGRAM  RATE  LIST"
	TITLE$(2%) = "Billing Services System"
	TITLE$(3%) = ""

	!
	! Heading
	!
	TITLE$(4%) = "Program    RateUOM EffDate        Rate"
	TITLE$(5%) = "."

	LYT_LINE$ = "$Program:011,$RateUOM:019,DEffDate:034,VRate:042"

	%PAGE

17000	!******************************************************************
	! OUTPUT REPORT
	!******************************************************************

	IF FROM.ITEM$ = ""
	THEN
		RESET #BS_RATE.CH%
	ELSE
		FIND #BS_RATE.CH%, KEY#0% GE FROM.ITEM$,REGARDLESS
	END IF

 GetNextRec:
17020	!
	! Main loop starts here
	!
	GOTO ExitProgram IF UTL_REPORTX::STAT

	!
	! Get next record
	!
	GET #BS_RATE.CH%, REGARDLESS

	!
	! Check current record
	!
	GOTO ExitTotal IF (BS_RATE::PRG > TO.ITEM$) AND &
		TO.ITEM$ <> ""

	GOTO GetNextRec IF WLDCRD$ <> "" AND &
		COMP_STRING(EDIT$(BS_RATE::PRG,-1%), WLDCRD$) = 0%

17300	!
	! Print out one line
	!
	TEXT$ = BS_RATE::PRG + " " + &
		BS_RATE::RATEUOM + "      " + &
		PRNT_DATE(BS_RATE::EFFDATE,8%) + " " + &
		FORMAT$(BS_RATE::RATE, "#,###.##")

	CALL OUTP_LINE(LYT_LINE$, UTL_REPORTX, TITLE$(), TEXT$, 0%)

	GOTO ExitProgram IF UTL_REPORTX::STAT

17350	!
	! Try for next record
	!
	GOTO GetNextRec

 ExitTotal:
17400	!
	! Handle end of report
	!

 ExitProgram:
	CALL OUTP_FINISH(UTL_REPORTX)

	!
	! Exit to next program or menu
	!
	IF TRM$(UTL_REPORTX::NEXTRUN) = ""
	THEN
		CALL SUBR_3EXITPROGRAM(SCOPE, "", "")
	ELSE
		CALL SUBR_3EXITPROGRAM(SCOPE, "RUN " + UTL_REPORTX::NEXTRUN, "")
	END IF

	%PAGE

 HelpError:
	!******************************************************************
	! Help Message for an error
	!******************************************************************
	CALL HELP_34MESSAGE(SCOPE, NUM1$(ERL) + " " + ERT$(ERR), &
		"E", ERN$, FILENAME$, NUM1$(ERR))
	UTL_REPORTX::STAT = -1%
	GOTO ExitProgram

	%Page

19000	!******************************************************************
	! ERROR TRAPPING
	!******************************************************************

	SELECT ERR
	CASE 154%	! Locked record
		SLEEP 5%
		RESUME
	END SELECT

	FILENAME$ = ""
	SELECT ERL

	CASE 300%
		FILENAME$ = "BS_CPTRATE"

	CASE 17000%
		CALL ENTR_3MESSAGE(SCOPE, &
			"Unable to find beginning record!", 0%)
		RESUME ExitProgram

	CASE 17020%	! End of file
		RESUME ExitTotal IF ERR = 11%
		FILENAME$ = "BS_RATE"

	END SELECT

	!
	! Untrapped error
	!
	RESUME HelpError

32767	END
