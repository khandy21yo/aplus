1	%TITLE "Print BT Bills Using a Form"
	%SBTTL "BT_FORM_JOURNAL"
	%IDENT "V3.6a Calico"

	!
	!	COPYRIGHT (C) 1987, 1988 BY
	!	Computer Management Center, Inc.
	!	Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! ID:BTJOUR
	!
	! Abstract:HELP
	!	.p
	!	The ^*Print Bills using a Form\* option provides the means to print a bill
	!	for each guardian in a specified form. This bill may be printed in alignment
	!	form and must include the following fields:
	!	.b
	!	.lm 15
	!	.list 0,"*"
	!	.le
	!	Guardian Name
	!	.le
	!	Guardian Address
	!	.le
	!	Guardian Number
	!	.le
	!	Client
	!	.le
	!	Rate
	!	.le
	!	Total Amount
	!	.els
	!
	! Index:
	!	.x Print Bills Using a Form
	!	.x Bills>Print Using a Form
	!
	! Option:
	!
	!	BT_FORM$BTJOUR
	!
	! Compile:
	!
	!	$ BAS BT_SOURCE:BT_FORM_JOURNAL/LINE
	!	$ LINK/EXECUTABLE=BT_EXE:*.EXE BT_FORM_JOURNAL, -
	!		FUNC_LIB:CMCLINK/OPTION
	!	$ DELETE BT_FORM_JOURNAL.OBJ;*
	!
	! Author:
	!
	!	06/07/88 - Kevin Handy
	!
	! Modification history:
	!
	!	07/08/88 - Kevin Handy
	!		Modified journal file to contain child name
	!		instead of pointing to the employee master file.
	!
	!	06/04/91 - J. Shad Rydalch
	!		Added form fields for address that will keep
	!		post office happy.
	!
	!	06/18/91 - Craig Tanner
	!		Added section to update user UTL_REPORT file with the
	!		master.
	!
	!	03/12/92 - Kevin Handy
	!		Removed duplicate error trapping (check)
	!
	!	06/11/92 - Kevin Handy
	!		Modified to use OUTP_INITFORM function.
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	08/13/96 - Kevin Handy
	!		Reformat source code.
	!
	!	05/10/97 - Kevin Handy
	!		Lose PRNT.CH% variable
	!
	!	05/13/97 - Kevin Handy
	!		Reformat source code.
	!
	!	09/09/97 - Kevin Handy
	!		Lose unecessary external definitions
	!
	!	08/19/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!
	!	10/25/98 - Kevin Handy
	!		Don't erase SMG_SCREEN_DATA%, which is
	!		never created.
	!--
	%PAGE

	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	!
	! Include files
	!
	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE


	!
	! Define maps
	!
	%INCLUDE "SOURCE:[AR.OPEN]AR_35CUSTOM.HB"
	MAP (AR_35CUSTOM)	AR_35CUSTOM_CDD	AR_35CUSTOM

	%INCLUDE "SOURCE:[BT.OPEN]BT_JOURNALH.HB"
	MAP (BT_JOURNALH)	BT_JOURNALH_CDD	BT_JOURNALH

	%INCLUDE "SOURCE:[BT.OPEN]BT_JOURNALL.HB"
	MAP (BT_JOURNALL)	BT_JOURNALL_CDD	BT_JOURNALL

	%INCLUDE "SOURCE:[BT.OPEN]BT_CUSTYP.HB"
	MAP (BT_RESP_STAFF) BT_CUSTYP_CDD	BT_RESP_STAFF
	MAP (BT_CUSTYP) BT_CUSTYP_CDD	BT_CUSTYP

	%INCLUDE "SOURCE:[AR.OPEN]AR_CUSTYPE.HB"
	MAP (AR_CUSTYPE)		AR_CUSTYPE_CDD	AR_CUSTYPE


	%INCLUDE "SOURCE:[BT.OPEN]BT_CONTROL.HB"
	MAP (BT_CONTROL) BT_CONTROL_CDD BT_CONTROL

	%INCLUDE "SOURCE:[UTL.OPEN]UTL_REPORT.HB"
	MAP (UTL_REPORT) UTL_REPORT_CDD UTL_REPORT
	DECLARE UTL_REPORT_CDD UTL_REPORT_SYS

	%INCLUDE "SOURCE:[UTL.OPEN]UTL_REPORTX.HB"
	DECLARE UTL_REPORTX_CDD UTL_REPORTX

	%INCLUDE "SOURCE:[UTL.OPEN]FORM_GROUP.HB"
	DIM FORM_GROUP_CDD FORM_GROUP(20%)	! Max of 20 groups

	COM (BT_FORM) &
		TOTAL_RATE, &
		INVOICE_DATE$ = 8%, &
		PAGE_NUMBER%, &
		REF_NO$, &
		AR_CUSTOM.ADDLINE$(4%) = 50%

	!
	! External functions
	!
	EXTERNAL LONG		FUNCTION OUTP_FORMINIT
	EXTERNAL LONG		FUNCTION OUTP_FORMPRINT
	EXTERNAL LONG   FUNCTION OUTP_INITFORM

	%PAGE

	ON ERROR GOTO 19000

	!*******************************************************************
	! Initilize maintainence
	!*******************************************************************

	CALL READ_INITIALIZE

	REPORT$ = "BTJOUR"

	!
	! Look up device
	!
	CALL  READ_DEVICE("BT_FORM", BT_FORM.DEV$, STAT%)

	!***************************************************************
	! Open all of the files
	!***************************************************************

300	!
	! Open customer master file
	!
	%INCLUDE "SOURCE:[AR.OPEN]AR_35CUSTOM.OPN"

331	!
	! TM Control file
	!
	%INCLUDE "SOURCE:[BT.OPEN]BT_CONTROL.MOD"

	GET #BT_CONTROL.CH%, REGARDLESS

	UNLOCK #BT_CONTROL.CH%

	BT_CONTROL::INV_NUM = XLATE(BT_CONTROL::INV_NUM, &
		STRING$(48%,0%) + "0123456789")

335	!
	! Open the journal header file
	!
	%INCLUDE "SOURCE:[BT.OPEN]BT_JOURNALH.MOD"

336	!
	! Open the journal line file
	!
	%INCLUDE "SOURCE:[BT.OPEN]BT_JOURNALL.OPN"
337
	!
	! Open Customer Type file
	!
	%INCLUDE "SOURCE:[AR.OPEN]AR_CUSTYPE.OPN"

339	!
	! Open the staff file
	!
	%INCLUDE "SOURCE:[BT.OPEN]BT_CUSTYP.OPN"

340	!
	! Get Report
	!
	OPTN$ = "01" + BT_CONTROL::INV_NUM

	GOTO ExitProgram &
		IF OUTP_INITFORM(UTL_REPORTX, REPORT$, OPTN$) <> CMC$_NORMAL


	INVOICE_DATE$ = DATE_STOREDATE(UTL_REPORTX::OPTDEF(0%))
	!++
	! Abstract:FLD01
	!	^*(01) Invoice Date\*
	!	.p
	!	The ^*Invoice Date\* field provides the means to enter the date for which
	!	the invoices will be printed.  This field requires and entry,
	!
	! Index:
	!	.x Invice Date>Print Bills Using a Form
	!	.x Print Bills Using a Form>Invoice Date
	!	.x Date>Invoice
	!
	!--

	REF_NO$ = UTL_REPORTX::OPTDEF(1%)

	!++
	! Abstract:FLD02
	!	^*(02) Starting Invoice\*
	!	.p
	!	The ^*Starting Invoice\* provides the means contains the starting invoice
	!	number.  Normally this number is a default, but it may be changed if needed.
	!
	! Index:
	!	.x Starting Invoice
	!	.x Invoice>Starting
	!
	!--

	REPORT$ = REPORT$ + "$" + TRM$(UTL_REPORTX::OPTDEF(2%))
	!++
	! Abstract:FLD03
	!	^*(03) Form Name\*
	!	.p
	!	The ^* Form Name\* field provides for entry of the form which will be
	!	used in the printing.
	!
	! Index:
	!	.x Form Name
	!
	!--

	BATCH_NO$ = EDIT$(UTL_REPORTX::OPTDEF(3%), -1%)
	!++
	! Abstract:FLD04
	!	^*(04) Batch Number\*
	!	.p
	!	The ^*Batch Number\* field allows for entry of a selected
	!	batch number. Each Journal File is assigned
	!	a user batch number consisting of two (2) alphanumeric
	!	characters.
	!
	! Index:
	!	.x Batch Number>Order Entry Order Form
	!	.x Order Entry Order Form>Batch Number
	!
	!--

	!
	! Restore original values for the help message
	!
	SCOPE::PRG_IDENT = TEMP_IDENT$
	SCOPE::PRG_PROGRAM = TEMP_PROGRAM$

590	!
	! Load in the form
	!
	GOSUB LoadForm

	!
	! GOTO aligment routine
	!
	GOSUB Alignment

	%PAGE

2000	!*******************************************************************
	! Read through CDJ file
	!*******************************************************************

	RESET #BT_JOURNALH.CH%

2010	GET #BT_JOURNALH.CH%

	!
	! Skip unless invoice number field is blank
	!
	GOTO 2010 UNLESS BT_JOURNALH::INVNUM = ""

	TOTAL_RATE = 0.0

2020	!
	! Get the customer name for this line
	!
	GET #AR_35CUSTOM.CH%, KEY#0% EQ BT_JOURNALH::CUSNUM, REGARDLESS

	!
	! Create an address line format that reduces white space
	!
	I% = 0%

	IF EDIT$(AR_35CUSTOM::ADD1, -1%) <> ""
	THEN
		I% = I% + 1%
		AR_CUSTOM.ADDLINE$(I%) = &
			EDIT$(AR_35CUSTOM::ADD1, 8% + 16% + 32% + 128%)
	END IF

	IF EDIT$(AR_35CUSTOM::ADD2, -1%) <> ""
	THEN
		I% = I% + 1%
		AR_CUSTOM.ADDLINE$(I%) = &
			EDIT$(AR_35CUSTOM::ADD2, 8% + 16% + 32% + 128%)
	END IF

	IF EDIT$(AR_35CUSTOM::ADD3, -1%) <> ""
	THEN
		I% = I% + 1%
		AR_CUSTOM.ADDLINE$(I%) = &
			EDIT$(AR_35CUSTOM::ADD3, 8% + 16% + 32% + 128%)
	END IF

	I% = I% + 1%
	AR_CUSTOM.ADDLINE$(I%) = &
		EDIT$(EDIT$(AR_35CUSTOM::CITY, 128%) + ", " + &
		AR_35CUSTOM::STATE + " " + AR_35CUSTOM::ZIP + " " + &
		AR_35CUSTOM::COUNTRY, 8% + 16% + 32% + 128%)

	AR_CUSTOM.ADDLINE$(LOOP%) = "" FOR LOOP% = I% + 1% TO 4%

2040	!
	! Get customer type info
	!
	GET #BT_CUSTYP.CH%, KEY#0% EQ AR_35CUSTOM::TTYPE, REGARDLESS

2050
	GET #AR_CUSTYPE.CH%, KEY#0% EQ AR_35CUSTOM::TTYPE, REGARDLESS


2100	BT_JOURNALH::INVNUM = REF_NO$	! Increment invoice number

	GOSUB PrintForm

2200	UPDATE #BT_JOURNALH.CH%

	STAT% = FUNC_INCREMENT(REF_NO$)	! Increment invoice number

	GOTO 2010

 ExitProgram:
4000	!********************************************************************
	! Get control record and update check number
	!********************************************************************

	GET #BT_CONTROL.CH%, RECORD 1%

	BT_CONTROL::INV_NUM = REF_NO$

	UPDATE #BT_CONTROL.CH%

4010	!******************************************************************
	! Exit the program
	!******************************************************************

	CALL OUTP_FINISHNOTITLE(UTL_REPORTX)

	CLOSE BT_CONTROL.CH%

	!
	! Erase Display
	!
	SMG_STATUS% = SMG$ERASE_DISPLAY(SCOPE::SMG_OPTION)
	SMG_STATUS% = SMG$ERASE_DISPLAY(SCOPE::SMG_MESSAGE)

	!
	! Change the width
	!
	SMG_STATUS% = SMG$CHANGE_PBD_CHARACTERISTICS(SCOPE::SMG_PBID, 80%)

 ExitProgram1:
	CALL SUBR_3EXITPROGRAM(SCOPE, "", "")

	%PAGE

 PrintForm:
18000	!***************************************************************
	! Print the bill now
	!***************************************************************

	FOR FORM_LOOP% = 1% TO FORM_GROUP%

		!
		! Page if necessary
		!
		IF LINE_COUNT% > FORM_GROUP(FRM_TOP%)::NUMBER - 4%
		THEN
			GOSUB NewPage
		END IF

		!
		! Top of the form
		!
		IF (FORM_GROUP(FORM_LOOP%)::FGROUP = "FRM-TOP")
		THEN
			LINE_COUNT% = LINE_COUNT% + &
				OUTP_FORMPRINT(UTL_REPORTX, &
				FORM_LOOP%, &
				FORM_TEXT$, &
				FORM_GROUPS%, &
				FORM_GROUP(), &
				0%)
			GOTO 18080
		END IF

		!
		! Body of a group
		!
		IF (FORM_GROUP(FORM_LOOP%)::FGROUP = "FRM-BODY")
		THEN
			GOSUB GuardianGroups

			GOTO 18080
		END IF

		!
		! Bottom of the form
		!
		IF (FORM_GROUP(FORM_LOOP%)::FGROUP = "FRM-BOTTOM")
		THEN
			LINE_COUNT% = LINE_COUNT% + &
				OUTP_FORMPRINT(UTL_REPORTX, &
				FORM_LOOP%, &
				FORM_TEXT$, &
				FORM_GROUPS%, &
				FORM_GROUP(), &
				0%)
			GOTO 18080
		END IF

		!
		! Don't know what it is.  Fake it.
		!
		LINE_COUNT% = LINE_COUNT% + OUTP_FORMPRINT(UTL_REPORTX, &
			FORM_LOOP%, &
			FORM_TEXT$, &
			FORM_GROUPS%, &
			FORM_GROUP(), &
			0%)

18080	NEXT FORM_LOOP%

	!
	! Print lines to botton of the form
	!
	FOR LOOP% = LINE_COUNT% + 1% TO FORM_GROUP(FRM_TOP%)::NUMBER
		CALL OUTP_LINENOTITLE(LYT_LINE$, UTL_REPORTX, "", 0%)
	NEXT LOOP%

	LINE_COUNT% = 0%

18190	!
	! Done with this form
	!
	RETURN

 NewPage:
	!*******************************************************************
	! Skip to next page
	!*******************************************************************

	FOR LOOP% = LINE_COUNT% + 1% TO FORM_GROUP(FRM_TOP%)::NUMBER
		CALL OUTP_LINENOTITLE(LYT_LINE$, UTL_REPORTX, "", 0%)
	NEXT LOOP%

	!
	! Set up for new page
	!
	LINE_COUNT% = 0%
	PAGE_NUMBER% = PAGE_NUMBER% + 1%

	!
	! Print top of page, if we have one.
	!
	LINE_COUNT% = LINE_COUNT% + OUTP_FORMPRINT(UTL_REPORTX, &
		FRM_TOP%, &
		FORM_TEXT$, &
		FORM_GROUPS%, &
		FORM_GROUP(), &
		0%) &
		IF FRM_TOP%

	RETURN

	%PAGE

 GuardianGroups:
18200	!*******************************************************************
	! Print out all info that exists for a specific guardian
	!*******************************************************************

	THIS_COUNT% = 0%

	!
	! Get first item
	!
	FIND #BT_JOURNALL.CH%, KEY #0% GE BT_JOURNALH::CUSNUM, REGARDLESS

18210	!
	! Pull up next
	!
	GET #BT_JOURNALL.CH%, REGARDLESS

	!
	! Exit if done
	!
	GOTO 18290 IF BT_JOURNALH::CUSNUM <> BT_JOURNALL::CUSNUM

	!
	! Total invoice
	!
	TOTAL_RATE = TOTAL_RATE + BT_JOURNALL::RATE

18230	!
	! Print out line
	!

	!
	! Page if necessary
	!
	IF (LINE_COUNT% > FORM_GROUP(FRM_TOP%)::NUMBER - 8%) OR &
		((THIS_COUNT% >= FORM_GROUP(FORM_LOOP%)::NUMBER - 1%) AND &
		(FORM_GROUP(FORM_LOOP%)::NUMBER <> 0%))
	THEN
		GOSUB NewPage
	END IF

	TEMP_COUNT% = OUTP_FORMPRINT(UTL_REPORTX, &
		FORM_LOOP%, &
		FORM_TEXT$, &
		FORM_GROUPS%, &
		FORM_GROUP(), &
		0%)

	THIS_COUNT% = THIS_COUNT% + TEMP_COUNT%
	LINE_COUNT% = LINE_COUNT% + TEMP_COUNT%

	!
	! Get next record
	!
	GOTO 18210

18290	!
	! Exit from section
	!
	IF (FORM_GROUP(FORM_LOOP%)::NUMBER <> 0%)
	THEN
		FOR LOOP% = THIS_COUNT% + 1% TO FORM_GROUP(FRM_LOOP%)::NUMBER
			CALL OUTP_LINENOTITLE(LYT_LINE$, UTL_REPORTX, "", 0%)
			LINE_COUNT% = LINE_COUNT% + 1%
		NEXT LOOP%
	END IF

	RETURN

	%PAGE

 Alignment:
	!*******************************************************************
	! Print alignment form, if desireable
	!*******************************************************************

	SMG_STATUS% = SMG$ERASE_DISPLAY(SCOPE::SMG_OPTION)
	SMG_STATUS% = SMG$ERASE_DISPLAY(SCOPE::SMG_MESSAGE)

	PAGE_NUMBER% = 1%
	LINE_COUNT% = 0%

	UTL_REPORTX::LINENO = 0%
	UTL_REPORTX::PAGENO = 0%

	SCOPE::PRG_ITEM = "ALIGNMENT"
	JUNK$ = ENTR_3YESNO(SCOPE, SCOPE::SMG_OPTION, "", &
		"Do you want an alignment form?  Confirm then press <Do> ", &
		"N", 0%, "'E", "")

	SMG_STATUS% = SMG$ERASE_DISPLAY(SCOPE::SMG_OPTION)

	SELECT SCOPE::SCOPE_EXIT

	!
	! An exit key was typed
	!
	CASE 3%, SMG$K_TRM_CTRLZ, SMG$K_TRM_F10
		GOTO ExitProgram

	!
	! Return, etc. act as next screen
	!
	CASE 10%, 12%, 13%, SMG$K_TRM_F7, SMG$K_TRM_DO

	!
	! Case else
	!
	CASE ELSE
		CALL ENTR_3BADKEY(SCOPE,SCOPE::SCOPE_EXIT)
		GOTO Alignment

	END SELECT

	GOTO AlignmentReturn IF JUNK$ <> "Y"

	!
	! Load in the current form for the alignment
	!
	FOR FORM_LOOP% = 1% TO FORM_GROUP%

		IF (LEFT(FORM_GROUP(FORM_LOOP%)::FGROUP, 8%) = "FRM-BODY")
		THEN
			LINE_COUNT% = LINE_COUNT% + &
				OUTP_FORMPRINT(UTL_REPORTX, &
				FORM_LOOP%, &
				FORM_TEXT$, &
				FORM_GROUPS%, &
				FORM_GROUP(), &
				1%) &
				FOR I% = 1% TO 3%

			IF (FORM_GROUP(FORM_LOOP%)::NUMBER <> 0%)
			THEN
				FOR LOOP% = THIS_COUNT% + 1% TO FORM_GROUP(FRM_LOOP%)::NUMBER
					CALL OUTP_LINENOTITLE(LYT_LINE$, UTL_REPORTX, "", 0%)
					LINE_COUNT% = LINE_COUNT% + 1%
				NEXT LOOP%
			END IF
		ELSE
			LINE_COUNT% = LINE_COUNT% + &
				OUTP_FORMPRINT(UTL_REPORTX, &
				FORM_LOOP%, &
				FORM_TEXT$, &
				FORM_GROUPS%, &
				FORM_GROUP(), &
				1%)
		END IF

	NEXT FORM_LOOP%

	!
	! Print lines to botton of the form
	!
	CALL OUTP_LINENOTITLE(LYT_LINE$, UTL_REPORTX, "", 0%) &
		FOR LOOP% = LINE_COUNT% + 1% TO FORM_GROUP(FRM_TOP%)::NUMBER

	!
	! Do they need another?
	!
	GOTO Alignment

 AlignmentReturn:
	RETURN

	%PAGE

 LoadForm:
	!*******************************************************************
	! Initilize bill form
	!*******************************************************************

	!
	! Get form from the AR form library
	!
	SMG_STATUS% = OUTP_FORMINIT(	BT_FORM.DEV$ + "BT_FORM", REPORT$, &
		FORM_TEXT$, FORM_GROUP%, FORM_GROUP())

	!
	! Was there an error?
	!
	IF SMG_STATUS% <> 0%
	THEN
		CALL HELP_34MESSAGE(SCOPE, "statement form is missing", &
			"E", SCOPE::PRG_PROGRAM, REPORT$, NUM1$(SMG_STATUS%))
		GOTO ExitProgram
	END IF

	!
	! Search for the desired parts of the form
	!
	FRM_TOP% = 0%

	FOR I% = 1% TO FORM_GROUP%

		SELECT FORM_GROUP(I%)::FGROUP

		CASE "FRM-TOP"
			FRM_TOP% = I%

		END SELECT

	NEXT I%

	RETURN

	%PAGE

19000	!*******************************************************************
	! Trap errors
	!*******************************************************************

	!
	! Handle record locked errors
	!
	IF ERR = 154%
	THEN
		SLEEP 5%
		RESUME
	END IF

	FILENAME$ = ""
	SELECT ERL

	!
	! Unable to open customer master file
	!
	CASE 300%
		FILENAME$ = "AR_35CUSTOM"

	!
	! Unable to open the control file
	!
	CASE 331%, 4000%
		FILENAME$ = "BT_CONTROL"

	!
	! Unable to open journal file
	!
	CASE 335%
		FILENAME$ = "BT_JOURNALH" + BATCH_NO$

	CASE 336%
		FILENAME$ = "BT_JOURNALL"

	!
	! Unable to open WIP file
	!
	CASE 590%
		FILENAME$ = "BT_JOURNALL"

	CASE 337%
		FILENAME$ = "AR_CUSTYPE"

	!
	! Unable to open staff file
	!
	CASE 339%
		FILENAME$ = "BT_CUSTYP"

	!
	! End of journal header file
	!
	CASE 2000%, 2010%
		RESUME ExitProgram

	!
	! Unable to read customer file
	!
	CASE 2020%
		AR_35CUSTOM::CUSNUM = BT_JOURNALL::CUSNUM
		AR_35CUSTOM::CUSNAM = ""
		AR_35CUSTOM::ADD1 = ""
		AR_35CUSTOM::ADD2 = ""
		AR_35CUSTOM::CITY = ""
		AR_35CUSTOM::STATE = ""
		AR_35CUSTOM::ZIP = ""
		RESUME 2040

	!
	! Error finding customer type information
	!
	CASE 2040%
		BT_CUSTYP::CUSTYP = BT_JOURNALL::CUSNUM
		BT_CUSTYP::AR_ACCT = BT_JOURNALH::AR_ACCT
		BT_CUSTYP::REV_MASK = ""
		RESUME 2050

	CASE 2050%
		AR_CUSTYPE::DESCRIPTION = ""
		RESUME 2100

	!
	! End of WIP file
	!
	CASE 18200%, 18210%
		RESUME 18290

	END SELECT

	!
	! Untrapped error
	!
	RESUME 19990

19990	!
	! This moved from inside error to outside so that errors occuring
	! at lower levels could be trapped.  Basic will not allow any
	! error to occur inside of an error no matter if it is in a
	! different module.
	!
	CALL HELP_34MESSAGE(SCOPE, NUM1$(ERL) + " " + ERT$(ERR), &
		"E", ERN$, FILENAME$, NUM1$(ERR))

	GOTO ExitProgram

	!
	! Handle the cases where a file couldn't be opened
	!
	END

20000	SUB FORM_LOADVAR(VARNAME$, REALVALUE, TEXTVALUE$)

	!*******************************************************************
	! This function is used to return the value of a field back to the
	! form printer.
	!*******************************************************************

	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	!
	! Define maps
	!
	%INCLUDE "SOURCE:[AR.OPEN]AR_35CUSTOM.HB"
	MAP (AR_35CUSTOM)	AR_35CUSTOM_CDD	AR_35CUSTOM

	%INCLUDE "SOURCE:[BT.OPEN]BT_JOURNALH.HB"
	MAP (BT_JOURNALH)	BT_JOURNALH_CDD	BT_JOURNALH

	%INCLUDE "SOURCE:[BT.OPEN]BT_JOURNALL.HB"
	MAP (BT_JOURNALL)	BT_JOURNALL_CDD	BT_JOURNALL

	%INCLUDE "SOURCE:[BT.OPEN]BT_CUSTYP.HB"
	MAP (BT_RESP_STAFF) BT_CUSTYP_CDD	BT_RESP_STAFF
	MAP (BT_CUSTYP) BT_CUSTYP_CDD	BT_CUSTYP

	%INCLUDE "SOURCE:[AR.OPEN]AR_CUSTYPE.HB"
	MAP (AR_CUSTYPE)		AR_CUSTYPE_CDD	AR_CUSTYPE

	COM (BT_FORM) &
		TOTAL_RATE, &
		INVOICE_DATE$ = 8%, &
		PAGE_NUMBER%, &
		REF_NO$ = 8%, &
		AR_CUSTOM.ADDLINE$(4%) = 50%

	!
	! External functions
	!
	EXTERNAL LONG   FUNCTION LIBR_EXTRACTVAR

	%PAGE

	!
	! Set up default values
	!
	REALVALUE = 0.0
	TEXTVALUE$ = "????????"

	!
	! Pick by variable
	!
	SELECT VARNAME$

	!************************************************************
	! Fields for the AR_35CUSTOM file
	!************************************************************

	CASE "AR_35CUSTOM::CUSNUM", "AR_CUSTOM::CUSNUM"
		TEXTVALUE$ = AR_35CUSTOM::CUSNUM

	CASE "AR_35CUSTOM::CUSNAM", "AR_CUSTOM::CUSNAM"
		TEXTVALUE$ = AR_35CUSTOM::CUSNAM

	CASE "AR_35CUSTOM::TTYPE", "AR_CUSTOM::TTYPE"
		TEXTVALUE$ = AR_35CUSTOM::TTYPE

	CASE "AR_35CUSTOM::CATEGORY", "AR_CUSTOM::CATEGORY"
		TEXTVALUE$ = AR_35CUSTOM::CATEGORY

	CASE "AR_35CUSTOM::BDATE", "AR_CUSTOM::BDATE"
		TEXTVALUE$ = PRNT_DATE(AR_35CUSTOM::BDATE, 8%)

	CASE "AR_35CUSTOM::SSTATUS", "AR_CUSTOM::SSTATUS"
		TEXTVALUE$ = AR_35CUSTOM::SSTATUS

	CASE "AR_35CUSTOM::EDATE", "AR_CUSTOM::EDATE"
		TEXTVALUE$ = PRNT_DATE(AR_35CUSTOM::EDATE, 8%)

	CASE "AR_35CUSTOM::ADD1", "AR_CUSTOM::ADD1"
		TEXTVALUE$ = AR_35CUSTOM::ADD1

	CASE "AR_35CUSTOM::ADD2", "AR_CUSTOM::ADD2"
		TEXTVALUE$ = AR_35CUSTOM::ADD2

	CASE "AR_35CUSTOM::ADD3", "AR_CUSTOM::ADD3"
		TEXTVALUE$ = AR_35CUSTOM::ADD3

	CASE "AR_35CUSTOM::CITY", "AR_CUSTOM::CITY"
		TEXTVALUE$ = AR_35CUSTOM::CITY

	CASE "AR_35CUSTOM::STATE", "AR_CUSTOM::STATE"
		TEXTVALUE$ = AR_35CUSTOM::STATE

	CASE "AR_35CUSTOM::ZIP", "AR_CUSTOM::ZIP"
		TEXTVALUE$ = AR_35CUSTOM::ZIP

	CASE "AR_35CUSTOM::COUNTRY", "AR_CUSTOM::COUNTRY"
		TEXTVALUE$ = AR_35CUSTOM::COUNTRY

	CASE "AR_35CUSTOM::COUNTY", "AR_CUSTOM::COUNTY"
		TEXTVALUE$ = AR_35CUSTOM::COUNTY

	CASE "AR_CUSTOM.ADDLINE1"	! Substitute Customer Address
		TEXTVALUE$ = AR_CUSTOM.ADDLINE$(1%)

	CASE "AR_CUSTOM.ADDLINE2"	! Substitute Customer Address
		TEXTVALUE$ = AR_CUSTOM.ADDLINE$(2%)

	CASE "AR_CUSTOM.ADDLINE3"	! Substitute Customer Address
		TEXTVALUE$ = AR_CUSTOM.ADDLINE$(3%)

	CASE "AR_CUSTOM.ADDLINE4"	! Substitute Customer Address
		TEXTVALUE$ = AR_CUSTOM.ADDLINE$(4%)

	CASE "AR_35CUSTOM::PHONE", "AR_CUSTOM::PHONE"
		TEXTVALUE$ = AR_35CUSTOM::PHONE

	CASE "AR_35CUSTOM::METHOD", "AR_CUSTOM::METHOD"
		TEXTVALUE$ = AR_35CUSTOM::METHOD

	CASE "AR_35CUSTOM::STMTFLG", "AR_CUSTOM::STMTFLG"
		TEXTVALUE$ = AR_35CUSTOM::STMTFLG

	CASE "AR_35CUSTOM::ALPSRT", "AR_CUSTOM::ALPSRT"
		TEXTVALUE$ = AR_35CUSTOM::ALPSRT

	CASE "AR_35CUSTOM::SERCHRG", "AR_CUSTOM::SERCHRG"
		TEXTVALUE$ = AR_35CUSTOM::SERCHRG

	CASE "AR_35CUSTOM::TAXCODE", "AR_CUSTOM::TAXCODE"
		TEXTVALUE$ = AR_35CUSTOM::TAXCODE

	CASE "AR_35CUSTOM::TAXEXEMP", "AR_CUSTOM::TAXEXEMP"
		TEXTVALUE$ = AR_35CUSTOM::TAXEXEMP

	CASE "AR_35CUSTOM::LOCATION", "AR_CUSTOM::LOCATION"
		TEXTVALUE$ = AR_35CUSTOM::LOCATION

	CASE "AR_35CUSTOM::TERMS", "AR_CUSTOM::TERMS"
		TEXTVALUE$ = AR_35CUSTOM::TERMS

	CASE "AR_35CUSTOM::CARRIER", "AR_CUSTOM::CARRIER"
		TEXTVALUE$ = AR_35CUSTOM::CARRIER

	CASE "AR_35CUSTOM::SALESMAN", "AR_CUSTOM::SALESMAN"
		TEXTVALUE$ = AR_35CUSTOM::SALESMAN

	CASE "AR_35CUSTOM::CREDITLIM", "AR_CUSTOM::CREDITLIM"
		REALVALUE  = AR_35CUSTOM::CREDITLIM

	CASE "AR_35CUSTOM::DISCOUNT", "AR_CUSTOM::DISCOUNT"
		REALVALUE  = AR_35CUSTOM::DISCOUNT

	CASE "AR_35CUSTOM::BACKORDER", "AR_CUSTOM::BACKORDER"
		TEXTVALUE$ = AR_35CUSTOM::BACKORDER

	CASE "AR_35CUSTOM::TAXFLAG", "AR_CUSTOM::TAXFLAG"
		TEXTVALUE$ = AR_35CUSTOM::TAXFLAG

	!************************************************************
	! BT_JOURNALH fields
	!************************************************************

	CASE "BT_JOURNALH::CUSNUM"
		TEXTVALUE$ = BT_JOURNALH::CUSNUM

	CASE "BT_JOURNALH::INVNUM"
		TEXTVALUE$ = BT_JOURNALH::INVNUM

	CASE "BT_JOURNALH::AR_ACCT"
		TEXTVALUE$ = BT_JOURNALH::AR_ACCT

	CASE "BT_JOURNALH::TRADAT"
		TEXTVALUE$ = BT_JOURNALH::TRADAT

	CASE "BT_JOURNALH::NOTES"
		BT_JOURNALH.DEV1$ = "SYS$LOGIN:" IF BT_JOURNALH.DEV1$ = ""
		ST% = LIBR_EXTRACTVAR( &
			BT_JOURNALH.DEV1$ + "BT_JOURNALH", &
			LAST_NOTE$, &
			BT_JOURNALH::CUSNUM)
		POS1% = 1%
		TEXT$ = ""
 BreakLine:
		WHILE 1
			POS2% = INSTR(POS1%,LAST_NOTE$,'13'C)
			EXIT BreakLine IF POS2% = 0%
			TEXT$ = TEXT$ + SEG$(LAST_NOTE$,POS1%,POS2%) + '10'C
			POS1% = POS2% + 1%
		NEXT
		TEXTVALUE$ = TEXT$

	!************************************************************
	! BT_JOURNALL fields
	!************************************************************

	CASE "BT_JOURNALL::CUSNUM"
		TEXTVALUE$ = BT_JOURNALL::CUSNUM

	CASE "BT_JOURNALL::CHILD"
		TEXTVALUE$ = BT_JOURNALL::CHILD

	CASE "BT_JOURNALL::RATE"
		REALVALUE = BT_JOURNALL::RATE

	CASE "BT_JOURNALL::ACCT"
		TEXTVALUE$ = BT_JOURNALL::ACCT

	!************************************************************
	! Customer type
	!************************************************************

	CASE "BT_CUSTYP::CUSTYP"
		TEXTVALUE$ = BT_CUSTYP::CUSTYP

	CASE "BT_CUSTYP::DESCR"
		TEXTVALUE$ = AR_CUSTYPE::DESCRIPTION

	!************************************************************
	! Non fielded values
	!************************************************************

	CASE "TOTAL_RATE"
		REALVALUE = TOTAL_RATE

	CASE "INVOICE_DATE"
		TEXTVALUE$ = PRNT_DATE(INVOICE_DATE$, 6%)

	CASE "PAGE_NUMBER"
		REALVALUE = PAGE_NUMBER%
		TESTVALUE$ = NUM1$(PAGE_NUMBER%)

	CASE "INVOICE_NUMBER"
		TEXTVALUE$ = REF_NO$

	END SELECT

	END SUB
