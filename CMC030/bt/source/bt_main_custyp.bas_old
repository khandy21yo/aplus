1	%TITLE "Type Definition File Maintainence"
	%SBTTL "BT_MAIN_CUSTYP"
	%IDENT "V3.6a Calico"

	FUNCTION LONG BT_MAIN_CUSTYP(CDD_WINDOW_CDD SMG_WINDOW, &
		LONG MOPTION, LONG MLOOP, LONG MFLAG, STRING MVALUE)

	!
	! COPYRIGHT (C) 1987, 1988 BY
	!
	! Computer Management Center
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! Abstract:HELP
	!	.p
	!	The ^*Maintain AR to GL Table\* option in the Bill Tuition Master
	!	Table menu provides the means to access the file where Guardian Types
	!	are associated along with related Accounts Receivable and General
	!	Ledger revenue account masks.
	!
	! Index:
	!	.x Type Definition File
	!
	! Option:
	!
	!
	! Compile:
	!
	!	$ BAS BT_SOURCE:BT_MAIN_CUSTYP/LINE
	!	$ LIB FUNC_LIB:CMCFUN/REP BT_MAIN_CUSTYP
	!	$ DELETE BT_MAIN_CUSTYP.OBJ;*
	!
	!
	! Author:
	!
	!	05/25/88 - Kevin Handy
	!
	! Modification history:
	!
	!	05/11/92 - Dan Perkins
	!		Use FUNC_TESTENTRY to test input.
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	10/17/96 - Kevin Handy
	!		Reformat source code
	!
	!	05/23/97 - Kevin Handy
	!		Use integer for #key
	!
	!	08/19/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!
	!	03/10/99 - Kevin Handy
	!		Fix FIND bug
	!--
	%PAGE

	!
	! Set up compiling options
	!
	OPTION SIZE = (REAL GFLOAT, INTEGER LONG)

	!
	! Include files
	!
	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "FUNC_INCLUDE:MAIN_WINDOW.COM"
	%INCLUDE "FUNC_INCLUDE:AR_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:GL_WINDOW.INC"

	!
	! CDD inclusions and MAPs
	!
	%INCLUDE "SOURCE:[CDD.OPEN]CDD_WINDOW.HB"

	%INCLUDE "SOURCE:[BT.OPEN]BT_CUSTYP.HB"
	MAP (BT_CUSTYP)		BT_CUSTYP_CDD		BT_CUSTYP
	MAP (BT_CUSTYP_OLD)	BT_CUSTYP_CDD	BT_CUSTYP_OLD, BT_CUSTYP2

	%INCLUDE "SOURCE:[GL.OPEN]GL_CHART.HB"
	MAP (GL_CHART)		GL_CHART_CDD		GL_CHART

	%INCLUDE "SOURCE:[AR.OPEN]AR_CUSTYPE.HB"
	MAP (AR_CUSTYPE)	AR_CUSTYPE_CDD		AR_CUSTYPE

	!
	! Common areas
	!
	! These areas store information that is re-used between
	! calls to these functions.
	!
	COM (CH_BT_CUSTYP) &
		BT_CUSTYP.CH%, &
		BT_CUSTYP.READONLY%

	!
	! External functions
	!
	EXTERNAL LONG   FUNCTION MAIN_WINDOW
	EXTERNAL LONG   FUNCTION FUNC_TESTENTRY

	ON ERROR GOTO 29000

	%PAGE

	SELECT MOPTION

	!******************************************************************
	! Initialization
	!
	! This option is used to initialize the window structure,
	! set up the default values for add, and open all files
	! necessary that have not already been opened.
	!******************************************************************
	CASE OPT_INIT
		!
		! Define window
		!
		SMG_WINDOW::DESCR = "Guardian Type maintenance"
		SMG_WINDOW::NHELP = "BT_MAIN_CUSTYP"
		SMG_WINDOW::HSIZE = 78%
		SMG_WINDOW::VSIZE = 18%
		SMG_WINDOW::HVIEW = 78%
		SMG_WINDOW::VVIEW = 18%
		SMG_WINDOW::HPOS  = 2%
		SMG_WINDOW::VPOS  = 2%
		SMG_WINDOW::NITEMS= 3%
		SMG_WINDOW::FLAGS = 0%

		SMG_WINDOW::NKEYS = 1%
		SMG_WINDOW::KNAME(0%) = "Guardian_type"
			SMG_WINDOW::KFIELD(0%, 0%) = 1%
			SMG_WINDOW::KFIELD(0%, 1%) = 1%
		!
		! Load in defaults for chart
		!
		CALL READ_DEFAULTS(SMG_WINDOW) &
			IF INSTR(1%, " QV", MVALUE) <= 1%

700		!
		! Declare channels
		!
		IF BT_CUSTYP.CH% > 0%
		THEN
			!
			! Already open, set flag to read-only if
			! was that way from last time.
			!
			SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%) &
				IF BT_CUSTYP.READONLY%
			GOTO 790
		END IF

		!
		! Open main file (existing) for modification
		!
750		%INCLUDE "SOURCE:[BT.OPEN]BT_CUSTYP.CRE"
		BT_CUSTYP.READONLY% = 0%
		GOTO 790

760		!
		! If unable to open for modify, try to open
		! with read access only.
		!
		%INCLUDE "SOURCE:[BT.OPEN]BT_CUSTYP.OPN"
		SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%)
		BT_CUSTYP.READONLY% = -1%

		GOTO 790

770		!
		! File not able to open, so reset channel
		!
		CALL ASSG_FREECHANNEL(BT_CUSTYP.CH%)

		GOTO ExitFunction

790		SMG_WINDOW::CHAN  = BT_CUSTYP.CH%
		RESET #BT_CUSTYP.CH%
		GET #BT_CUSTYP.CH%, REGARDLESS

	%PAGE

20100	!******************************************************************
	! Display the background
	!
	! This option is used to display the background information
	! on the screen.  It must first clear any junk on the screen,
	! and then write the background onto it.
	!******************************************************************
	CASE OPT_BACKGROUND

		SMG_STATUS% = SMG$BEGIN_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

		SMG_STATUS% = SMG$ERASE_DISPLAY(SMG_WINDOW::WNUMBER)

		DATA	6,  1, "(01) Guardian Type", &
			8,  1, "(02) AR Account", &
			10,  1, "(03) Acct for Diaper", &
			0,  0, ""

		RESTORE

		READ XPOS%, YPOS%, XSTR$
		I%=0%
		WHILE (XPOS% <> 0)
			I% = I% + 1%
			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				XSTR$, XPOS%, YPOS%) &
				IF (SMG_WINDOW::HFLAG(I%) AND 2%)=0%
		READ XPOS%, YPOS%, XSTR$
		NEXT

		SMG_STATUS% = SMG$END_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

20200	!******************************************************************
	! Enter/Display/Default
	!
	! This option is used to enter the data from the user, display
	! data, set defaults, and return the data back according to
	! MFLAG.
	!******************************************************************
	CASE OPT_ENTRY
		TEMP$, TEMP1$ = TRM$(SCOPE::PRG_ITEM)
		TEMP$ = "View starting at" IF TEMP$ = "View"

		SCOPE::PRG_ITEM = "FLD" + FORMAT$(MLOOP, "<0>##")

		SCOPE::SCOPE_EXIT = 0%

 Eloop:		SELECT MLOOP

		CASE 1%

	!++
	! Abstract:FLD001
	!
	!	^*(01) Guardian Type\*
	!	.p
	!	The ^*Guardian Type\* field in a Guardian Type Maintanance record
	!	provides the means to identify a guardian type with a two (2) character
	!	alphanumeric code.
	!	.p
	!	Guardian types are determined in reference to the type of services
	!	to be rendered to a client which the guardian represents.
	!	.p
	!	Pressing ^*<List Choices>\* while the cursor is located at this
	!	field, will display a list of customer types.
	!
	! Index:
	!	.x Guardian>Type
	!	.x Type>Guardian
	!--

			BT_CUSTYP::CUSTYP = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"6;22", TEMP$, &
				BT_CUSTYP::CUSTYP, MFLAG, "'E", MVALUE)

			IF SCOPE::SCOPE_EXIT = SMG$K_TRM_F14
			THEN
				IF (MAIN_WINDOW(AR_MAIN_CUSTYPE.ID, "VX") = 1%)
				THEN
					BT_CUSTYP::CUSTYP = AR_CUSTYPE::CUSTYPE
				END IF
				GOTO ELoop
			END IF


		CASE 2%

	!++
	! Abstract:FLD002
	!	^*(02) Account Receivable Account\*
	!	.p
	!	The ^*Account Receivable Account\* field in a Guardian Type Maintenance record
	!	provides the means to enter the Account Receivable account in the
	!	Chart of Accounts which will be debited when billings are generated
	!	for a specific Guardian type.
	!	.p
	!	Pressing ^*<List Choices>\* while the cursor is located at this
	!	field, will display a list of the Chart of Accounts.
	!
	! Index:
	!	.x Account Receivable Account
	!	.x Account>Account Receivable
	!
	!--

			BT_CUSTYP::AR_ACCT = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"8;22", TEMP$, &
				BT_CUSTYP::AR_ACCT, MFLAG, "'E", MVALUE)

			IF (SCOPE::SCOPE_EXIT = SMG$K_TRM_F14)
			THEN
				IF (MAIN_WINDOW(GL_MAIN_CHART.ID, "VX  ") = 1%)
				THEN
					BT_CUSTYP::AR_ACCT = &
						GL_CHART::ACCT
				END IF
				GOTO Eloop
			END IF

		CASE 3%

	!++
	! Abstract:FLD003
	!	^*(03) Account for Diapers\*
	!	.p
	!	The ^*Account for Diapers\* in a Guardian Type Maintanance record
	!	provides the means to designate the account in the Chart of
	!	Accounts which will be charged when the use of diapers is requried.
	!	.p
	!	Pressing ^*<List Choices>\* while the cursor is located at this field
	!	will display a list of the Chart of Accounts.
	!
	! Index:
	!	.x Account For Diapers
	!
	!--

			BT_CUSTYP::REV_MASK = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"10;22", TEMP$, &
				BT_CUSTYP::REV_MASK, MFLAG, "'E", MVALUE)

			IF (SCOPE::SCOPE_EXIT = SMG$K_TRM_F14)
			THEN
				IF (MAIN_WINDOW(GL_MAIN_CHART.ID, "VX  ") = 1%)
				THEN
					BT_CUSTYP::REV_MASK = &
						GL_CHART::ACCT
				END IF
				GOTO Eloop
			END IF

		END SELECT

		SCOPE::PRG_ITEM = TEMP1$

20300	CASE OPT_TESTENTRY
		BT_MAIN_CUSTYP = 0%

		SELECT MLOOP

		CASE 1%
			BT_MAIN_CUSTYP = FUNC_TESTENTRY( SMG_WINDOW, &
				BT_CUSTYP::CUSTYP, &
				AR_CUSTYPE::DESCRIPTION, &
				"BT", MLOOP, "PRG", &
				"Customer Type", AR_MAIN_CUSTYPE.ID)

			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				AR_CUSTYPE::DESCRIPTION, 6%, 40%,, SMG$M_BOLD)

			IF (MVALUE = "ADD")
			THEN
				GET #BT_CUSTYP.CH%, &
					KEY#0% EQ BT_CUSTYP::CUSTYP + "", &
					REGARDLESS

				BT_MAIN_CUSTYP = 2%
				CALL ENTR_3MESSAGE(SCOPE, &
					"Record Already Exists", 1%)
			END IF

		CASE 2%
			BT_MAIN_CUSTYP = FUNC_TESTENTRY( SMG_WINDOW, &
				BT_CUSTYP::AR_ACCT, &
				GL_CHART::DESCR, &
				"BT", MLOOP, "PRG", &
				"Account", GL_MAIN_CHART.ID)

			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				GL_CHART::DESCR, 8%, 43%,, SMG$M_BOLD)

		CASE 3%
			BT_MAIN_CUSTYP = FUNC_TESTENTRY( SMG_WINDOW, &
				BT_CUSTYP::REV_MASK, &
				GL_CHART::DESCR, &
				"BT", MLOOP, "PRG", &
				"Account", GL_MAIN_CHART.ID)

			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				GL_CHART::DESCR, 10%, 43%,, SMG$M_BOLD)

		END SELECT

	CASE OPT_DISPLAY

		IF (SMG_WINDOW::HFLAG(1%) AND 2%)=0%
		THEN
			IF MAIN_WINDOW(AR_MAIN_CUSTYPE.ID, &
				"Q0" + BT_CUSTYP::CUSTYP) <> 1%
			THEN
				AR_CUSTYPE::DESCRIPTION = &
					STRING$(40%, ASCII("?"))
			END IF
			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				AR_CUSTYPE::DESCRIPTION, 6%, 40%,, SMG$M_BOLD)
		END IF


		IF (SMG_WINDOW::HFLAG(2%) AND 2%)=0%
		THEN
			IF MAIN_WINDOW(GL_MAIN_CHART.ID, &
				"Q0" + BT_CUSTYP::AR_ACCT) = 1%
			THEN
				TEMP$ = GL_CHART::DESCR
			ELSE
				TEMP$ = STRING$(40%, ASCII("?"))
			END IF
			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				TEMP$, 8%, 43%,, SMG$M_BOLD)
		END IF

		IF (SMG_WINDOW::HFLAG(3%) AND 2%)=0%
		THEN
			IF MAIN_WINDOW(GL_MAIN_CHART.ID, &
				"Q0" + BT_CUSTYP::REV_MASK) = 1%
			THEN
				TEMP$ = GL_CHART::DESCR
			ELSE
				TEMP$ = STRING$(40%, ASCII("?"))
			END IF
			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				TEMP$, 10%, 43%,, SMG$M_BOLD)
		END IF

20500	CASE OPT_SETOLD
		BT_CUSTYP_OLD = BT_CUSTYP

	CASE OPT_RESETOLD
		BT_CUSTYP = BT_CUSTYP_OLD

	CASE OPT_SETDEFAULT
		BT_CUSTYP2 = BT_CUSTYP

	CASE OPT_RESETDEFAULT
		BT_CUSTYP = BT_CUSTYP2

	CASE OPT_VIEW
		SELECT MLOOP

		CASE 1%

			MVALUE = "Type AR Account         " + &
				"Revenue Mask       Description"

		CASE 2%

			MVALUE = "005,024,043"

		CASE 3%

			MVALUE = &
				BT_CUSTYP::CUSTYP + " " + &
				BT_CUSTYP::AR_ACCT + " " + &
				BT_CUSTYP::REV_MASK + " " + &
				AR_CUSTYPE::DESCRIPTION

		END SELECT
	CASE OPT_FIND
		SELECT MLOOP

		CASE 0%
			FIND #BT_CUSTYP.CH%, &
				KEY #0% GE BT_CUSTYP::CUSTYP + "", &
				REGARDLESS

		END SELECT

	END SELECT

 ExitFunction:
	EXIT FUNCTION

	%PAGE

29000	!******************************************************************
	! Trap errors
	!******************************************************************
	SELECT ERR
	!
	! Locked block
	!
	CASE 154%
		SLEEP 1%
		RESUME
	END SELECT

	SELECT ERL
	!
	! Unable to open Read/Write
	!
	CASE 750%
		RESUME 760 IF ERR = 10%
		BT_MAIN_CUSTYP = ERR
		RESUME 770

	!
	! Unable to open Read/Only
	!
	CASE 760%
		BT_MAIN_CUSTYP = ERR
		RESUME 770

	!
	! Unable to reset
	!
	CASE 790%
		RESUME 32767

	!
	! Record does not already exist
	!
	CASE 20300%
		RESUME 32767 IF ERR = 155%

	END SELECT

	ON ERROR GO BACK

	%PAGE

32767	END FUNCTION
