1	%TITLE "Billing Table Maintenance"
	%SBTTL "BT_MAIN_JOURNAL"
	%IDENT "V3.6a Calico"

	FUNCTION LONG BT_MAIN_JOURNAL(CDD_WINDOW_CDD SMG_WINDOW, &
		LONG MOPTION, LONG MLOOP, LONG MFLAG, STRING MVALUE)

	!
	! COPYRIGHT (C) 1987,1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! Abstract:HELP
	!	.p
	!	The ^*Maintain Billing Tuition Journal\* provides for
	!	entry or editing of  billing data for
	!	a specific guardian. The screen also contains Line__items where billing
	!	information may be entered and maintained.
	!
	! Index:
	!	.x Billing>Maintain Journal
	!	.x Maintain>Billing Journal
	!	.x Journal>Billing Maintenance
	!
	! Option:
	!
	! Compile:
	!
	!	$ BAS BT_SOURCE:BT_MAIN_JOURNAL/LINE
	!	$ LIB FUNC_LIB:CMCFUN/REP BT_MAIN_JOURNAL
	!	$ DELETE BT_MAIN_JOURNAL.OBJ;*
	!
	! Author:
	!
	!	05/26/88 - Kevin Handy
	!
	! Modification history:
	!
	!	04/27/88 - Kevin Handy
	!		Modified to allow R/O open of file if R/W open fails.
	!
	!	07/08/88 - Kevin Handy
	!		Modified journal file to contain child name
	!		instead of pointing to payroll name file.
	!
	!	01/02/91 - Craig Tanner
	!		Updated to use the AR_35CUSTOM file (instead of
	!		AR_CUSTOM) and got rid of the BT_CUSTOMEX file.
	!
	!	02/26/92 - Kevin Handy
	!		Fixed to use "AR_MAIN_CUSTOM" instead of
	!		"BT_MAIN_CUSTOM" which someone (Frank) deleted.
	!
	!	05/11/92 - Dan Perkins
	!		Use FUNC_TESTENTRY to test input.
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 Calico coding standards
	!
	!	04/20/95 - Kevin Handy
	!		Modified to use AR_MAIN_35CUSTOM instead of
	!		AR_MAIN_CUSTOM.
	!
	!	10/17/96 - Kevin Handy
	!		Reformat source code
	!
	!	05/23/97 - Kevin Handy
	!		Use integer for #key
	!
	!	08/19/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!
	!	03/10/99 - Kevin Handy
	!		Fix FIND bug
	!--
	%PAGE

	!
	! Set up compiling options
	!
	OPTION SIZE = (REAL GFLOAT, INTEGER LONG)

	!
	! Include files
	!
	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "FUNC_INCLUDE:MAIN_WINDOW.COM"
	%INCLUDE "FUNC_INCLUDE:BT_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:GL_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:AR_WINDOW.INC"

	!
	! CDD inclusions and MAPs
	!
	%INCLUDE "SOURCE:[CDD.OPEN]CDD_WINDOW.HB"

	%INCLUDE "SOURCE:[BT.OPEN]BT_JOURNALH.HB"
	MAP (BT_JOURNALH)	BT_JOURNALH_CDD		BT_JOURNALH
	MAP (BT_JOURNALH_OLD)	BT_JOURNALH_CDD BT_JOURNALH_OLD, BT_JOURNALH2

	%INCLUDE "SOURCE:[BT.OPEN]BT_TUITION.HB"
	MAP (BT_TUITION)	BT_TUITION_CDD		BT_TUITION

	%INCLUDE "SOURCE:[AR.OPEN]AR_35CUSTOM.HB"
	MAP (AR_35CUSTOM)	AR_35CUSTOM_CDD		AR_35CUSTOM

	%INCLUDE "SOURCE:[BT.OPEN]BT_CONTROL.HB"
	MAP (BT_CONTROL)	BT_CONTROL_CDD		BT_CONTROL

	%INCLUDE "SOURCE:[GL.OPEN]GL_CHART.HB"
	MAP (GL_CHART)		GL_CHART_CDD		GL_CHART

	%INCLUDE "SOURCE:[BT.OPEN]BT_CUSTYP.HB"
	MAP (BT_CUSTYP)		BT_CUSTYP_CDD		BT_CUSTYP

	!
	! Common areas
	!
	! These areas store information that is re-used between
	! calls to these functions.
	!
	COM (CH_BT_JOURNALH) &
		BT_JOURNALH.CH%, &
		BT_JOURNALH.READONLY

	COM (CH_BT_CONTROL) &
		BT_CONTROL.CH%

	COM (CH_BT_CUSTYP) &
		BT_CUSTYP.CH%

	COM (CH_AR_35CUSTOM) &
		AR_35CUSTOM.CH%

	COM (BATCH) &
		BATCH_NO$

	!
	! External functions
	!
	EXTERNAL LONG   FUNCTION LIBR_EXTRACTVAR
	EXTERNAL LONG   FUNCTION LIBR_INSERTTEXT
	EXTERNAL LONG   FUNCTION LIBR_DELETE
	EXTERNAL LONG   FUNCTION MAIN_WINDOW
	EXTERNAL LONG   FUNCTION MAIN_JOURNAL
	EXTERNAL LONG   FUNCTION FUNC_TESTENTRY

	!
	! Declare variables
	!
	DECLARE RFA TEMP_RFA

	ON ERROR GOTO 29000

	%PAGE

	SELECT MOPTION

	!******************************************************************
	! Initialization
	!
	! This option is used to initialize the window structure,
	! set up the default values for add, and open all files
	! necessary that have not already been opened.
	!******************************************************************

	CASE OPT_INIT
		!
		! Define window
		!
		SMG_WINDOW::DESCR = "Billing Tuition Journal"
		SMG_WINDOW::NHELP = "BT_MAIN_JOURNAL"
		SMG_WINDOW::HSIZE = 78%
		SMG_WINDOW::VSIZE = 18%
		SMG_WINDOW::HVIEW = 78%
		SMG_WINDOW::VVIEW = 18%
		SMG_WINDOW::HPOS  = 2%
		SMG_WINDOW::VPOS  = 2%
		SMG_WINDOW::NITEMS= 4%
		SMG_WINDOW::FLAGS = 0%

		SMG_WINDOW::NKEYS = 1%
		SMG_WINDOW::KNAME(0%) = "Billing-number"
			SMG_WINDOW::KFIELD(0%, 0%) = 1%
			SMG_WINDOW::KFIELD(0%, 1%) = 1%

		!
		! Load in defaults for chart
		!
		CALL READ_DEFAULTS(SMG_WINDOW) &
			IF INSTR(1%, " QV", MVALUE) <= 1%

700		!
		! Get info required for main file
		!
		IF BT_JOURNALH.CH% > 0%
		THEN
			!
			! Already open, set flag to read-only if
			! was that way from last time.
			!
			SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%) &
				IF BT_JOURNALH.READONLY%
			GOTO 790
		END IF

		BT_JOURNALH.DEV1$ = "SYS$LOGIN:" &
			IF BT_JOURNALH.DEV1$ = ""

		!
		! Open main file (existing) for modification
		!
750		%INCLUDE "SOURCE:[BT.OPEN]BT_JOURNALH.CRE"
		BT_JOURNALH.READONLY% = 0%
		GOTO 790

760		!
		! If unable to open for modify, try to open
		! with read access only.
		!
		%INCLUDE "SOURCE:[BT.OPEN]BT_JOURNALH.OPN"
		SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%)
		BT_JOURNALH.READONLY% = -1%

		GOTO 790

770		!
		! File not able to open, so reset channel
		!
		CALL ASSG_FREECHANNEL(BT_JOURNALH.CH%)

		EXIT FUNCTION

790		SMG_WINDOW::CHAN  = BT_JOURNALH.CH%

		RESET #BT_JOURNALH.CH%
		GET #BT_JOURNALH.CH%, REGARDLESS


20100	!******************************************************************
	! Display the background
	!
	! This option is used to display the background information
	! on the screen.  It must first clear any junk on the screen,
	! and then write the background onto it.
	!******************************************************************
	CASE OPT_BACKGROUND

		SMG_STATUS% = SMG$BEGIN_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

		SMG_STATUS% = SMG$ERASE_DISPLAY(SMG_WINDOW::WNUMBER)

		DATA	2,  1, "(01) Guardian", &
			6,  1, "(02) Invoice", &
			7,  1, "(03) AR Account", &
			8,  1, "(04) Tran Date", &
			0,  0, ""

		RESTORE

		READ XPOS%, YPOS%, XSTR$
		I%=0%
		WHILE (XPOS% <> 0)
			I% = I% + 1%
			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				XSTR$, XPOS%, YPOS%) &
				IF (SMG_WINDOW::HFLAG(I%) AND 2%)=0%
		READ XPOS%, YPOS%, XSTR$
		NEXT

		SMG_STATUS% = SMG$END_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

	!
	! Select function
	!
	CASE OPT_OPTLIST

		MVALUE = MVALUE + " Line_items nOtes"

	!
	! Direction
	!
	CASE OPT_MOREMENU
		SELECT SCOPE::PRG_ITEM

		!
		! Line option
		!
		CASE "Line_items"
	!++
	! Abstract:LINE_ITEMS
	!	^*Line-Items\*
	!	.p
	!	The ^*Line__Items\* option in the COMMAND Menu provides
	!	a means to enter and maintain billing information for
	!	a particular guardian.
	!
	! Index:
	!	.x Option>Line_Items
	!	.x Line_Items>Option
	!
	!--
			!
			! Make sure there is a header
			!
			TEMP_RFA = GETRFA(BT_JOURNALH.CH%)

			BT_MAIN_JOURNAL = MAIN_JOURNAL(BT_MAIN_JOURNALL.ID, "")

		CASE "nOtes"
	!++
	! Abstract:NOTES
	!	^*Notes\*
	!	.p
	!	The ^*Notes\* option in the COMMAND Menu provides
	!	a means to maintain notes for a particular guardian.
	!
	! Index:
	!	.x Option>Notes
	!	.x Notes>Option
	!
	!--
			!
			! Display notes
			!
			BT_JOURNALH.DEV1$ = "SYS$LOGIN:" &
				IF BT_JOURNALH.DEV1$ = ""

			ST% = LIBR_EXTRACTVAR( &
				TRM$(BT_JOURNALH.DEV1$) + "BT_JOURNALH", &
				LAST_NOTE$, &
				EDIT$(BT_JOURNALH::CUSNUM, 2%))

			LAST_NOTE$ = ENTR_NOTE(SMG_WINDOW::WNUMBER, &
				"9;1", TEMP$, &
				LAST_NOTE$, 64%, "10;78", MVALUE)

			ST% = LIBR_INSERTTEXT( &
				TRM$(BT_JOURNALH.DEV1$) + "BT_JOURNALH", &
				LAST_NOTE$, &
				EDIT$(BT_JOURNALH::CUSNUM, 2%))
		END SELECT


20200	!******************************************************************
	! Enter/Display/Default
	!
	! This option is used to enter the data from the user, display
	! data, set defaults, and return the data back according to
	! MFLAG.
	!******************************************************************

	CASE OPT_ENTRY
		TEMP$, TEMP1$ = TRM$(SCOPE::PRG_ITEM)
		TEMP$ = "View starting at" IF TEMP$ = "View"

		SCOPE::PRG_ITEM = "FLD" + FORMAT$(MLOOP, "<0>##")

		SCOPE::SCOPE_EXIT = 0%

 Eloop:		SELECT MLOOP

		CASE 1%
	!++
	! Abstract:FLD001
	!	^*(01) Guardian\*
	!	.p
	!	The ^*Guardian\* field provides for entry of the Guardian
	!	number corresponding to this particular transaction.
	!	.p
	!	The Guardian number must be a valid number established in
	!	the Billing Tuition Masterfile.
	!	.p
	!	Pressing ^*<List Choices>\* while the cursor is located
	!	at this field will display a list of valid Guardian numbers.
	!
	! Index:
	!	.x Gaurdian>Maintain Billing Tuition Journal
	!	.x Maintain Billing Tuition Journal>Gaurdian
	!
	!--
			BT_JOURNALH::CUSNUM = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"2;17", TEMP$, &
				BT_JOURNALH::CUSNUM, MFLAG, "'E", MVALUE)

			IF (SCOPE::SCOPE_EXIT = SMG$K_TRM_F14)
			THEN
				IF (MAIN_WINDOW(AR_MAIN_35CUSTOM.ID, "VX") = 1%)
				THEN
					BT_JOURNALH::CUSNUM = &
						AR_35CUSTOM::CUSNUM
				END IF
				GOTO ELoop
			END IF

		CASE 2%
	!++
	! Abstract:FLD002
	!	^*(02) Invoice\*
	!	.p
	!	The ^*Invoice\* field provides a means to enter an invoice _#.
	!	This number is entered automatically be the system when a bill is
	!	printed for the guardian. Manual entry should not be necessary.
	!	.p
	!	The field provides eight (8) spaces for an alphanumeric entry.
	!
	! Index:
	!	.x Invoice>Maintain Billing Tuition Journal
	!	.x Maintain Billing Tuition Journal>Invoice
	!
	!--
			BT_JOURNALH::INVNUM = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"6;17", TEMP$, &
				BT_JOURNALH::INVNUM, MFLAG, "'E", MVALUE)

		CASE 3%
	!++
	! Abstract:FLD003
	!	.x AR Account Number
	!	^*(03) Accounts Receivable Account\*
	!	.p
	!	The ^*Accoubts Receivable Account\* field is provided to enter the appropriate
	!	General Ledger Accounts Receivable number.
	!	.p
	!	This field will default to the account number defined in the
	!	^*MASTER TABLE ARTOGL\* program for the guardian type entered
	!	in the ^*MAST NAME\* program during the add operation.
	!	.p
	!	Pressing ^*<List Choices>\* at this field will provide a list
	!	of valid General Ledger account numbers.
	!
	! Index:
	!
	!--
			IF ((MFLAG AND 1%) = 0%) AND &
				(BT_JOURNALH::AR_ACCT = "") AND &
				(TEMP$ = "Add")
			THEN
				GOSUB GetDefaultAccount
				BT_JOURNALH::AR_ACCT = BT_CUSTYP::AR_ACCT
			END IF

			BT_JOURNALH::AR_ACCT = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"7;17", TEMP$, &
				BT_JOURNALH::AR_ACCT, MFLAG, "'E", MVALUE)

			IF (SCOPE::SCOPE_EXIT = SMG$K_TRM_F14)
			THEN
				IF (MAIN_WINDOW(GL_MAIN_CHART.ID, "VX") = 1%)
				THEN
					BT_JOURNALH::AR_ACCT = &
						GL_CHART::ACCT
				END IF
				GOTO ELoop
			END IF

		CASE 4%
	!++
	! Abstract:FLD004
	!	.x Transaction>Date
	!	^*(04) Transaction Date\*
	!	.p
	!	The ^*Transaction Date\* field provides the means to enter the date of
	!	a particular transaction.
	!	.p
	!	The field requires an entry and will not default to the current
	!	date.  The format for entry is MMDDYY or MMDDYYYY.
	!
	! Index:
	!	.x Date>Transaction
	!
	!--
			BT_JOURNALH::TRADAT = ENTR_3DATE(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				"8;17", TEMP$, &
				BT_JOURNALH::TRADAT, MFLAG, "'E", MVALUE)

		END SELECT

		SCOPE::PRG_ITEM = TEMP1$

	CASE OPT_AFTEROPT

		SELECT SCOPE::PRG_ITEM

		CASE "Add"
			BT_MAIN_JOURNAL = MAIN_JOURNAL(BT_MAIN_JOURNALL.ID, "A")

		CASE "Change", "Blank", "Initialize"

			IF BT_JOURNALH_OLD::CUSNUM <> BT_JOURNALH::CUSNUM
			THEN
				TEMP$ = BT_JOURNALH::CUSNUM + ""
				BT_JOURNALH = BT_JOURNALH_OLD
				BT_MAIN_JOURNAL = &
					MAIN_JOURNAL(BT_MAIN_JOURNALL.ID, &
					"C" + TEMP$)

				BT_JOURNALH.DEV1$ = "SYS$LOGIN:" &
					IF BT_JOURNALH.DEV1$ = ""

				ST% = LIBR_EXTRACTVAR( &
					TRM$(BT_JOURNALH.DEV1$) + &
					"BT_JOURNALH", &
					LAST_NOTE$, &
					EDIT$(BT_JOURNALH_OLD::CUSNUM, 2%))

				ST% = LIBR_DELETE( &
					TRM$(BT_JOURNALH.DEV1$) + &
					"BT_JOURNALH", &
					EDIT$(BT_JOURNALH_OLD::CUSNUM, 2%))

				ST% = LIBR_INSERTTEXT( &
					TRM$(BT_JOURNALH.DEV1$) + &
					"BT_JOURNALH", &
					LAST_NOTE$, &
					EDIT$(BT_JOURNALH::CUSNUM, 2%))
			END IF

		CASE "Erase"

			BT_JOURNALH.DEV1$ = "SYS$LOGIN:" &
				IF BT_JOURNALH.DEV1$ = ""

			ST% = LIBR_DELETE( &
				TRM$(BT_JOURNALH.DEV1$) + "BT_JOURNALH", &
				EDIT$(BT_JOURNALH::CUSNUM, 2%))

			BT_MAIN_JOURNAL = MAIN_JOURNAL(BT_MAIN_JOURNALL.ID, "E")

		END SELECT

	CASE OPT_TESTOPT

20300	CASE OPT_TESTENTRY
		BT_MAIN_JOURNAL = 0%

		SELECT MLOOP

		CASE 1%
			AR_35CUSTOM::CUSNAM = "????????????????????"
			AR_35CUSTOM::ADD1   = "????????????????????"
			AR_35CUSTOM::ADD2   = "????????????????????"
			AR_35CUSTOM::CITY   = "????????????????????"
			AR_35CUSTOM::STATE  = "????????????????????"
			AR_35CUSTOM::ZIP    = "????????????????????"

			BT_MAIN_JOURNAL = FUNC_TESTENTRY( SMG_WINDOW, &
				BT_JOURNALH::CUSNUM, &
				AR_35CUSTOM::CUSNAM, &
				"BT", MLOOP, "PRG", &
				"Guardian", AR_MAIN_35CUSTOM.ID)

			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				AR_35CUSTOM::CUSNAM, &
				2%, 36%, , SMG$M_BOLD)

			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				AR_35CUSTOM::ADD1, &
				3%, 36%, , SMG$M_BOLD)

			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				AR_35CUSTOM::ADD2, &
				4%, 36%, , SMG$M_BOLD)

			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				TRM$(AR_35CUSTOM::CITY) + ", " + &
					TRM$(AR_35CUSTOM::STATE) + "  " + &
					AR_35CUSTOM::ZIP + SPACE$(40%), &
				5%, 36%, , SMG$M_BOLD)

			GOSUB GetDefaultAccount

		CASE 3%
			BT_MAIN_JOURNAL = FUNC_TESTENTRY( SMG_WINDOW, &
				BT_JOURNALH::AR_ACCT, &
				GL_CHART::DESCR, &
				"BT", MLOOP, "PRG", &
				"Account", GL_MAIN_CHART.ID)

			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				GL_CHART::DESCR, &
				7%, 36%, , SMG$M_BOLD)

		END SELECT

20400	CASE OPT_DISPLAY

		IF (SMG_WINDOW::HFLAG(1%) AND 2%)=0%
		THEN
			IF MAIN_WINDOW(AR_MAIN_35CUSTOM.ID, &
				"Q0" + BT_JOURNALH::CUSNUM) <> 1%
			THEN
				AR_35CUSTOM::CUSNAM = "????????????????????"
				AR_35CUSTOM::ADD1 = "????????????????????"
				AR_35CUSTOM::ADD2 = "????????????????????"
				AR_35CUSTOM::CITY = "????????????????????"
				AR_35CUSTOM::STATE = "????????????????????"
				AR_35CUSTOM::ZIP = "????????????????????"
			END IF

			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				AR_35CUSTOM::CUSNAM, &
				2%, 36%, , SMG$M_BOLD)
			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				AR_35CUSTOM::ADD1, &
				3%, 36%, , SMG$M_BOLD)
			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				AR_35CUSTOM::ADD2, &
				4%, 36%, , SMG$M_BOLD)
			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				TRM$(AR_35CUSTOM::CITY) + ", " + &
					TRM$(AR_35CUSTOM::STATE) + "  " + &
					AR_35CUSTOM::ZIP + SPACE$(40%), &
				5%, 36%, , SMG$M_BOLD)
		END IF

		IF (SMG_WINDOW::HFLAG(3%) AND 2%)=0%
		THEN
			IF MAIN_WINDOW(GL_MAIN_CHART.ID, &
				"Q0" + BT_JOURNALH::AR_ACCT) <> 1%
			THEN
				GL_CHART::DESCR = "????????????????????"
			END IF

			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				GL_CHART::DESCR, &
				7%, 36%, , SMG$M_BOLD)
		END IF

		GOSUB GetDefaultAccount

20500	CASE OPT_SETOLD
		BT_JOURNALH_OLD = BT_JOURNALH

	CASE OPT_RESETOLD
		BT_JOURNALH = BT_JOURNALH_OLD

	CASE OPT_SETDEFAULT
		BT_JOURNALH2 = BT_JOURNALH

	CASE OPT_RESETDEFAULT
		BT_JOURNALH = BT_JOURNALH2

	CASE OPT_VIEW
		SELECT MLOOP

		CASE 1%
			MVALUE = "  Guardian   Invoice    Account            TranDate"

		CASE 2%
			MVALUE = "011,020,039"

		CASE 3%
			MVALUE = &
				BT_JOURNALH::CUSNUM + " " + &
				BT_JOURNALH::INVNUM + " " + &
				BT_JOURNALH::AR_ACCT + " " + &
				PRNT_DATE(BT_JOURNALH::TRADAT, 8%)

		END SELECT
	CASE OPT_FIND
		SELECT MLOOP

		CASE 0%
			FIND #BT_JOURNALH.CH%, &
				KEY #0% GE BT_JOURNALH::CUSNUM + "", &
				REGARDLESS

		END SELECT

	END SELECT

	EXIT FUNCTION

	%PAGE

 GetDefaultAccount:
28100	!*******************************************************************
	! This subroutine searches the customer type file for the
	! default account number.
	!*******************************************************************

28110	!
	! Get gl info
	!
	GET #BT_CUSTYP.CH%, KEY#0% EQ AR_35CUSTOM::TTYPE + "", REGARDLESS

28190	RETURN

29000	!***************************************************************
	! Trap errors
	!***************************************************************
	SELECT ERL
	!
	! Unable to open Read/Write
	!
	CASE 750%
		RESUME 760 IF ERR = 10%
		BT_MAIN_JOURNAL = ERR
		RESUME 770

	!
	! Unable to open Read/Only
	!
	CASE 760%
		BT_MAIN_JOURNAL = ERR
		RESUME 770

	!
	! Unable to reset
	!
	CASE 790%
		RESUME 32767

	!
	! Record does not already exist
	!
	CASE 20300%
		RESUME 32767 IF ERR = 155%

	!
	! Unable to load in customer type record
	!
	CASE 28110%
		BT_CUSTYP::AR_ACCT = ""
		BT_CUSTYP::REV_MASK = ""

		RESUME 28190

	END SELECT

	ON ERROR GO BACK

32767	END FUNCTION
