1	%TITLE "Sales Journal Header Maintenance"
	%SBTTL "BT_MAIN_JOURNALL"
	%IDENT "V3.6a Calico"

	FUNCTION LONG BT_MAIN_JOURNALL(CDD_WINDOW_CDD SMG_WINDOW, &
		LONG MOPTION, LONG MLOOP, LONG MFLAG, STRING MVALUE)

	!
	! COPYRIGHT (C) 1987,1988 BY
	!
	! Computer Management Center
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! Abstract:HELP
	!	.p
	!	This program maintains the Marketing SJH file.
	!
	! Index:
	!
	! Option:
	!
	!
	! Compile:
	!
	!	$ BAS BT_SOURCE:BT_MAIN_JOURNALL/LINE
	!	$ LIB FUNC_LIB:CMCFUN/REP BT_MAIN_JOURNALL
	!	$ DELETE BT_MAIN_JOURNALL.OBJ;*
	!
	! Author:
	!
	!	05/26/88 - Kevin Handy
	!
	! Modification history:
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	10/17/96 - Kevin Handy
	!		Reformat source code
	!
	!	05/23/97 - Kevin Handy
	!		Use integer for #key
	!
	!	08/19/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!
	!	03/08/99 - Kevin Handy
	!		Fix FIND bug
	!
	!	08/05/99 - Kevin Handy
	!		Remove line 760, 770 which were not referenced
	!--
	%PAGE

	!
	! Set up compiling options
	!
	OPTION SIZE = (REAL GFLOAT, INTEGER LONG)

	!
	! Include files
	!
	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "FUNC_INCLUDE:MAIN_WINDOW.COM"
	%INCLUDE "FUNC_INCLUDE:BT_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:GL_WINDOW.INC"

	!
	! CDD inclusions and MAPs
	!
	%INCLUDE "SOURCE:[CDD.OPEN]CDD_WINDOW.HB"

	%INCLUDE "SOURCE:[BT.OPEN]BT_JOURNALL.HB"
	MAP (BT_JOURNALL)	BT_JOURNALL_CDD	BT_JOURNALL
	MAP (BT_JOURNALL_OLD)	BT_JOURNALL_CDD	BT_JOURNALL_OLD, BT_JOURNALL2

	%INCLUDE "SOURCE:[BT.OPEN]BT_JOURNALH.HB"
	MAP (BT_JOURNALH)	BT_JOURNALH_CDD		BT_JOURNALH

	%INCLUDE "SOURCE:[PR.OPEN]PR_EMP_MASTER.HB"
	MAP (PR_EMP_MASTER)	PR_EMP_MASTER_CDD	PR_EMP_MASTER

	%INCLUDE "SOURCE:[GL.OPEN]GL_CHART.HB"
	MAP (GL_CHART)		GL_CHART_CDD		GL_CHART

	%INCLUDE "SOURCE:[BT.OPEN]BT_TUITION.HB"
	MAP (BT_TUITION)		BT_TUITION_CDD		BT_TUITION

	%INCLUDE "SOURCE:[BT.OPEN]BT_CUSTYP.HB"
	MAP (BT_CUSTYP)		BT_CUSTYP_CDD		BT_CUSTYP

	!
	! Create array to contain pointers and totals
	!
	RECORD RARRAY_RECORD
		RFA	LINRFA		! Rfa pointer for record
		REAL	RATE
	END RECORD

	!
	! Common Areas
	!
	! These areas store information that is re-used between
	! calls to these functions.
	!
	COM (TT_BT_JOURNALL) RARRAY_RECORD RARRAY(300%)

	COM (CH_BT_JOURNALH) &
		BT_JOURNALH.CH% &

	COM (CH_BT_JOURNALL) &
		BT_JOURNALL.CH%, &
		BATCH_NO$ = 2%

	COM (CH_BT_TUITION) &
		BT_TUITION.CH%

	COM (CH_BT_CUSTYP) &
		BT_CUSTYP.CH%

	!
	! External functions
	!
	EXTERNAL LONG   FUNCTION MAIN_WINDOW

	ON ERROR GOTO 29000

	%PAGE

	SELECT MOPTION

	!******************************************************************
	! Initialization
	!
	! This option is used to initialize the window structure,
	! set up the default values for add, and open all files
	! necessary that have not already been opened.
	!******************************************************************

	CASE OPT_INIT
		!
		! Define window
		!
		SMG_WINDOW::DESCR = "Line items"
		SMG_WINDOW::NHELP = "BT_MAIN_JOURNALL"
		SMG_WINDOW::HSIZE = 78%
		SMG_WINDOW::VSIZE = 9%
		SMG_WINDOW::HPOS  = 2%
		SMG_WINDOW::VPOS  = 11%
		SMG_WINDOW::NITEMS= 3%
		SMG_WINDOW::FLAGS = 0%

		SMG_WINDOW::TOPLIN = 3%
		SMG_WINDOW::BOTLIN = 8%

		!
		! Load in defaults for chart
		!
		CALL READ_DEFAULTS(SMG_WINDOW)

700		!
		! Declare channels
		!
		IF BT_JOURNALL.CH% > 0%
		THEN
			!
			! Already open, set flag to read-only if
			! was that way from last time.
			!
			SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%) &
				IF BT_JOURNALL.READONLY%
			GOTO 790
		END IF

		!
		! Open main file (existing) for modification
		!
750		%INCLUDE "SOURCE:[BT.OPEN]BT_JOURNALL.CRE"
		BT_JOURNALL.READONLY% = 0%
		GOTO 790

790		SMG_WINDOW::CHAN  = BT_JOURNALL.CH%

	%PAGE

20100	!******************************************************************
	! Display the background
	!
	! This option is used to display the background information
	! on the screen.  It must first clear any junk on the screen,
	! and then write the background onto it.
	!******************************************************************
	CASE OPT_BACKGROUND

		SMG_STATUS% = SMG$BEGIN_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

		SMG_STATUS% = SMG$ERASE_DISPLAY(SMG_WINDOW::WNUMBER)

		SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
			"  (01)"+SPACE$(36%)+"(02)     (03)" + SPACE$(23%), &
			1%, 1%, , SMG$M_REVERSE)
		SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
			"  Client"+SPACE$(35%)+"Amount  Account"+SPACE$(20%), &
			2%, 1%, , SMG$M_REVERSE)

		SMG_STATUS% = SMG$END_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

	!
	! Extra display stuff
	!
	CASE OPT_DISPLAY

		SMG_STATUS% = SMG$BEGIN_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

		!
		! Generate totals
		!
		RATE = 0.0

		FOR I% = 1% TO SMG_WINDOW::TOTREC

			RATE = RATE + RARRAY(I%)::RATE

		NEXT I%

		!
		! Display totals
		!
		SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
			"Number of lines" + &
			FORMAT$(SMG_WINDOW::TOTREC, "###") + &
			SPACE$(21%) + &
			FORMAT$(RATE, "###,###.##") + &
			SPACE$(29%), &
			SMG_WINDOW::VSIZE, 1%, , SMG$M_REVERSE)

		!
		! Paint lines on screen
		!
		FOR I% = 1% TO 2%

			A% = VAL%(MID("039,050", I%*4%-3%, 3%))

			SMG_STATUS% = SMG$DRAW_LINE(SMG_WINDOW::WNUMBER, &
				1%, A%, SMG_WINDOW::BOTLIN, A%)

		NEXT I%

		SMG_STATUS% = SMG$END_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

20200	!******************************************************************
	! Enter/Display/Default
	!
	! This option is used to enter the data from the user, display
	! data, set defaults, and return the data back according to
	! MFLAG.
	!******************************************************************
	CASE OPT_ENTRY
		TEMP$, TEMP1$ = TRM$(SCOPE::PRG_ITEM)
		TEMP$ = "View starting at" IF TEMP$ = "View"

		SCOPE::PRG_ITEM = "FLD" + FORMAT$(MLOOP, "<0>##")

		XLINE$ = NUM1$(SMG_WINDOW::CURLIN)

 ELoop:		SCOPE::SCOPE_EXIT = 0%

		SELECT MLOOP

		CASE 1%

	!++
	! Abstract:FLD001
	!	^*(01) Client\*
	!	.p
	!	The ^*Client\* field provides the means to enter the name of the
	!	Client related to a particular Guardian.
	!	.p
	!	The field may contain forty (40) alphanumeric characters.
	!
	! Index:
	!	.x Client
	!
	!--

			BT_JOURNALL::CHILD = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";3", TEMP$, &
				BT_JOURNALL::CHILD, MFLAG, &
				"'LLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLL", MVALUE)

			IF (TEMP$ = "Add") AND ((MFLAG AND 1%) = 0%)
			THEN
				GOSUB ReadTable
			END IF

		CASE 2%

	!++
	! Abstract:FLD002
	!	^*(02) Amount\*
	!	.p
	!	The ^*Amount\* field is provided to enter the amount of
	!	a particular transaction.
	!	.p
	!	The field may contain a figure as large as 999,999.99.
	!
	! Index:
	!	.x Amount
	!
	!--

			BT_JOURNALL::RATE = ENTR_3NUMBER(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";40", TEMP$, &
				BT_JOURNALL::RATE, MFLAG, &
				"###,###.##", MVALUE)

		CASE 3%

	!++
	! Abstract:FLD003
	!	^*(03) Account\*
	!	.p
	!	The ^*Account\* field contains the General Ledger
	!	account number to which this particular line item has reference,
	!	ordinarily a revenue account.
	!	.p
	!	This field will default to the account number defined in the
	!	^*MASTER TABLE ARTOGL\* program for the guardian type defined
	!	in the ^*MASTER NAME\* program for the current guardian.
	!	.p
	!	Pressing ^*<List Choices>\* while the cursor is located at
	!	this field will display a list of valid General Ledger account
	!	numbers.
	!
	! Index:
	!	.x General Ledger>Account Number
	!	.x Account Number>General Ledger
	!
	!--

			BT_JOURNALL::ACCT = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";51", TEMP$, &
				BT_JOURNALL::ACCT, MFLAG, "'E", MVALUE)

			IF (SCOPE::SCOPE_EXIT = SMG$K_TRM_F14)
			THEN
				IF (MAIN_WINDOW(GL_MAIN_CHART.ID, "VX") = 1%)
				THEN
					BT_JOURNALL::ACCT = &
						GL_CHART::ACCT
				END IF
				GOTO ELoop
			END IF
		END SELECT

		SCOPE::PRG_ITEM = TEMP1$

	!
	! Test values
	!
20300	CASE OPT_TESTENTRY
		BT_MAIN_JOURNALL = 0%

		SELECT MLOOP

		CASE 1%
			BT_MAIN_JOURNALL = 1% IF BT_JOURNALL::CHILD = ""

		END SELECT

	!
	! Set BT_JOURNALL_OLD value
	!
20500	CASE OPT_SETOLD
		BT_JOURNALL_OLD = BT_JOURNALL

	!
	! Restore BT_JOURNALL_OLD value
	!
	CASE OPT_RESETOLD
		BT_JOURNALL = BT_JOURNALL_OLD

	!
	! Set default value
	!
	CASE OPT_SETDEFAULT
		BT_JOURNALL2 = BT_JOURNALL

	!
	! Restore default value
	!
	CASE OPT_RESETDEFAULT
		BT_JOURNALL = BT_JOURNALL2

		!
		! Set special default values for the key of a new record
		! which is the most likely case when this function is to
		! be called.
		!
		BT_JOURNALL::CUSNUM = BT_JOURNALH::CUSNUM

	!
	! Find
	!
	CASE OPT_FIND
		SELECT MLOOP

		CASE 0%
			FIND #BT_JOURNALL.CH%, &
				KEY#0% GE BT_JOURNALL::CUSNUM + "", &
				REGARDLESS
		END SELECT

	!
	! Handle array of records
	!
27000	CASE OPT_ARRAY

		!
		! Select sub-option of array
		!
		SELECT MLOOP

		!
		! Load array with line items
		!
		CASE 1%

			!
			! Empty array
			!
			SMG_WINDOW::TOTREC = 0%

27110			!
			! Search for first record
			!
			FIND #SMG_WINDOW::CHAN, &
				KEY #0% GE BT_JOURNALH::CUSNUM + "", &
				REGARDLESS

27120			!
			! Get a record
			!
			GET #SMG_WINDOW::CHAN

			IF BT_JOURNALL::CUSNUM = BT_JOURNALH::CUSNUM
			THEN
				!
				! Add information to array
				!
				SMG_WINDOW::TOTREC = SMG_WINDOW::TOTREC + 1%
				RARRAY(SMG_WINDOW::TOTREC)::LINRFA = &
					GETRFA(SMG_WINDOW::CHAN)
				RARRAY(SMG_WINDOW::TOTREC)::RATE = &
					BT_JOURNALL::RATE
				GOTO 27120
			END IF

		!
		! Remove one element of the array
		!
		CASE 2%
			!
			! Remove item pointed to by Mflag
			!
			FOR I% = MFLAG TO SMG_WINDOW::TOTREC - 1%
				RARRAY(I%) = RARRAY(I% + 1%)
			NEXT I%

		!
		! Set array item to current record
		!
		CASE 3%
			RARRAY(MFLAG)::LINRFA = GETRFA(SMG_WINDOW::CHAN)
			RARRAY(SMG_WINDOW::TOTREC)::RATE = &
				BT_JOURNALL::RATE

		!
		! Load in current record, locked
		!
		CASE 4%
27200			GET #SMG_WINDOW::CHAN, RFA RARRAY(MFLAG)::LINRFA

		!
		! Load in current record, unlocked
		!
		CASE 5%
			GET #SMG_WINDOW::CHAN, RFA RARRAY(MFLAG)::LINRFA, &
				REGARDLESS

		!
		! Change the current record's key to match header.  The
		! new key is probibly passes through MVALUE, unless some
		! other means is devised.
		!
		CASE 6%
			BT_JOURNALL::CUSNUM = RIGHT(MVALUE, 2%)

		END SELECT

	END SELECT

 ExitFunction:
	EXIT FUNCTION

	%PAGE

28100	!*******************************************************************
	! Read the billing table
	!*******************************************************************
 ReadTable:
	!
	! Try to find billing table record
	!
	GET #BT_TUITION.CH%, &
		KEY #0% EQ BT_JOURNALL::CUSNUM + BT_JOURNALL::CHILD, &
		REGARDLESS

28110	!
	! Search for current record
	!
	IF (BT_TUITION::CUSNUM = BT_JOURNALL::CUSNUM) AND &
		(BT_TUITION::CHILD = BT_JOURNALL::CHILD)
	THEN
		!
		! Verify the date range
		!
		IF (BT_TUITION::FROMDATE <= BT_JOURNALH::TRADAT) AND &
			((BT_TUITION::TODATE = "") OR &
			(BT_TUITION::TODATE >= BT_JOURNALH::TRADAT))
		THEN
			!
			! Is in range, use data
			!
			GOTO 28120
		ELSE
			!
			! Is not in range, try next record
			!
			GET #BT_TUITION.CH%, REGARDLESS
			GOTO 28110
		END IF
	ELSE
		!
		! No record found for this employee/customer pair
		!
		GOTO 28190
	END IF

28120	!
	! Set up information in main record
	!
	BT_JOURNALL::RATE = BT_TUITION::RATE
	BT_JOURNALL::ACCT = BT_TUITION::ACCOUNT

	TEMP = ENTR_3NUMBER(SCOPE, SMG_WINDOW::WNUMBER, &
		XLINE$ + ";40", TEMP$, &
		BT_JOURNALL::RATE, 1%, "###,###.##", MVALUE)

	TEMP$ = ENTR_3STRING(SCOPE, SMG_WINDOW::WNUMBER, &
		XLINE$ + ";51", TEMP$, &
		BT_JOURNALL::ACCT, 1%, "'E", MVALUE)

28190	RETURN

	%PAGE

29000	!*******************************************************************
	! Trap errors
	!*******************************************************************
	SELECT ERL
	CASE 750%
		RESUME 32767

	!
	! Record does not already exist
	!
	CASE 20300%
		RESUME 32767 IF ERR = 155%

	!
	! Error while loading array
	!
	CASE 27110%
		!
		! Nothing to load
		!
		RESUME ExitFunction

	CASE 27120%
		!
		! Read all records
		!
		RESUME ExitFunction IF ERR = 11%

	CASE 28100%
		!
		! Unable to find billing table record
		!
		RESUME 28190

	END SELECT

	ON ERROR GO BACK

32767	END FUNCTION
