1	%TITLE "Customer Type Description"
	%SBTTL "BT_RPRT_CUSTYP"
	%IDENT "V3.6a Calico"

	!
	! COPYRIGHT (C) 1986 BY
	!
	! Computer Management Center
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! ID:BT051
	!
	! Abstract:HELP
	!	.p
	!	The ^*List Accounts Receivable to General Ledger Table\* option in the Billing
	!	Tuition Master List
	!	menu provides the means to print a report which contains the following
	!	information:
	!	.b
	!	.lm +10
	!	.list 0,"*"
	!	.le
	!	Guardian Type
	!	.le
	!	Account Receivable _#
	!	.le
	!	Revenue Account Mask
	!	.le
	!	Description
	!	.els
	!
	! Index:
	!	.x AR to GL Table>Report
	!	.x Report>AR to GL Table
	!
	! Option:
	!
	!
	! Compile:
	!
	!	$ BAS BT_SOURCE:BT_RPRT_CUSTYP/LINE
	!	$ LINK/EXE=BT_EXE: BT_RPRT_CUSTYP, FUNC_LIB:CMCLINK/OPTION
	!	$ DELETE BT_RPRT_CUSTYP.OBJ;*
	!
	! AUTHOR:
	!
	!	05/04/88 - Lance Williams
	!
	! MODIFICATION HISTORY:
	!
	!	03/29/93 - Kevin Handy
	!		Clean up (Check)
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	09/03/96 - Kevin Handy
	!		Reformat source code.
	!
	!	05/23/97 - Kevin Handy
	!		Use integer for #key
	!
	!	08/19/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--
	%PAGE

	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	!
	! Include files
	!
	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "SOURCE:[UTL.OPEN]UTL_REPORTX.HB"
	DECLARE UTL_REPORTX_CDD UTL_REPORTX

	%INCLUDE "SOURCE:[BT.OPEN]BT_CUSTYP.HB"
	MAP (BT_CUSTYP) BT_CUSTYP_CDD BT_CUSTYP

	%INCLUDE "SOURCE:[AR.OPEN]AR_CUSTYPE.HB"
	MAP (AR_CUSTYPE)		AR_CUSTYPE_CDD	AR_CUSTYPE

	%PAGE

	ON ERROR GOTO 19000

	!
 Init:	! Initilize report
	!
	CALL OUTP_INITFROMFILE(UTL_REPORTX, 132%)

	GOTO ExitProgram IF UTL_REPORTX::STAT

	FROM.ITEM$ = EDIT$(UTL_REPORTX::OPTDEF(0%), 132%)

	!++
	! Abstract:FLD01
	!	^*(01) From Type\*
	!	.p
	!	The ^*From Type\* value entered in this field will cause the
	!	printing of the report to begin with the selected Guardian Type
	!	by entering the selection in this field.
	!	.p
	!	A blank setting will cause the report to begin with the first
	!	Guardian Type in the file.
	!
	! Index:
	!	.x From Type>AR to GL Table
	!	.x AR to GL Table>From Type
	!	.x Type>From
	!
	!--

	TO.ITEM$ = EDIT$(UTL_REPORTX::OPTDEF(1%), 132%)

	!++
	! Abstract:FLD02
	!	^*(02) To Type\*
	!	.p
	!	The ^*To Type\* value entered in this field will cause the printing
	!	of the report to end with the selected Guardian Type by entering the
	!	selection in this field.
	!	.p
	!	A blank setting will cause the report to end with the last Guardian
	!	Type in the file.
	!
	! Index:
	!	.x To Type>AR to GL Table
	!	.x AR to GL Table>To Type
	!	.x Type>To
	!
	!--

	WLDCRD$ = EDIT$(UTL_REPORTX::OPTDEF(2%), -1%)

	!++
	! Abstract:FLD03
	!	^*(03) Wildcard\*
	!	.p
	!	The ^*Wildcard\* field provides the means to select designated
	!	Guardian types to be printed by entering a "wildcard" value in this
	!	field.
	!
	! Index:
	!	.x Wildcard>AR to GL Table
	!	.x AR to GL Table>Wildcard
	!
	!--


300	%INCLUDE "SOURCE:[BT.OPEN]BT_CUSTYP.OPN"

310	%INCLUDE "SOURCE:[AR.OPEN]AR_CUSTYPE.OPN"

 ReportTitle:
	!
	! Title
	!
	TITLE$(1%) = "CUSTOMER TYPE REPORT"
	TITLE$(2%) = "Billing Tuition system"
	TITLE$(3%) = ""

	!
	! Heading
	!
	!		 1234567890123456789012345678901234567890
	TITLE$(4%) = "CustType Account#             " + &
		"AcctMask             Description"
	TITLE$(5%) = "."

	LYT_LINE$ = "$CustType:009,$Account#:030,$AcctMask:051,$Description:091"

	%PAGE

17000	!******************************************************************
	! OUTPUT REPORT
	!******************************************************************

	IF FROM.ITEM$ = ""
	THEN
		RESET #BT_CUSTYP.CH%
	ELSE
		FIND #BT_CUSTYP.CH%, KEY#0% GE FROM.ITEM$,REGARDLESS
	END IF

 GetNextRec:
17020	!
	! Main loop starts here
	!
	GOTO ExitProgram IF UTL_REPORTX::STAT

	!
	! Get next record
	!
	GET #BT_CUSTYP.CH%, REGARDLESS

	!
	! Check current record
	!
	GOTO ExitTotal IF (BT_CUSTYP::CUSTYP > TO.ITEM$) AND TO.ITEM$ <> ""

	GOTO GetNextRec &
		IF COMP_STRING(EDIT$(BT_CUSTYP::CUSTYP,-1%), WLDCRD$) = 0% &
		AND WLDCRD$ <> ""

17030
	GET #AR_CUSTYPE.CH%, KEY#0% EQ BT_CUSTYP::CUSTYP, REGARDLESS

17300	!
	! Print out one line
	!
	TEXT$ = BT_CUSTYP::CUSTYP + "       " + &
		BT_CUSTYP::AR_ACCT + "   " + &
		BT_CUSTYP::REV_MASK + "   " + &
		AR_CUSTYPE::DESCRIPTION

	CALL OUTP_LINE(LYT_LINE$, UTL_REPORTX, TITLE$(), TEXT$, 0%)

	GOTO ExitProgram IF UTL_REPORTX::STAT

17350	!
	! Try for next record
	!
	GOTO GetNextRec

 ExitTotal:
17400	!
	! Handle end of report
	!

 ExitProgram:
	CALL OUTP_FINISH(UTL_REPORTX)

	!
	! Exit to next program or menu
	!
	IF TRM$(UTL_REPORTX::NEXTRUN) = ""
	THEN
		CALL SUBR_3EXITPROGRAM(SCOPE, "", "")
	ELSE
		CALL SUBR_3EXITPROGRAM(SCOPE, "RUN " + UTL_REPORTX::NEXTRUN, "")
	END IF

	%PAGE

 HelpError:
	!******************************************************************
	! Help Message for an error
	!******************************************************************
	CALL HELP_34MESSAGE(SCOPE, NUM1$(ERL) + " " + ERT$(ERR), &
		"E", ERN$, FILENAME$, NUM1$(ERR))
	UTL_REPORTX::STAT = -1%
	GOTO ExitProgram

	%Page

19000	!******************************************************************
	! ERROR TRAPPING
	!******************************************************************

	SELECT ERR
	CASE 154%	! Locked record
		SLEEP 5%
		RESUME
	END SELECT

	FILENAME$ = ""
	SELECT ERL

	CASE 300%
		FILENAME$ = "BT_CUSTYP"

	CASE 310%
		FILENAME$ = "AR_CUSTYPE"

	CASE 17000%
		CALL ENTR_3MESSAGE(SCOPE, &
			"Unable to find beginning record!", 0%)
		RESUME ExitProgram

	CASE 17020%	! End of file
		RESUME ExitTotal IF ERR = 11%
		FILENAME$ = "BT_CUSTYP"

	CASE 17030%
		RESUME 17300 IF ERR = 155%
		FILENAME$ = "AR_CUSTYPE"

	END SELECT

	!
	! Untrapped error
	!
	RESUME HelpError

32767	END
