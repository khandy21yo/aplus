1	%TITLE "Employee Billing Description"
	%SBTTL "BT_RPRT_TUITION"
	%IDENT "V3.6a Calico"

	!
	! COPYRIGHT (C) 1986 BY
	!
	! Computer Management Center
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! ID:BT050
	!
	! Abstract:HELP
	!	.p
	!	The ^*List Tuition Billing Table\* option in the Billing Tuition
	!	Master List menu provides the means to print a report which lists
	!	the guardians and related billing information.
	!	.p
	!	The report includes the following information:
	!	.b
	!	.lm +15
	!	.ls 0,"*"
	!	.le
	!	Guardian
	!	.le
	!	Client
	!	.le
	!	From Date
	!	.le
	!	To Date
	!	.le
	!	Rate
	!	.els
	!
	! Index:
	!	.x Report>Tuition Billing Table
	!	.x Tuition Billing Table>Report
	!
	! Option:
	!
	!
	! Compile:
	!
	!	$ BAS BT_SOURCE:BT_RPRT_TUITION/LINE
	!	$ LINK/EXEC=BT_EXE: BT_RPRT_TUITION, FUNC_LIB:CMCLINK/OPTION
	!	$ DELETE BT_RPRT_TUITION.OBJ;*
	!
	! AUTHOR:
	!
	!	05/25/88 - Kevin Handy
	!
	! MODIFICATION HISTORY:
	!
	!	07/08/88 - Kevin Handy
	!		Modified tuition file and journal file to
	!		have child name instead of pointing to the
	!		payroll name file.
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	09/03/96 - Kevin Handy
	!		Reformat source code
	!
	!	05/23/97 - Kevin Handy
	!		Use integer for #key
	!
	!	08/19/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--
	%PAGE

	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	!
	! Include files
	!
	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "SOURCE:[UTL.OPEN]UTL_REPORTX.HB"
	DECLARE UTL_REPORTX_CDD UTL_REPORTX

	%INCLUDE "SOURCE:[BT.OPEN]BT_TUITION.HB"
	MAP (BT_TUITION) BT_TUITION_CDD BT_TUITION

	%PAGE

	ON ERROR GOTO 19000

	!
 Init:	! Initilize report
	!
	CALL OUTP_INITFROMFILE(UTL_REPORTX, 132%)

	GOTO ExitProgram IF UTL_REPORTX::STAT

	FROM.ITEM$ = EDIT$(UTL_REPORTX::OPTDEF(0%), 132%)

	!++
	! Abstract:FLD01
	!	^*(01) From Item\*
	!	.p
	!	The ^*From Item\* value entered in this field causes
	!	the report to begin with a selected item by entering the
	!	selection in this field.
	!	.p
	!	A blank setting will cause the report to begin with the first
	!	item in the file.
	!
	! Index:
	!	.x From Item>Employee Billing Description
	!	.x Employee Billing Description>From Item
	!	.x Item>From
	!
	!--

	TO.ITEM$ = EDIT$(UTL_REPORTX::OPTDEF(1%), 132%)

	!++
	! Abstract:FLD02
	!	^*(02) To Item\*
	!	.p
	!	The ^*To Item\* value entered in this field causes the
	!	report to end with a selected itemby entering the selection
	!	in this field.
	!	.p
	!	A blank setting will cause the report to end with the last
	!	item in the file.
	!
	! Index:
	!	.x To Item>Employee Billing Description Table
	!	.x Employee Billing Description Table>To Item
	!	.x Item>To
	!
	!--

	WLDCRD$ = EDIT$(UTL_REPORTX::OPTDEF(2%), -1%)

	!++
	! Abstract:FLD03
	!	^*(03) Wildcard\*
	!	.p
	!	The ^*Wildcard\* field in the Tuition Billing File report setting
	!	screen provides the means to select designated Guardian types to
	!	be printed in the report by entering a "wildcard" value in this
	!	field.
	!
	! Index:
	!	.x Wildcard>Employee Billing Description Table
	!	.x Employee Billing Description Table>Wildcard
	!
	!--


300	%INCLUDE "SOURCE:[BT.OPEN]BT_TUITION.OPN"

 ReportTitle:
	!
	! Title
	!
	TITLE$(1%) = "BILLING TUITION REPORT"
	TITLE$(2%) = "Billing Tuition System"
	TITLE$(3%) = ""

	!
	! Heading
	!
	!		 1234567890123456789012345678901234567890
	TITLE$(4%) = "Guardian      Client                                    " + &
		"  FromDate   ToDate           Rate  Account"
	TITLE$(5%) = "."
	TITLE$(6%) = ""

	LYT_LINE$ = "$Guardian:014,$Client:058,DFromDate:069," + &
		"DToDate:086,VRate:092,$Account:111"

	%PAGE

17000	!******************************************************************
	! OUTPUT REPORT
	!******************************************************************

	IF FROM.ITEM$ = ""
	THEN
		RESET #BT_TUITION.CH%
	ELSE
		FIND #BT_TUITION.CH%, KEY#0% GE FROM.ITEM$,REGARDLESS
	END IF

 GetNextRec:
17020	!
	! Main loop starts here
	!
	GOTO ExitProgram IF UTL_REPORTX::STAT

	!
	! Get next record
	!
	GET #BT_TUITION.CH%, REGARDLESS

	!
	! Check current record
	!
	GOTO ExitTotal IF (BT_TUITION::CUSNUM > TO.ITEM$) AND TO.ITEM$ <> ""

	GOTO GetNextRec &
		IF COMP_STRING(EDIT$(BT_TUITION::CUSNUM,-1%), WLDCRD$) = 0% &
		AND WLDCRD$ <> ""

17300	!
	! Print out one line
	!

	IF BT_TUITION::CUSNUM <> EMPLOYEE$ AND EMPLOYEE$ <> ""
	THEN
		CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), "", 0%)
	END IF

	TEXT$ = BT_TUITION::CUSNUM + "    " + &
		BT_TUITION::CHILD + "    " + &
		PRNT_DATE(BT_TUITION::FROMDATE,8%) + " " + &
		PRNT_DATE(BT_TUITION::TODATE,8%) + "  " + &
		FORMAT$(BT_TUITION::RATE,"######.##") + "  " + &
		BT_TUITION::ACCOUNT


	CALL OUTP_LINE(LYT_LINE$, UTL_REPORTX, TITLE$(), TEXT$, 0%)

	GOTO ExitProgram IF UTL_REPORTX::STAT
	EMPLOYEE$ = BT_TUITION::CUSNUM

17350	!
	! Try for next record
	!
	GOTO GetNextRec

 ExitTotal:
17400	!
	! Handle end of report
	!

 ExitProgram:
	CALL OUTP_FINISH(UTL_REPORTX)

	!
	! Exit to next program or menu
	!
	IF TRM$(UTL_REPORTX::NEXTRUN) = ""
	THEN
		CALL SUBR_3EXITPROGRAM(SCOPE, "", "")
	ELSE
		CALL SUBR_3EXITPROGRAM(SCOPE, "RUN " + UTL_REPORTX::NEXTRUN, "")
	END IF

	%PAGE

 HelpError:
	!******************************************************************
	! Help Message for an error
	!******************************************************************
	CALL HELP_34MESSAGE(SCOPE, NUM1$(ERL) + " " + ERT$(ERR), &
		"E", ERN$, FILENAME$, NUM1$(ERR))
	UTL_REPORTX::STAT = -1%
	GOTO ExitProgram

	%Page

19000	!******************************************************************
	! ERROR TRAPPING
	!******************************************************************

	SELECT ERR
	CASE 154%	! Locked record
		SLEEP 5%
		RESUME
	END SELECT

	FILENAME$ = ""
	SELECT ERL

	CASE 300%
		FILENAME$ = "BT_TUITION"

	CASE 17000%
		CALL ENTR_3MESSAGE(SCOPE, &
			"Unable to find beginning record!", 0%)
		RESUME ExitProgram

	CASE 17020%	! End of file
		RESUME ExitTotal IF ERR = 11%
		FILENAME$ = "BT_TUITION"

	END SELECT

	!
	! Untrapped error
	!
	RESUME HelpError

32767	END
