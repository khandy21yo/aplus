1	%TITLE "String Comparison Function"
	%SBTTL "COMP_STRING"
	%IDENT "V3.3"

	FUNCTION INTEGER COMP_STRING(TEST_STRING$, MATCH_STRING$)

	!
	!	COPYRIGHT (C) 1988 BY
	!	Computer Management Center, Inc.
	!	Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a committment by
	! Computer management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	!
	! Abstract:HELP
	!	.b
	!	.lm +5
	!	This function compares a string to a pattern.
	!	It returns 0 if there is no match, and -1
	!	if there is.
	!	.lm -5
	!
	! Index:
	!
	! Parameters:
	!
	!	TEST_STRING$
	!		The passed string to be tested.
	!
	!	MATCH_STRING$
	!		The passed pattern to compare against.
	!		This value can be anything that is allowed in the
	!		"Wildcard" as defined in other manuels.
	!		('*'s, ",", "/", "?", ...)
	!
	!	Returned value
	!		An integer value containing the result of the
	!		comparison.
	!		(0 if match fails and -1 it succeeds.)
	!
	! Environment:
	!
	!	VAX/VMS BASIC V4.4
	!
	! Compile:
	!
	!	$ BAS FUNC_SOURCE:COMP_STRING/NOLINE
	!	$ LIB FUNC_LIB:CMC_3VECTOR/REP COMP_STRING
	!	$ DELETE COMP_STRING.OBJ;*
	!
	! AUTHOR:
	!
	!	11/11/85 - Kevin Handy
	!
	! MODIFICATION HISTORY:
	!
	!	04/27/88 - Kevin Handy
	!		Modified to use the VAX function STR$MATCH_WILD
	!		which gives a much nicer range of matching
	!		(as well as being more consistant with the
	!		rest of VMS wildcards)
	!
	!	05/05/88 - Kevin Handy
	!		Modified to trim compare string, so that it will
	!		work like it did originally.
	!
	!	09/09/91 - Kevin Handy
	!		Modify documentation of function.
	!
	!	03/14/92 - Kevin Handy
	!		Clean up vars (checkvar)
	!--
	%PAGE

	!++
	!
	! Variables:
	!
	!	TEST_STRINGA$
	!		String that text the two values.
	!
	!	IS.MATCH%
	!		Integer that gives the results of comparisons.
	!
	!	STR2$
	!		String that is the second value.
	!
	!--

	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	!
	! External functions
	!
	EXTERNAL LONG FUNCTION STR$MATCH_WILD
	EXTERNAL LONG CONSTANT STR$_MATCH
	EXTERNAL LONG CONSTANT STR$_NOMATCH

	!
	! Trim the test string
	!
	TEST_STRINGA$	= TRM$(TEST_STRING$)

	IS.MATCH%	= 0%			! No match
	IS.MATCH%	= -1% IF MATCH_STRING$ = "*"	! Yes match all
	STR2$		= TRM$(MATCH_STRING$) + ","

 L1000:	IF IS.MATCH% = 0%
	THEN
		!
		! Strip off one item to match
		!
		N.ITEM$	= LEFT(STR2$, INSTR(1%, STR2$, ",") - 1%)
		STR2$	= RIGHT(STR2$, LEN(N.ITEM$) + 2%)

		!
		! A slash is used for from-to, but if there is a wildcard
		! character in it, it is ignored.
		!
		SLASH%	= INSTR(1%, N.ITEM$, "/")
		SLASH%	= 0% IF INSTR(1%, N.ITEM$, "?") OR &
			INSTR(1%, N.ITEM$, "%") OR &
			INSTR(1%, N.ITEM$, "*")

		!
		! Handle as either from/to, or as a comparison string
		!
		IF SLASH%
		THEN
			!
			! From/to
			!
			IS.MATCH% = LEFT(N.ITEM$,SLASH%-1%) <= TEST_STRINGA$ &
				AND TEST_STRINGA$ <= RIGHT(N.ITEM$, SLASH% + 1%)
		ELSE
			!
			! Change all question marks (?) to percent signes (%)
			! so that old type of comparison from RSTS/E will
			! still work.
			!
			FIXUP% = INSTR(1%, N.ITEM$, "?")
			WHILE FIXUP%
				N.ITEM$ = LEFT(N.ITEM$, FIXUP% - 1%) + "%" + &
					RIGHT(N.ITEM$, FIXUP% + 1%)
				FIXUP% = INSTR(1%, N.ITEM$, "?")
			NEXT

			IS.MATCH% = -1% &
			IF (STR$MATCH_WILD(TEST_STRINGA$, N.ITEM$) = STR$_MATCH)

		END IF
	END IF

 L2000:	GOTO L1000 IF STR2$ <> "" AND IS.MATCH% = 0%
	COMP_STRING	= IS.MATCH%

	END FUNCTION
