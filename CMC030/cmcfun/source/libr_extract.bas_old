1	%TITLE "Extract a File from a Text Library"
	%SBTTL "LIBR_EXTRACT"
	%IDENT "V3.3"

	FUNCTION LONG LIBR_EXTRACT(LIB_NAME$, FILE_NAME$, KEY_NAME$)

	!
	!		COPYRIGHT (C) 1987 BY
	!		Computer Management Center, Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	!
	! Abstract:HELP
	!	.p
	!	This function pulls text out of a library, and puts
	!	it into a specified text file.
	!
	! Parameters:
	!
	!	LIB_NAME$
	!		Passed name of the library to pull text from.
	!
	!	FILE_NAME$
	!		Passed name of file to write text into.
	!
	!	KEY_NAME$
	!		Passed key for text to be pulled.
	!
	!	Returns a status code.
	!
	! Example:
	!
	!	ST% = LIBR_EXTRACT("HELP_GL", "TEXT.FILE", "ADDRESS")
	!
	! Environment:
	!
	!	VAX/VMS BASIC V4.4
	!
	! Compile:
	!
	!	$ BAS FUNC_SOURCE:LIBR_EXTRACT/NOLINE
	!	$ LIB FUNC_LIB:CMC_3VECTOR/REP LIBR_EXTRACT
	!	$ DELETE LIBR_EXTRACT.OBJ;*
	!
	! Author:
	!
	!	07/01/87 - Kevin Handy
	!
	! Modification history:
	!
	!	07/19/88 - Kevin Handy
	!		Removed defaulting to REF:
	!
	!	11/30/88 - Kevin Handy
	!		Modified to close library if open fails.
	!
	!	03/14/92 - kevin Handy
	!		Clean up vars (checkvar)
	!--
	%PAGE

	!++
	!
	! Variables:
	!
	!	TXRFA
	!		Variable used to test the data.
	!
	!--
	%PAGE

	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	!
	! Include files
	!
 !	%INCLUDE "FUNC_INCLUDE:SCOPE.COM"
	%INCLUDE "FUNC_INCLUDE:LIBRARY.COM"

	!
	! External functions
	!
	EXTERNAL LONG FUNCTION LIB$GET_LUN
	EXTERNAL LONG FUNCTION LIB$FREE_LUN

	!
	! Declare variables
	!
 !	MAP (LBR_JUNKJUNK) TEXT$ = 132%		% Read buffer
	DECLARE RFA TXRFA

	%PAGE

	ON ERROR GOTO 19000
	LIBR_EXTRACT = 1%

100	!
	! Set up the control structure if necessary
	!
	ST% = LBR$INI_CONTROL(LR.INDEX%, LBR$C_READ)

	IF (ST% AND 1%) = 0%
	THEN
 !		CALL ENTR_3MESSAGE(SCOPE, "Unable to initilize library", 0%)
		LIBR_EXTRACT = ST%
		EXIT FUNCTION
	END IF

200	!
	! Open the library function
	!
	ST% = LBR$OPEN(LR.INDEX%, LIB_NAME$, , ".TLB")

	IF (ST% AND 1%) = 0%
	THEN
		LIBR_EXTRACT = ST%
		GOTO CloseLibrary
	END IF

300	!
	! Search for key in file
	!
	ST% = LBR$LOOKUP_KEY(LR.INDEX%, KEY_NAME$, TXRFA)

	IF (ST% AND 1%) = 0%
	THEN
		LIBR_EXTRACT = ST%
		GOTO CloseLibrary
	END IF

400	!
	! Open up output file
	!
	ST% = LIB$GET_LUN(OUTPUT.CH%)

	OPEN FILE_NAME$ FOR OUTPUT AS FILE OUTPUT.CH%, &
		RECORDSIZE 255%

500	!
	! Copy over text
	!
 Loop:
	TEXT$ = SPACE$(132%)
	ST% = LBR$GET_RECORD(LR.INDEX%, TEXT$)

	IF (ST% AND 1%) = 1%
	THEN
		PRINT #OUTPUT.CH%, TRM$(TEXT$)
		GOTO Loop
	END IF

1000	!
	! Close library file
	!
 CloseLibrary:
	ST% = LBR$CLOSE(LR.INDEX%)
	CLOSE OUTPUT.CH%
	ST% = LIB$FREE_LUN(OUTPUT.CH%)

	EXIT FUNCTION

19000	!
	! Error trapping
	!
	LIBR_EXTRACT = -ERR
	RESUME CloseLibrary

	END FUNCTION
