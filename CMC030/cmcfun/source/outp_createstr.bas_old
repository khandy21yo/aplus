1	%TITLE "Create Print String to Be Run Through WRIT_STRING"
	%SBTTL "OUTP_CREATESTR"
	%IDENT "V3.6 Calico"

	FUNCTION STRING OUTP_CREATESTR(SEQ$, ITEM$)
	!
	!	COPYRIGHT (C) 1986 BY
	!	Computer Management Center, Inc.
	!	Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	!
	! Abstract:HELP
	!	.p
	!	Create a device control string.
	!
	! Parameters:
	!
	!	SEQ$
	!		The passed string that holds the print string to the run
	!		through WRIT_STRING.
	!
	!	ITEM$
	!		The passed string that holds the item to be run through.
	!
	!
	!	Returned value
	!		Creates a print string to be run through
	!		WRIT_STRING.
	!
	! Example:
	!
	! Index:
	!
	!	.x Create>String
	!	.x String>Create
	!
	! Environment:
	!
	!	VAX/VMS BASIC V4.4
	!
	! Compile:
	!
	!	$ BAS FUNC_SOURCE:OUTP_CREATESTR/NOLINE
	!	$ LIB FUNC_LIB:CMC_3VECTOR/REP OUTP_CREATESTR
	!	$ DELETE OUTP_CREATESTR.OBJ;*
	!
	! Author:
	!
	!	01/01/86 - Kevin Handy
	!
	! Modification history:
	!
	!	03/09/92 - Kevin Handy
	!		Added "*2" type.
	!
	!	07/06/93 - Kevin Handy
	!		Modified to use VAL%( instead of VAL( which
	!		should be slightly faster.
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!--
	%PAGE

	!++
	!
	! Variables:
	!
	!
	!	STRNG$
	!		String that holds the value of the print string.
	!
	!	ITEM1$
	!		String that holds the value of the item string.
	!
	!	I%
	!		Integer used to search for special cases of the
	!		print string.
	!
	!--
	%PAGE

	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	!
	! Include files
	!
 !	%INCLUDE "FUNC_INCLUDE:PRINT.COM"

	%PAGE

	!
	! Initilization
	!
	STRNG$ = TRM$(SEQ$)
	ITEM1$ = TRM$(ITEM$)
	OUTSTR$ = ""

	!
	! Loop through string
	!
	I% = 0%

1000	!
	! *0, Special cases
	!
	I% = INSTR(I% + 1%, STRNG$, "*0")
	IF I%
	THEN
		STRNG$ = LEFT(STRNG$, I% - 1%) + ITEM1$ + RIGHT(STRNG$, I% + 2%)
		I% = I% + LEN(ITEM1$) - 1%
		GOTO 1000
	END IF

	I% = 0%

1010	!
	! *1 Special cases
	!
	I% = INSTR(I% + 1%, STRNG$, "*1")
	IF I%
	THEN
		STRNG$ = LEFT(STRNG$, I% - 1%) + "/" + &
			FORMAT$(VAL%(ITEM1$) + 31%, "<0>##") + &
			RIGHT(STRNG$, I% + 2%)
		GOTO 1010
	END IF

1020	!
	! *2 Special cases
	!
	I% = INSTR(I% + 1%, STRNG$, "*2")
	IF I%
	THEN
		STRNG$ = LEFT(STRNG$, I% - 1%) + "/" + &
			FORMAT$(VAL%(ITEM1$), "<0>##") + &
			RIGHT(STRNG$, I% + 2%)
		GOTO 1020
	END IF

	OUTP_CREATESTR = STRNG$

	END FUNCTION
