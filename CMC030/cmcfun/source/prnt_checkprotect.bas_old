1	%TITLE "Convert Number into Text String for Check"
	%SBTTL "PRNT_CHECKPROTECT"
	%IDENT "V3.3"

	FUNCTION STRING PRNT_CHECKPROTECT(AMOUNT)

	!
	!		COPYRIGHT (C) 1987 BY
	!		Computer Management Center, Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	!
	! Abstract:HELP
	!	.b
	!	.lm +5
	!	This function converts an amount (Two decimal digits
	!	at the most please) into a text string for the
	!	check protect amount.
	!	.lm -5
	!
	! Index:
	!
	! Parameters:
	!
	!	AMOUNT
	!		A passed number (max 999,999,999.99, min 0)
	!
	!	The return is a string of the format:
	!
	!		Four Thousand Eighty Seven Dollars and No/100 Cents
	!
	! Example:
	!
	!	PRNT_CHECKPROTECT(100)
	!
	! Environment:
	!
	!	VAX/VMS BASIC V4.4
	!
	! Compile:
	!
	!	$ BAS FUNC_SOURCE:PRNT_CHECKPROTECT
	!	$ LIB FUNC_LIB:CMC_3VECTOR/REP PRNT_CHECKPROTECT
	!	$ DELETE PRNT_CHECKPROTECT.OBJ;*
	!
	! Author:
	!
	!	10/09/87 - Kevin Handy
	!
	! Modification history:
	!
	!	09/18/89 - Kevin Handy
	!		Cleaned up some odd looking code, decreased
	!		size slightly.
	!
	!	09/18/89 - Kevin Handy
	!		Broke into two functions.  One to convert integer
	!		part (PRNT_NUMBERTEXT), and this one to add the
	!		pennies onto it.
	!--

	%PAGE

	!++
	!
	! Variables
	!
	!	GROUPING$
	!		String that converts to the various groupings.
	!
	!	AMOUNT$
	!		String convert into a print using format.
	!
	!--

	%PAGE

	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	EXTERNAL STRING FUNCTION PRNT_NUMBERTEXT(LONG)

	!
	! Make sure it is a legal check amount
	!
	IF (AMOUNT > 999999999.99) OR (AMOUNT < 0)
	THEN
		PRNT_CHECKPROTECT = ""
		EXIT FUNCTION
	END IF

	!
	! Do conversion to text for whole part
	!
	AMOUNT% = FIX(AMOUNT)
	IF AMOUNT% = 0%
	THEN
		RESULT$ = "No"
	ELSE
		RESULT$ = PRNT_NUMBERTEXT(AMOUNT%)
	END IF

	!
	! Now convert pennies
	!
	AMOUNT$ = FORMAT$((AMOUNT - FIX(AMOUNT)) * 100.0 + .01, "<0>#")

	IF AMOUNT$ = "00"
	THEN
		RESULT$ = RESULT$ + " and No/100"
	ELSE
		RESULT$ = RESULT$ + " and " + AMOUNT$ + "/100"
	END IF

	PRNT_CHECKPROTECT = RESULT$

	END FUNCTION
