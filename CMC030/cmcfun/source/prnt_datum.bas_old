1	%TITLE "Format Date into DD MMM YYYY Format"
	%SBTTL "PRNT_DATUM"
	%IDENT "V3.3"

	FUNCTION STRING PRNT_DATUM(INDATE$,FRM%)

	!
	!	COPYRIGHT (C) 1985 BY
	!	Computer Management Center, Inc.
	!	Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	!
	! Abstract:HELP
	!	.b
	!	.lm +5
	!	This function formats a date into following formats
	!	depending on FRM%
	!	.lm -5
	!
	! Index:
	!
	! Parameters:
	!
	!	INDATE$
	!		The passed date the user wants to format.
	!
	!	FRM%
	!		The passed integer that tells how the date should be formatted.
	!
	!		1% - date given in MMDDYYYY format (1).
	!			date given in YYYYMMDD format (0).
	!
	!		2% - return MMMMDDYYYY (2).
	!			return DDMMMYYYY (0).
	!
	!		4% - return MMM DD YYYY (0).
	!			return MMMDDYY (4).
	!
	!		8% - strip off leading spaces.
	!
	!	Returned value
	!		The date that was formatted to the user's
	!		specification.
	!
	! Example:
	!
	!	DATES$ = PRNT_DATUM("040588",1%)
	!
	! Environment:
	!
	!	VAX/VMS BASIC V4.4
	!
	! Compile:
	!
	!	$ BAS FUNC_SOURCE:PRNT_DATUM/NOLINE
	!	$ LIB FUNC_LIB:CMC_3VECTOR/REP PRNT_DATUM
	!	$ DELETE PRNT_DATUM.OBJ;*
	!
	! AUTHOR:
	!
	!	04/09/88 - Frank Starman
	!
	! MODIFICATION HISTORY:
	!
	!	03/26/93 - Kevin Handy
	!		Clean up (Check)
	!
	!	07/06/93 - Kevin Handy
	!		Modified to use VAL%( instead of VAL( which
	!		should be slightly faster.
	!
	!	07/28/93 - Kevin Handy
	!		Added comments about FRM%.
	!--
	%PAGE

	!++
	!
	! Variables:
	!
	!	MON%
	!		Integer that extracts the months from INDATE$.
	!
	!	DAY%
	!		Integer that extracts the days.
	!
	!	YEA%
	!		Integer that extracts the year.
	!
	!	PRIDATUM$
	!		String that holds the formatted date.
	!
	!--
	%PAGE

	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	ON ERROR GOTO 19000

	DIM	MONTH$(99%)

	MONTH$(0%)	="   "
	MONTH$(1%)	="JAN"
	MONTH$(2%)	="FEB"
	MONTH$(3%)	="MAR"
	MONTH$(4%)	="APR"
	MONTH$(5%)	="MAY"
	MONTH$(6%)	="JUN"
	MONTH$(7%)	="JUL"
	MONTH$(8%)	="AUG"
	MONTH$(9%)	="SEP"
	MONTH$(10%)	="OCT"
	MONTH$(11%)	="NOV"
	MONTH$(12%)	="DEC"

	PRIDATUM$	= INDATE$

	IF (FRM% AND 1%)
	THEN
		MON% = VAL%(LEFT(INDATE$, 2%))
		DAY% = VAL%(MID(INDATE$, 3%, 2%))
		YEA% = VAL%(MID(INDATE$, 5%, 4%))
	ELSE
		MON% = VAL%(MID(INDATE$, 5%, 2%))
		DAY% = VAL%(MID(INDATE$, 7%, 2%))
		YEA% = VAL%(LEFT(INDATE$, 4%))
	END IF

	MONTH$(MON%)	= "???" IF MON%>12%

	IF (FRM% AND 4%)
	THEN
		IF (FRM% AND 2%)
		THEN
			PRIDATUM$ = MONTH$(MON%)+FORMAT$(DAY%,"<%>#")+ &
				FORMAT$(YEA%,"<%>###")
		ELSE
			PRIDATUM$ = FORMAT$(DAY%,"<%>#")+MONTH$(MON%)+ &
				FORMAT$(YEA%,"<%>###")
		END IF
	ELSE
		IF (FRM% AND 2%)
		THEN
			PRIDATUM$ = MONTH$(MON%)+" "+FORMAT$(DAY%,"<%>#")+ &
				" "+FORMAT$(YEA%,"<%>###")
		ELSE
			PRIDATUM$ = FORMAT$(DAY%,"<%>#")+" "+MONTH$(MON%)+ &
				" "+FORMAT$(YEA%,"<%>###")
		END IF
	END IF

	IF (FRM% AND 8%) = 0%
	THEN
		PRIDATUM$	= LEFT(EDIT$(PRIDATUM$,8%)+ &
			SPACE$(LEN(PRIDATUM$)),LEN(PRIDATUM$))
	END IF

 EndFunct:
	PRNT_DATUM	= PRIDATUM$

	EXIT FUNCTION

19000	!
	!	Trap errors
	!
	RESUME EndFunct

	END FUNCTION
