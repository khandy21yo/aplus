1	%TITLE "Convert Number into Text String for Check"
	%SBTTL "PRNT_NUMBERTEXT"
	%IDENT "V3.3"

	FUNCTION STRING PRNT_NUMBERTEXT(AMOUNT%)

	!
	!		COPYRIGHT (C) 1987 BY
	!		Computer Management Center, Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	!
	! Abstract:HELP
	!	.b
	!	.lm +5
	!	This function converts an amount (Two decimal digits
	!	at the most please) into a text string for the
	!	check protect amount.
	!	.lm -5
	!
	! Index:
	!
	! Parameters:
	!
	!	AMOUNT%
	!		A passed number (max 999,999,999.99, min 0)
	!
	!	The return is a string of the format:
	!
	!		Four Thousand Eighty Seven Dollars and No/100 Cents
	!
	! Example:
	!
	!	PRNT_NUMBERTEXT(100)
	!
	! Environment:
	!
	!	VAX/VMS BASIC V4.4
	!
	! Compile:
	!
	!	$ BAS FUNC_SOURCE:PRNT_NUMBERTEXT
	!	$ LIB FUNC_LIB:CMC_3VECTOR/REP PRNT_NUMBERTEXT
	!	$ DELETE PRNT_NUMBERTEXT.OBJ;*
	!
	! Author:
	!
	!	10/09/87 - Kevin Handy
	!
	! Modification history:
	!
	!	09/18/89 - Kevin Handy
	!		Cleaned up some odd looking code, decreased
	!		size slightly.
	!--

	%PAGE

	!++
	!
	! Variables
	!
	!	GROUPING$
	!		String that converts to the various groupings.
	!
	!	AMOUNT$
	!		String convert into a print using format.
	!
	!--

	%PAGE

	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	!
	! Create array for numbers
	!
	DIM ONEDIGIT$(19%), TENDIGIT$(9%)

	!
	! Make sure it is a legal check amount
	!
	IF (AMOUNT% > 999999999%) OR (AMOUNT% < 0%)
	THEN
		PRNT_NUMBERTEXT = ""
		EXIT FUNCTION
	END IF

	!
	! Create useful arrays
	!
	ONEDIGIT$(1%) = "One"
	ONEDIGIT$(2%) = "Two"
	ONEDIGIT$(3%) = "Three"
	ONEDIGIT$(4%) = "Four"
	ONEDIGIT$(5%) = "Five"
	ONEDIGIT$(6%) = "Six"
	ONEDIGIT$(7%) = "Seven"
	ONEDIGIT$(8%) = "Eight"
	ONEDIGIT$(9%) = "Nine"
	ONEDIGIT$(10%) = "Ten"
	ONEDIGIT$(11%) = "Eleven"
	ONEDIGIT$(12%) = "Twelve"
	ONEDIGIT$(13%) = "Thirteen"
	ONEDIGIT$(14%) = "Fourteen"
	ONEDIGIT$(15%) = "Fifteen"
	ONEDIGIT$(16%) = "Sixteen"
	ONEDIGIT$(17%) = "Seventeen"
	ONEDIGIT$(18%) = "Eighteen"
	ONEDIGIT$(19%) = "Nineteen"

	TENDIGIT$(1%) = "Ten"
	TENDIGIT$(2%) = "Twenty"
	TENDIGIT$(3%) = "Thirty"
	TENDIGIT$(4%) = "Forty"
	TENDIGIT$(5%) = "Fifty"
	TENDIGIT$(6%) = "Sixty"
	TENDIGIT$(7%) = "Seventy"
	TENDIGIT$(8%) = "Eighty"
	TENDIGIT$(9%) = "Ninety"

	!
	! Convert into a print using format
	!
	AMOUNT$ = FORMAT$(AMOUNT%, "#########")

	!
	! Do conversion to text for whole part, three digits at a time
	!
	RESULT$ = ""

	GROUPING$ = "Million"
	SEGMENT$ = MID(AMOUNT$, 1%, 3%)
	GOSUB DoSegment

	GROUPING$ = "Thousand"
	SEGMENT$ = MID(AMOUNT$, 4%, 3%)
	GOSUB DoSegment

	GROUPING$ = ""
	SEGMENT$ = MID(AMOUNT$, 7%, 3%)
	GOSUB DoSegment

	!
	! Fix it up a little
	!
	RESULT$ = EDIT$(RESULT$, 8% + 16% + 128%)

	PRNT_NUMBERTEXT = RESULT$

	EXIT FUNCTION


	!*******************************************************************
	! Handle a segment of three digits
	!*******************************************************************
 DoSegment:

	RESULT1$ = ""

	!
	! Handle hundreds
	!
	DIGIT$ = LEFT(SEGMENT$, 1%)

	IF (DIGIT$ <> " ") AND (DIGIT$ <> "0")
	THEN
		GOSUB OneDigit
		RESULT1$ = RESULT1$ + " Hundred"
	END IF

	!
	! Handle tens
	!
	DIGIT$ = MID(SEGMENT$, 2%, 1%)

	IF (DIGIT$ = "1")
	THEN
		!
		! 10 Through 19 need special handling
		!
		DIGIT$ = MID(SEGMENT$, 2%, 2%)
		GOSUB OneDigit
	ELSE
		!
		! Handle 10's
		!
		IF (DIGIT$ <> " ") AND (DIGIT$ <> "0")
		THEN
			GOSUB TenDigit
		END IF

		!
		! Handle units
		!
		DIGIT$ = MID(SEGMENT$, 3%, 1%)

		IF (DIGIT$ <> " ") AND (DIGIT$ <> "0")
		THEN
			GOSUB OneDigit
		END IF
	END IF

	!
	! Add to main string
	!
	IF RESULT1$ <> ""
	THEN
		RESULT$ = RESULT$ + " " + RESULT1$ + " " + GROUPING$
	END IF

	RETURN

	!*******************************************************************
	! OneDigit, Convert one digit to text
	!*******************************************************************
 OneDigit:
	RESULT1$ = RESULT1$ + " " + ONEDIGIT$(VAL%(DIGIT$))
	RETURN

	!*******************************************************************
	! TenDigit, Convert one digit to text
	!*******************************************************************
 TenDigit:
	RESULT1$ = RESULT1$ + " " + TENDIGIT$(VAL%(DIGIT$))
	RETURN

	END FUNCTION
