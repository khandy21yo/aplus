1	! Special program to set vendor names in GL from AP
	!
	OPTION SIZE = (INTEGER LONG, REAL DOUBLE)

	%INCLUDE "SOURCE:[GL.OPEN]GL_YYYY_PP.HB"
	MAP (GL_YYYY_PP) GL_YYYY_PP_CDD GL_YYYY_PP

	%INCLUDE "SOURCE:[AP.OPEN]AP_VENDOR.HB"
	MAP (AP_VENDOR) AP_VENDOR_CDD AP_VENDOR

100	!
	! Open vendor file
	!
	AP_VENDOR.CH% = 10%
	AP_VENDOR.DEV$ = ""

	AP_VENDOR.NAME$ = AP_VENDOR.DEV$ + "AP_VENDOR.MAS"

	OPEN AP_VENDOR.NAME$ FOR INPUT AS FILE AP_VENDOR.CH%, &
		ORGANIZATION INDEXED FIXED, &
		MAP AP_VENDOR, &
		PRIMARY KEY &
			AP_VENDOR::VENNUM, &
		ALTERNATE KEY &
			AP_VENDOR::VENNAM &
			DUPLICATES CHANGES, &
		ALTERNATE KEY &
			AP_VENDOR::ALPSRT &
			DUPLICATES CHANGES, &
		ALTERNATE KEY &
			AP_VENDOR::STATE &
			DUPLICATES CHANGES, &
		ALTERNATE KEY &
			AP_VENDOR::ZIP &
			DUPLICATES CHANGES, &
		ACCESS READ, ALLOW MODIFY

110	!
	! Open period file
	!
	GL_YYYY_PP.CH% = 12%
	GL_YYYY_PP.DEV$ = ""
	YYYY_PP$ = "1990_01"

	GL_YYYY_PP.NAME$ = GL_YYYY_PP.DEV$+"GL_"+YYYY_PP$+".LED"

	OPEN GL_YYYY_PP.NAME$ FOR INPUT AS FILE GL_YYYY_PP.CH%, &
		ORGANIZATION INDEXED FIXED, &
		MAP GL_YYYY_PP, &
		PRIMARY KEY &
		( &
			GL_YYYY_PP::ACCT, &
			GL_YYYY_PP::TRANDAT &
		)	DUPLICATES, &
		ALTERNATE KEY &
		( &
			GL_YYYY_PP::SUBACC, &
			GL_YYYY_PP::OPERATION, &
			GL_YYYY_PP::ACCT &
		)	DUPLICATES CHANGES, &
		ALTERNATE KEY &
		( &
			GL_YYYY_PP::XREFNO, &
			GL_YYYY_PP::ACCT &
		)	DUPLICATES CHANGES, &
		ALTERNATE KEY &
		( &
			GL_YYYY_PP::CKNO, &
			GL_YYYY_PP::ACCT &
		)	DUPLICATES CHANGES, &
		ALTERNATE KEY &
			GL_YYYY_PP::BTHNUM &
			DUPLICATES CHANGES, &
		ACCESS MODIFY, ALLOW MODIFY

	ON ERROR GOTO 19000

1000	!
	! Startup loop
	!
	RESET #GL_YYYY_PP.CH%


1100	GET #GL_YYYY_PP.CH%

1110	IF ((GL_YYYY_PP::SOURCE = "PJ") OR (GL_YYYY_PP::SOURCE = "CD")) &
		AND (EDIT$(GL_YYYY_PP::DESCR, -1%) = "")
	THEN
		GET #AP_VENDOR.CH%, KEY#0% EQ GL_YYYY_PP::XREFNO

		GL_YYYY_PP::DESCR = AP_VENDOR::VENNAM

		UPDATE #GL_YYYY_PP.CH%
		PRINT ".";
		PRINT IF CCPOS(0%) >= 50%
	END IF

	GOTO 1100

2000	CLOSE AP_VENDOR.CH%
	CLOSE GL_YYYY_PP.CH%

19000	!
	! Trap errors
	!
	SELECT ERL
		CASE 1100%
			RESUME 2000 IF ERR = 11%

		CASE 1110%
			PRINT "#";
			RESUME 1100
	END SELECT

32767	END
