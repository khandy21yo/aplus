1	%TITLE "Product/Deal Maintenance"
	%SBTTL "PC_MAIN_DEAL_PRODUCT"
	%IDENT "V3.6a Calico"

	FUNCTION LONG PC_MAIN_DEAL_PRODUCT(CDD_WINDOW_CDD SMG_WINDOW, &
		LONG MOPTION, LONG MLOOP, LONG MFLAG, STRING MVALUE)

	!
	! COPYRIGHT (C) 2001 BY
	!
	! Software Solutions, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Software Solutions, Inc.
	!
	! Software Solutions, Inc. assumes no responsibility for the use
	! or reliability of its software on equipment which is not
	! supported by Software Solutions, Inc.
	!
	!++
	! Abstract:HELP
	!	.b
	!	.lm +5
	!	The ^*Product Deal\* file maintains
	!	a list of which "deals" that a customer is subscribed to.
	!	.lm -5
	!
	! Index:
	!	.x Deal Maintenance
	!
	! Option:
	!
	! Compile:
	!
	!	$ BAS PC_SOURCE:PC_MAIN_DEAL_PRODUCT/LINE
	!	$ LIB FUNC_LIB:CMCFUN/REP PC_MAIN_DEAL_PRODUCT
	!	$ DELETE PC_MAIN_DEAL_PRODUCT.OBJ;*
	!
	! Author:
	!
	!	07/19/2001 - Kevin Handy
	!
	! Modification history:
	!
	!	10/05/2001 - Kevin Handy
	!		New fields (percent, account)
	!--

	!
	! Set up compiling options
	!
	OPTION SIZE = (REAL GFLOAT, INTEGER LONG)

	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "FUNC_INCLUDE:PD_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:UTL_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:MAIN_WINDOW.COM"
	%INCLUDE "FUNC_INCLUDE:GL_WINDOW.INC"

	%INCLUDE "SOURCE:[CDD.OPEN]CDD_WINDOW.HB"

	%INCLUDE "SOURCE:[AR.OPEN]AR_35CUSTOM.HB"
	MAP (AR_35CUSTOM)	AR_35CUSTOM_CDD	AR_35CUSTOM

	%INCLUDE "SOURCE:[PC.OPEN]PC_DEAL.HB"
	MAP (PC_DEAL)		PC_DEAL_CDD	PC_DEAL

	%INCLUDE "SOURCE:[PC.OPEN]PC_DEAL_PRODUCT.HB"
	MAP (PC_DEAL_PRODUCT)		PC_DEAL_PRODUCT_CDD	PC_DEAL_PRODUCT
	MAP (PC_DEAL_PRODUCT_OLD)	PC_DEAL_PRODUCT_CDD	PC_DEAL_PRODUCT_OLD
	MAP (PC_DEAL_PRODUCT_DEF)	PC_DEAL_PRODUCT_CDD	PC_DEAL_PRODUCT_DEF

	%INCLUDE "SOURCE:[PD.OPEN]PD_PRODUCT.HB"
	MAP (PD_PRODUCT) PD_PRODUCT_CDD PD_PRODUCT
	DECLARE PD_PRODUCT_CDD PD_PRODUCT_EXAM

	%INCLUDE "SOURCE:[GL.OPEN]GL_CHART.HB"
	MAP (GL_CHART)		GL_CHART_CDD		GL_CHART

	!
	! Common Areas
	!
	! These areas store information that is re-used between
	! calls to these functions.
	!
	COM (CH_PC_DEAL_PRODUCT) &
		PC_DEAL_PRODUCT.CH%, &
		PC_DEAL_PRODUCT.READONLY%

	!
	! External functions
	!
	EXTERNAL LONG    FUNCTION FUNC_TESTENTRY
	EXTERNAL LONG    FUNCTION MAIN_WINDOW
	EXTERNAL LONG   FUNCTION PD_EXAM_PRODUCT
	EXTERNAL LONG	FUNCTION READ_35SET

	%PAGE

	ON ERROR GOTO 29000

	SELECT MOPTION

	!
	! Initialization
	!
	! This option is used to initialize the window structure,
	! set up the default values for add, and open all files
	! necessary that have not already been opened.
	!
	CASE OPT_INIT

		!*************************************************************
		! Set up information
		!*************************************************************

		!
		! Define window
		!
		SMG_WINDOW::DESCR = "Deal/Product List"
		SMG_WINDOW::CURREC = -2%
		SMG_WINDOW::NHELP = "PC_MAIN_DEAL_PRODUCT"
		SMG_WINDOW::HSIZE = 76%
		SMG_WINDOW::VSIZE = 11%
		SMG_WINDOW::HPOS  = 3%
		SMG_WINDOW::VPOS  = 8%
		SMG_WINDOW::FLAGS = 0%
		SMG_WINDOW::NITEMS= 4%

		SMG_WINDOW::NKEYS = 1%
		SMG_WINDOW::KNAME(0%) = "Product"
			SMG_WINDOW::KFIELD(0%, 0%) = 1%
			SMG_WINDOW::KFIELD(0%, 1%) = 1%

		SMG_WINDOW::HVIEW = 76%
		SMG_WINDOW::VVIEW = 11%
		SMG_WINDOW::VVPOS = 8%
		SMG_WINDOW::VHPOS = 3%

		CALL READ_DEFAULTS(SMG_WINDOW) &
			IF INSTR(1%, " QV", MVALUE) <= 1%

700		!
		! Declare channels
		!
		IF PC_DEAL_PRODUCT.CH% > 0%
		THEN
			!
			! Already open, set flag to read-only if was
			! that way from last time.
			!
			SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%) &
				IF PC_DEAL_PRODUCT.READONLY%
			GOTO 790
		END IF

		!
		! Open main file (existing) for modification
		!
750		WHEN ERROR IN
			%INCLUDE "SOURCE:[PC.OPEN]PC_DEAL_PRODUCT.CRE"
		USE
			CONTINUE 760 IF ERR = 10%
			PC_MAIN_DEAL_PRODUCT = ERR
			CONTINUE 770
		END WHEN

		PC_DEAL_PRODUCT.READONLY% = 0%
		GOTO 790

760		!
		! If unable to open for modify, try to open with read
		! access only.
		!
		WHEN ERROR IN
			%INCLUDE "SOURCE:[PC.OPEN]PC_DEAL_PRODUCT.OPN"
		USE
			PC_MAIN_DEAL_PRODUCT = ERR
			CONTINUE 770
		END WHEN

		SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%)
		PC_DEAL_PRODUCT.READONLY% = -1%

		GOTO 790

770		!
		! File not open, so reset channel
		!
		CALL ASSG_FREECHANNEL(PC_DEAL_PRODUCT.CH%)

		EXIT FUNCTION

790		SMG_WINDOW::CHAN  = PC_DEAL_PRODUCT.CH%
		WHEN ERROR IN
			RESET #PC_DEAL_PRODUCT.CH%
			GET #PC_DEAL_PRODUCT.CH%, REGARDLESS
		USE
			CONTINUE 32767
		END WHEN

	!
	! Display the background
	!
	! This option is used to display the background information on the
	! screen.  It must first clear any junk on the screen, and then
	! write the background onto it.
	!
20100	CASE OPT_BACKGROUND

		SMG_STATUS% = SMG$BEGIN_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

		SMG_STATUS% = SMG$ERASE_DISPLAY(SMG_WINDOW::WNUMBER)


		DATA	03,05, "(01) Product", &
			04,05, "(02) Price", &
			05,05, "(03) Percent", &
			06,05, "(04) Account", &
			0, 0, ""

		RESTORE

		READ XPOS%, YPOS%, XSTR$
		I% = 0%

		WHILE (XPOS% <> 0)
			I% = I% + 1%

			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				XSTR$, XPOS%, YPOS%) IF (SMG_WINDOW::HFLAG(I%) AND 2%)=0%

			READ XPOS%, YPOS%, XSTR$
		NEXT

		SMG_STATUS% = SMG$END_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

	!
	! Enter/Display/Default
	!
	! This option is used to enter the data from the user, display data,
	! set defaults, and return the data back according to MFLAG.
	!
20200	CASE OPT_ENTRY

		TEMP$, TEMP1$ = TRM$(SCOPE::PRG_ITEM)
		TEMP$ = "View starting at" IF TEMP$ = "View"

		SCOPE::PRG_ITEM = "FLD" + FORMAT$(MLOOP, "<0>##")

 ReEnter:	SCOPE::SCOPE_EXIT = 0%

		SELECT MLOOP

		CASE 1%

	!++
	! Abstract:FLD001
	!	^*(01) Product\*
	!	.b
	!	.lm +5
	!	Inventory Product Number.
	!	.lm -5
	!
	! Index:
	!	.x Product>Deal Maintenance
	!	.x Deal Maintenance>Price
	!
	!--

			PC_DEAL_PRODUCT::PRODUCT = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, "03;28", TEMP$, &
				PC_DEAL_PRODUCT::PRODUCT, MFLAG, "'E", MVALUE)

			IF (SCOPE::SCOPE_EXIT = SMG$K_TRM_F14)
			THEN
				IF MAIN_WINDOW(PD_MAIN_PRODUCT.ID, "VX") = 1%
				THEN
					PC_DEAL_PRODUCT::PRODUCT = &
						PD_PRODUCT::PRODUCT_NUM
				END IF
				GOTO Reenter
			END IF

		CASE 2%

	!++
	! Abstract:FLD002
	!	^*(02) Price\*
	!	.b
	!	.lm +5
	!	The price to give for this product for anyone having this deal.
	!	.lm -5
	!
	! Index:
	!	.x Price>Deal Maintenance
	!	.x Deal Maintenance>Price
	!
	!--

			PC_DEAL_PRODUCT::PRICE = ENTR_3NUMBER(SCOPE, &
				SMG_WINDOW::WNUMBER, "04;28", TEMP$, &
				PC_DEAL_PRODUCT::PRICE, MFLAG, "####.####", &
				MVALUE)

	!++
	! Abstract:FLD003
	!	^*(03) Percent\*
	!	.b
	!	.lm +5
	!	A percentage discount to use on this deal.
	!	.lm -5
	!
	! Index:
	!	.x Percent>Deal Maintenance
	!	.x Deal Maintenance>Percent
	!
	!--

			PC_DEAL_PRODUCT::PERCENT = ENTR_3NUMBER(SCOPE, &
				SMG_WINDOW::WNUMBER, "05;28", TEMP$, &
				PC_DEAL_PRODUCT::PERCENT, MFLAG, "####.####", &
				MVALUE)

		CASE 4%

	!++
	! Abstract:FLD001
	!	^*(01) Product\*
	!	.b
	!	.lm +5
	!	Inventory Product Number.
	!	.lm -5
	!
	! Index:
	!	.x Product>Deal Maintenance
	!	.x Deal Maintenance>Price
	!
	!--

			PC_DEAL_PRODUCT::ACCOUNT = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, "06;28", TEMP$, &
				PC_DEAL_PRODUCT::ACCOUNT, MFLAG, "'E", MVALUE)

			IF (SCOPE::SCOPE_EXIT = SMG$K_TRM_F14)
			THEN
				IF (MAIN_WINDOW(GL_MAIN_CHART.ID, "VX  ") = 1%)
				THEN
					PC_DEAL_PRODUCT::ACCOUNT = &
						GL_CHART::ACCT
				END IF
				GOTO ReEnter
			END IF


		END SELECT

		SCOPE::PRG_ITEM = TEMP1$

	!
	! Test values
	!
20300	CASE OPT_TESTENTRY

		PC_MAIN_DEAL_PRODUCT = 0%

		SELECT MLOOP

		CASE 1%
			IF PD_EXAM_PRODUCT(PC_DEAL_PRODUCT::PRODUCT, &
				PD_PRODUCT_EXAM) = CMC$_UNDEFINED
			THEN
				EXIT_STATUS% = FUNC_TESTENTRY(SMG_WINDOW, &
					PC_DEAL_PRODUCT::PRODUCT, &
					PD_PRODUCT::DESCRIPTION, &
					"OE", MLOOP, "PROD", &
					"Product", PD_MAIN_PRODUCT.ID)

				PC_MAIN_DEAL_PRODUCT = EXIT_STATUS%
			ELSE
				IF PD_PRODUCT_EXAM::SSTATUS <> "A"
				THEN
					SMG_STATUS% = SMG$RING_BELL(SCOPE::SMG_KBID)
					CALL ENTR_3MESSAGE(SCOPE, &
						"Product is not active", 1%)
					IF READ_35SET("IC_ALLOW", "PRODUCT", &
						UTL_SET_READ) <> CMC$_NORMAL
					THEN
						PC_MAIN_DEAL_PRODUCT = 1%
					END IF
				END IF
			END IF

		CASE 4%
			PC_MAIN_DEAL_PRODUCT = FUNC_TESTENTRY(SMG_WINDOW, &
				PC_DEAL_PRODUCT::ACCOUNT, GL_CHART::DESCR, &
				"AR", MLOOP, "ACCT", &
				"Account number", GL_MAIN_CHART.ID)

			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				LEFT(GL_CHART::DESCR, 30%), &
				6%, 45%, , SMG$M_BOLD)

		END SELECT

	CASE OPT_DISPLAY

		IF MAIN_WINDOW(GL_MAIN_CHART.ID, &
			"Q0" + PC_DEAL_PRODUCT::ACCOUNT) <> 1%
		THEN
			GL_CHART::DESCR = "????????????????????"
		END IF

		SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
			LEFT(GL_CHART::DESCR,30%), &
			6%, 45%, , SMG$M_BOLD)

	!
	! Set PC_DEAL_PRODUCT_OLD value
	!
20500	CASE OPT_SETOLD
		PC_DEAL_PRODUCT_OLD = PC_DEAL_PRODUCT

	!
	! Restore PC_DEAL_PRODUCT_OLD value
	!
	CASE OPT_RESETOLD
		PC_DEAL_PRODUCT = PC_DEAL_PRODUCT_OLD

	!
	! Set default value
	!
	CASE OPT_SETDEFAULT
		PC_DEAL_PRODUCT_DEF = PC_DEAL_PRODUCT

	!
	! Restore default value
	!
	CASE OPT_RESETDEFAULT
		PC_DEAL_PRODUCT = PC_DEAL_PRODUCT_DEF
		PC_DEAL_PRODUCT::DEAL = PC_DEAL::DEAL

	!
	! View header
	!
	CASE OPT_VIEW

		SELECT MLOOP

		!
		! Title (One line only)
		!
		CASE 1%
			MVALUE = "  Product      Price"

		!
		! Positions of lines
		!
		CASE 2%
			MVALUE = "017"

		!
		! Convert current record into text
		!
		CASE 3%
			MVALUE = PC_DEAL_PRODUCT::PRODUCT + " " + &
				FORMAT$(PC_DEAL_PRODUCT::PRICE, "#####.####")

		END SELECT

	!
	! Find
	!
	CASE OPT_FIND

		SELECT MLOOP

		CASE 0%
			FIND #SMG_WINDOW::CHAN, &
				KEY#0% GE PC_DEAL_PRODUCT::DEAL + &
				PC_DEAL_PRODUCT::PRODUCT, &
				REGARDLESS

		END SELECT

	!
	! Handle array of records
	!
	CASE OPT_SUBWIND

		SELECT MLOOP

		!
		! Find first record (if there is any)
		!
		CASE 1%
			!
			! Set init value
			!
			SMG_WINDOW::CURREC = -1%

			!
27110			!
			! Search for first record
			!
			WHEN ERROR IN
				FIND #SMG_WINDOW::CHAN, &
					KEY#0% EQ PC_DEAL::DEAL, &
					REGARDLESS

				!
				! Get a record
				!
				GET #SMG_WINDOW::CHAN
				SMG_WINDOW::CURREC = 0%
			USE
				CONTINUE 28000 IF ERR = 155%
				EXIT HANDLER
			END WHEN

		!
		! Find starting record (if there is any)
		!
		CASE 2%
			!
			! Set init value
			!
			SMG_WINDOW::CURREC = -1%

27115			!
			! Search for starting record
			!
			SELECT MFLAG

			CASE 0%
				WHEN ERROR IN
					FIND #SMG_WINDOW::CHAN, &
						KEY#0% GE MVALUE + &
						PC_DEAL_PRODUCT::DEAL, &
						REGARDLESS
				USE
					CONTINUE 28000 IF ERR = 155%
					EXIT HANDLER
				END WHEN
			END SELECT

			!
			! Get a record
			!
			SMG_WINDOW::CURREC = 0%

		!
		! Check if still right key
		!
		CASE 3%
			IF PC_DEAL_PRODUCT::DEAL = MVALUE
			THEN
				SMG_WINDOW::CURREC = 0%
			ELSE
				SMG_WINDOW::CURREC = -1%
			END IF

		!
		! Set the new key
		!
		CASE 6%
			PC_DEAL_PRODUCT::DEAL = MVALUE

		END SELECT


	END SELECT

28000	EXIT FUNCTION

29000	!
	! Trap errors
	!
	ON ERROR GO BACK

32767	END FUNCTION
