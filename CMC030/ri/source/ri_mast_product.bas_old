1	%TITLE "Product Master Files Maintenance"
	%SBTTL "RI_MAST_PRODUCT"
	%IDENT "V3.6a Calico"

	!
	! COPYRIGHT (C) 1987, 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! Abstract:HELP
	!	.p
	!	The ^*Product Master\* file option provides the means to access
	!	routines where records for new or additional products are
	!	entered and maintained. The screen also provides the means to
	!	assign ingredients and access the Standard Cost File. An option
	!	to enter directions for recipe preparation is an additional feature.
	!
	! Index:
	!	.x Add>Product
	!	.x Erase>Product
	!	.x Change>Product
	!	.x Maintain>Product
	!	.x Product>Maintain
	!	.x Product>Add
	!	.x Product>Erase
	!	.x Product>Change
	!
	! Option:
	!	PD_MAIN_PRODUCT$HELP
	!	RI_MAIN_STRUCTURE$HELP
	!	RI_MAIN_STRUCTURESCAN$HELP
	!	PC_MAIN_COST$HELP
	!
	! Compile:
	!
	!	$ BAS RI_SOURCE:RI_MAST_PRODUCT/NOLINE
	!	$ LINK/EXE=RI_EXE: RI_MAST_PRODUCT,-
	!		FUNC_LIB:CMCLINK/OPTION
	!	$ DELETE RI_MAST_PRODUCT.OBJ;*
	!
	! Author:
	!
	!	06/27/88 - Frank Starman
	!
	! Modification History:
	!
	!	02/26/92 - Kevin Handy
	!		Added modification history section
	!
	!	02/26/92 - Kevin Handy
	!		Removed references to function PD_MAIN_PACK since
	!		Frank deleted it so programs couldn't compile.
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	10/28/96 - Kevin Handy
	!		Reformat source code.
	!
	!	08/28/97 - Kevin Handy
	!		Lose unecessary external definitions
	!
	!	08/22/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--
	%PAGE

	!
	! Set up compiling options
	!
	OPTION SIZE = (REAL GFLOAT, INTEGER LONG)

	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "FUNC_INCLUDE:PD_WINDOW.INC"

	!
	! External functions
	!
	EXTERNAL LONG FUNCTION MAIN_WINDOW

	%PAGE

	!*******************************************************************
	! Initilize maintainence
	!*******************************************************************

	CALL READ_INITIALIZE

	!*******************************************************************
	! Handle main program
	!*******************************************************************

	V% = MAIN_WINDOW(PD_MAIN_PRODUCT.ID, "")

	!******************************************************************
	! End of the program
	!******************************************************************

	CALL SUBR_3EXITPROGRAM(SCOPE, "", "")

19990	END



20000	FUNCTION LONG MAINT_GROUP(CDD_WINDOW_CDD SMG_WINDOW, &
		LONG MOPTION, LONG MLOOP, LONG MFLAG, STRING MVALUE)

	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "FUNC_INCLUDE:PC_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:PD_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:RI_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:UTL_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:MAIN_WINDOW.COM"
	%INCLUDE "SOURCE:[CDD.OPEN]CDD_WINDOW.HB"

	%INCLUDE "SOURCE:[PD.OPEN]PD_PRODUCT.HB"
	MAP	(PD_PRODUCT)		PD_PRODUCT_CDD	PD_PRODUCT
	MAP	(PD_PRODUCT_OLD)	PD_PRODUCT_CDD	PD_PRODUCT_OLD
	MAP	(PD_PRODUCT_ONE)	PD_PRODUCT_CDD	PD_PRODUCT_ONE

	EXTERNAL LONG FUNCTION MAIN_WINDOW
	EXTERNAL LONG FUNCTION LIBR_EXTRACTVAR
	EXTERNAL LONG FUNCTION LIBR_INSERTTEXT
	EXTERNAL LONG FUNCTION LIBR_DELETE

	EXTERNAL LONG FUNCTION PD_MAIN_PRODUCT
	EXTERNAL LONG FUNCTION PD_MAIN_PRODTYPE
	EXTERNAL LONG FUNCTION PD_MAIN_CATEGORY
	EXTERNAL LONG FUNCTION PD_MAIN_LABEL

	EXTERNAL LONG FUNCTION RI_MAIN_STRUCTURE
	EXTERNAL LONG FUNCTION RI_MAIN_STRUCTURESCAN

	EXTERNAL LONG FUNCTION PC_MAIN_COST

	EXTERNAL LONG FUNCTION UTL_MAIN_LOCATION
	EXTERNAL LONG FUNCTION UTL_MAIN_MEASURE

	%PAGE

	!
	! Process the proper window
	!
	SELECT SMG_WINDOW::IDENT

	CASE PD_MAIN_PRODUCT.ID

		MAINT_GROUP = PD_MAIN_PRODUCT(SMG_WINDOW, &
			MOPTION, MLOOP, MFLAG, MVALUE)

		SELECT MOPTION

		CASE OPT_OPTLIST
			MVALUE = MVALUE + " cosT inGredients directiOns"

		CASE OPT_MOREMENU
			PD_PRODUCT_ONE = PD_PRODUCT
			SELECT EDIT$(MVALUE, -1%)

			!
			! Componets
			!
			CASE "INGREDIENTS"
				MAINT_GROUP = MAIN_WINDOW(RI_MAIN_STRUCTURE.ID, "")

			!
			! Cost
			!
			CASE "COST"
				MAINT_GROUP = MAIN_WINDOW(PC_MAIN_COST.ID, "")

			!
			! Display notes
			!
			CASE "DIRECTIONS"
				CALL READ_DEVICE("RI_RELATION", &
					RI_RELATION.DEV$,STAT%)

				ST% = LIBR_EXTRACTVAR( &
					TRM$(RI_RELATION.DEV$) + &
					"RI_RELATION", &
					LAST_NOTE$, &
					TRM$(PD_PRODUCT::PRODUCT_NUM))

				LAST_NOTE$ = ENTR_NOTE(SMG_WINDOW::WNUMBER, &
					"8;1", TEMP$, &
					LAST_NOTE$, 64%, "11;78", MVALUE)

				ST% = LIBR_INSERTTEXT( &
					TRM$(RI_RELATION.DEV$) + &
					"RI_RELATION", &
					LAST_NOTE$, &
					TRM$(PD_PRODUCT::PRODUCT_NUM))


			END SELECT

		CASE OPT_AFTEROPT

			SELECT MVALUE

			!
			! Need to remove under old key, and insert under
			! (possibly) new key
			!
			CASE "Change", "Blank", "Initialize"

			IF (PD_PRODUCT_OLD::PRODUCT_NUM <> &
				PD_PRODUCT::PRODUCT_NUM)
			THEN
				PD_PRODUCT_ONE = PD_PRODUCT_OLD
				MAINT_GROUP = MAIN_WINDOW( &
					PC_MAIN_COST.ID, "C")

				MAINT_GROUP = MAIN_WINDOW( &
					RI_MAIN_STRUCTURE.ID, "C")

				CALL READ_DEVICE("RI_RELATION", &
					RI_RELATION.DEV$,STAT%)

				ST% = LIBR_EXTRACTVAR( &
					TRM$(RI_RELATION.DEV$) + &
					"RI_RELATION", &
					LAST_NOTE$, &
					TRM$(PD_PRODUCT_OLD::PRODUCT_NUM))

				ST% = LIBR_DELETE( &
					TRM$(RI_RELATION.DEV$) + &
					"RI_RELATION", &
					TRM$(PD_PRODUCT_OLD::PRODUCT_NUM))

				ST% = LIBR_INSERTTEXT( &
					TRM$(RI_RELATION.DEV$) + "RI_RELATION", &
					LAST_NOTE$, &
					TRM$(PD_PRODUCT::PRODUCT_NUM))
			END IF

			!
			! Need to remove text
			!
			CASE "Erase"

				PD_PRODUCT_ONE = PD_PRODUCT
				MAINT_GROUP = MAIN_WINDOW(PC_MAIN_COST.ID, "E")
				MAINT_GROUP = MAIN_WINDOW(RI_MAIN_STRUCTURE.ID, "E")

				CALL READ_DEVICE("RI_RELATION", &
					RI_RELATION.DEV$,STAT%)

				ST% = LIBR_DELETE( &
					TRM$(RI_RELATION.DEV$) + &
					"RI_RELATION", &
					TRM$(PD_PRODUCT::PRODUCT_NUM))

			END SELECT

		END SELECT

	CASE PD_MAIN_PRODTYPE.ID
		MAINT_GROUP = PD_MAIN_PRODTYPE(SMG_WINDOW, &
			MOPTION, MLOOP, MFLAG, MVALUE)

	CASE PD_MAIN_CATEGORY.ID
		MAINT_GROUP = PD_MAIN_CATEGORY(SMG_WINDOW, &
			MOPTION, MLOOP, MFLAG, MVALUE)

	CASE PD_MAIN_LABEL.ID
		MAINT_GROUP = PD_MAIN_LABEL(SMG_WINDOW, &
			MOPTION, MLOOP, MFLAG, MVALUE)

	CASE UTL_MAIN_LOCATION.ID
		MAINT_GROUP = UTL_MAIN_LOCATION(SMG_WINDOW, &
			MOPTION, MLOOP, MFLAG, MVALUE)

	CASE UTL_MAIN_MEASURE.ID
		MAINT_GROUP = UTL_MAIN_MEASURE(SMG_WINDOW, &
			MOPTION, MLOOP, MFLAG, MVALUE)

	CASE PC_MAIN_COST.ID

		SELECT MOPTION

		CASE OPT_RESETDEFAULT
			MVALUE = PD_PRODUCT_ONE::PRODUCT_NUM

		CASE OPT_SUBWIND

			SELECT MLOOP

			CASE 6%
				MVALUE = PD_PRODUCT::PRODUCT_NUM

			CASE ELSE
				MVALUE = PD_PRODUCT_ONE::PRODUCT_NUM

			END SELECT

		END SELECT

		MAINT_GROUP = PC_MAIN_COST(SMG_WINDOW, &
			MOPTION, MLOOP, MFLAG, MVALUE)

	CASE RI_MAIN_STRUCTURE.ID

		SELECT MOPTION
		CASE OPT_RESETDEFAULT
			MVALUE = PD_PRODUCT_ONE::PRODUCT_NUM

		CASE OPT_SUBWIND
			SELECT MLOOP
			CASE 6%
				MVALUE = PD_PRODUCT::PRODUCT_NUM

			CASE ELSE
				MVALUE = PD_PRODUCT_ONE::PRODUCT_NUM
			END SELECT
		END SELECT

		MAINT_GROUP = RI_MAIN_STRUCTURE(SMG_WINDOW, &
			MOPTION, MLOOP, MFLAG, MVALUE)

		SELECT MOPTION
		CASE OPT_OPTLIST
			MVALUE = MVALUE + " recOrd"

		CASE OPT_MOREMENU
			MAINT_GROUP = 16% OR MAIN_WINDOW(RI_MAIN_STRUCTURESCAN.ID, "")
		END SELECT

	!++
	! Abstract:RECORD
	! Option:RI_MAIN_STRUCTURESCAN
	!--

	CASE RI_MAIN_STRUCTURESCAN.ID
		MAINT_GROUP = RI_MAIN_STRUCTURESCAN(SMG_WINDOW, &
			MOPTION, MLOOP, MFLAG, MVALUE)

	END SELECT

32767	END FUNCTION
