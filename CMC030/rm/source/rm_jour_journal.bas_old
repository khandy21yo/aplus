1	%TITLE "Restaurant Journal"
	%SBTTL "RM_JOUR_JOURNAL"
	%IDENT "V3.6a Calico"

	!
	! COPYRIGHT (C) 1987, 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! Abstract:HELP
	!	.p
	!	The ^*Restaurant Journal Entry\* program provides for entry and maintenance of
	!	the Restaurant journal file.
	!
	! Index:
	!	.x Restaurant Journal
	!
	! Option:
	!	RM_MAIN_JOURNAL$HELP
	!	RM_MAIN_JOURPROD$HELP
	!	RM_MAIN_JOURPOST$HELP
	!	RM_FUNC_COPYBATCH$HELP
	!
	! Compile:
	!
	!	$ BAS RM_SOURCE:RM_JOUR_JOURNAL/LINE
	!	$ LINK/EXE=RM_EXE: RM_JOUR_JOURNAL, -
	!		FUNC_LIB:CMCLINK/OPTION
	!	$ DELETE RM_JOUR_JOURNAL.OBJ;*
	!
	! Author:
	!
	!	11/12/87 - Frank F. Starman
	!
	! Modification history:
	!
	!	02/09/89 - Kevin Handy
	!		Modified for changes in ENTR_ENTER.
	!
	!	04/23/93 - Kevin Handy
	!		Clean up (Check)
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!		Fix last parameter to entr_3choice
	!
	!	10/28/96 - Kevin Handy
	!		Reformat source code.
	!
	!	08/26/97 - Kevin Handy
	!		Change 'val' to 'val%'
	!
	!	08/22/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!
	!	06/11/99 - Kevin Handy
	!		TRM the BATCH_NO$ before taking it's length
	!--
	%PAGE

	!
	! Set up compiling options
	!
	OPTION SIZE = (REAL GFLOAT, INTEGER LONG)

	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "FUNC_INCLUDE:RM_WINDOW.INC"

	!
	! Common Areas
	!
	! These areas store information that is re-used between
	! calls to these functions.
	!
	! This common area must be mapped in both the main program and
	! in MAINT_GROUP.
	!
	COM (BATCH_RM_JOURNAL) &
		BATCH_NO$ = 2%
	COM (CH_RM_JOURNAL) &
		RM_JOURNAL.CH%, &
		RM_JOURNAL.READONLY%

	%PAGE

	!
	! Initilization section - Prepare to do anything
	!
	ON ERROR GOTO 19000

	!
	! Initialize all the standard stuff through an external call
	!
	CALL READ_INITIALIZE


	!
	! Set up for help
	!
	SCOPE::PRG_ITEM = "fld001yr"

300	!
	! Query user for year of file
	!
	CALL READ_DEVICE("RM_JOURNAL",RM_JOURNAL.DEV$,STAT%)
	CALL FIND_FILE( RM_JOURNAL.DEV$ + "RM_JOURNAL_*.JRL", RM_JOURNAL_FILE$(), &
		16%,"","")

	RM_JOURNAL_FILE% = VAL%(RM_JOURNAL_FILE$(0%))

	IF RM_JOURNAL_FILE%
	THEN
		RM_JOURNAL_FILE$(LOOP%) = &
			MID(RM_JOURNAL_FILE$(LOOP%),12%,2%) &
				FOR LOOP% = 1% TO RM_JOURNAL_FILE%

		TEMP$ = "Restaurant Journal Files"

		X% = ENTR_3CHOICE(SCOPE, "", "", RM_JOURNAL_FILE$(), "", &
			0%, TEMP$, "", 0%)

		IF X% > 0%
		THEN
			BATCH_NO$ = EDIT$(RM_JOURNAL_FILE$(X%),-1%)
			GOTO 400
		END IF
	END IF

	SELECT SCOPE::SCOPE_EXIT
	CASE 3%, SMG$K_TRM_F10, SMG$K_TRM_CTRLZ	! Exit key ?
		GOTO ExitProgram

	END SELECT

	!
	! Ask for year
	!
	SMG_STATUS% = SMG$CREATE_VIRTUAL_DISPLAY &
	( &
		20%, &
		80%, &
		SMG_SCREEN_DATA% &
	)

	SMG_STATUS% = SMG$PASTE_VIRTUAL_DISPLAY &
	( &
		SMG_SCREEN_DATA%, &
		SCOPE::SMG_PBID, &
		1%, &
		1% &
	)

	SMG_STATUS% = SMG$PUT_CHARS(SMG_SCREEN_DATA%, "Batch number:", 11%, 30%)

320	BATCH_NO$ = "01"

	SELECT ENTR_3ENTER(SCOPE, SMG_SCREEN_DATA%, 11%, 43%, BATCH_NO$, -1%, 16%)

	CASE 3%, SMG$K_TRM_F10, SMG$K_TRM_CTRLZ	! Exit key ?
		GOTO ExitProgram

	END SELECT

	BATCH_NO$ = EDIT$(BATCH_NO$, -1%)

	IF LEN(TRM$(BATCH_NO$)) <> 2%
	THEN
		CALL ENTR_3MESSAGE(SCOPE, "Please enter the batch number in XX format", 0%)
		GOTO 320
	END IF

400	SMG_STATUS% = SMG$DELETE_VIRTUAL_DISPLAY(SMG_SCREEN_DATA%)

1000	!******************************************************************
	! Handle the main file
	!******************************************************************

	!
	! Maintain file
	!
	V% = MAIN_WINDOW(RM_MAIN_JOURNAL.ID, "")

	!******************************************************************
	! End of the program
	!******************************************************************

 ExitProgram:
	CALL SUBR_3EXITPROGRAM(SCOPE, "", "")

19000	!******************************************************************
	! Error trapping
	!******************************************************************

	ON ERROR GOTO 0

19990	END


20000	FUNCTION LONG MAINT_GROUP(CDD_WINDOW_CDD SMG_WINDOW, LONG MOPTION, &
		LONG MLOOP, LONG MFLAG, STRING MVALUE)

	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	!
	! Include files
	!
	%INCLUDE "FUNC_INCLUDE:MAIN_WINDOW.COM"

	%INCLUDE "SOURCE:[CDD.OPEN]CDD_WINDOW.HB"

	%INCLUDE "FUNC_INCLUDE:RM_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:PD_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:PC_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:UTL_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:GL_WINDOW.INC"

	!
	! External functions
	!
	EXTERNAL LONG FUNCTION RM_MAIN_JOURNAL
	EXTERNAL LONG FUNCTION RM_MAIN_JOURPROD
	EXTERNAL LONG FUNCTION RM_MAIN_JOURPOST

	EXTERNAL LONG FUNCTION UTL_MAIN_LOCATION
	EXTERNAL LONG FUNCTION UTL_MAIN_TRANSTYPE

	EXTERNAL LONG FUNCTION GL_MAIN_CHART
	EXTERNAL LONG FUNCTION PD_MAIN_PRODUCT
	EXTERNAL LONG FUNCTION PC_MAIN_PRCTYPE

	%PAGE

	SELECT SMG_WINDOW::IDENT

	CASE RM_MAIN_JOURNAL.ID
		MAINT_GROUP = RM_MAIN_JOURNAL(SMG_WINDOW, MOPTION, &
			MLOOP, MFLAG, MVALUE)

	CASE RM_MAIN_JOURPROD.ID
		MAINT_GROUP = RM_MAIN_JOURPROD(SMG_WINDOW, MOPTION, &
			MLOOP, MFLAG, MVALUE)

	CASE RM_MAIN_JOURPOST.ID
		MAINT_GROUP = RM_MAIN_JOURPOST(SMG_WINDOW, MOPTION, &
			MLOOP, MFLAG, MVALUE)

	CASE UTL_MAIN_LOCATION.ID
		MAINT_GROUP = UTL_MAIN_LOCATION(SMG_WINDOW, MOPTION, &
			MLOOP, MFLAG, MVALUE)

	CASE UTL_MAIN_TRANSTYPE.ID
		MAINT_GROUP = UTL_MAIN_TRANSTYPE(SMG_WINDOW, MOPTION, &
			MLOOP, MFLAG, MVALUE)

	CASE PD_MAIN_PRODUCT.ID
		MAINT_GROUP = PD_MAIN_PRODUCT(SMG_WINDOW, MOPTION, &
			MLOOP, MFLAG, MVALUE)

	CASE PC_MAIN_PRCTYPE.ID
		MAINT_GROUP = PC_MAIN_PRCTYPE(SMG_WINDOW, MOPTION, &
			MLOOP, MFLAG, MVALUE)

	CASE GL_MAIN_CHART.ID
		MAINT_GROUP = GL_MAIN_CHART(SMG_WINDOW, MOPTION, &
			MLOOP, MFLAG, MVALUE)

	END SELECT

32767	END FUNCTION
	!+-+-+
	!++
	! Abstract:FLD001YR
	!	^*Batch Number\*
	!	.p
	!	The ^*Batch Number\* field provides the means to enter the number which
	!	indicates the batch which will be entered and maintained.  This field may
	!	contain two (2) alphanumeric characters.
	!
	! Index:
	!	.x Batch Number
	!	.x Number>Batch
	!
	!--
