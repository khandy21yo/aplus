1	%TITLE "Restaurant Expand Journal"
	%SBTTL "RM_MAIN_JOURPOST"
	%IDENT "V3.6a Calico"

	FUNCTION LONG RM_MAIN_JOURPOST(CDD_WINDOW_CDD SMG_WINDOW, &
		LONG MOPTION, LONG MLOOP, LONG MFLAG, STRING MVALUE)

	!
	! COPYRIGHT (C) 1987, 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! Abstract:HELP
	!	.p
	!	The ^*Expand\* program provides the means to expand the posting to include
	!	all the ingredients used in each product sold for each transaction type.
	!
	! Index:
	!	.x Restaurant Expand Journal
	!	.x Expand Journal
	!	.x Journal>Expand
	!
	! Option:
	!
	! Compile:
	!
	!	$ BAS RM_SOURCE:RM_MAIN_JOURPOST/LINE
	!	$ LIB FUNC_LIB:CMCFUN/REP RM_MAIN_JOURPOST
	!	$ DELETE RM_MAIN_JOURPOST.OBJ;*
	!
	! Author:
	!
	!	11/13/87 - Frank F. Starman
	!
	! Modification history:
	!
	!	05/19/88 - Aaron Redd
	!		Modified to allow R/O open of file if R/W open fails.
	!
	!	06/30/88 - Frank F. Starman
	!		A new layout
	!
	!	04/23/93 - Kevin Handy
	!		Clean up (Check)
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	10/28/96 - Kevin Handy
	!		Reformat source code
	!
	!	06/03/97 - Kevin Handy
	!		Use integer for #key
	!
	!	08/22/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--
	%PAGE

	!
	! Set up compiling options
	!
	OPTION SIZE = (REAL GFLOAT, INTEGER LONG)

	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "FUNC_INCLUDE:RM_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:MAIN_WINDOW.COM"

	%INCLUDE "SOURCE:[CDD.OPEN]CDD_WINDOW.HB"

	%INCLUDE "SOURCE:[RM.OPEN]RM_JOURPOST.HB"
	MAP	(RM_JOURPOST)		RM_JOURPOST_CDD	RM_JOURPOST
	MAP	(RM_JOURPOST_OLD)	RM_JOURPOST_CDD	RM_JOURPOST_OLD, &
						RM_JOURPOST2

	%INCLUDE "SOURCE:[RM.OPEN]RM_JOURNAL.HB"
	MAP	(RM_JOURNAL)		RM_JOURNAL_CDD	RM_JOURNAL

	!
	! Common Areas
	!
	! These areas store information that is re-used between
	! calls to these functions.
	!
	COM (BATCH_RM_JOURNAL) &
		BATCH_NO$ = 2%
	COM (CH_RM_JOURNAL) &
		RM_JOURNAL.CH%, &
		RM_JOURNAL.READONLY%
	COM (CH_RM_JOURPOST) &
		RM_JOURPOST.CH%, &
		RM_JOURPOST.READONLY%

	!
	! Create array to contain pointers and totals
	!
	RECORD RARRAY_RECORD
		RFA	LINRFA		! Rfa pointer for record
	END RECORD

	MAP (TT_RM_JOURPOST) RARRAY_RECORD RARRAY(10000%)

	%PAGE

	ON ERROR GOTO 29000

	SELECT MOPTION

	!
	! Initialization
	!
	! This option is used to initialize the window structure,
	! set up the default values for add, and open all files
	! necessary that have not already been opened.
	!
	CASE OPT_INIT

		!
		! Define window
		!
		SMG_WINDOW::DESCR = "Expanded Daily Transaction"
		SMG_WINDOW::NHELP = "RM_MAIN_JOURPOST"
		SMG_WINDOW::HSIZE = 78%
		SMG_WINDOW::VSIZE = 15%
		SMG_WINDOW::HVIEW = 78%
		SMG_WINDOW::VVIEW = 18%
		SMG_WINDOW::HPOS  = 2%
		SMG_WINDOW::VPOS  = 5%
		SMG_WINDOW::NITEMS= 5%
		SMG_WINDOW::FLAGS = 0%

		SMG_WINDOW::TOPLIN = 2%
		SMG_WINDOW::BOTLIN = 14%

		!
		! Load in defaults
		!
		CALL READ_DEFAULTS(SMG_WINDOW) &
			IF INSTR(1%, " QV", MVALUE) <= 1%

700		!
		! Declare channels
		!
		IF RM_JOURPOST.CH% > 0%
		THEN
			!
			! Already open, set flag to read-only if was
			! that way from last time.
			!
			SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%) &
				IF RM_JOURPOST.READONLY%
			GOTO 790
		END IF

		!
		! Open main file (existing) for modification
		!
750		%INCLUDE "SOURCE:[RM.OPEN]RM_JOURPOST.CRE"
		RM_JOURPOST.READONLY% = 0%
		GOTO 790

760		!
		! If unable to open for modify, try to open with read
		! access only.
		!
		%INCLUDE "SOURCE:[RM.OPEN]RM_JOURPOST.OPN"
		SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%)
		RM_JOURPOST.READONLY% = -1%

		GOTO 790

770		!
		! File not open, so reset channel
		!
		CALL ASSG_FREECHANNEL(RM_JOURPOST.CH%)

		EXIT FUNCTION

790		SMG_WINDOW::CHAN  = RM_JOURPOST.CH%
		RESET #RM_JOURPOST.CH%
		GET #RM_JOURPOST.CH%, REGARDLESS

	!
	! More options
	!
	CASE OPT_OPTLIST
		MVALUE = "Find Next Restore Help eXit"

	!
	! Display the background
	!
	! This option is used to display the background information on the
	! screen.  It must first clear any junk on the screen, and then
	! write the background onto it.
	!
20100	CASE OPT_BACKGROUND

		SMG_STATUS% = SMG$BEGIN_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

		SMG_STATUS% = SMG$ERASE_DISPLAY(SMG_WINDOW::WNUMBER)

		SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
			"  Product#       Date       TransType     Quantity" + &
			"                            ", &
			1%, 1%, , SMG$M_REVERSE)

		SMG_STATUS% = SMG$END_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

	!
	! Extra display stuff
	!
	CASE OPT_DISPLAY

		SMG_STATUS% = SMG$BEGIN_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

		!
		! Display totals
		!
		SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
			"Number of lines" + &
			FORMAT$(SMG_WINDOW::TOTREC, "####") + SPACE$(60%), &
			SMG_WINDOW::VSIZE, 1%, , SMG$M_REVERSE)

		!
		! Paint lines on screen
		!
		FOR I% = 1% TO 4%
			A% = VAL%(MID("017,028,040,051",I%*4%-3%, 3%))

			SMG_STATUS% = SMG$DRAW_LINE(SMG_WINDOW::WNUMBER, &
				1%, A%, SMG_WINDOW::BOTLIN, A%)
		NEXT I%

		SMG_STATUS% = SMG$END_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

	!
	! Enter/Display/Default
	!
	! This option is used to enter the data from the user, display data,
	! set defaults, and return the data back according to MFLAG.
	!
20200	CASE OPT_ENTRY

		TEMP$, TEMP1$ = TRM$(SCOPE::PRG_ITEM)
		TEMP$ = "View starting at" IF TEMP$ = "View"

		SCOPE::PRG_ITEM = "FLD" + FORMAT$(MLOOP, "<0>##")

		XLINE$ = NUM1$(SMG_WINDOW::CURLIN)

 Reentry:	SCOPE::SCOPE_EXIT = 0%

		SELECT MLOOP

		CASE 1%
			RM_JOURPOST::PRODUCT = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, XLINE$ + ";3", TEMP$, &
				RM_JOURPOST::PRODUCT, MFLAG, "'E", MVALUE)

		CASE 2%
			RM_JOURPOST::ACTDATE = ENTR_3DATE(SCOPE, &
				SMG_WINDOW::WNUMBER, XLINE$ + ";18", TEMP$, &
				RM_JOURPOST::ACTDATE, MFLAG, "'E", &
				MVALUE)

		CASE 3%
			RM_JOURPOST::TTYPE = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, XLINE$ + ";29", TEMP$, &
				RM_JOURPOST::TTYPE, MFLAG, "'E", MVALUE)

		CASE 4%

			RM_JOURPOST::QUANTITY = ENTR_3NUMBER(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";41",TEMP$, RM_JOURPOST::QUANTITY, &
				MFLAG, "###,###.###", MVALUE)

		END SELECT

		SCOPE::PRG_ITEM = TEMP1$

	!
	! Set RM_JOURPOST_OLD value
	!
20500	CASE OPT_SETOLD
		RM_JOURPOST_OLD = RM_JOURPOST

	!
	! Restore RM_JOURPOST_OLD value
	!
	CASE OPT_RESETOLD
		RM_JOURPOST = RM_JOURPOST_OLD

	!
	! Set default value
	!
	CASE OPT_SETDEFAULT
		RM_JOURPOST2 = RM_JOURPOST

	!
	! Restore default value
	!
	CASE OPT_RESETDEFAULT
		RM_JOURPOST = RM_JOURPOST2

	!
	! Find
	!
	CASE OPT_FIND
		SELECT MLOOP

		CASE 0%
			FIND #RM_JOURPOST.CH%, &
				KEY#0% GE RM_JOURPOST::LOCATION + &
				RM_JOURPOST::STARTDATE + &
				RM_JOURPOST::TRANSTYPE, REGARDLESS
		END SELECT

	!
	! Handle array of records
	!
27000	CASE OPT_ARRAY

		!
		! Select sub-option of array
		!
		SELECT MLOOP

		!
		! Load array with line items
		!
		CASE 1%

			!
			! Empty array
			!
			SMG_WINDOW::TOTREC = 0%

27110			!
			! Search for first record
			!
			FIND #SMG_WINDOW::CHAN, &
				KEY #0% GE RM_JOURNAL::LOCATION + &
				RM_JOURNAL::STARTDATE + &
				RM_JOURNAL::TRANSTYPE, &
				REGARDLESS

27120			!
			! Get a record
			!
			GET #SMG_WINDOW::CHAN

			IF RM_JOURPOST::LOCATION + &
				RM_JOURPOST::STARTDATE + &
				RM_JOURPOST::TRANSTYPE = &
			RM_JOURNAL::LOCATION + &
				RM_JOURNAL::STARTDATE+ &
				RM_JOURNAL::TRANSTYPE
			THEN
				!
				! Add information to array
				!
				SMG_WINDOW::TOTREC = SMG_WINDOW::TOTREC + 1%
				RARRAY(SMG_WINDOW::TOTREC)::LINRFA = &
					GETRFA(SMG_WINDOW::CHAN)
				GOTO 27120
			END IF

		!
		! Remove one element of the array
		!
		CASE 2%
			!
			! Remove item pointed to by Mflag
			!
			RARRAY(I%) = RARRAY(I% + 1%) &
				FOR I% = MFLAG TO SMG_WINDOW::TOTREC - 1%

		!
		! Set array item to current record
		!
		CASE 3%
			RARRAY(MFLAG)::LINRFA = GETRFA(SMG_WINDOW::CHAN)

		!
		! Load in current record, locked
		!
		CASE 4%
27200			GET #SMG_WINDOW::CHAN, RFA RARRAY(MFLAG)::LINRFA

		!
		! Load in current record, unlocked
		!
		CASE 5%
			GET #SMG_WINDOW::CHAN, RFA RARRAY(MFLAG)::LINRFA, &
				REGARDLESS

		!
		! Change the current record's key to match header.  The
		! new key probably passes through MVALUE, unless some
		! other means is devised.
		!
		CASE 6%
			RM_JOURPOST::LOCATION   = MID(MVALUE,2%,4%)
			RM_JOURPOST::STARTDATE  = MID(MVALUE,6%,8%)
			RM_JOURPOST::TRANSTYPE  = MID(MVALUE,14%,2%)
		END SELECT
	END SELECT

28000	EXIT FUNCTION

	%PAGE

29000	!*******************************************************************
	! Trap errors
	!*******************************************************************

	SELECT ERL
	!
	! Unable to open Read/Write
	!
	CASE 750%
		RESUME 760 IF ERR = 10%
		RM_MAIN_JOURPOST = ERR
		RESUME 770

	!
	! Unable to open Read/Only
	!
	CASE 760%
		RM_MAIN_JOURPOST = ERR
		RESUME 770

	!
	! Unable to reset
	!
	CASE 790%
		RESUME 32767

	!
	! Error while loading array
	!
	CASE 27110%
		!
		! Nothing to load
		!
		RESUME 28000

	CASE 27120%
		!
		! Read all records
		!
		RESUME 28000 IF ERR = 11%

	END SELECT

	ON ERROR GO BACK

32767	END FUNCTION
