1	%TITLE "Worksheet Print Flags"
	%SBTTL "RM_MAIN_TRANSFLAG"
	%IDENT "V3.6a Calico"

	FUNCTION LONG RM_MAIN_TRANSFLAG(CDD_WINDOW_CDD SMG_WINDOW, LONG MOPTION, &
		LONG MLOOP, LONG MFLAG, STRING MVALUE)

	!
	! COPYRIGHT (C) 1987, 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! Abstract:HELP
	!	.p
	!	This program maintains Worksheet print flag file
	!
	! Index:
	!
	! Option:
	!
	! Compile:
	!
	!	$ BAS RM_SOURCE:RM_MAIN_TRANSFLAG/LINE
	!	$ LIB FUNC_LIB:CMCFUN/REP RM_MAIN_TRANSFLAG
	!	$ DELETE RM_MAIN_TRANSFLAG.OBJ;*
	!
	! Author:
	!
	!	06/29/88 - Frank Starman
	!
	! Modification history:
	!
	!	05/07/92 - Dan Perkins
	!		Use FUNC_TESTENTRY to test input.
	!
	!	06/12/92 - Kevin Handy
	!		Clean up (check)
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	10/29/96 - Kevin Handy
	!		Reformat source code.
	!
	!	06/03/97 - Kevin Handy
	!		Use integer for #key
	!
	!	08/22/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--
	%PAGE

	!
	! Set up compiling options
	!
	OPTION SIZE = (REAL GFLOAT, INTEGER LONG)

	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "FUNC_INCLUDE:RM_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:PD_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:UTL_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:MAIN_WINDOW.COM"

	%INCLUDE "SOURCE:[CDD.OPEN]CDD_WINDOW.HB"

	%INCLUDE "SOURCE:[RM.OPEN]RM_TRANSFLAG.HB"
	MAP (RM_TRANSFLAG)	RM_TRANSFLAG_CDD	RM_TRANSFLAG
	MAP (RM_TRANSFLAG_OLD)	RM_TRANSFLAG_CDD	RM_TRANSFLAG_OLD, &
							RM_TRANSFLAG2

	%INCLUDE "SOURCE:[UTL.OPEN]UTL_TRANSTYPE.HB"
	MAP (UTL_TRANSTYPE)	UTL_TRANSTYPE_CDD	UTL_TRANSTYPE

	%INCLUDE "SOURCE:[PD.OPEN]PD_PRODUCT.HB"
	MAP (PD_PRODUCT)	PD_PRODUCT_CDD		PD_PRODUCT

	!
	! Common Areas
	!
	! These areas store information that is re-used between
	! calls to these functions.
	!
	COM (CH_RM_TRANSFLAG) &
		RM_TRANSFLAG.CH%, &
		RM_TRANSFLAG.READONLY%

	!
	! External functions
	!
	EXTERNAL LONG    FUNCTION MAIN_WINDOW
	EXTERNAL LONG    FUNCTION FUNC_TESTENTRY

	%PAGE

	ON ERROR GOTO 29000

	SELECT MOPTION

	!
	! Initialization
	!
	! This option is used to initialize the window structure,
	! set up the default values for add, and open all files
	! necessary that have not already been opened.
	!
	CASE OPT_INIT

		!*************************************************************
		! Set up information
		!*************************************************************

		!
		! Define window
		!
		SMG_WINDOW::DESCR = "Worksheet print flag"
		SMG_WINDOW::CURREC = -2%
		SMG_WINDOW::NHELP = "RM_MAIN_TRANSFLAG"
		SMG_WINDOW::HSIZE = 76%
		SMG_WINDOW::VSIZE = 8%
		SMG_WINDOW::HPOS  = 3%
		SMG_WINDOW::VPOS  = 11%
		SMG_WINDOW::FLAGS = 0%
		SMG_WINDOW::NITEMS= 2%

		SMG_WINDOW::NKEYS = 1%
		SMG_WINDOW::KNAME(0%) = "Trans_type"
			SMG_WINDOW::KFIELD(0%, 0%) = 1%
			SMG_WINDOW::KFIELD(0%, 1%) = 1%

		SMG_WINDOW::HVIEW = 76%
		SMG_WINDOW::VVIEW = 8%
		SMG_WINDOW::VHPOS = 3%
		SMG_WINDOW::VVPOS = 11%

		CALL READ_DEFAULTS(SMG_WINDOW) &
			IF INSTR(1%, " QV", MVALUE) <= 1%

700		!
		! Declare channels
		!
		IF RM_TRANSFLAG.CH% > 0%
		THEN
			!
			! Already open, set flag to read-only if was
			! that way from last time.
			!
			SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%) &
				IF RM_TRANSFLAG.READONLY%
			GOTO 790
		END IF

		!
		! Open main file (existing) for modification
		!
750		%INCLUDE "SOURCE:[RM.OPEN]RM_TRANSFLAG.CRE"
		RM_TRANSFLAG.READONLY% = 0%
		GOTO 790

760		!
		! If unable to open for modify, try to open with read
		! access only.
		!
		%INCLUDE "SOURCE:[RM.OPEN]RM_TRANSFLAG.OPN"
		SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%)
		RM_TRANSFLAG.READONLY% = -1%

		GOTO 790

770		!
		! File not open, so reset channel
		!
		CALL ASSG_FREECHANNEL(RM_TRANSFLAG.CH%)

		EXIT FUNCTION

790		SMG_WINDOW::CHAN  = RM_TRANSFLAG.CH%
		RESET #RM_TRANSFLAG.CH%
		GET #RM_TRANSFLAG.CH%, REGARDLESS

	!
	! Display the background
	!
	! This option is used to display the background information on the
	! screen.  It must first clear any junk on the screen, and then
	! write the background onto it.
	!
20100	CASE OPT_BACKGROUND

		SMG_STATUS% = SMG$BEGIN_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

		SMG_STATUS% = SMG$ERASE_DISPLAY(SMG_WINDOW::WNUMBER)


		DATA	03,  10, "(01) Transaction Type", &
			0,   0, ""

		RESTORE

		READ XPOS%, YPOS%, XSTR$
		I%=0%
		WHILE (XPOS% <> 0)
			I% = I% + 1%
			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				XSTR$, XPOS%, YPOS%) &
				IF (SMG_WINDOW::HFLAG(I%) AND 2%)=0%
		READ XPOS%, YPOS%, XSTR$
		NEXT

		SMG_STATUS% = SMG$END_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

	!
	! Enter/Display/Default
	!
	! This option is used to enter the data from the user, display data,
	! set defaults, and return the data back according to MFLAG.
	!
20200	CASE OPT_ENTRY

		TEMP$, TEMP1$ = TRM$(SCOPE::PRG_ITEM)
		TEMP$ = "View starting at" IF TEMP$ = "View"

		SCOPE::PRG_ITEM = "FLD" + FORMAT$(MLOOP, "<0>##")

 ReEnter:	SCOPE::SCOPE_EXIT = 0%

		SELECT MLOOP

		CASE 1%

			RM_TRANSFLAG::TRANSTYPE = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, "03;35", TEMP$, &
				RM_TRANSFLAG::TRANSTYPE, MFLAG, "'E", &
				MVALUE)

			IF (SCOPE::SCOPE_EXIT = SMG$K_TRM_F14)
			THEN
				IF MAIN_WINDOW(UTL_MAIN_TRANSTYPE.ID, "V0") = 1%
				THEN
					RM_TRANSFLAG::TRANSTYPE = &
						UTL_TRANSTYPE::CODE
				END IF
				GOTO Reenter
			END IF

		END SELECT

		SCOPE::PRG_ITEM = TEMP1$

	!
	! Test values
	!
20300	CASE OPT_TESTENTRY
		RM_MAIN_TRANSFLAG = 0%

		SELECT MLOOP

		CASE 1%
			!
			! Is the input defined?
			!
			RM_MAIN_TRANSFLAG = FUNC_TESTENTRY(SMG_WINDOW, &
				RM_TRANSFLAG::TRANSTYPE, &
				UTL_TRANSTYPE::DESCRIPTION, &
				"RM", MLOOP, "PRG", &
				"Transaction type", UTL_MAIN_TRANSTYPE.ID)

			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				UTL_TRANSTYPE::DESCRIPTION,3%,38%, ,SMG$M_BOLD)

			IF (MVALUE = "ADD")
			THEN
				GET #SMG_WINDOW::CHAN, &
					KEY#0% EQ RM_TRANSFLAG::PRODUCT + &
					RM_TRANSFLAG::TRANSTYPE, &
					REGARDLESS

				RM_MAIN_TRANSFLAG = 2%
				CALL ENTR_3MESSAGE(SCOPE, &
					"Record Already Exists", 0%)
			END IF

		END SELECT

	CASE OPT_DISPLAY

		IF (SMG_WINDOW::HFLAG(1%) AND 2%)=0%
		THEN
			DESCRIPTION$ = STRING$(20%,63%)
			DESCRIPTION$ = UTL_TRANSTYPE::DESCRIPTION &
				IF MAIN_WINDOW(UTL_MAIN_TRANSTYPE.ID, &
				"Q0" + RM_TRANSFLAG::TRANSTYPE) = 1%

			!
			! Display name (ALSO IN TESTENTRY)
			!
			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				DESCRIPTION$,3%,38%, ,SMG$M_BOLD)
		END IF

	!
	! Set RM_TRANSFLAG_OLD value
	!
20500	CASE OPT_SETOLD
		RM_TRANSFLAG_OLD = RM_TRANSFLAG

	!
	! Restore RM_TRANSFLAG_OLD value
	!
	CASE OPT_RESETOLD
		RM_TRANSFLAG = RM_TRANSFLAG_OLD

	!
	! Set default value
	!
	CASE OPT_SETDEFAULT
		RM_TRANSFLAG2 = RM_TRANSFLAG

	!
	! Restore default value
	!
	CASE OPT_RESETDEFAULT
		RM_TRANSFLAG = RM_TRANSFLAG2
		RM_TRANSFLAG::PRODUCT = MVALUE

	!
	! View header
	!
	CASE OPT_VIEW
		SELECT MLOOP

		!
		! Title (One line only)
		!
		CASE 1%
			MVALUE = "  TransType"

		!
		! Positions of lines
		!
		CASE 2%
			MVALUE = "012"

		!
		! Convert current record into text
		!
		CASE 3%
			MVALUE = RM_TRANSFLAG::TRANSTYPE

		END SELECT

	!
	! Find
	!
	CASE OPT_FIND
		SELECT MLOOP
		CASE 0%
			FIND #SMG_WINDOW::CHAN, &
				KEY#0% GE RM_TRANSFLAG::PRODUCT + &
				RM_TRANSFLAG::TRANSTYPE, &
				REGARDLESS

		END SELECT

	!
	! Handle array of records
	!
	CASE OPT_SUBWIND

		SELECT MLOOP

		!
		! Find first record (if there is any)
		!
		CASE 1%

			!
			! Set init value
			!
			SMG_WINDOW::CURREC = -1%

			!
27110			!
			! Search for first record
			!
			FIND #SMG_WINDOW::CHAN, &
				KEY #0% EQ MVALUE, REGARDLESS

			!
			! Get a record
			!
			GET #SMG_WINDOW::CHAN
			SMG_WINDOW::CURREC = 0%

		!
		! Check if still right key
		!
		CASE 3%
			SMG_WINDOW::CURREC = -1%
			IF RM_TRANSFLAG::PRODUCT = MVALUE
			THEN
				SMG_WINDOW::CURREC = 0%
			END IF

		!
		! Set the new key
		CASE 6%
			RM_TRANSFLAG::PRODUCT = MVALUE

		END SELECT

	END SELECT

28000	EXIT FUNCTION

29000	!
	! Trap errors
	!
	SELECT ERL
	!
	! Unable to open Read/Write
	!
	CASE 750%
		RESUME 760 IF ERR = 10%
		RM_MAIN_TRANSFLAG = ERR
		RESUME 770

	!
	! Unable to open Read/Only
	!
	CASE 760%
		RM_MAIN_TRANSFLAG = ERR
		RESUME 770

	!
	! Unable to reset
	!
	CASE 790%
		RESUME 32767

	!
	! Record does not already exist
	!
	CASE 20300%
		RESUME 32767 IF ERR = 155%

	!
	! Error while loading array
	!
	CASE 27110%
		!
		! Nothing to load
		!
		RESUME 28000 IF ERR=155%

	END SELECT

	ON ERROR GO BACK

32767	END FUNCTION
	!+-+-+
	!++
	! Abstract:FLD001
	!	^*(01) Transaction Type\*
	!	.p
	!	The ^*Transaction Type\* field provides a means to enter a code for a
	!	particular ingredient which has been established in the Transaction Type
	!	Table.  By pressing ^*<List Choices>\* in this field, a list of valid
	!	transaction types will be displayed.
	!	.p
	!	The field will accommodate two (2) alphanumeric characters.
	!
	! Index:
	!	.x Transaction Type
	!	.x Type>Transaction
	!
	!--
	!+-+-+
	!++
	! Abstract:RECORD
	!	^*Record\*
	!	.p
	!	The ^*Record\* option in the COMMAND menu provides the means to access easy
	!	detailed information for the transaction type.
	!
	!
	! Index:
	!	.x Record
	!
	!--
