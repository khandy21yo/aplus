1	%TITLE "Worksheet Transaction Account List"
	%SBTTL "RM_RPRT_TRANSACT"
	%IDENT "V3.6a Calico"

	!
	! COPYRIGHT (C) 1986, 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! ID:RM018
	!
	! Abstract:HELP
	!	.p
	!	This program prints out a list of the Product TRANSACT
	!	description file
	!
	! Index:
	!
	! Option:
	!
	! Compile:
	!
	!	$ BAS RM_SOURCE:RM_RPRT_TRANSACT/LINE
	!	$ LINK/EXE=RM_EXE: RM_RPRT_TRANSACT, FUNC_LIB:CMCLINK/OPTION
	!	$ DELETE RM_RPRT_TRANSACT.OBJ;*
	!
	! Author:
	!
	!	10/22/87 - Lance Williams
	!
	! Modification History:
	!
	!	04/23/93 - Kevin Handy
	!		Clean up (Check)
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	09/13/96 - Kevin Handy
	!		Reformat source code
	!
	!	06/03/97 - Kevin Handy
	!		Use integer for #key
	!
	!	08/22/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--
	%PAGE

	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	!
	! Include files
	!
	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "SOURCE:[UTL.OPEN]UTL_REPORTX.HB"
	DECLARE				UTL_REPORTX_CDD		UTL_REPORTX

	%INCLUDE "SOURCE:[RM.OPEN]RM_TRANSACTION.HB"
	MAP	(RM_TRANSACTION)	RM_TRANSACTION_CDD	RM_TRANSACTION

	%INCLUDE "SOURCE:[PD.OPEN]PD_PRODUCT.HB"
	MAP	(PD_PRODUCT)		PD_PRODUCT_CDD		PD_PRODUCT

	%PAGE

	ON ERROR GOTO 19000

	!
 Init:	! Initilize report
	!
	CALL OUTP_INITFROMFILE(UTL_REPORTX, 132%)

	GOTO ExitProgram IF UTL_REPORTX::STAT

	FROM.ITEM$ = EDIT$(UTL_REPORTX::OPTDEF(0%), 132%)
	TO.ITEM$ = EDIT$(UTL_REPORTX::OPTDEF(1%), 132%)
	WLDCRD$ = EDIT$(UTL_REPORTX::OPTDEF(2%), -1%)
	SORT.BY$ = EDIT$(UTL_REPORTX::OPTDEF(9%), 132%)

	SELECT SORT.BY$
	CASE "P"
		SORT.KEY% = 0%
		ADD.TITLE$ = "BY  PRODUCT NUMBER"
	CASE "B"
		SORT.KEY% = 1%
		ADD.TITLE$ = "BY  BATCH NUMBER"
	END SELECT


300	%INCLUDE "SOURCE:[RM.OPEN]RM_TRANSACTION.OPN"

310	%INCLUDE "SOURCE:[PD.OPEN]PD_PRODUCT.OPN"

 ReportTitle:
	!
	! Title
	!
	TITLE$(1%) = "RESTAURANT  TRANSACTION  FILE  LIST  " + &
		ADD.TITLE$
	TITLE$(2%) = "Restaurant Management System"
	TITLE$(3%) = ""

	!
	! Heading
	!
	TITLE$(4%) = "Product#       Description                              "+ &
		"Loc  Date       TT   Quantity     "+ &
		"Price      Cost PostDate   PostTime Batch"
	TITLE$(5%) = "."

	LYT_LINE$ = "$Product#:015,$Description:056,$Loc:061," + &
		"DDate:072,$TT:077,VQuantity:090,VPrice:101," + &
		"VCost:106,DPostDate:117,TPostTime:126,$Batch:131"

	%PAGE

17000	!******************************************************************
	! OUTPUT REPORT
	!******************************************************************

	IF FROM.ITEM$ = ""
	THEN
		RESET #RM_TRANSACTION.CH%,KEY#SORT.KEY%
	ELSE
		FIND #RM_TRANSACTION.CH%, KEY#SORT.KEY% GE FROM.ITEM$,REGARDLESS
	END IF

 GetNextRec:
17020	!
	! Main loop starts here
	!
	GOTO ExitProgram IF UTL_REPORTX::STAT

	!
	! Get next record
	!
	GET #RM_TRANSACTION.CH%, REGARDLESS

	!
	! Check current record
	!
	SELECT SORT.BY$

	CASE "B"
		GOTO ExitTotal IF (RM_TRANSACTION::BATCH > TO.ITEM$) AND &
			TO.ITEM$ <> ""
		GOTO GetNextRec IF WLDCRD$ <> "" AND &
			COMP_STRING(EDIT$(RM_TRANSACTION::BATCH,-1%), &
			WLDCRD$) = 0%

	CASE "P"
		GOTO ExitTotal IF (RM_TRANSACTION::PRODUCT > TO.ITEM$) AND &
			TO.ITEM$ <> ""
		GOTO GetNextRec IF WLDCRD$ <> "" AND &
			COMP_STRING(EDIT$(RM_TRANSACTION::PRODUCT,-1%), &
			WLDCRD$) = 0%

	END SELECT

17200	PD_PRODUCT::DESCRIPTION = &
		STRING$(LEN(PD_PRODUCT::DESCRIPTION),ASCII("?"))
	GET #PD_PRODUCT.CH%, KEY#0% EQ RM_TRANSACTION::PRODUCT, REGARDLESS

17300	!
	! Print out one line
	!
	TEXT$ = RM_TRANSACTION::PRODUCT			+ " " + &
		PD_PRODUCT::DESCRIPTION			+ " " + &
		RM_TRANSACTION::LOCATION		+ " " + &
		PRNT_DATE(RM_TRANSACTION::TRANSDATE,8%)	+ " " + &
		RM_TRANSACTION::TRANSTYPE		+ " " + &
		FORMAT$(RM_TRANSACTION::QUANTITY,"######.###") + " " + &
		FORMAT$(RM_TRANSACTION::PRICE,"######.##") + " " + &
		FORMAT$(RM_TRANSACTION::COST,"######.##") + " " + &
		PRNT_DATE(RM_TRANSACTION::POSTDATE,8%)	+ " " + &
		PRNT_TIME(RM_TRANSACTION::POSTTIME,O%)	+ " " + &
		RM_TRANSACTION::BATCH			+ " "

	CALL OUTP_LINE(lyt_line$, UTL_REPORTX, TITLE$(), TEXT$, 0%)

17350	!
	! Try for next record
	!
	GOTO GetNextRec

 ExitTotal:
17400	!
	! Handle end of report
	!

 ExitProgram:
	CALL OUTP_FINISH(UTL_REPORTX)

	!
	! Exit to next program or menu
	!
	IF TRM$(UTL_REPORTX::NEXTRUN) = ""
	THEN
		CALL SUBR_3EXITPROGRAM(SCOPE, "", "")
	ELSE
		CALL SUBR_3EXITPROGRAM(SCOPE, "RUN " + UTL_REPORTX::NEXTRUN, "")
	END IF

	%PAGE

 HelpError:
	!******************************************************************
	! Help Message for an error
	!******************************************************************
	CALL HELP_34MESSAGE(SCOPE, NUM1$(ERL) + " " + ERT$(ERR), &
		"E", ERN$, FILENAME$, NUM1$(ERR))
	UTL_REPORTX::STAT = -1%
	GOTO ExitProgram

	%Page

19000	!******************************************************************
	! ERROR TRAPPING
	!******************************************************************

	SELECT ERR
	CASE 154%	! Locked record
		SLEEP 5%
		RESUME
	END SELECT

	FILENAME$ = ""
	SELECT ERL

	CASE 300%
		FILENAME$ = "RM_TRANSACTION"

	CASE 310%
		RESUME ReportTitle IF ERR = 5%
		FILENAME$ = "PD_PRODUCT"

	CASE 17000%
		CALL ENTR_3MESSAGE(SCOPE, &
			"Unable to find beginning record!", 0%)
		RESUME ExitProgram

	CASE 17020%	! End of file
		RESUME ExitTotal IF ERR = 11%
		FILENAME$ = "RM_TRANSACTION"

	CASE 17200%	! Record not found
		RESUME 17300 IF ERR = 155% OR ERR = 9%
		FILENAME$ = "PD_PRODUCT"

	END SELECT

	!
	! Untrapped error
	!
	RESUME HelpError

32767	END
