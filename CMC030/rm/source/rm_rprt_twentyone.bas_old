1	%TITLE "Twenty One Days Period"
	%SBTTL "RM_RPRT_TWENTYONE"
	%IDENT "V3.6a Calico"

	!
	! COPYRIGHT (C) 1986, 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! ID:RM003
	!
	! Abstract:HELP
	!	.p
	!	The ^*Twenty One Days Period\* program prints out report containing the
	!	sales totals for a twenty one day period. Contained in this report are the
	!	following fields:
	!	.b
	!	.lm 15
	!	.list 0,"*"
	!	.le
	!	Location
	!	.le
	!	Time
	!	.le
	!	Day of the Week
	!	.le
	!	Date
	!	.els
	!
	! Index:
	!	.x Twenty One Day Period
	!
	! Option:
	!
	! Compile:
	!
	!	$ BAS RM_SOURCE:RM_RPRT_TWENTYONE/LINE
	!	$ LINK/EXE=RM_EXE: RM_RPRT_TWENTYONE, FUNC_LIB:CMCLINK/OPTION
	!	$ DELETE RM_RPRT_TWENTYONE.OBJ;*
	!
	! Author:
	!
	!	09/30/87 - Frank F. Starman
	!
	! Modification History:
	!
	!	04/23/93 - Kevin Handy
	!		Clean up (Check)
	!
	!	06/11/93 - Kevin Handy
	!		Clean up (Check)
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	09/13/96 - Kevin Handy
	!		Reformat source code.
	!
	!	06/03/97 - Kevin Handy
	!		Use integer for #key
	!
	!	08/22/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--
	%PAGE

	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	!
	! Include files
	!
	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "SOURCE:[UTL.OPEN]UTL_REPORTX.HB"
	DECLARE		UTL_REPORTX_CDD	UTL_REPORTX

	%INCLUDE "SOURCE:[RM.OPEN]RM_HISTORY.HB"
	MAP	(RM_HISTORY)	RM_HISTORY_CDD		RM_HISTORY

	%INCLUDE "SOURCE:[UTL.OPEN]UTL_LOCATION.HB"
	MAP	(UTL_LOCATION)	UTL_LOCATION_CDD	UTL_LOCATION

	%PAGE

	ON ERROR GOTO 19000

	DIM INTEGER POSITION(95%,20%,1%), REAL COL.TOTAL(20%,1%)

	!
 Init:	! Initilize report
	!
	PRINT.WIDTH% = 132%
	CALL OUTP_INITFROMFILE(UTL_REPORTX, PRINT.WIDTH%)

	GOTO ExitProgram IF UTL_REPORTX::STAT

	FROM.ITEM$ = EDIT$(UTL_REPORTX::OPTDEF(0%), 132%)
	TO.ITEM$ = EDIT$(UTL_REPORTX::OPTDEF(1%), 132%)
	WLDCRD$ = EDIT$(UTL_REPORTX::OPTDEF(2%), -1%)
	CATEGORY$ = EDIT$(UTL_REPORTX::OPTDEF(4%), 132%)
	TO.DATE$ = DATE_STOREDATE(EDIT$(UTL_REPORTX::OPTDEF(5%), 132%))


300	%INCLUDE "SOURCE:[RM.OPEN]RM_HISTORY.OPN"

310	%INCLUDE "SOURCE:[UTL.OPEN]UTL_LOCATION.OPN"

	DAY.WEEK$(1%) = "MONDAY           "
	DAY.WEEK$(2%) = "TUESDAY          "
	DAY.WEEK$(3%) = "WEDNESDAY        "
	DAY.WEEK$(4%) = "THURSDAY         "
	DAY.WEEK$(5%) = "FRIDAY           "
	DAY.WEEK$(6%) = "SATURDAY         "
	DAY.WEEK$(7%) = "SUNDAY           "

	TO_DATE% = DATE_DAYCODE(TO.DATE$)
	FROM.DATE$ = DATE_INVDCODE(TO_DATE% - 20%)
	FROM_DATE% = TO_DATE% - 20%

	HEADER.LINE1$ = "PERIOD FROM "+PRNT_DATE(FROM.DATE$,8%)+" TO "+ &
		PRNT_DATE(TO.DATE$,8%)
	HEADER.LINE2$ = ""
	HEADER.LINE2$ = HEADER.LINE2$+DAY.WEEK$(DATE_DAYOFWEEK(FROM_DATE%+I%)) &
		FOR I%=0% TO 6%
	HEADER.LINE2$ = "TIME   "+HEADER.LINE2$+"   AVE"

	HEADER.LINE3$ = SPACE$(6%)
	FOR I%=0% TO 6%
		FOR J%=0% TO 2%
			HEADER.LINE3$ = HEADER.LINE3$+ &
				" "+RIGHT(DATE_INVDCODE(FROM_DATE%+7%*J%+I%),5%)
		NEXT J%
		HEADER.LINE3$ = HEADER.LINE3$+" |"
	NEXT I%

 ReportTitle:
	!
	! Title
	!
	TITLE$(1%) = "HALF  HOUR"

	SELECT CATEGORY$

	CASE "01"
		TITLE$(1%) = TITLE$(1%)+"  PROJECTED  SALES"
	CASE "02"
		TITLE$(1%) = TITLE$(1%)+"  ACTUAL  SALES"
	CASE "03"
		TITLE$(1%) = TITLE$(1%)+"  PROJECTED  LABOR"
	CASE "04"
		TITLE$(1%) = TITLE$(1%)+"  ACTUAL  LABOR"
	END SELECT

	TITLE$(2%) = "Restaurant Management System"
	TITLE$(3%) = ""

	TITLE$(5%) = HEADER.LINE2$
	TITLE$(6%) = HEADER.LINE3$

	%PAGE

17000	!******************************************************************
	! OUTPUT REPORT
	!******************************************************************

	IF FROM.ITEM$ = ""
	THEN
		RESET #RM_HISTORY.CH%
	ELSE
		FIND #RM_HISTORY.CH%, KEY#0% GE CATEGORY$+FROM.ITEM$,REGARDLESS
	END IF

 GetNextRec:
17020	!
	! Main loop starts here
	!
	GOTO ExitProgram IF UTL_REPORTX::STAT

	!
	! Get next record
	!
	GET #RM_HISTORY.CH%, REGARDLESS

	STORE$ = RM_HISTORY::LOCATION
	TEST_DATE$ = FROM.DATE$

	GOTO 17350 IF WLDCRD$ <>"" AND &
		COMP_STRING(EDIT$(RM_HISTORY::LOCATION,-1%), WLDCRD$) = 0%

	GOTO ExitTotal IF (RM_HISTORY::LOCATION > TO.ITEM$) AND &
		TO.ITEM$ <> ""

17030	UTL_LOCATION::LOCNAME = STRING$(40%,63%)
	GET #UTL_LOCATION.CH%, KEY#0% EQ RM_HISTORY::LOCATION,REGARDLESS

17040	TITLE$(4%) = LEFT("==== "+RM_HISTORY::LOCATION+" "+ &
		EDIT$(UTL_LOCATION::LOCNAME,128%)+" "+ &
		STRING$(PRINT.WIDTH%,ASCII("=")),PRINT.WIDTH%-37%)+ &
		" "+HEADER.LINE1$

	!
	! Set variables to zero
	!
	GOSUB 18000

	FIND #RM_HISTORY.CH%, KEY#0% GE CATEGORY$+RM_HISTORY::LOCATION+ &
		FROM.DATE$, REGARDLESS

17050	GET #RM_HISTORY.CH%, REGARDLESS

	!
	! Check current record
	!
	GOTO 17350 IF RM_HISTORY::CATEGORY <> CATEGORY$ OR &
		RM_HISTORY::LOCATION <> STORE$ OR &
		RM_HISTORY::ACTION_DATE > TO.DATE$

	!
	! Create array to print
	!
	FOR COLUMN%=0% TO 20%

		!
		! Leave zero on the spaces
		WHILE RM_HISTORY::ACTION_DATE <> TEST_DATE$
			GOTO 17300 IF COLUMN%=21%
			COLUMN% = COLUMN%+1%
			TEST_DATE$ = DATE_INVDCODE(DATE_DAYCODE(TEST_DATE$) + &
				1%)
		NEXT

		COL% = 3%*COLUMN%-20%*INT(COLUMN%/7%)

		DIFFTIM$ = TIME_ADD(RM_HISTORY::TIME_TO,"-", &
				PRNT_TIME(RM_HISTORY::TIME_FROM,1%))
		HOURS%   = VAL%(LEFT(DIFFTIM$,2%))
		MINUT%   = VAL%(MID(DIFFTIM$,4%,2%))
		HALF.HOURS% = 2% * HOURS% + SGN(MINUT%) + &
			INT(MINUT%/31%)
		HALF.HOURS% = 48% IF HOURS%=0%

		DIFFTIM$ = TIME_ADD(RM_HISTORY::TIME_FROM,"-","00:00:00")
		HOURS%   = VAL%(LEFT(DIFFTIM$,2%))
		MINUT%   = VAL%(MID(DIFFTIM$,4%,2%))
		FROM.HOURS% = 2% * HOURS% + SGN(MINUT%) + &
			INT(MINUT%/31%)

		I%=0%

		FOR LINE% =FROM.HOURS% TO FROM.HOURS%+HALF.HOURS%-1%
			POSITION(LINE%,COL%,0%) = &
				RM_HISTORY::AMOUNT_QTY(I%)
			POSITION(LINE%,COL%,1%) = 1%
			I%=I%+1%
		NEXT LINE%

		LINE.TOP% = FROM.HOURS% &
			IF LINE.TOP%>FROM.HOURS%
		LINE.BOT% = LINE% &
			IF LINE.BOT%<LINE%

		GET #RM_HISTORY.CH%, REGARDLESS

		GOTO 17300 IF CATEGORY$ + STORE$ <> &
			RM_HISTORY::CATEGORY + RM_HISTORY::LOCATION

		TEST_DATE$ = DATE_INVDCODE(DATE_DAYCODE(TEST_DATE$) + 1%)

	NEXT COLUMN%

17300	!
	! Print array POSITION
	!

	CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), "", 999%)

	FOR LINE%=LINE.TOP% TO LINE.BOT%

		LINE.TOTAL = 0.
		TEXT$ = LEFT(TIME_ADD("00:00:00","+",FORMAT$(30%*(LINE%+1%), &
			":####:")),5%)+" "
		FOR COLUMN%=0% TO 20%
			COL.TOTAL(COLUMN%,0%) = COL.TOTAL(COLUMN%,0%) + &
				POSITION(LINE%,COLUMN%,0%)
			IF POSITION(LINE%,COLUMN%,1%)=1%
			THEN
				TEXT$ = TEXT$ + &
				FORMAT$(POSITION(LINE%,COLUMN%,0%),"#####")
				COL.TOTAL(COLUMN%,1%) = 1.
			ELSE
				TEXT$ = TEXT$ + "     "
			END IF
			LINE.TOTAL = LINE.TOTAL + POSITION(LINE%,COLUMN%,0%)

			CLM%=3%*INT(COLUMN%/3%)+2%
			TEXT$ = TEXT$+" |" IF CLM%=COLUMN%

		NEXT COLUMN%
		AVE = 0.
		AVE = AVE + POSITION(LINE%,J%,1%) &
			FOR J%=0% TO 20%
		IF AVE <> 0.
		THEN
			TEXT$ = TEXT$ +	FORMAT$(LINE.TOTAL/AVE,"####.##")
		END IF

		CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, -50%)

	NEXT LINE%

	CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), "", -50%)

	AVE,TOTAL = 0.

	TEXT$ = "TOTAL"

	FOR COLUMN%=0% TO 20% STEP 2%
		TEXT$ = TEXT$ + FORMAT$(COL.TOTAL(COLUMN%,0%),"<%>#####    ")
		TOTAL = TOTAL + COL.TOTAL(COLUMN%,0%)
		CLM%=3%*INT(COLUMN%/3%)+2%
		TEXT$ = TEXT$ + "  " IF CLM%-1% = COLUMN% OR CLM%=COLUMN%
	NEXT COLUMN%

	CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, -50%)

	TEXT$ = "TOTAL     "

	FOR COLUMN%=1% TO 19% STEP 2%
		TEXT$ = TEXT$ + FORMAT$(COL.TOTAL(COLUMN%,0%),"<%>#####    ")
		TOTAL = TOTAL +COL.TOTAL(COLUMN%,0%)
		CLM%=3%*INT(COLUMN%/3%)+2%
		TEXT$ = TEXT$ + "  " IF CLM%-1% = COLUMN% OR CLM%=COLUMN%
	NEXT COLUMN%

	AVE = AVE + COL.TOTAL(J%,1%) FOR J%=0% TO 20%
	TEXT$ = TEXT$ + FORMAT$(TOTAL/AVE,"<%>####.##") IF AVE<>0.

	CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, -50%)

	GOTO ExitProgram IF UTL_REPORTX::STAT

17350	!
	! Try for next record
	!

	FIND #RM_HISTORY.CH%, KEY#0% GE CATEGORY$+STORE$+"99999999", REGARDLESS

	GOTO GetNextRec

 ExitTotal:
17400	!
	! Handle end of report
	!

 ExitProgram:
	CALL OUTP_FINISH(UTL_REPORTX)

	!
	! Exit to next program or menu
	!
	IF TRM$(UTL_REPORTX::NEXTRUN) = ""
	THEN
		CALL SUBR_3EXITPROGRAM(SCOPE, "", "")
	ELSE
		CALL SUBR_3EXITPROGRAM(SCOPE, "RUN " + UTL_REPORTX::NEXTRUN, "")
	END IF

	%PAGE

18000	!
	! Set initial values
	!
	LINE.TOP% = 96%
	LINE.BOT% = -1%

	FOR J% = 0% TO 20%
		FOR K% =0% TO 1%
			COL.TOTAL(J%,K%)   = 0.
			POSITION(I%,J%,K%) = 0% FOR I%=0% TO 95%
		NEXT K%
	NEXT J%

	RETURN

 HelpError:
	!******************************************************************
	! Help Message for an error
	!******************************************************************
	CALL HELP_34MESSAGE(SCOPE, NUM1$(ERL) + " " + ERT$(ERR), &
		"E", ERN$, FILENAME$, NUM1$(ERR))
	UTL_REPORTX::STAT = -1%
	GOTO ExitProgram

	%Page

19000	!******************************************************************
	! ERROR TRAPPING
	!******************************************************************

	SELECT ERR
	CASE 154%	! Locked record
		SLEEP 5%
		RESUME
	END SELECT

	FILENAME$ = ""
	SELECT ERL

	CASE 300%
		FILENAME$ = "RM_HISTORY"

	CASE 310%
		FILENAME$ = "UTL_LOCATION"

	CASE 17000%
		CALL ENTR_3MESSAGE(SCOPE, &
			"Unable to find beginning record!", 0%)
		RESUME ExitProgram

	CASE 17020%	! End of file
		RESUME ExitTotal IF ERR = 11%
		FILENAME$ = "RM_HISTORY"

	CASE 17030%	! End of file
		RESUME 17040 IF ERR = 155%
		FILENAME$ = "UTL_LOCATION"

	CASE 17040%
		FILENAME$ = "RM_HISTORY"
		RESUME 17300 IF ERR = 155%

	CASE 17050%	! Can't find next store
		RESUME 17300 IF ERR = 11%
		FILENAME$ = "RM_HISTORY"

	CASE 17350%	! Can't find next store
		RESUME ExitTotal
	END SELECT

	!
	! Untrapped error
	!
	RESUME HelpError

32767	END
	!+-+-+
	!++
	! Abstract:FLD01
	!	^*(01) From Location\*
	!	.p
	!	A ^*From Location\* value entered in this field causes the printing of the
	!	report to begin with the selected location.
	!	.p
	!	A blank field causes the report to start with the first location record in
	!	the file.
	!
	! Index:
	!	.x From Location
	!	.x Location>From
	!
	!--
	!+-+-+
	!++
	! Abstract:FLD02
	!	^*(02) To Location\*
	!	.p
	!	A ^*To Location\* value entered in this field causes the printing of the
	!	report to end with the selected location.
	!	.p
	!	A blank field causes the report to end with the last location record in the
	!	file.
	!
	! Index:
	!	.x To Location
	!	.x Location>To
	!
	!--
	!+-+-+
	!++
	! Abstract:FLD03
	!	^*(03) Wildcard\*
	!	.p
	!	The ^*Wildcard\* setting enables the user to print a report including selected
	!	Locations only using the wildcarding techniques.
	!
	! Index:
	!	.x Wildcard
	!
	!--
	!+-+-+
	!++
	! Abstract:FLD05
	!	^*(05) Item\*
	!	.p
	!	The ^*Item\* field provides the means to enter the item or items which will
	!	be printed on the report.  Wildcarding techniques may be used in this field.
	!
	! Index:
	!	.x Item
	!
	!--
	!+-+-+
	!++
	! Abstract:FLD06
	!	^*(06) To Date\*
	!	.p
	!	The value entered in the ^*To Date\* field causes the report to end with
	!	the selected date.
	!	.p
	!	A blank setting causes the report to end with the last date in the file.
	!
	! Index:
	!	.x To Date
	!	.x Date>To
	!
	!--
