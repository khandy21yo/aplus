1	%TITLE "TV_MAIN_ARTOGL - TV Traffic AR to GL Table Maintenance"
	%SBTTL "TV system"
	%IDENT "V3.6a Calico"

	FUNCTION LONG TV_MAIN_ARTOGL(CDD_WINDOW_CDD SMG_WINDOW, &
		LONG MOPTION, LONG MLOOP, LONG MFLAG, STRING MVALUE)

	!
	! COPYRIGHT (C) 1987, 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	!
	! Abstract:
	!
	!	This program maintains the TV Traffic AR to GL table.
	!
	! Index:
	!
	!	AR to GL
	!	AR to GL Table
	!	ARTOGL
	!	ARTOGL Table
	!
	! Input:
	!
	!
	! Output:
	!
	!
	! Example:
	!
	!
	! Environment:
	!
	!	VAX/VMS BASIC+2 V4.4
	!
	! Compile:
	!
	!	$ BAS TV_SOURCE:TV_MAIN_ARTOGL/LINE
	!	$ LIB/REP FUNC_LIB:CMCFUN TV_MAIN_ARTOGL
	!	$ DELETE TV_MAIN_ARTOGL.OBJ;*
	!
	! Author:
	!
	!	11/10/87 - B. Craig Larsen
	!
	! Modification history:
	!
	!	05/09/88 - Aaron Redd
	!		Modified to allow R/O open of file if R/W open fails.
	!
	!	11/19/88 - J. Shad Rydalch
	!		Modified Main_Window #'s to .ID
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	08/22/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--
	%PAGE

	!
	! Set up compiling options
	!
	OPTION SIZE = (REAL GFLOAT, INTEGER LONG)

	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "FUNC_INCLUDE:GL_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:TV_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:MAIN_WINDOW.COM"

	%INCLUDE "SOURCE:[CDD.OPEN]CDD_WINDOW.HB"

	%INCLUDE "SOURCE:[GL.OPEN]GL_CHART.HB"
	MAP (GL_CHART)		GL_CHART_CDD	GL_CHART

	%INCLUDE "SOURCE:[TV.OPEN]TV_ARTOGL.HB"
	MAP (TV_ARTOGL)		TV_ARTOGL_CDD	TV_ARTOGL
	MAP (FOO_BAR_BLAH1)	TV_ARTOGL_CDD	TV_ARTOGL_OLD, TV_ARTOGL2

	!
	! Common Areas
	!
	! These areas store information that is re-used between
	! calls to these functions.
	!
	COM (CH_TV_ARTOGL) &
		TV_ARTOGL.CH%, &
		TV_ARTOGL.READONLY%

	!
	! External functions
	!
	EXTERNAL STRING FUNCTION READ_SET

	EXTERNAL LONG   FUNCTION MAIN_WINDOW

	%PAGE

	ON ERROR GOTO 29000

	SELECT MOPTION

	!
	! Initialization
	!
	! This option is used to initialize the window structure,
	! set up the default values for add, and open all files
	! necessary that have not already been opened.
	!
	CASE OPT_INIT
		!
		! Define window
		!
		SMG_WINDOW::DESCR = "TV Traffic AR to GL Table Maintenance"
		SMG_WINDOW::NHELP = "TV_MAST_ARTOGL"
		SMG_WINDOW::HSIZE = 78%
		SMG_WINDOW::VSIZE = 18%
		SMG_WINDOW::HVIEW = 78%
		SMG_WINDOW::VVIEW = 18%
		SMG_WINDOW::HPOS  = 2%
		SMG_WINDOW::VPOS  = 2%
		SMG_WINDOW::NITEMS= 7%
		SMG_WINDOW::FLAGS = 0%

		SMG_WINDOW::NKEYS = 1%
		SMG_WINDOW::KNAME(0%) = "Customer_type"
			SMG_WINDOW::KFIELD(0%, 0%) = 1%
			SMG_WINDOW::KFIELD(0%, 1%) = 1%

		!
		! Load in defaults
		!
		CALL READ_DEFAULTS(SMG_WINDOW) &
			IF INSTR(1%, " QV", MVALUE) <= 1%

700		!
		! Declare channels
		!
		IF TV_ARTOGL.CH% > 0%
		THEN
			!
			! Already open, set flag to read-only if was
			! that way from last time.
			!
			SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%) &
				IF TV_ARTOGL.READONLY%
			GOTO 790
		END IF

		!
		! Open main file (existing) for modification
		!
750		%INCLUDE "SOURCE:[TV.OPEN]TV_ARTOGL.CRE"
		TV_ARTOGL.READONLY% = 0%
		GOTO 790

760		!
		! If unable to open for modify, try to open with read
		! access only.
		!
		%INCLUDE "SOURCE:[TV.OPEN]TV_ARTOGL.OPN"
		SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%)
		TV_ARTOGL.READONLY% = -1%

		GOTO 790

770		!
		! File not open, so reset channel
		!
		CALL ASSG_FREECHANNEL(TV_ARTOGL.CH%)

		EXIT FUNCTION

790		SMG_WINDOW::CHAN  = TV_ARTOGL.CH%
		RESET #TV_ARTOGL.CH%
		GET #TV_ARTOGL.CH%, REGARDLESS

	!
	! Display the background
	!
	! This option is used to display the background information on the
	! screen.  It must first clear any junk on the screen, and then
	! write the background onto it.
	!
20100	CASE OPT_BACKGROUND

		SMG_STATUS% = SMG$BEGIN_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

		SMG_STATUS% = SMG$ERASE_DISPLAY(SMG_WINDOW::WNUMBER)

		DATA	05, 04, "(01) Customer Type", &
			06, 04, "(02) Description", &
			08, 04, "(03) AR         Account", &
			09, 04, "(04) Sales      Account", &
			10, 04, "(05) Discount   Account", &
			11, 04, "(06) Production Account", &
			12, 04, "(07) Discount Percentage", &
			12, 33, "%", &
			0,  0, ""

		RESTORE

		READ XPOS%, YPOS%, XSTR$

		WHILE (XPOS% <> 0)
			SMG_STATUS% = SMG$PUT_CHARS( &
				SMG_WINDOW::WNUMBER,XSTR$, XPOS%, YPOS%)
			READ XPOS%, YPOS%, XSTR$
		NEXT

		SMG_STATUS% = SMG$END_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

	!
	! Enter/Display/Default
	!
	! This option is used to enter the data from the user, display data,
	! set defaults, and return the data back according to MFLAG.
	!
20200	CASE OPT_ENTRY
		TEMP$, TEMP1$ = TRM$(SCOPE::PRG_ITEM)
		TEMP$ = "View starting at" IF TEMP$ = "View"

		SCOPE::PRG_ITEM = "FLD" + FORMAT$(MLOOP, "<0>##")

 ELoop:		SCOPE::SCOPE_EXIT = 0%

		SELECT MLOOP

		CASE 1%
			TV_ARTOGL::CUSTYP = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, "05;30", TEMP$, &
				TV_ARTOGL::CUSTYP, MFLAG, "'E", MVALUE )

		CASE 2%
			TV_ARTOGL::DESCR = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, "6;30", TEMP$, &
				TV_ARTOGL::DESCR, MFLAG, "'E", MVALUE )

		CASE 3%
			TV_ARTOGL::AR_ACCT = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, "8;30", TEMP$, &
				TV_ARTOGL::AR_ACCT, MFLAG, "'E", MVALUE )

			IF SCOPE::SCOPE_EXIT = SMG$K_TRM_F14
			THEN
				TV_ARTOGL::AR_ACCT= GL_CHART::ACCT &
					IF MAIN_WINDOW(GL_MAIN_CHART.ID, "VX") = 1%
				GOTO ELoop
			END IF

		CASE 4%
			TV_ARTOGL::SALE_ACCT = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, "9;30", TEMP$, &
				TV_ARTOGL::SALE_ACCT, MFLAG, "'E", MVALUE )

			IF SCOPE::SCOPE_EXIT = SMG$K_TRM_F14
			THEN
				TV_ARTOGL::SALE_ACCT= GL_CHART::ACCT &
					IF MAIN_WINDOW(GL_MAIN_CHART.ID, "VX") = 1%
				GOTO ELoop
			END IF

		CASE 5%
			TV_ARTOGL::DISC_ACCT = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, "10;30", TEMP$, &
				TV_ARTOGL::DISC_ACCT, MFLAG, "'E", MVALUE )

			IF SCOPE::SCOPE_EXIT = SMG$K_TRM_F14
			THEN
				TV_ARTOGL::DISC_ACCT= GL_CHART::ACCT &
					IF MAIN_WINDOW(GL_MAIN_CHART.ID, "VX") = 1%
				GOTO ELoop
			END IF

		CASE 6%
			TV_ARTOGL::PRODUCTION_ACCT = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, "11;30", TEMP$, &
				TV_ARTOGL::PRODUCTION_ACCT, MFLAG, "'E", &
				MVALUE )

			IF SCOPE::SCOPE_EXIT = SMG$K_TRM_F14
			THEN
				TV_ARTOGL::PRODUCTION_ACCT= GL_CHART::ACCT &
					IF MAIN_WINDOW(GL_MAIN_CHART.ID, "VX") = 1%
				GOTO ELoop
			END IF

		CASE 7%
			TV_ARTOGL::DISC_PER = (ENTR_3NUMBER(SCOPE, &
				SMG_WINDOW::WNUMBER, "12;30", TEMP$, &
				TV_ARTOGL::DISC_PER * .01, MFLAG, &
				"###.##", MVALUE )) * 100.

		END SELECT

		SCOPE::PRG_ITEM = TEMP1$

	!
	! Test values
	!
20300	CASE OPT_TESTENTRY

		TV_MAIN_ARTOGL = 0%

		SELECT MLOOP

		CASE 1%
			IF TV_ARTOGL::CUSTYP = ""
			THEN
				TV_MAIN_ARTOGL = 1%
			ELSE
				IF (MVALUE = "ADD")
				THEN
					GET #TV_ARTOGL.CH%, &
						KEY#0 EQ TV_ARTOGL::CUSTYP, &
						REGARDLESS
					TV_MAIN_ARTOGL = 2%
					CALL ENTR_3MESSAGE(SCOPE, &
						"Record Already Exists", 1%)
				END IF
			END IF

		CASE 3%, 4%, 5%, 6%
			TEMP_ACCT$ = TV_ARTOGL::AR_ACCT
			TEMP_ACCT$ = TV_ARTOGL::SALE_ACCT IF MLOOP = 4%
			TEMP_ACCT$ = TV_ARTOGL::DISC_ACCT IF MLOOP = 5%
			TEMP_ACCT$ = TV_ARTOGL::PRODUCTION_ACCT IF MLOOP = 6%

			IF TEMP_ACCT$ <> ""
			THEN
				!
				! Is the input defined?
				!
				DISPLAY_DESCR$ = STRING$(40%,63%)
				IF MAIN_WINDOW(GL_MAIN_CHART.ID,"Q0"+TEMP_ACCT$) <> 1%
				THEN
					!
					! See if they are allowing undefined inputs
					!
					V$ = LEFT(READ_SET(TRM$(SCOPE::PRG_PROGRAM), "ACCT"), 1%)
					V$ = LEFT(READ_SET("TV_ALLOW", "ACCT"), 1%) &
						IF V$ = ""

					IF V$ = "N"
					THEN
						!
						! Don't let them get past is we don't
						! allow them to enter undefined values.
						!
						TV_MAIN_ARTOGL = 1%
						CALL ENTR_3MESSAGE(SCOPE, &
							"Account number is undefined ", 1%)
					ELSE
						!
						! Verify that they really want to
						! enter an undefined value.
						!
						V$ = ENTR_3YESNO(SCOPE, &
							SMG_WINDOW::WNUMBER, &
							"", "Account number is " + &
							"undefined, confirm entry " + &
							"then press <DO> ", &
							"N", 0%, "", "")
						TV_MAIN_ARTOGL = 1% IF V$ = "N"
					END IF

				ELSE
					DISPLAY_DESCR$= GL_CHART::DESCR

				SELECT MLOOP

				CASE 3%
					SMG_STATUS% = SMG$PUT_CHARS( &
						SMG_WINDOW::WNUMBER, &
						LEFT(DISPLAY_DESCR$,30%), &
						8%,49%, ,SMG$M_BOLD)
				CASE 4%
					SMG_STATUS% = SMG$PUT_CHARS( &
						SMG_WINDOW::WNUMBER, &
						LEFT(DISPLAY_DESCR$,30%), &
						9%,49%, ,SMG$M_BOLD)
				CASE 5%
					SMG_STATUS% = SMG$PUT_CHARS( &
						SMG_WINDOW::WNUMBER, &
						LEFT(DISPLAY_DESCR$,30%), &
						10%,49%, ,SMG$M_BOLD)
				CASE 6%
					SMG_STATUS% = SMG$PUT_CHARS( &
						SMG_WINDOW::WNUMBER, &
						LEFT(DISPLAY_DESCR$,30%), &
						11%,49%, ,SMG$M_BOLD)
				END SELECT

				END IF
			END IF

		END SELECT


	CASE OPT_DISPLAY

		DISPLAY_DESCR$ = STRING$(40%,63%)
		DISPLAY_DESCR$ = GL_CHART::DESCR &
			IF MAIN_WINDOW(GL_MAIN_CHART.ID,"Q0"+TV_ARTOGL::AR_ACCT) = 1%
		!
		! Display name (ALSO IN TESTENTRY)
		!
		SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
			LEFT(DISPLAY_DESCR$,30%),8%,49%, ,SMG$M_BOLD)

		DISPLAY_DESCR$ = STRING$(40%,63%)
		DISPLAY_DESCR$ = GL_CHART::DESCR &
			IF MAIN_WINDOW(GL_MAIN_CHART.ID,"Q0"+TV_ARTOGL::SALE_ACCT) = 1%
		!
		! Display name (ALSO IN TESTENTRY)
		!
		SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
			LEFT(DISPLAY_DESCR$,30%),9%,49%, ,SMG$M_BOLD)

		DISPLAY_DESCR$ = STRING$(40%,63%)
		DISPLAY_DESCR$ = GL_CHART::DESCR &
			IF MAIN_WINDOW(GL_MAIN_CHART.ID,"Q0"+TV_ARTOGL::DISC_ACCT) = 1%
		!
		! Display name (ALSO IN TESTENTRY)
		!
		SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
			LEFT(DISPLAY_DESCR$,30%),10%,49%, ,SMG$M_BOLD)

		DISPLAY_DESCR$ = STRING$(40%,63%)
		DISPLAY_DESCR$ = GL_CHART::DESCR &
			IF MAIN_WINDOW(GL_MAIN_CHART.ID,"Q0"+TV_ARTOGL::PRODUCTION_ACCT) = 1%
		!
		! Display name (ALSO IN TESTENTRY)
		!
		SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
			LEFT(DISPLAY_DESCR$,30%),11%,49%, ,SMG$M_BOLD)

	!
	! Set TV_ARTOGL_OLD value
	!
20500	CASE OPT_SETOLD

		TV_ARTOGL_OLD = TV_ARTOGL

	!
	! Restore TV_ARTOGL_OLD value
	!
	CASE OPT_RESETOLD

		TV_ARTOGL = TV_ARTOGL_OLD

	!
	! Set default value
	!
	CASE OPT_SETDEFAULT

		TV_ARTOGL2 = TV_ARTOGL

	!
	! Restore default value
	!
	CASE OPT_RESETDEFAULT

		TV_ARTOGL = TV_ARTOGL2

	!
	! View header
	!
	CASE OPT_VIEW

		SELECT MLOOP

		!
		! Title (One line only)
		!
		CASE 1%
			MVALUE = "  TP   Description         "

		!
		! Positions of lines
		!
		CASE 2%
			MVALUE = "006"

		!
		! Convert current record into text
		!
		CASE 3%
			MVALUE = TV_ARTOGL::CUSTYP + "   " + &
				TV_ARTOGL::DESCR

		END SELECT

	!
	! Find
	!
	CASE OPT_FIND
		SELECT MLOOP
		CASE 0%
			FIND #TV_ARTOGL.CH%, KEY#0 &
				GE TV_ARTOGL::CUSTYP, REGARDLESS
		END SELECT
	END SELECT

 ExitFunction:
	EXIT FUNCTION

29000	!
	! Trap Errors
	!
	SELECT ERL
		!
		! Unable to open Read/Write
		!
		CASE 750%
			RESUME 760 IF ERR = 10%
			TV_MAIN_ARTOGL = ERR
			RESUME 770

		!
		! Unable to open Read/Only
		!
		CASE 760%
			TV_MAIN_ARTOGL = ERR
			RESUME 770

		!
		! Unable to reset
		!
		CASE 790%
			RESUME 32767

	END SELECT

	RESUME ExitFunction

32767	END FUNCTION
