1	%TITLE "TV_MAIN_BREAK - Break File Maintenance"
	%SBTTL "TV system"
	%IDENT "V3.6a Calico"

	FUNCTION LONG TV_MAIN_BREAK(CDD_WINDOW_CDD SMG_WINDOW, &
		LONG MOPTION, LONG MLOOP, LONG MFLAG, STRING MVALUE)

	!
	! COPYRIGHT (C) 1987, 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	!
	! Abstract:
	!
	!	This program maintains the TV Traffic Break file.
	!
	! Index:
	!
	! Environment:
	!
	!	VAX/VMS BASIC+2 V4.4
	!
	! Compile:
	!
	!	$ BAS TV_SOURCE:TV_MAIN_BREAK/LINE
	!	$ LIB/REP FUNC_LIB:CMCFUN TV_MAIN_BREAK
	!	$ DELETE TV_MAIN_BREAK.OBJ;*
	!
	! Author:
	!
	!	11/20/87 - Kevin Handy
	!
	! Modification history:
	!
	!	05/09/88 - Aaron Redd
	!		Modified to allow R/O open of file if R/W open fails.
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	08/22/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--
	%PAGE

	!
	! Set up compiling options
	!
	OPTION SIZE = (REAL GFLOAT, INTEGER LONG)

	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE


	%INCLUDE "FUNC_INCLUDE:MAIN_WINDOW.COM"
	%INCLUDE "FUNC_INCLUDE:TV_WINDOW.INC"
	%INCLUDE "SOURCE:[CDD.OPEN]CDD_WINDOW.HB"

	%INCLUDE "SOURCE:[TV.OPEN]TV_BREAK.HB"
	MAP (TV_BREAK)		TV_BREAK_CDD	TV_BREAK
	MAP (TV_BREAK_OLD)	TV_BREAK_CDD	TV_BREAK_OLD, TV_BREAK2

	%INCLUDE "SOURCE:[TV.OPEN]TV_PROGRAM.HB"
	MAP (TV_PROGRAM)	TV_PROGRAM_CDD	TV_PROGRAM

	%INCLUDE "SOURCE:[TV.OPEN]TV_BREAK_TYPE.HB"
	MAP (TV_BREAK_TYPE)	TV_BREAK_TYPE_CDD	TV_BREAK_TYPE

	!
	! Common Areas
	!
	! These areas store information that is re-used between
	! calls to these functions.
	!
	COM (CH_TV_BREAK) &
		TV_BREAK.CH%, &
		TV_BREAK.READONLY%

	!
	! Create array to contain pointers and totals
	!
	RECORD RARRAY_RECORD
		RFA	LINRFA		! Rfa pointer for record
	END RECORD

	MAP (TT_TV_BREAK) RARRAY_RECORD RARRAY(300%)	! Allocate for 300

	!
	! External functions
	!
	EXTERNAL STRING FUNCTION READ_SET

	EXTERNAL LONG    FUNCTION MAIN_WINDOW

	%PAGE

	ON ERROR GOTO 29000

	SELECT MOPTION

	!
	! Initialization
	!
	! This option is used to initialize the window structure,
	! set up the default values for add, and open all files
	! necessary that have not already been opened.
	!
	CASE OPT_INIT
		!
		! Define window
		!
		SMG_WINDOW::DESCR = "Commercial Breaks"
		SMG_WINDOW::NHELP = "TV_MAIN_BREAK"
		SMG_WINDOW::HSIZE = 78%
		SMG_WINDOW::VSIZE = 12%
		SMG_WINDOW::HPOS  = 2%
		SMG_WINDOW::VPOS  = 8%
		SMG_WINDOW::NITEMS= 8%
		SMG_WINDOW::FLAGS = 0%

		SMG_WINDOW::TOPLIN = 3%
		SMG_WINDOW::BOTLIN = 12%
		SMG_WINDOW::LINREC = 2%

		!
		! Load in defaults
		!
		CALL READ_DEFAULTS(SMG_WINDOW) &
			IF INSTR(1%, " QV", MVALUE) <= 1%

700		!
		! Declare channels
		!
		IF TV_BREAK.CH% > 0%
		THEN
			!
			! Already open, set flag to read-only if was
			! that way from last time.
			!
			SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%) &
				IF TV_BREAK.READONLY%
			GOTO 790
		END IF

		!
		! Open main file (existing) for modification
		!
750		%INCLUDE "SOURCE:[TV.OPEN]TV_BREAK.CRE"
		TV_BREAK.READONLY% = 0%
		GOTO 790

760		!
		! If unable to open for modify, try to open with read
		! access only.
		!
		%INCLUDE "SOURCE:[TV.OPEN]TV_BREAK.OPN"
		SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%)
		TV_BREAK.READONLY% = -1%

		GOTO 790

770		!
		! File not open, so reset channel
		!
		CALL ASSG_CHANNEL(TV_BREAK.CH%, STAT%)

		EXIT FUNCTION

790		SMG_WINDOW::CHAN  = TV_BREAK.CH%
		RESET #TV_BREAK.CH%
		GET #TV_BREAK.CH%, REGARDLESS

	!
	! Display the background
	!
	! This option is used to display the background information on the
	! screen.  It must first clear any junk on the screen, and then
	! write the background onto it.
	!
20100	CASE OPT_BACKGROUND

		SMG_STATUS% = SMG$BEGIN_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

		SMG_STATUS% = SMG$ERASE_DISPLAY(SMG_WINDOW::WNUMBER)

		SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
			"   (01)    (02)   (03)   (04) (05)  " + &
			" (06)     (07)           (08)" + SPACE$(14%), &
			1%, 1%, , SMG$M_REVERSE)
		SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
			"  Rel Time Type  Length  Max  Pri   " + &
			" Match    Description / Comment            ", &
			2%, 1%, , SMG$M_REVERSE)

		SMG_STATUS% = SMG$END_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

	!
	! Extra display stuff
	!
	CASE OPT_DISPLAY

		SMG_STATUS% = SMG$BEGIN_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

		!
		! Paint lines on screen
		!
		FOR I% = 1% TO 6%

			A% = VAL%(MID("011,016,025,030,036,045", I%*4%-3%, 3%))

			SMG_STATUS% = SMG$DRAW_LINE(SMG_WINDOW::WNUMBER, &
				1%, A%, SMG_WINDOW::BOTLIN, A%)

		NEXT I%

		SMG_STATUS% = SMG$END_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

	!
	! Enter/Display/Default
	!
	! This option is used to enter the data from the user, display data,
	! set defaults, and return the data back according to MFLAG.
	!
20200	CASE OPT_ENTRY
		TEMP$, TEMP1$ = TRM$(SCOPE::PRG_ITEM)

		SCOPE::PRG_ITEM = "FLD" + FORMAT$(MLOOP, "<0>##")

		XLINE$ = NUM1$(SMG_WINDOW::CURLIN)

 E0Loop:	SCOPE::SCOPE_EXIT = 0%

		SELECT MLOOP

		CASE 1%
			TV_BREAK::RUN_TIME = ENTR_3TIME(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";3", TEMP$, &
				TV_BREAK::RUN_TIME, MFLAG, "H", MVALUE)

		CASE 2%
			TV_BREAK::BRKTYPE = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";13", TEMP$, &
				TV_BREAK::BRKTYPE, MFLAG, &
				"'E", MVALUE)

			IF SCOPE::SCOPE_EXIT = SMG$K_TRM_F14
			THEN
				TV_BREAK::BRKTYPE = TV_BREAK_TYPE::BTYPE &
					IF MAIN_WINDOW(TV_MAIN_BREAK_TYPE.ID, "V0  ") = 1%
				GOTO E0Loop
			END IF

		CASE 3%
			TV_BREAK::LENGTH = ENTR_3TIME(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";17", TEMP$, &
				TV_BREAK::LENGTH, MFLAG, "H", MVALUE)

		CASE 4%
			TV_BREAK::MAXCOM = ENTR_3NUMBER(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";26", TEMP$, &
				TV_BREAK::MAXCOM * 1.0, MFLAG, "####", MVALUE)

		CASE 5%
			TV_BREAK::PRIORITY = ENTR_3NUMBER(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";31", TEMP$, &
				TV_BREAK::PRIORITY * 1.0, MFLAG, "####", MVALUE)

		CASE 6%
			TV_BREAK::MATCH = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";37", TEMP$, &
				TV_BREAK::MATCH, MFLAG, "'E", MVALUE)

		CASE 7%
			TV_BREAK::DESCR = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";46", TEMP$, &
				TV_BREAK::DESCR, MFLAG, "'E", MVALUE)

		CASE 8%
			TV_BREAK::COMMENT = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				NUM1$(SMG_WINDOW::CURLIN + 1%) + ";46", TEMP$, &
				TV_BREAK::COMMENT, MFLAG, "'E", MVALUE)

		END SELECT

		SCOPE::PRG_ITEM = TEMP1$

	!
	! Test values
	!
20300	CASE OPT_TESTENTRY
		TV_MAIN_BREAK = 0%

		SELECT MLOOP

		CASE 1%
			IF (TV_BREAK::RUN_TIME > TV_PROGRAM::LENGTH)
			THEN
				CALL ENTR_3MESSAGE(SCOPE, &
					"Relative time outside of program!", 0%)
			END IF

		CASE 2%
			! Is the input defined?
			!
			IF MAIN_WINDOW(TV_MAIN_BREAK_TYPE.ID,"Q0"+TV_BREAK::BRKTYPE) <> 1%
			THEN
				!
				! See if they are allowing undefined inputs
				!
				V$ = LEFT(READ_SET(TRM$(SCOPE::PRG_PROGRAM), "BRKTYP"), 1%)
				V$ = LEFT(READ_SET("TV_ALLOW", "BRKTYP"), 1%) &
					IF V$ = ""

				IF V$ = "N"
				THEN
					!
					! Don't let them get past is we don't allow
					! them to enter undefined values.
					!
					TV_MAIN_BREAK = 1%
					CALL ENTR_3MESSAGE(SCOPE, &
						"Input undefined ", 0%)
				ELSE
					!
					! Verify that they really want to enter an
					! undefined value.
					!
					V$ = ENTR_3YESNO(SCOPE, &
						SMG_WINDOW::WNUMBER, "", &
						"Input undefined, enter anyway", &
						"N", 0%, "", "")
					TV_MAIN_BREAK = 1% IF V$ = "N"
				END IF
			END IF

		END SELECT

	!
	! Set TV_BREAK_OLD value
	!
20500	CASE OPT_SETOLD
		TV_BREAK_OLD = TV_BREAK

	!
	! Restore TV_BREAK_OLD value
	!
	CASE OPT_RESETOLD
		TV_BREAK = TV_BREAK_OLD

	!
	! Set default value
	!
	CASE OPT_SETDEFAULT
		TV_BREAK2 = TV_BREAK

	!
	! Restore default value
	!
	CASE OPT_RESETDEFAULT
		TV_BREAK = TV_BREAK2

		!
		! Set special default values for the key of a new record
		! which is the most likely case when this function is to
		! be called.
		!
		TV_BREAK::PRGNUM = TV_PROGRAM::PRGNUM


	!
	! Find
	!
	CASE OPT_FIND
 !		SELECT MLOOP
 !
 !		CASE 0%
			FIND #TV_BREAK.CH%, &
				KEY#0 GE TV_BREAK::PRGNUM, &
				REGARDLESS
 !		END SELECT

	!
	! Handle array of records
	!
27000	CASE OPT_ARRAY

		!
		! Select sub-option of array
		!
		SELECT MLOOP

		!
		! Load array with line items
		!
		CASE 1%

			!
			! Empty array
			!
			SMG_WINDOW::TOTREC = 0%

27110			!
			! Search for first record
			!
			FIND #SMG_WINDOW::CHAN, &
				KEY #0 GE TV_PROGRAM::PRGNUM, &
				REGARDLESS

27120			!
			! Get a record
			!
			GET #SMG_WINDOW::CHAN

			IF (TV_BREAK::PRGNUM = TV_PROGRAM::PRGNUM)
			THEN
				!
				! Add information to array
				!
				SMG_WINDOW::TOTREC = SMG_WINDOW::TOTREC + 1%
				RARRAY(SMG_WINDOW::TOTREC)::LINRFA = &
					GETRFA(SMG_WINDOW::CHAN)
				GOTO 27120
			END IF

		!
		! Remove one element of the array
		!
		CASE 2%
			!
			! Remove item pointed to by Mflag
			!
			FOR I% = MFLAG TO SMG_WINDOW::TOTREC - 1%
				RARRAY(I%) = RARRAY(I% + 1%)
			NEXT I%

		!
		! Set array item to current record
		!
		CASE 3%
			RARRAY(MFLAG)::LINRFA = GETRFA(SMG_WINDOW::CHAN)

		!
		! Load in current record, locked
		!
		CASE 4%
27200			GET #SMG_WINDOW::CHAN, RFA RARRAY(MFLAG)::LINRFA

		!
		! Load in current record, unlocked
		!
		CASE 5%
			GET #SMG_WINDOW::CHAN, RFA RARRAY(MFLAG)::LINRFA, &
				REGARDLESS

		!
		! Change the current record's key to match header.  The
		! new key is probibly passes through MVALUE, unless some
		! other means is devised.
		!
		CASE 6%
			TV_BREAK::PRGNUM = MID(MVALUE, 2%, 10%)

		END SELECT
	END SELECT

28000	EXIT FUNCTION

	%PAGE

29000	!*******************************************************************
	! Trap errors
	!*******************************************************************

	SELECT ERL
		!
		! Unable to open Read/Write
		!
		CASE 750%
			RESUME 760 IF ERR = 10%
			TV_MAIN_BREAK = ERR
			RESUME 770

		!
		! Unable to open Read/Only
		!
		CASE 760%
			TV_MAIN_BREAK = ERR
			RESUME 770

		!
		! Unable to reset
		!
		CASE 790%
			RESUME 32767

		!
		! Record does not already exist
		!
		CASE 20300%
			RESUME 32767 IF ERR = 155%

		!
		! Error while loading array
		!
		CASE 27110%
			!
			! Nothing to load
			!
			RESUME 28000

		CASE 27120%
			!
			! Read all records
			!
			RESUME 28000 IF ERR = 11%

	END SELECT

	ON ERROR GO BACK

32767	END FUNCTION
