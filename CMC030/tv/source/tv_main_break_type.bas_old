1	%TITLE "TV_MAIN_BREAK_TYPE - TV Traffic Break Type Table Maintenance"
	%SBTTL "TV system"
	%IDENT "V3.6a Calico"

	FUNCTION LONG TV_MAIN_BREAK_TYPE(CDD_WINDOW_CDD SMG_WINDOW, &
		LONG MOPTION, LONG MLOOP, LONG MFLAG, STRING MVALUE)

	!
	! COPYRIGHT (C) 1987, 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	!
	! Abstract:
	!
	!	This program maintains the TV Traffic Break Type table.
	!
	! Index:
	!
	!	BREAK_TYPE
	!	BREAK_TYPE Table
	!
	! Input:
	!
	!
	! Output:
	!
	!
	! Example:
	!
	!
	! Environment:
	!
	!	VAX/VMS BASIC+2 V4.4
	!
	! Compile:
	!
	!	$ BAS TV_SOURCE:TV_MAIN_BREAK_TYPE/LINE
	!	$ LIB/REP FUNC_LIB:CMCFUN TV_MAIN_BREAK_TYPE
	!	$ DELETE TV_MAIN_BREAK_TYPE.OBJ;*
	!
	! Author:
	!
	!	11/10/87 - B. Craig Larsen
	!
	! Modification history:
	!
	!	05/09/88 - Aaron Redd
	!		Modified to allow R/O open of file if R/W open fails.
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	08/22/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--
	%PAGE

	!
	! Set up compiling options
	!
	OPTION SIZE = (REAL GFLOAT, INTEGER LONG)

	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE


	%INCLUDE "FUNC_INCLUDE:MAIN_WINDOW.COM"

 !	%INCLUDE %FROM %CDD "CDD$TOP.CDD.CDD_WINDOW"
	%INCLUDE "SOURCE:[CDD.OPEN]CDD_WINDOW.HB"

 !	%INCLUDE %FROM %CDD "CDD$TOP.TV.TV_BREAK_TYPE"
	%INCLUDE "SOURCE:[TV.OPEN]TV_BREAK_TYPE.HB"
	MAP (TV_BREAK_TYPE) TV_BREAK_TYPE_CDD TV_BREAK_TYPE
	MAP (FOO_BAR_BLAH1) TV_BREAK_TYPE_CDD TV_BREAK_TYPE_OLD, TV_BREAK_TYPE2

	!
	! Common Areas
	!
	! These areas store information that is re-used between
	! calls to these functions.
	!
	COM (CH_TV_BREAK_TYPE) &
		TV_BREAK_TYPE.CH%, &
		TV_BREAK_TYPE.READONLY%

	COM (TT_TV_BREAK_TYPE) &
		FLAGTITLE$ = 20%, &
		FLAGTYPE$(4%) = 20%

	%PAGE

	ON ERROR GOTO 29000

	SELECT MOPTION

	!
	! Initialization
	!
	! This option is used to initialize the window structure,
	! set up the default values for add, and open all files
	! necessary that have not already been opened.
	!
	CASE OPT_INIT
		!
		! Define window
		!
		SMG_WINDOW::DESCR = "TV Traffic Break Type Table Maintenance"
		SMG_WINDOW::NHELP = "TV_MAST_BREAK_TYPE"
		SMG_WINDOW::HSIZE = 78%
		SMG_WINDOW::VSIZE = 18%
		SMG_WINDOW::HVIEW = 78%
		SMG_WINDOW::VVIEW = 18%
		SMG_WINDOW::HPOS  = 2%
		SMG_WINDOW::VPOS  = 2%
		SMG_WINDOW::NITEMS= 3%
		SMG_WINDOW::FLAGS = 0%

		SMG_WINDOW::NKEYS = 1%
		SMG_WINDOW::KNAME(0%) = "Break_type"
			SMG_WINDOW::KFIELD(0%, 0%) = 1%
			SMG_WINDOW::KFIELD(0%, 1%) = 1%

		!
		! Load in defaults
		!
		CALL READ_DEFAULTS(SMG_WINDOW) &
			IF INSTR(1%, " QV", MVALUE) <= 1%

		!
		! Break Flag Types
		!
		FLAGTITLE$ = "List of Flags"
		FLAGTYPE$(0%) = "4"
		FLAGTYPE$(1%) = "0 - Avails                 "
		FLAGTYPE$(2%) = "1 - No avails              "
		FLAGTYPE$(3%) = "2 - In-tape (No avails)    "
		FLAGTYPE$(4%) = "3 - Avails with id         "

700		!
		! Declare channels
		!
		IF TV_BREAK_TYPE.CH% > 0%
		THEN
			!
			! Already open, set flag to read-only if was
			! that way from last time.
			!
			SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%) &
				IF TV_BREAK_TYPE.READONLY%
			GOTO 790
		END IF

		!
		! Open main file (existing) for modification
		!
750		%INCLUDE "SOURCE:[TV.OPEN]TV_BREAK_TYPE.CRE"
		TV_BREAK_TYPE.READONLY% = 0%
		GOTO 790

760		!
		! If unable to open for modify, try to open with read
		! access only.
		!
		%INCLUDE "SOURCE:[TV.OPEN]TV_BREAK_TYPE.OPN"
		SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%)
		TV_BREAK_TYPE.READONLY% = -1%

		GOTO 790

770		!
		! File not open, so reset channel
		!
		CALL ASSG_FREECHANNEL(TV_BREAK_TYPE.CH%)

		EXIT FUNCTION

790		SMG_WINDOW::CHAN  = TV_BREAK_TYPE.CH%
		RESET #TV_BREAK_TYPE.CH%
		GET #TV_BREAK_TYPE.CH%, REGARDLESS

	!
	! Display the background
	!
	! This option is used to display the background information on the
	! screen.  It must first clear any junk on the screen, and then
	! write the background onto it.
	!
20100	CASE OPT_BACKGROUND

		SMG_STATUS% = SMG$BEGIN_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

		SMG_STATUS% = SMG$ERASE_DISPLAY(SMG_WINDOW::WNUMBER)

		DATA	05, 04, "(01) Break Type", &
			06, 04, "(02) Break Description", &
			07, 04, "(03) Break Flag", &
			0,  0, ""

		RESTORE

		READ XPOS%, YPOS%, XSTR$

		WHILE (XPOS% <> 0)
			SMG_STATUS% = SMG$PUT_CHARS( &
				SMG_WINDOW::WNUMBER,XSTR$, XPOS%, YPOS%)
			READ XPOS%, YPOS%, XSTR$
		NEXT

		SMG_STATUS% = SMG$END_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

	!
	! Enter/Display/Default
	!
	! This option is used to enter the data from the user, display data,
	! set defaults, and return the data back according to MFLAG.
	!
20200	CASE OPT_ENTRY

		TEMP$, TEMP1$ = TRM$(SCOPE::PRG_ITEM)
		TEMP$ = "View starting at" IF TEMP$ = "View"

		SCOPE::PRG_ITEM = "FLD" + FORMAT$(MLOOP, "<0>##")

		SCOPE::SCOPE_EXIT = 0%

		SELECT MLOOP

		CASE 1%
			TV_BREAK_TYPE::BTYPE = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, "05;30", TEMP$, &
				TV_BREAK_TYPE::BTYPE, MFLAG, "'E", MVALUE )

		CASE 2%
			TV_BREAK_TYPE::DESCR = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, "6;30", TEMP$, &
				TV_BREAK_TYPE::DESCR, MFLAG, "'E", MVALUE )

		CASE 3%
			TV_BREAK_TYPE::BFLAG = ENTR_3STRINGLIST(SCOPE, &
				SMG_WINDOW::WNUMBER, "07;30", TEMP$, &
				TV_BREAK_TYPE::BFLAG, MFLAG, "'", &
				MVALUE, FLAGTYPE$(), FLAGTITLE$, "000")

		END SELECT

		SCOPE::PRG_ITEM = TEMP1$

	!
	! Test values
	!
20300	CASE OPT_TESTENTRY

		TV_MAIN_BREAK_TYPE = 0%

		SELECT MLOOP

		CASE 1%
			IF TV_BREAK_TYPE::BTYPE = ""
			THEN
				TV_MAIN_BREAK_TYPE = 1%
			ELSE
				IF (MVALUE = "ADD")
				THEN
					GET #TV_BREAK_TYPE.CH%, &
						KEY#0 EQ TV_BREAK_TYPE::BTYPE, &
						REGARDLESS
					TV_MAIN_BREAK_TYPE = 2%
					CALL ENTR_3MESSAGE(SCOPE, &
						"Record Already Exists", 1%)
				END IF
			END IF

		CASE 3%
			IF TV_BREAK_TYPE::BFLAG = ""
			THEN
				TV_MAIN_BREAK_TYPE = 1%
			ELSE
				GOOD% = 0%
				GOOD% = -1% IF TV_BREAK_TYPE::BFLAG = &
					LEFT(EC$(I%),1%) FOR I%=1% TO VAL(EC$(0%))

				IF GOOD% = 0%
				THEN
					X% = ENTR_3CHOICE(SCOPE, &
						"","",EC$(),"", 128%, &
						"List of Flags", "", 0%)
					IF X% > 0%
					THEN
						TV_BREAK_TYPE::BFLAG = LEFT(EC$(X%), 1%)
						TV_MAIN_BREAK_TYPE = 1%
					END IF
				END IF
			END IF

		END SELECT

	CASE OPT_DISPLAY

	!
	! Set TV_BREAK_TYPE_OLD value
	!
20500	CASE OPT_SETOLD

		TV_BREAK_TYPE_OLD = TV_BREAK_TYPE

	!
	! Restore TV_BREAK_TYPE_OLD value
	!
	CASE OPT_RESETOLD

		TV_BREAK_TYPE = TV_BREAK_TYPE_OLD

	!
	! Set default value
	!
	CASE OPT_SETDEFAULT

		TV_BREAK_TYPE2 = TV_BREAK_TYPE

	!
	! Restore default value
	!
	CASE OPT_RESETDEFAULT

		TV_BREAK_TYPE = TV_BREAK_TYPE2

	!
	! View header
	!
	CASE OPT_VIEW

		SELECT MLOOP

		!
		! Title (One line only)
		!
		CASE 1%
			MVALUE = "  TP   Description            Flag"

		!
		! Positions of lines
		!
		CASE 2%
			MVALUE = "006,029"

		!
		! Convert current record into text
		!
		CASE 3%
			MVALUE = TV_BREAK_TYPE::BTYPE + "   " + &
				TV_BREAK_TYPE::DESCR + "    " + &
				TV_BREAK_TYPE::BFLAG

		END SELECT

	!
	! Find
	!
	CASE OPT_FIND
		SELECT MLOOP
		CASE 0%
			FIND #TV_BREAK_TYPE.CH%, KEY#0 &
				GE TV_BREAK_TYPE::BTYPE, REGARDLESS
		END SELECT
	END SELECT

 ExitFunction:
	EXIT FUNCTION

29000	!
	! Trap errors
	!
	SELECT ERL
		!
		! Unable to open Read/Write
		!
		CASE 750%
			RESUME 760 IF ERR = 10%
			TV_MAIN_BREAK_TYPE = ERR
			RESUME 770

		!
		! Unable to open Read/Only
		!
		CASE 760%
			TV_MAIN_BREAK_TYPE = ERR
			RESUME 770

		!
		! Unable to reset
		!
		CASE 790%
			RESUME 32767

	END SELECT

32767	END FUNCTION
