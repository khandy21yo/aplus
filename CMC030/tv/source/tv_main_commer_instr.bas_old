1	%TITLE "TV_MAIN_COMMER_INSTR - Commercial Instruction Maintenance"
	%SBTTL "TV system"
	%IDENT "V3.6a Calico"

	FUNCTION LONG TV_MAIN_COMMER_INSTR(CDD_WINDOW_CDD SMG_WINDOW, &
		LONG MOPTION, LONG MLOOP, LONG MFLAG, STRING MVALUE)

	!
	! COPYRIGHT (C) 1987, 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	!
	! Abstract:
	!
	!	This program maintains the commercial instruction file.
	!
	! Index:
	!
	! Environment:
	!
	!	VAX/VMS BASIC+2 V4.4
	!
	! Compile:
	!
	!	$ BAS TV_SOURCE:TV_MAIN_COMMER_INSTR/LINE
	!	$ LIB/REP FUNC_LIB:CMCFUN TV_MAIN_COMMER_INSTR
	!	$ DELETE TV_MAIN_COMMER_INSTR.OBJ;*
	!
	! Author:
	!
	!	11/24/87 - Kevin Handy
	!
	! Modification history:
	!
	!	05/10/88 - Aaron Redd
	!		Modified to allow R/O open of file if R/W open fails.
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	08/22/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--
	%PAGE

	!
	! Set up compiling options
	!
	OPTION SIZE = (REAL GFLOAT, INTEGER LONG)

	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE


	%INCLUDE "FUNC_INCLUDE:MAIN_WINDOW.COM"

 !	%INCLUDE %FROM %CDD "CDD$TOP.CDD.CDD_WINDOW"
	%INCLUDE "SOURCE:[CDD.OPEN]CDD_WINDOW.HB"

 !	%INCLUDE %FROM %CDD "CDD$TOP.TV.TV_COMMER_SKED_INSTR"
	%INCLUDE "SOURCE:[TV.OPEN]TV_COMMER_SKED_INSTR.HB"
	MAP (TV_COMMER_SKED_INSTR)	TV_COMMER_SKED_INSTR_CDD	TV_COMMER_SKED_INSTR
	MAP (TV_COMMER_SKED_INSTR_OLD)	TV_COMMER_SKED_INSTR_CDD	TV_COMMER_SKED_INSTR_OLD, TV_COMMER_SKED_INSTR2

 !	%INCLUDE %FROM %CDD "CDD$TOP.TV.TV_COMMER"
	%INCLUDE "SOURCE:[TV.OPEN]TV_COMMER.HB"
	MAP (TV_COMMER)	TV_COMMER_CDD	TV_COMMER

	!
	! Common Areas
	!
	! These areas store information that is re-used between
	! calls to these functions.
	!
	COM (CH_TV_COMMER_SKED_INSTR) &
		TV_COMMER_SKED_INSTR.CH%, &
		TV_COMMER_SKED_INSTR.READONLY%

	COM (CH_TV_COMMER) &
		TV_COMMER.CH%

	!
	! Create array to contain pointers and totals
	!
	RECORD RARRAY_RECORD
		RFA	LINRFA		! Rfa pointer for record
	END RECORD

	MAP (TT_TV_COMMER_SKED_INSTR) RARRAY_RECORD RARRAY(300%)	! Allocate for 300

	%PAGE

	ON ERROR GOTO 29000

	SELECT MOPTION

	!
	! Initialization
	!
	! This option is used to initialize the window structure,
	! set up the default values for add, and open all files
	! necessary that have not already been opened.
	!
	CASE OPT_INIT

		!
		! Define window
		!
		SMG_WINDOW::DESCR = "Line items"
		SMG_WINDOW::NHELP = "TV_MAIN_COMMER_INSTR"
		SMG_WINDOW::HSIZE = 78%
		SMG_WINDOW::VSIZE = 12%
		SMG_WINDOW::HPOS  = 2%
		SMG_WINDOW::VPOS  = 8%
		SMG_WINDOW::NITEMS= 17%
		SMG_WINDOW::FLAGS = 0%

		SMG_WINDOW::TOPLIN = 3%
		SMG_WINDOW::BOTLIN = 11%
		SMG_WINDOW::LINREC = 3%

		!
		! Load in defaults
		!
		CALL READ_DEFAULTS(SMG_WINDOW) &
			IF INSTR(1%, " QV", MVALUE) <= 1%

700		!
		! Declare channels
		!
		IF TV_COMMER_SKED_INSTR.CH% > 0%
		THEN
			!
			! Already open, set flag to read-only if was
			! that way from last time.
			!
			SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%) &
				IF TV_COMMER_SKED_INSTR.READONLY%
			GOTO 790
		END IF

		!
		! Open main file (existing) for modification
		!
750		%INCLUDE "SOURCE:[TV.OPEN]TV_COMMER_SKED_INSTR.CRE"
		TV_COMMER_SKED_INSTR.READONLY% = 0%
		GOTO 790

760		!
		! If unable to open for modify, try to open with read
		! access only.
		!
		%INCLUDE "SOURCE:[TV.OPEN]TV_COMMER_SKED_INSTR.OPN"
		SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%)
		TV_COMMER_SKED_INSTR.READONLY% = -1%

		GOTO 790

770		!
		! File not open, so reset channel
		!
		CALL ASSG_FREECHANNEL(TV_COMMER_SKED_INSTR.CH%)

		EXIT FUNCTION

790		SMG_WINDOW::CHAN  = TV_COMMER_SKED_INSTR.CH%
		RESET #TV_COMMER_SKED_INSTR.CH%
		GET #TV_COMMER_SKED_INSTR.CH%, REGARDLESS

	!
	! Display the background
	!
	! This option is used to display the background information on the
	! screen.  It must first clear any junk on the screen, and then
	! write the background onto it.
	!
20100	CASE OPT_BACKGROUND

		SMG_STATUS% = SMG$BEGIN_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

		SMG_STATUS% = SMG$ERASE_DISPLAY(SMG_WINDOW::WNUMBER)

		SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
			" (01) (02)/(03)  (04)/(05) (06)/(07) " + &
			"08 09 10 11 12 13 14   (16)     (17)     ", &
			1%, 1%, , SMG$M_REVERSE)
		SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
			"Sched Fr/To date Fr/To tm.   In/Out  " + &
			"Mo Tu We Th Fr Sa Su  Length  Rate/spot  ", &
			2%, 1%, , SMG$M_REVERSE)

		SMG_STATUS% = SMG$END_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

	!
	! Extra display stuff
	!
	CASE OPT_DISPLAY

		SMG_STATUS% = SMG$BEGIN_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

		!
		! Display totals
		!
		SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
			"Number of lines" + &
			FORMAT$(SMG_WINDOW::TOTREC, "###") + SPACE$(59%), &
			SMG_WINDOW::VSIZE, 1%, , SMG$M_REVERSE)

		!
		! Paint lines on screen
		!
		FOR I% = 1% TO 6%

			A% = VAL%(MID("006,017,027,037,058,067", I%*4%-3%, 3%))

			SMG_STATUS% = SMG$DRAW_LINE(SMG_WINDOW::WNUMBER, &
				1%, A%, SMG_WINDOW::BOTLIN, A%)

		NEXT I%

		SMG_STATUS% = SMG$END_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

	!
	! Enter/Display/Default
	!
	! This option is used to enter the data from the user, display data,
	! set defaults, and return the data back according to MFLAG.
	!
20200	CASE OPT_ENTRY

		TEMP$, TEMP1$ = TRM$(SCOPE::PRG_ITEM)

		SCOPE::PRG_ITEM = "FLD" + FORMAT$(MLOOP, "<0>##")

		XLINE$ = NUM1$(SMG_WINDOW::CURLIN)
		XLINE1$ = NUM1$(SMG_WINDOW::CURLIN + 1%)

 E0Loop:	SCOPE::SCOPE_EXIT = 0%

		SELECT MLOOP

		CASE 1%
			TV_COMMER_SKED_INSTR::SKED_NUM = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";3", TEMP$, &
				TV_COMMER_SKED_INSTR::SKED_NUM, MFLAG, "'E", MVALUE)

		CASE 2%
			TV_COMMER_SKED_INSTR::START_DATE = ENTR_3DATE(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";7", TEMP$, &
				TV_COMMER_SKED_INSTR::START_DATE, MFLAG, &
				"'E", MVALUE)

		CASE 3%
			TV_COMMER_SKED_INSTR::END_DATE = ENTR_3DATE(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE1$ + ";7", TEMP$, &
				TV_COMMER_SKED_INSTR::END_DATE, MFLAG, "'E", MVALUE)

		CASE 4%
			TV_COMMER_SKED_INSTR::START_TIME = ENTR_3TIME(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";18", TEMP$, &
				TV_COMMER_SKED_INSTR::START_TIME, MFLAG, "H", MVALUE)

		CASE 5%
			TV_COMMER_SKED_INSTR::END_TIME = ENTR_3TIME(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE1$ + ";18", TEMP$, &
				TV_COMMER_SKED_INSTR::END_TIME, MFLAG, "H", MVALUE)

		CASE 6%
			TV_COMMER_SKED_INSTR::IN_WEEKS = ENTR_3NUMBER(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";30", TEMP$, &
				TV_COMMER_SKED_INSTR::IN_WEEKS * 1.0, &
				MFLAG, "###", MVALUE)

		CASE 7%
			TV_COMMER_SKED_INSTR::OUT_WEEKS = ENTR_3NUMBER(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE1$ + ";30", TEMP$, &
				TV_COMMER_SKED_INSTR::OUT_WEEKS * 1.0, &
				MFLAG, "###", MVALUE)

		CASE 8% TO 14%
			TV_COMMER_SKED_INSTR::SPOTS_PER_DAY(MLOOP - 8%) = &
				ENTR_3NUMBER(SCOPE, SMG_WINDOW::WNUMBER, &
				XLINE$ + ";" + NUM1$(38% + 3% * (MLOOP - 8%)), &
				TEMP$, &
				TV_COMMER_SKED_INSTR::SPOTS_PER_DAY(MLOOP - 8%) * 1.0, &
				MFLAG, "<%>#", MVALUE)

			GOTO E0Loop IF TV_COMMER_SKED_INSTR::SPOTS_PER_DAY(MLOOP-8%) > 99%

			IF (MFLAG% AND 1%) = 0%
			THEN
				TFLAG% = 0%
				TTOTAL% = 0%
				FOR LOOP% = 0% TO 6%
					TEMP% = TV_COMMER_SKED_INSTR::SPOTS_PER_DAY(LOOP%)

					IF TEMP% < 0%
					THEN
						TFLAG% = -1%
					ELSE
						TTOTAL% = TTOTAL% + TEMP%
					END IF
				NEXT LOOP%

				TV_COMMER_SKED_INSTR::TOTAL_SPOTS = TTOTAL% &
					IF TFLAG% = 0%

				TV_COMMER_SKED_INSTR::TOTAL_SPOTS = &
					ENTR_3NUMBER(SCOPE, &
					SMG_WINDOW::WNUMBER, &
					XLINE1$ + ";50", TEMP$, &
					TV_COMMER_SKED_INSTR::TOTAL_SPOTS * 1.0, &
					1%, "####", MVALUE)
			END IF

		CASE 15%
			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
				"(15) Total:", &
				SMG_WINDOW::CURLIN + 1%, 38%, , SMG$M_REVERSE)

			TFLAG% = 0%
			TTOTAL% = 0%
			FOR LOOP% = 0% TO 6%
				TEMP% = TV_COMMER_SKED_INSTR::SPOTS_PER_DAY(LOOP%)

				IF TEMP% < 0%
				THEN
					TFLAG% = -1%
				ELSE
					TTOTAL% = TTOTAL% + TEMP%
				END IF
			NEXT LOOP%

			TV_COMMER_SKED_INSTR::TOTAL_SPOTS = TTOTAL% &
				IF TFLAG% = 0%

			TEMP% = MFLAG
			TEMP% = TEMP% OR 1% IF TFLAG% = 0%

			TV_COMMER_SKED_INSTR::TOTAL_SPOTS = &
				ENTR_3NUMBER(SCOPE, SMG_WINDOW::WNUMBER, &
				XLINE1$ + ";50", TEMP$, &
				TV_COMMER_SKED_INSTR::TOTAL_SPOTS * 1.0, &
				TEMP%, "####", MVALUE)

		CASE 16%
			TV_COMMER_SKED_INSTR::LENGTH = ENTR_3TIME(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";59", TEMP$, &
				TV_COMMER_SKED_INSTR::LENGTH, MFLAG, "H", MVALUE)

		CASE 17%
			TV_COMMER_SKED_INSTR::RATE_PER_SPOT = ENTR_3NUMBER(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";68", TEMP$, &
				TV_COMMER_SKED_INSTR::RATE_PER_SPOT * 1.0, &
				MFLAG, "#####.##", MVALUE)


		END SELECT

		SCOPE::PRG_ITEM = TEMP1$

	!
	! Test values
	!
20300	CASE OPT_TESTENTRY
		TV_MAIN_COMMER_INSTR = 0%

	!
	! Set TV_COMMER_SKED_INSTR_OLD value
	!
20500	CASE OPT_SETOLD
		TV_COMMER_SKED_INSTR_OLD = TV_COMMER_SKED_INSTR

	!
	! Restore TV_COMMER_SKED_INSTR_OLD value
	!
	CASE OPT_RESETOLD
		TV_COMMER_SKED_INSTR = TV_COMMER_SKED_INSTR_OLD

	!
	! Set default value
	!
	CASE OPT_SETDEFAULT
		TV_COMMER_SKED_INSTR2 = TV_COMMER_SKED_INSTR

	!
	! Restore default value
	!
	CASE OPT_RESETDEFAULT
		TV_COMMER_SKED_INSTR = TV_COMMER_SKED_INSTR2

		!
		! Set special default values for the key of a new record
		! which is the most likely case when this function is to
		! be called.
		!
		TV_COMMER_SKED_INSTR::FRMNUM = TV_COMMER::FRMNUM


	!
	! Find
	!
	CASE OPT_FIND
		SELECT MLOOP

		CASE 0%
			FIND #TV_COMMER_SKED_INSTR.CH%, &
				KEY#0 GE TV_COMMER_SKED_INSTR::FRMNUM, &
				REGARDLESS
		END SELECT

26000	!
	! Handle updating the COMMER_HEADER flag when a change is
	! made.
	!
	CASE OPT_AFTEROPT
		SELECT SCOPE::PRG_ITEM

		CASE "Add", "Change", "Blank", "Erase", "Initialize"

			!
			! Update the modcount and change flag as necessary
			!
			IF TV_COMMER::CONFIRM = "Y"
			THEN
				GET #TV_COMMER.CH%, &
					KEY #0 EQ TV_COMMER_SKED_INSTR::FRMNUM

				TV_COMMER::MOD_NUM = &
					TV_COMMER::MOD_NUM + 1%
				TV_COMMER::CONFIRM = "N"

				UPDATE #TV_COMMER.CH%
			END IF
		END SELECT

	!
	! Handle array of records
	!
27000	CASE OPT_ARRAY

		!
		! Select sub-option of array
		!
		SELECT MLOOP

		!
		! Load array with line items
		!
		CASE 1%

			!
			! Empty array
			!
			SMG_WINDOW::TOTREC = 0%

27110			!
			! Search for first record
			!
			FIND #SMG_WINDOW::CHAN, &
				KEY #0 GE TV_COMMER::FRMNUM, &
				REGARDLESS

27120			!
			! Get a record
			!
			GET #SMG_WINDOW::CHAN

			IF (TV_COMMER_SKED_INSTR::FRMNUM = TV_COMMER::FRMNUM)
			THEN
				!
				! Add information to array
				!
				SMG_WINDOW::TOTREC = SMG_WINDOW::TOTREC + 1%
				RARRAY(SMG_WINDOW::TOTREC)::LINRFA = &
					GETRFA(SMG_WINDOW::CHAN)
				GOTO 27120
			END IF

		!
		! Remove one element of the array
		!
		CASE 2%
			!
			! Remove item pointed to by Mflag
			!
			FOR I% = MFLAG TO SMG_WINDOW::TOTREC - 1%
				RARRAY(I%) = RARRAY(I% + 1%)
			NEXT I%

		!
		! Set array item to current record
		!
		CASE 3%
			RARRAY(MFLAG)::LINRFA = GETRFA(SMG_WINDOW::CHAN)

		!
		! Load in current record, locked
		!
		CASE 4%
27200			GET #SMG_WINDOW::CHAN, RFA RARRAY(MFLAG)::LINRFA

		!
		! Load in current record, unlocked
		!
		CASE 5%
			GET #SMG_WINDOW::CHAN, RFA RARRAY(MFLAG)::LINRFA, &
				REGARDLESS

		!
		! Change the current record's key to match header.  The
		! new key is probibly passes through MVALUE, unless some
		! other means is devised.
		!
		CASE 6%
			TV_COMMER_SKED_INSTR::FRMNUM = RIGHT(MVALUE, 2%)

		END SELECT
	END SELECT

28000	EXIT FUNCTION

	%PAGE

29000	!*******************************************************************
	! Trap errors
	!*******************************************************************

	SELECT ERL
		!
		! Unable to open Read/Write
		!
		CASE 750%
			RESUME 760 IF ERR = 10%
			TV_MAIN_COMMER_INSTR = ERR
			RESUME 770

		!
		! Unable to open Read/Only
		!
		CASE 760%
			TV_MAIN_COMMER_INSTR = ERR
			RESUME 770

		!
		! Unable to reset
		!
		CASE 790%
			RESUME 32767

		!
		! Record does not already exist
		!
		CASE 20300%
			RESUME 32767 IF ERR = 155%

		!
		! Error while loading array
		!
		CASE 27110%
			!
			! Nothing to load
			!
			RESUME 28000

		CASE 27120%
			!
			! Read all records
			!
			RESUME 28000 IF ERR = 11%

	END SELECT

	ON ERROR GO BACK

32767	END FUNCTION
