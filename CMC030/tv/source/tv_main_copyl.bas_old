1	%TITLE "TV_MAIN_COPYL - Copy Line File Maintenance"
	%SBTTL "TV system"
	%IDENT "V3.6a Calico"

	FUNCTION LONG TV_MAIN_COPYL(CDD_WINDOW_CDD SMG_WINDOW, &
		LONG MOPTION, LONG MLOOP, LONG MFLAG, STRING MVALUE)

	!
	! COPYRIGHT (C) 1987, 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	!
	! Abstract:
	!
	!	This program maintains the copy line file.
	!
	! Index:
	!
	! Environment:
	!
	!	VAX/VMS BASIC+2 V4.4
	!
	! Compile:
	!
	!	$ BAS TV_SOURCE:TV_MAIN_COPYL/LINE
	!	$ LIB/REP FUNC_LIB:CMCFUN TV_MAIN_COPYL
	!	$ DELETE TV_MAIN_COPYL.OBJ;*
	!
	! Author:
	!
	!	11/16/87 - Kevin Handy
	!
	! Modification history:
	!
	!	05/11/88 - Aaron Redd
	!		Modified to allow R/O open of file if R/W open fails.
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	08/22/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--
	%PAGE

	!
	! Set up compiling options
	!
	OPTION SIZE = (REAL GFLOAT, INTEGER LONG)

	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "FUNC_INCLUDE:TV_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:MAIN_WINDOW.COM"

 !	%INCLUDE %FROM %CDD "CDD$TOP.CDD.CDD_WINDOW"
	%INCLUDE "SOURCE:[CDD.OPEN]CDD_WINDOW.HB"

 !	%INCLUDE %FROM %CDD "CDD$TOP.TV.TV_COPY_SPOTS"
	%INCLUDE "SOURCE:[TV.OPEN]TV_COPY_SPOTS.HB"
	MAP (TV_COPY_SPOTS)		TV_COPY_SPOTS_CDD	TV_COPY_SPOTS
	MAP (TV_COPY_SPOTS_OLD)		TV_COPY_SPOTS_CDD	TV_COPY_SPOTS_OLD, TV_COPY_SPOTS2

 !	%INCLUDE %FROM %CDD "CDD$TOP.TV.TV_COPY_INSTR"
	%INCLUDE "SOURCE:[TV.OPEN]TV_COPY_INSTR.HB"
	MAP (TV_COPY_INSTR)		TV_COPY_INSTR_CDD	TV_COPY_INSTR

 !	%INCLUDE %FROM %CDD "CDD$TOP.TV.TV_CART_INVENTORY"
	%INCLUDE "SOURCE:[TV.OPEN]TV_CART_INVENTORY.HB"
	MAP (TV_CART_INVENTORY)		TV_CART_INVENTORY_CDD	TV_CART_INVENTORY

	!
	! Common Areas
	!
	! These areas store information that is re-used between
	! calls to these functions.
	!
	COM (CH_TV_COPY_SPOTS) &
		TV_COPY_SPOTS.CH%, &
		TV_COPY_SPOTS.READONLY%

	!
	! Create array to contain pointers and totals
	!
	RECORD RARRAY_RECORD
		RFA	LINRFA		! Rfa pointer for record
		STRING	SLINE = 3	! Line number
	END RECORD

	MAP (TT_TV_COPY_SPOTS) RARRAY_RECORD RARRAY(300%)	! Allocate for 300

	!
	! External functions
	!
	EXTERNAL LONG   FUNCTION MAIN_WINDOW

	%PAGE

	ON ERROR GOTO 29000

	SELECT MOPTION

	!
	! Initialization
	!
	! This option is used to initialize the window structure,
	! set up the default values for add, and open all files
	! necessary that have not already been opened.
	!
	CASE OPT_INIT

		!
		! Define window
		!
		SMG_WINDOW::DESCR = "Line items"
		SMG_WINDOW::NHELP = "TV_COPY_SPOTS"
		SMG_WINDOW::HSIZE = 78%
		SMG_WINDOW::VSIZE = 12%
		SMG_WINDOW::HPOS  = 2%
		SMG_WINDOW::VPOS  = 8%
		SMG_WINDOW::NITEMS= 4%
		SMG_WINDOW::FLAGS = 0%

		SMG_WINDOW::TOPLIN = 3%
		SMG_WINDOW::BOTLIN = 11%

		!
		! Load in defaults
		!
		CALL READ_DEFAULTS(SMG_WINDOW) &
			IF INSTR(1%, " QV", MVALUE) <= 1%

700		!
		! Declare channels
		!
		IF TV_COPY_SPOTS.CH% > 0%
		THEN
			!
			! Already open, set flag to read-only if was
			! that way from last time.
			!
			SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%) &
				IF TV_COPY_SPOTS.READONLY%
			GOTO 790
		END IF

		!
		! Open main file (existing) for modification
		!
750		%INCLUDE "SOURCE:[TV.OPEN]TV_COPY_SPOTS.CRE"
		TV_COPY_SPOTS.READONLY% = 0%
		GOTO 790

760		!
		! If unable to open for modify, try to open with read
		! access only.
		!
		%INCLUDE "SOURCE:[TV.OPEN]TV_COPY_SPOTS.OPN"
		SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%)
		TV_COPY_SPOTS.READONLY% = -1%

		GOTO 790

770		!
		! File not open, so reset channel
		!
		CALL ASSG_FREECHANNEL(TV_COPY_SPOTS.CH%)

		EXIT FUNCTION

790		SMG_WINDOW::CHAN  = TV_COPY_SPOTS.CH%
		RESET #TV_COPY_SPOTS.CH%
		GET #TV_COPY_SPOTS.CH%, REGARDLESS

	!
	! Display the background
	!
	! This option is used to display the background information on the
	! screen.  It must first clear any junk on the screen, and then
	! write the background onto it.
	!
20100	CASE OPT_BACKGROUND

		SMG_STATUS% = SMG$BEGIN_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

		SMG_STATUS% = SMG$ERASE_DISPLAY(SMG_WINDOW::WNUMBER)

		SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
			"(01)    (02)    (03)                 " + &
			"(04)                    " + SPACE$(21%), &
			1%, 1%, , SMG$M_REVERSE)
		SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
			" Num Cart No.   Agency Cart #        " + &
			"Description             " + SPACE$(21%), &
			2%, 1%, , SMG$M_REVERSE)

		SMG_STATUS% = SMG$END_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

	!
	! Extra display stuff
	!
	CASE OPT_DISPLAY

		SMG_STATUS% = SMG$BEGIN_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

		!
		! Display totals
		!
		SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
			"Number of lines" + &
			FORMAT$(SMG_WINDOW::TOTREC, "###") + SPACE$(59%), &
			SMG_WINDOW::VSIZE, 1%, , SMG$M_REVERSE)

		!
		! Paint lines on screen
		!
		FOR I% = 1% TO 3%

			A% = VAL%(MID("005,016,037", I%*4%-3%, 3%))

			SMG_STATUS% = SMG$DRAW_LINE(SMG_WINDOW::WNUMBER, &
				1%, A%, SMG_WINDOW::BOTLIN, A%)

		NEXT I%

		SMG_STATUS% = SMG$END_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

	!
	! Enter/Display/Default
	!
	! This option is used to enter the data from the user, display data,
	! set defaults, and return the data back according to MFLAG.
	!
20200	CASE OPT_ENTRY

		TEMP$, TEMP1$ = TRM$(SCOPE::PRG_ITEM)

		SCOPE::PRG_ITEM = "FLD" + FORMAT$(MLOOP, "<0>##")

		XLINE$ = NUM1$(SMG_WINDOW::CURLIN)

 E0Loop:	SCOPE::SCOPE_EXIT = 0%

		SELECT MLOOP

		CASE 1%
			TV_COPY_SPOTS::SPOT_NUM = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";3", TEMP$, &
				TV_COPY_SPOTS::SPOT_NUM, MFLAG, "'E", MVALUE)

		CASE 2%
			TV_COPY_SPOTS::CART_NUM = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";6", TEMP$, &
				TV_COPY_SPOTS::CART_NUM, MFLAG, &
				"'E", MVALUE)

			SELECT SCOPE::SCOPE_EXIT
			CASE SMG$K_TRM_F14
				TV_COPY_SPOTS::CART_NUM = TV_CART_INVENTORY::CARTNUM &
					IF MAIN_WINDOW(TV_MAIN_CART.ID, "VX") = 1%
				GOTO E0Loop

			END SELECT

		CASE 3%
			TV_COPY_SPOTS::AGENCY_CART = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";17", TEMP$, &
				TV_COPY_SPOTS::AGENCY_CART, MFLAG, "'E", MVALUE)

		CASE 4%
			TV_COPY_SPOTS::DESCR = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";38", TEMP$, &
				TV_COPY_SPOTS::DESCR, MFLAG, "'E", MVALUE)

		END SELECT

		SCOPE::PRG_ITEM = TEMP1$

	!
	! Test values
	!
20300	CASE OPT_TESTENTRY
		TV_MAIN_COPYL = 0%

	!
	! Set TV_COPY_SPOTS_OLD value
	!
20500	CASE OPT_SETOLD
		TV_COPY_SPOTS_OLD = TV_COPY_SPOTS

	!
	! Restore TV_COPY_SPOTS_OLD value
	!
	CASE OPT_RESETOLD
		TV_COPY_SPOTS = TV_COPY_SPOTS_OLD

	!
	! Set default value
	!
	CASE OPT_SETDEFAULT
		TV_COPY_SPOTS2 = TV_COPY_SPOTS

	!
	! Restore default value
	!
	CASE OPT_RESETDEFAULT
		TV_COPY_SPOTS = TV_COPY_SPOTS2

		!
		! Set special default values for the key of a new record
		! which is the most likely case when this function is to
		! be called.
		!
		TV_COPY_SPOTS::FRMNUM = TV_COPY_INSTR::FRMNUM
		TV_COPY_SPOTS::SEQNUM = TV_COPY_INSTR::SEQNUM


	!
	! Find
	!
	CASE OPT_FIND
		SELECT MLOOP

		CASE 0%
			FIND #TV_COPY_SPOTS.CH%, &
				KEY#0 GE TV_COPY_SPOTS::FRMNUM + &
					TV_COPY_SPOTS::SEQNUM, &
				REGARDLESS
		END SELECT

	!
	! Handle array of records
	!
27000	CASE OPT_ARRAY

		!
		! Select sub-option of array
		!
		SELECT MLOOP

		!
		! Load array with line items
		!
		CASE 1%

			!
			! Empty array
			!
			SMG_WINDOW::TOTREC = 0%

27110			!
			! Search for first record
			!
			FIND #SMG_WINDOW::CHAN, &
				KEY #0 GE TV_COPY_INSTR::FRMNUM + &
					TV_COPY_INSTR::SEQNUM, &
				REGARDLESS

27120			!
			! Get a record
			!
			GET #SMG_WINDOW::CHAN

			IF (TV_COPY_SPOTS::FRMNUM = TV_COPY_INSTR::FRMNUM) AND &
				(TV_COPY_SPOTS::SEQNUM = TV_COPY_INSTR::SEQNUM)
			THEN
				!
				! Add information to array
				!
				SMG_WINDOW::TOTREC = SMG_WINDOW::TOTREC + 1%
				RARRAY(SMG_WINDOW::TOTREC)::LINRFA = &
					GETRFA(SMG_WINDOW::CHAN)
				GOTO 27120
			END IF

		!
		! Remove one element of the array
		!
		CASE 2%
			!
			! Remove item pointed to by Mflag
			!
			FOR I% = MFLAG TO SMG_WINDOW::TOTREC - 1%
				RARRAY(I%) = RARRAY(I% + 1%)
			NEXT I%

		!
		! Set array item to current record
		!
		CASE 3%
			RARRAY(MFLAG)::LINRFA = GETRFA(SMG_WINDOW::CHAN)

		!
		! Load in current record, locked
		!
		CASE 4%
27200			GET #SMG_WINDOW::CHAN, RFA RARRAY(MFLAG)::LINRFA

		!
		! Load in current record, unlocked
		!
		CASE 5%
			GET #SMG_WINDOW::CHAN, RFA RARRAY(MFLAG)::LINRFA, &
				REGARDLESS

		!
		! Change the current record's key to match header.  The
		! new key is probibly passes through MVALUE, unless some
		! other means is devised.
		!
		CASE 6%
			TV_COPY_SPOTS::FRMNUM = MID(MVALUE, 2%, 8%)
			TV_COPY_SPOTS::SEQNUM = MID(MVALUE, 10%, 2%)

		END SELECT
	END SELECT

28000	EXIT FUNCTION

	%PAGE

29000	!*******************************************************************
	! Trap errors
	!*******************************************************************

	!
	! Trap errors
	!
	SELECT ERL
		!
		! Unable to open Read/Write
		!
		CASE 750%
			RESUME 760 IF ERR = 10%
			TV_MAIN_COPYL = ERR
			RESUME 770

		!
		! Unable to open Read/Only
		!
		CASE 760%
			TV_MAIN_COPYL = ERR
			RESUME 770

		!
		! Unable to reset
		!
		CASE 790%
			RESUME 32767

		!
		! Error while loading array
		!
		CASE 27110%
			!
			! Nothing to load
			!
			RESUME 28000

		CASE 27120%
			!
			! Read all records
			!
			RESUME 28000 IF ERR = 11%

	END SELECT

	ON ERROR GO BACK

32767	END FUNCTION
