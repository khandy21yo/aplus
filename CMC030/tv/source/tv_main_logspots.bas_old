1	%TITLE "TV_MAIN_LOGSPOTS - Logspots Journal File Maintenance"
	%SBTTL "TV system"
	%IDENT "V3.6a Calico"

	FUNCTION LONG TV_MAIN_LOGSPOTS(CDD_WINDOW_CDD SMG_WINDOW, &
		LONG MOPTION, LONG MLOOP, LONG MFLAG, STRING MVALUE)

	!
	! COPYRIGHT (C) 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	!
	! Abstract:
	!
	!	This program maintains the logspots file as a journal.
	!
	! Index:
	!
	!
	! Environment:
	!
	!	VAX/VMS BASIC+2 V4.4
	!
	! Compile:
	!
	!	$ BAS TV_SOURCE:TV_MAIN_LOGSPOTS/LINE
	!	$ LIB/REP FUNC_LIB:CMCFUN TV_MAIN_LOGSPOTS
	!	$ DELETE TV_MAIN_LOGSPOTS.OBJ;*
	!
	! Author:
	!
	!	02/02/88 - Kevin Handy
	!
	! Modification history:
	!
	!	05/13/88 - Aaron Redd
	!		Modified to allow R/O open of file if R/W open fails.
	!
	!	08/08/88 - Kevin Handy
	!		Added sequence number field.
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	08/22/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--
	%PAGE

	!
	! Set up compiling options
	!
	OPTION SIZE = (REAL GFLOAT, INTEGER LONG)

	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "FUNC_INCLUDE:TV_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:MAIN_WINDOW.COM"

	%INCLUDE "SOURCE:[CDD.OPEN]CDD_WINDOW.HB"

	%INCLUDE "SOURCE:[TV.OPEN]TV_LOGSPOTS.HB"
	MAP (TV_LOGSPOTS)		TV_LOGSPOTS_CDD	TV_LOGSPOTS
	MAP (TV_LOGSPOTS_OLD)		TV_LOGSPOTS_CDD	TV_LOGSPOTS_OLD, TV_LOGSPOTS2

	%INCLUDE "SOURCE:[TV.OPEN]TV_LOG_BREAK.HB"
	MAP (TV_LOG_BREAK)		TV_LOG_BREAK_CDD	TV_LOG_BREAK

	%INCLUDE "SOURCE:[AR.OPEN]AR_CUSTOM.HB"
	MAP (AR_CUSTOM)			AR_CUSTOM_CDD		AR_CUSTOM

	%INCLUDE "SOURCE:[TV.OPEN]TV_SPOTS_FLAG.HB"
	MAP (TV_SPOTS_FLAG)		TV_SPOTS_FLAG_CDD	TV_SPOTS_FLAG

	%INCLUDE "SOURCE:[TV.OPEN]TV_LOG_CLASS.HB"
	MAP (TV_LOG_CLASS)		TV_LOG_CLASS_CDD	TV_LOG_CLASS

	!
	! Common Areas
	!
	! These areas store information that is re-used between
	! calls to these functions.
	!
	COM (CH_TV_LOGSPOTS) &
		TV_LOGSPOTS.CH%, &
		TV_LOGSPOTS.READONLY%

	!
	! Create array to contain pointers and totals
	!
	RECORD RARRAY_RECORD
		RFA	LINRFA		! Rfa pointer for record
	END RECORD

	MAP (TT_TV_LOGSPOTS) RARRAY_RECORD RARRAY(300%)	! Allocate for 300

	COM (TT_TV_SPOT) &
		SPOTTITLE$ = 20%, &
		SPOT$(3%) = 20%

	COM (TT_TV_schtype) &
		SCHTYPETITLE$ = 20%, &
		SCHTYPE$(3%) = 20%

	MAP (SCHED_DATE) SCHED_DATE$ = 8%

	!
	! External functions
	!
	EXTERNAL STRING FUNCTION READ_SET

	EXTERNAL LONG    FUNCTION MAIN_WINDOW

	%PAGE

	ON ERROR GOTO 29000

	SELECT MOPTION

	!
	! Initialization
	!
	! This option is used to initialize the window structure,
	! set up the default values for add, and open all files
	! necessary that have not already been opened.
	!
	CASE OPT_INIT

		!
		! Define window
		!
		SMG_WINDOW::DESCR = "Spots"
		SMG_WINDOW::NHELP = "TV_MAIN_LOGSPOTS"
		SMG_WINDOW::HSIZE = 78%
		SMG_WINDOW::VSIZE = 12%
		SMG_WINDOW::HPOS  = 2%
		SMG_WINDOW::VPOS  = 8%
		SMG_WINDOW::NITEMS= 11%
		SMG_WINDOW::FLAGS = 0%

		SMG_WINDOW::TOPLIN = 3%
		SMG_WINDOW::BOTLIN = 12%
		SMG_WINDOW::LINREC = 1%

		SPOTTITLE$ = "Flag  Description"
		SPOT$(0%) = "3"
		SPOT$(1%) = "S - Schedule "
		SPOT$(2%) = "N - Not Run  "
		SPOT$(3%) = "R - Run      "

		SCHTYPETITLE$ = "Flag  Description"
		SCHTYPE$(0%) = "3"
		SCHTYPE$(1%) = "0 - Commercial"
		SCHTYPE$(2%) = "1 - Fill "
		SCHTYPE$(3%) = "2 - In-break"

		!
		! Load in defaults
		!
		CALL READ_DEFAULTS(SMG_WINDOW) &
			IF INSTR(1%, " QV", MVALUE) <= 1%

700		!
		! Declare channels
		!
		IF TV_LOGSPOTS.CH% > 0%
		THEN
			!
			! Already open, set flag to read-only if was
			! that way from last time.
			!
			SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%) &
				IF TV_LOGSPOTS.READONLY%
			GOTO 790
		END IF

		!
		! Open main file (existing) for modification
		!
750		%INCLUDE "SOURCE:[TV.OPEN]TV_LOGSPOTS.CRE"
		TV_LOGSPOTS.READONLY% = 0%
		GOTO 790

760		!
		! If unable to open for modify, try to open with read
		! access only.
		!
		%INCLUDE "SOURCE:[TV.OPEN]TV_LOGSPOTS.OPN"
		SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%)
		TV_LOGSPOTS.READONLY% = -1%

		GOTO 790

770		!
		! File not open, so reset channel
		!
		CALL ASSG_FREECHANNEL(TV_LOGSPOTS.CH%)

		EXIT FUNCTION

790		SMG_WINDOW::CHAN  = TV_LOGSPOTS.CH%
		RESET #TV_LOGSPOTS.CH%
		GET #TV_LOGSPOTS.CH%, REGARDLESS

	!
	! Display the background
	!
	! This option is used to display the background information on the
	! screen.  It must first clear any junk on the screen, and then
	! write the background onto it.
	!
20100	CASE OPT_BACKGROUND

		SMG_STATUS% = SMG$BEGIN_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

		SMG_STATUS% = SMG$ERASE_DISPLAY(SMG_WINDOW::WNUMBER)

		SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
			"  (01)       (02)     (03) (04) (05) (06) " + &
			"  (07)     (08)   (09)  (10)    (11)", &
			1%, 1%, , SMG$M_REVERSE)
		SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
			"  Customer   Order    Line Type Flag Code " + &
			" Length    Rate   Class Descr   Seq ", &
			2%, 1%, , SMG$M_REVERSE)

		SMG_STATUS% = SMG$END_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

	!
	! Extra display stuff
	!
	CASE OPT_DISPLAY

		SMG_STATUS% = SMG$BEGIN_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

		!
		! Display totals
		!
 !		SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
 !			"Number of lines" + &
 !			FORMAT$(SMG_WINDOW::TOTREC, "###") + SPACE$(59%), &
 !			SMG_WINDOW::VSIZE, 1%, , SMG$M_REVERSE)

		!
		! Paint lines on screen
		!
		FOR I% = 1% TO 10%

			A% = VAL%(MID("013,022,027,032,037,042,051,060,066,074", I%*4%-3%, 3%))

			SMG_STATUS% = SMG$DRAW_LINE(SMG_WINDOW::WNUMBER, &
				1%, A%, SMG_WINDOW::BOTLIN, A%)

		NEXT I%

		SMG_STATUS% = SMG$END_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

	!
	! Enter/Display/Default
	!
	! This option is used to enter the data from the user, display data,
	! set defaults, and return the data back according to MFLAG.
	!
20200	CASE OPT_ENTRY

		TEMP$, TEMP1$ = TRM$(SCOPE::PRG_ITEM)

		SCOPE::PRG_ITEM = "FLD" + FORMAT$(MLOOP, "<0>##")

		XLINE$ = NUM1$(SMG_WINDOW::CURLIN)

 E0Loop:	SCOPE::SCOPE_EXIT = 0%

		SELECT MLOOP

		CASE 1%
			TV_LOGSPOTS::CUSNUM = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";3", TEMP$, &
				TV_LOGSPOTS::CUSNUM, &
				MFLAG, "'E", MVALUE)

			SELECT SCOPE::SCOPE_EXIT
			CASE SMG$K_TRM_F14
				TV_LOGSPOTS::CUSNUM = AR_CUSTOM::CUSNUM &
					IF MAIN_WINDOW(TV_MAIN_CUSTOM.ID, "VX") = 1%
				GOTO E0Loop

			CASE SMG$K_TRM_F17
				ST% = MAIN_WINDOW( TV_MAIN_CUSTOM.ID, &
					"M0"+TV_LOGSPOTS::CUSNUM )
				GOTO E0Loop

			END SELECT

		CASE 2%
			TV_LOGSPOTS::FRMNUM = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";14", TEMP$, &
				TV_LOGSPOTS::FRMNUM, &
				MFLAG, "'E", MVALUE)

		CASE 3%
			TV_LOGSPOTS::SKEDNUM = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";24", TEMP$, &
				TV_LOGSPOTS::SKEDNUM, MFLAG, "'E", MVALUE)

		CASE 4%
			TV_LOGSPOTS::SCH_TYPE = ENTR_3STRINGLIST(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";29", TEMP$, &
				TV_LOGSPOTS::SCH_TYPE, MFLAG, &
				"'", MVALUE, &
				SCHTYPE$(), SCHTYPETITLE$, "005" )

		CASE 5%
			TV_LOGSPOTS::SPOTS_FLAG = ENTR_3STRINGLIST(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";34", TEMP$, &
				TV_LOGSPOTS::SPOTS_FLAG, MFLAG, "'", MVALUE, &
				SPOT$(),SPOTTITLE$, "005" )

		CASE 6%
			TV_LOGSPOTS::SPOTS_CODE = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";39", TEMP$, &
				TV_LOGSPOTS::SPOTS_CODE, MFLAG, "'E", MVALUE)

			IF SCOPE::SCOPE_EXIT = SMG$K_TRM_F14
			THEN
				TV_LOGSPOTS::SPOTS_CODE = TV_SPOTS_FLAG::CODE &
					IF MAIN_WINDOW(TV_MAIN_SPOTS_FLAG.ID, "VX" + TV_LOGSPOTS::SPOTS_FLAG) = 1%
				GOTO E0Loop
			END IF

		CASE 7%
			TV_LOGSPOTS::LENGTH = ENTR_3TIME(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";43", TEMP$, &
				TV_LOGSPOTS::LENGTH, MFLAG, "'E", MVALUE)

		CASE 8%
			TV_LOGSPOTS::RATE = ENTR_3NUMBER(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";52", TEMP$, &
				TV_LOGSPOTS::RATE, MFLAG, "#####.##", MVALUE )

		CASE 9%
			TV_LOGSPOTS::CLASS = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";61", TEMP$, &
				TV_LOGSPOTS::CLASS, MFLAG, "'E", MVALUE )

			IF SCOPE::SCOPE_EXIT = SMG$K_TRM_F14
			THEN
				TV_LOGSPOTS::CLASS = TV_LOG_CLASS::CLASS &
					IF MAIN_WINDOW(TV_MAIN_LOG_CLASS.ID, "V0  ") = 1%
				GOTO E0Loop
			END IF

		CASE 10%
			TV_LOGSPOTS::DESCR = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";67", TEMP$, &
				TV_LOGSPOTS::DESCR, MFLAG, "'LLLLLL", MVALUE )

		CASE 11%
			TV_LOGSPOTS::SEQNUM = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				XLINE$ + ";76", TEMP$, &
				TV_LOGSPOTS::SEQNUM, MFLAG, "'L", MVALUE )

		END SELECT

		SCOPE::PRG_ITEM = TEMP1$

	!
	! Test values
	!
20300	CASE OPT_TESTENTRY
		TV_MAIN_LOGSPOTS = 0%

		SELECT MLOOP

		CASE 1%
			! Is the input defined?
			!
			DISPLAY_DESCR$ = STRING$(40%,63%)
			IF MAIN_WINDOW(TV_MAIN_CUSTOM.ID,"Q0"+TV_LOGSPOTS::CUSNUM) <> 1%
			THEN
				!
				! See if they are allowing undefined inputs
				!
				V$ = LEFT(READ_SET(TRM$(SCOPE::PRG_PROGRAM), "CUSNUM"), 1%)
				V$ = LEFT(READ_SET("TV_ALLOW", "CUSNUM"), 1%) &
					IF V$ = ""

				IF V$ = "N"
				THEN
					!
					! Don't let them get past is we don't allow
					! them to enter undefined values.
					!
					TV_MAIN_LOGSPOTS = 1%
					CALL ENTR_3MESSAGE(SCOPE, &
						"Input undefined ", 0%)
				ELSE
					!
					! Verify that they really want to enter an
					! undefined value.
					!
					V$ = ENTR_3YESNO(SCOPE, &
						SMG_WINDOW::WNUMBER, "", &
						"Input undefined, enter anyway", &
						"N", 0%, "", "")
					TV_MAIN_LOGSPOTS = 1% IF V$ = "N"
				END IF
			ELSE
				DISPLAY_DESCR$= LEFT( AR_CUSTOM::CUSNAM, 40% )
			END IF

 !			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
 !				DISPLAY_DESCR$,'1'L,'36'L, ,SMG$M_BOLD)


		CASE 6%
			! Is the input defined?
			!
			DISPLAY_DESCR$ = STRING$(30%,63%)
			IF MAIN_WINDOW(TV_MAIN_SPOTS_FLAG.ID,"Q0"+TV_LOGSPOTS::SPOTS_FLAG+ &
				TV_LOGSPOTS::SPOTS_CODE) <> 1%
			THEN
				!
				! See if they are allowing undefined inputs
				!
				V$ = LEFT(READ_SET(TRM$(SCOPE::PRG_PROGRAM), "SPOCDE"), 1%)
				V$ = LEFT(READ_SET("TV_ALLOW", "SPOCDE"), 1%) &
					IF V$ = ""

				IF V$ = "N"
				THEN
					!
					! Don't let them get past is we don't allow
					! them to enter undefined values.
					!
					TV_MAIN_LOGSPOTS = 1%
					CALL ENTR_3MESSAGE(SCOPE, &
						"Input undefined ", 0%)
				ELSE
					!
					! Verify that they really want to enter an
					! undefined value.
					!
					V$ = ENTR_3YESNO(SCOPE, &
						SMG_WINDOW::WNUMBER, "", &
						"Input undefined, enter anyway", &
						"N", 0%, "", "")
					TV_MAIN_LOGSPOTS = 1% IF V$ = "N"
				END IF
			ELSE
				DISPLAY_DESCR$= TV_SPOTS_FLAG::DESCR
			END IF

 !			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
 !				DISPLAY_DESCR$,'9'L,'36'L, ,SMG$M_BOLD)

		CASE 9%
			! Is the input defined?
			!
			DISPLAY_DESCR$ = STRING$(40%,63%)
			IF MAIN_WINDOW(TV_MAIN_LOG_CLASS.ID,"Q0"+TV_LOGSPOTS::CLASS) <> 1%
			THEN
				!
				! See if they are allowing undefined inputs
				!
				V$ = LEFT(READ_SET(TRM$(SCOPE::PRG_PROGRAM), "LOGCLA"), 1%)
				V$ = LEFT(READ_SET("TV_ALLOW", "LOGCLA"), 1%) &
					IF V$ = ""

				IF V$ = "N"
				THEN
					!
					! Don't let them get past is we don't allow
					! them to enter undefined values.
					!
					TV_MAIN_LOGSPOTS = 1%
					CALL ENTR_3MESSAGE(SCOPE, &
						"Input undefined ", 0%)
				ELSE
					!
					! Verify that they really want to enter an
					! undefined value.
					!
					V$ = ENTR_3YESNO(SCOPE, &
						SMG_WINDOW::WNUMBER, "", &
						"Input undefined, enter anyway", &
						"N", 0%, "", "")
					TV_MAIN_LOGSPOTS = 1% IF V$ = "N"
				END IF
			ELSE
				DISPLAY_DESCR$= TV_LOG_CLASS::DESCR
			END IF

 !			SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
 !				DISPLAY_DESCR$, 18%, 36%, ,SMG$M_BOLD)



		END SELECT

	!
	! Set TV_LOGSPOTS_OLD value
	!
20500	CASE OPT_SETOLD
		TV_LOGSPOTS_OLD = TV_LOGSPOTS

	!
	! Restore TV_LOGSPOTS_OLD value
	!
	CASE OPT_RESETOLD
		TV_LOGSPOTS = TV_LOGSPOTS_OLD

	!
	! Set default value
	!
	CASE OPT_SETDEFAULT
		TV_LOGSPOTS2 = TV_LOGSPOTS

	!
	! Restore default value
	!
	CASE OPT_RESETDEFAULT
		TV_LOGSPOTS = TV_LOGSPOTS2

		!
		! Set special default values for the key of a new record
		! which is the most likely case when this function is to
		! be called.
		!
		TV_LOGSPOTS::SCH_DATE = TV_LOG_BREAK::DATE
		TV_LOGSPOTS::SCH_TIME = TV_LOG_BREAK::SCH_TIME
		TV_LOGSPOTS::AGENCY_NUM = ""
		TV_LOGSPOTS::CARTNUM = ""
		TV_LOGSPOTS::CUTNUM = ""
		TV_LOGSPOTS::FROM_TIME_SLOT = ""
		TV_LOGSPOTS::TO_TIME_SLOT = ""
		TV_LOGSPOTS::INVNUM = ""
		TV_LOGSPOTS::INVDAT = ""
		TV_LOGSPOTS::POSTDATE = ""
		TV_LOGSPOTS::RUN_TIME = TV_LOGSPOTS::SCH_TIME
		TV_LOGSPOTS::CONFLICT = ""

	!
	! Find
	!
	CASE OPT_FIND
		SELECT MLOOP

		CASE 0%
			FIND #TV_LOGSPOTS.CH%, &
				KEY #1% GE TV_LOGSPOTS::SCH_TIME, &
				REGARDLESS
		END SELECT

	!
	! Handle array of records
	!
27000	CASE OPT_ARRAY

		!
		! Select sub-option of array
		!
		SELECT MLOOP

		!
		! Load array with line items
		!
		CASE 1%

			!
			! Empty array
			!
			SMG_WINDOW::TOTREC = 0%

27110			!
			! Search for first record
			!
			FIND #SMG_WINDOW::CHAN, &
				KEY #1 GE TV_LOG_BREAK::SCH_TIME, &
				REGARDLESS

27120			!
			! Get a record
			!
			GET #SMG_WINDOW::CHAN

			IF (TV_LOGSPOTS::SCH_TIME = TV_LOG_BREAK::SCH_TIME)
			THEN
				!
				! Add information to array
				!
				SMG_WINDOW::TOTREC = SMG_WINDOW::TOTREC + 1%
				RARRAY(SMG_WINDOW::TOTREC)::LINRFA = &
					GETRFA(SMG_WINDOW::CHAN)
				GOTO 27120
			END IF

		!
		! Remove one element of the array
		!
		CASE 2%
			!
			! Remove item pointed to by Mflag
			!
			FOR I% = MFLAG TO SMG_WINDOW::TOTREC - 1%
				RARRAY(I%) = RARRAY(I% + 1%)
			NEXT I%

		!
		! Set array item to current record
		!
		CASE 3%
			RARRAY(MFLAG)::LINRFA = GETRFA(SMG_WINDOW::CHAN)

		!
		! Load in current record, locked
		!
		CASE 4%
27200			GET #SMG_WINDOW::CHAN, RFA RARRAY(MFLAG)::LINRFA

		!
		! Load in current record, unlocked
		!
		CASE 5%
			GET #SMG_WINDOW::CHAN, RFA RARRAY(MFLAG)::LINRFA, &
				REGARDLESS

		!
		! Change the current record's key to match header.  The
		! new key is probibly passes through MVALUE, unless some
		! other means is devised.
		!
		CASE 6%
			TV_LOGSPOTS::SCH_TIME = RIGHT(MVALUE, 2%)

		END SELECT
	END SELECT

28000	EXIT FUNCTION

	%PAGE

29000	!*******************************************************************
	! Trap errors
	!*******************************************************************

	SELECT ERL
	!
	! Unable to open Read/Write
	!
	CASE 750%
		RESUME 760 IF ERR = 10%
		TV_MAIN_LOGSPOTS = ERR
		RESUME 770

	!
	! Unable to open Read/Only
	!
	CASE 760%
		TV_MAIN_LOGSPOTS = ERR
		RESUME 770

	!
	! Unable to reset
	!
	CASE 790%
		RESUME 32767

	!
	! Record does not already exist
	!
	CASE 20300%
		RESUME 32767 IF ERR = 155%

	!
	! Error while loading array
	!
	CASE 27110%
		!
		! Nothing to load
		!
		RESUME 28000

	CASE 27120%
		!
		! Read all records
		!
		RESUME 28000 IF ERR = 11%

	END SELECT

	ON ERROR GO BACK

32767	END FUNCTION
