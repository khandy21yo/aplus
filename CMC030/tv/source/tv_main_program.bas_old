1	%TITLE "TV_MAIN_PROGRAM - TV Traffic Program Maintenance"
	%SBTTL "TV system"
	%IDENT "V3.6a Calico"

	FUNCTION LONG TV_MAIN_PROGRAM(CDD_WINDOW_CDD SMG_WINDOW, &
		LONG MOPTION, LONG MLOOP, LONG MFLAG, STRING MVALUE)

	!
	! COPYRIGHT (C) 1987, 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	!
	! Abstract:
	!
	!	This program maintains the TV Traffic Program file.
	!
	! Index:
	!
	!	PROGRAM
	!
	! Input:
	!
	!
	! Output:
	!
	!
	! Example:
	!
	!
	! Environment:
	!
	!	VAX/VMS BASIC+2 V4.4
	!
	! Compile:
	!
	!	$ BAS TV_SOURCE:TV_MAIN_PROGRAM/LINE
	!	$ LIB/REP FUNC_LIB:CMCFUN TV_MAIN_PROGRAM
	!	$ DELETE TV_MAIN_PROGRAM.OBJ;*
	!
	! Author:
	!
	!	11/11/87 - B. Craig Larsen
	!
	! Modification history:
	!
	!	11/20/87 - Kevin Handy
	!		Major Layout change.
	!
	!	02/13/88 - Craig Larson and Kevin Handy
	!		Automatic setting of program number.
	!
	!	05/13/88 - Aaron Redd
	!		Modified to allow R/O open of file if R/W open fails.
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	08/22/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--
	%PAGE

	!
	! Set up compiling options
	!
	OPTION SIZE = (REAL GFLOAT, INTEGER LONG)

	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "FUNC_INCLUDE:TV_WINDOW.INC"
	%INCLUDE "FUNC_INCLUDE:MAIN_WINDOW.COM"

 !	%INCLUDE %FROM %CDD "CDD$TOP.CDD.CDD_WINDOW"
	%INCLUDE "SOURCE:[CDD.OPEN]CDD_WINDOW.HB"

 !	%INCLUDE %FROM %CDD "CDD$TOP.TV.TV_LOG_TYPE"
	%INCLUDE "SOURCE:[TV.OPEN]TV_LOG_TYPE.HB"
	MAP (TV_LOG_TYPE)	TV_LOG_TYPE_CDD		TV_LOG_TYPE

 !	%INCLUDE %FROM %CDD "CDD$TOP.TV.TV_LOG_SOURCE"
	%INCLUDE "SOURCE:[TV.OPEN]TV_LOG_SOURCE.HB"
	MAP (TV_LOG_SOURCE)	TV_LOG_SOURCE_CDD	TV_LOG_SOURCE

 !	%INCLUDE %FROM %CDD "CDD$TOP.TV.TV_PROGRAM"
	%INCLUDE "SOURCE:[TV.OPEN]TV_PROGRAM.HB"
	MAP (TV_PROGRAM)	TV_PROGRAM_CDD		TV_PROGRAM
	MAP (FOO_BAR_BLAH1)	TV_PROGRAM_CDD		TV_PROGRAM_OLD, TV_PROGRAM2

 !	%INCLUDE %FROM %CDD "CDD$TOP.TV.TV_CONTROL"
	%INCLUDE "SOURCE:[TV.OPEN]TV_CONTROL.HB"
	MAP (TV_CONTROL)	TV_CONTROL_CDD		TV_CONTROL

	!
	! Common Areas
	!
	! These areas store information that is re-used between
	! calls to these functions.
	!
	COM (CH_TV_PROGRAM) &
		TV_PROGRAM.CH%, &
		TV_PROGRAM.READONLY%, &
		TV_CONTROL.CH%

	!
	! External functions
	!
	EXTERNAL STRING FUNCTION READ_SET

	EXTERNAL STRING	FUNCTION PRNT_DATE
	EXTERNAL STRING	FUNCTION PRNT_TIME

	EXTERNAL LONG   FUNCTION MAIN_WINDOW
	EXTERNAL LONG   FUNCTION MAIN_JOURNAL

	!
	! Declare variables
	!
	DECLARE RFA TEMP_RFA

	%PAGE

	ON ERROR GOTO 29000

	SELECT MOPTION

	!
	! Initialization
	!
	! This option is used to initialize the window structure,
	! set up the default values for add, and open all files
	! necessary that have not already been opened.
	!
	CASE OPT_INIT

		!
		! Define window
		!
		SMG_WINDOW::DESCR = "TV Traffic Program Maintenance"
		SMG_WINDOW::NHELP = "TV_MAST_PROGRAM"
		SMG_WINDOW::HSIZE = 78%
		SMG_WINDOW::VSIZE = 18%
		SMG_WINDOW::HVIEW = 130%
		SMG_WINDOW::VVIEW = 18%
		SMG_WINDOW::HPOS  = 2%
		SMG_WINDOW::VPOS  = 2%
		SMG_WINDOW::NITEMS= 15%
		SMG_WINDOW::FLAGS = 0%

		SMG_WINDOW::NKEYS = 2%
		SMG_WINDOW::KNAME(0%) = "Program_number"
			SMG_WINDOW::KFIELD(0%, 0%) = 1%
			SMG_WINDOW::KFIELD(0%, 1%) = -1%
		SMG_WINDOW::KNAME(1%) = "Title"
			SMG_WINDOW::KFIELD(1%, 0%) = 1%
			SMG_WINDOW::KFIELD(1%, 1%) = 10%

		!
		! Load in defaults
		!
		CALL READ_DEFAULTS(SMG_WINDOW) &
			IF INSTR(1%, " QV", MVALUE) <= 1%

700		!
		! Declare channels
		!
		IF TV_PROGRAM.CH% > 0%
		THEN
			!
			! Already open, set flag to read-only if was
			! that way from last time.
			!
			SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%) &
				IF TV_PROGRAM.READONLY%
			GOTO 790
		END IF

		!
		! Open main file (existing) for modification
		!
750		%INCLUDE "SOURCE:[TV.OPEN]TV_PROGRAM.CRE"
		TV_PROGRAM.READONLY% = 0%
		GOTO 790

760		!
		! If unable to open for modify, try to open with read
		! access only.
		!
		%INCLUDE "SOURCE:[TV.OPEN]TV_PROGRAM.OPN"
		SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%)
		TV_PROGRAM.READONLY% = -1%

		GOTO 790

770		!
		! File not open, so reset channel
		!
		CALL ASSG_FREECHANNEL(TV_PROGRAM.CH%)

		EXIT FUNCTION

790		SMG_WINDOW::CHAN  = TV_PROGRAM.CH%
		RESET #TV_PROGRAM.CH%
		GET #TV_PROGRAM.CH%, REGARDLESS

	!
	! Modify the menu
	!
	CASE OPT_OPTLIST
		MVALUE = MVALUE + " breaKs"

	!
	! Optional menu items
	!
	CASE OPT_MOREMENU
		SELECT SCOPE::PRG_ITEM

		!
		! Line option
		!
		CASE "breaKs"
			!
			! Make sure there is a header
			!
			TEMP_RFA = GETRFA(TV_PROGRAM.CH%)

			TV_MAIN_PROGRAM = MAIN_JOURNAL(TV_MAIN_BREAK.ID, "")

		END SELECT

	!
	! Display the background
	!
	! This option is used to display the background information on the
	! screen.  It must first clear any junk on the screen, and then
	! write the background onto it.
	!
20100	CASE OPT_BACKGROUND

		SMG_STATUS% = SMG$BEGIN_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

		SMG_STATUS% = SMG$ERASE_DISPLAY(SMG_WINDOW::WNUMBER)

		DATA	1,  4, "Program Number", &
			3,  4, "(01) Start Monday", &
			4,  4, "(02)       Tuesday", &
			5,  4, "(03)       Wednesday", &
			6,  4, "(04)       Thursday", &
			7,  4, "(05)       Friday", &
			8,  4, "(06)       Saturday", &
			9,  4, "(07)       Sunday", &
			11,  4, "(08) From Date", &
			12,  4, "(09) To   Date", &
			13,  4, "(10) Title", &
			14,  4, "(11) Source", &
			15,  4, "(12) Type", &
			16,  4, "(13) Length", &
			17,  4, "(14) Comment", &
			18,  4, "(15) Cutaway", &
			0,  0, ""

		RESTORE

		READ XPOS%, YPOS%, XSTR$

		WHILE (XPOS% <> 0)
			SMG_STATUS% = SMG$PUT_CHARS( &
				SMG_WINDOW::WNUMBER,XSTR$, XPOS%, YPOS%)
			READ XPOS%, YPOS%, XSTR$
		NEXT

		SMG_STATUS% = SMG$END_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

	!
	! Enter/Display/Default
	!
	! This option is used to enter the data from the user, display data,
	! set defaults, and return the data back according to MFLAG.
	!
20200	CASE OPT_ENTRY

		TEMP$, TEMP1$ = TRM$(SCOPE::PRG_ITEM)
		TEMP$ = "View starting at" IF TEMP$ = "View"

		IF MLOOP% >= 0%
		THEN
			SCOPE::PRG_ITEM = "FLD" + FORMAT$(MLOOP, "<0>##")
		ELSE
			SCOPE::PRG_ITEM = "FLD-" + FORMAT$(-MLOOP, "<0>#")
		END IF

 Eloop:		SCOPE::SCOPE_EXIT = 0%

		SELECT MLOOP

		CASE -1%
			TV_PROGRAM::PRGNUM = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, "1;27", TEMP$, &
				TV_PROGRAM::PRGNUM, MFLAG, "'E", MVALUE )

		CASE 1% TO 7%
			TV_PROGRAM::START_TIME(MLOOP - 1%) = ENTR_3TIME(SCOPE, &
				SMG_WINDOW::WNUMBER, &
				NUM1$(MLOOP + 2%) + ";27", TEMP$, &
				TV_PROGRAM::START_TIME(MLOOP - 1%), &
				MFLAG, "H", MVALUE )

		CASE 8%
			TV_PROGRAM::FROM_DATE = ENTR_3DATE(SCOPE, &
				SMG_WINDOW::WNUMBER, "11;27", TEMP$, &
				TV_PROGRAM::FROM_DATE, MFLAG, "'E", MVALUE )

		CASE 9%
			TV_PROGRAM::TO_DATE = ENTR_3DATE(SCOPE, &
				SMG_WINDOW::WNUMBER, "12;27", TEMP$, &
				TV_PROGRAM::TO_DATE, MFLAG, "'E", MVALUE )

		CASE 10%
			TV_PROGRAM::TITLE = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, "13;27", TEMP$, &
				TV_PROGRAM::TITLE, MFLAG, "'E", MVALUE )

		CASE 11%
			TV_PROGRAM::SOURCE = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, "14;27", TEMP$, &
				TV_PROGRAM::SOURCE, MFLAG, "'E", MVALUE )

			IF SCOPE::SCOPE_EXIT = SMG$K_TRM_F14
			THEN
				TV_PROGRAM::SOURCE = TV_LOG_SOURCE::SOURCE &
					IF MAIN_WINDOW(TV_MAIN_LOG_SOURCE.ID, "V0  ") = 1%
				GOTO ELoop
			END IF

		CASE 12%
			TV_PROGRAM::PTYPE = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, "15;27", TEMP$, &
				TV_PROGRAM::PTYPE, MFLAG, "'E", MVALUE )

			IF SCOPE::SCOPE_EXIT = SMG$K_TRM_F14
			THEN
				TV_PROGRAM::PTYPE = TV_LOG_TYPE::LTYPE &
					IF MAIN_WINDOW(TV_MAIN_LOG_TYPE.ID, "V0  ") = 1%
				GOTO ELoop
			END IF

		CASE 13%
			TV_PROGRAM::LENGTH = ENTR_3TIME(SCOPE, &
				SMG_WINDOW::WNUMBER, "16;27", TEMP$, &
				TV_PROGRAM::LENGTH, MFLAG, "H", MVALUE )

		CASE 14%
			TV_PROGRAM::COMMENT = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, "17;27", TEMP$, &
				TV_PROGRAM::COMMENT, MFLAG, "'E", MVALUE )

		CASE 15%
			TV_PROGRAM::CUTAWAY = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, "18;27", TEMP$, &
				TV_PROGRAM::CUTAWAY, MFLAG, "'E", MVALUE )

		END SELECT

		SCOPE::PRG_ITEM = TEMP1$

	!
	! Test values
	!
20300	CASE OPT_TESTENTRY

		TV_MAIN_PROGRAM = 0%

		SELECT MLOOP

		CASE 11%
			IF TV_PROGRAM::SOURCE = ""
			THEN
				TV_MAIN_PROGRAM = 1%
			ELSE
				!
				! Is the input defined?
				!
				DISPLAY_DESCR$ = STRING$(40%,63%)
				IF MAIN_WINDOW(TV_MAIN_LOG_SOURCE.ID,"Q0"+TV_PROGRAM::SOURCE) <> 1%
				THEN
					!
					! See if they are allowing undefined inputs
					!
					V$ = LEFT(READ_SET(TRM$(SCOPE::PRG_PROGRAM), "CODE"), 1%)
					V$ = LEFT(READ_SET("TV_ALLOW", "CODE"), 1%) &
						IF V$ = ""

					IF V$ = "N"
					THEN
						!
						! Don't let them get past is we don't
						! allow them to enter undefined values.
						!
						TV_MAIN_PROGRAM = 1%
						CALL ENTR_3MESSAGE(SCOPE, &
							"Program source is undefined ", 1%)
					ELSE
						!
						! Verify that they really want to
						! enter an undefined value.
						!
						V$ = ENTR_3YESNO(SCOPE, SMG_WINDOW::WNUMBER, &
							"", "Source code is " + &
							"undefined, confirm entry " + &
							"then press <DO> ", &
							"N", 0%, "", "")
						TV_MAIN_PROGRAM = 1% IF V$ = "N"
					END IF
				ELSE
					DISPLAY_DESCR$= TV_LOG_SOURCE::DESCR

				SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
					DISPLAY_DESCR$,14%,32%, ,SMG$M_BOLD)

				END IF
			END IF

		CASE 12%
			IF TV_PROGRAM::PTYPE = ""
			THEN
				TV_MAIN_PROGRAM = 1%
			ELSE
				!
				! Is the input defined?
				!
				DISPLAY_DESCR$ = STRING$(40%,63%)
				IF MAIN_WINDOW(TV_MAIN_LOG_TYPE.ID,"Q0"+TV_PROGRAM::PTYPE) <> 1%
				THEN
					!
					! See if they are allowing undefined inputs
					!
					V$ = LEFT(READ_SET(TRM$(SCOPE::PRG_PROGRAM), "CODE"), 1%)
					V$ = LEFT(READ_SET("TV_ALLOW", "CODE"), 1%) &
						IF V$ = ""

					IF V$ = "N"
					THEN
						!
						! Don't let them get past is we don't
						! allow them to enter undefined values.
						!
						TV_MAIN_PROGRAM = 1%
						CALL ENTR_3MESSAGE(SCOPE, "Program type is " + &
							"undefined ", 1%)
					ELSE
						!
						! Verify that they really want to
						! enter an undefined value.
						!
						V$ = ENTR_3YESNO(SCOPE, SMG_WINDOW::WNUMBER, &
							"", "Type code is " + &
							"undefined, confirm entry " + &
							"then press <DO> ", &
							"N", 0%, "", "")
						TV_MAIN_PROGRAM = 1% IF V$ = "N"
				ELSE
					DISPLAY_DESCR$= TV_LOG_TYPE::DESCR

				SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
					DISPLAY_DESCR$,15%,32%, ,SMG$M_BOLD)

				END IF
			END IF

		END SELECT


	CASE OPT_DISPLAY

		SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
			TV_PROGRAM::PRGNUM,1%, 27%, ,SMG$M_BOLD)

		DISPLAY_DESCR$ = STRING$(40%,63%)
		DISPLAY_DESCR$ = TV_LOG_SOURCE::DESCR &
			IF MAIN_WINDOW(TV_MAIN_LOG_SOURCE.ID,"Q0"+TV_PROGRAM::SOURCE) = 1%
		!
		! Display name (ALSO IN TESTENTRY)
		!
		SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
			DISPLAY_DESCR$,14%,32%, ,SMG$M_BOLD)

		DISPLAY_DESCR$ = STRING$(40%,63%)
		DISPLAY_DESCR$ = TV_LOG_TYPE::DESCR &
			IF MAIN_WINDOW(TV_MAIN_LOG_TYPE.ID,"Q0"+TV_PROGRAM::PTYPE) = 1%
		!
		! Display name (ALSO IN TESTENTRY)
		!
		SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
			DISPLAY_DESCR$,15%,32%, ,SMG$M_BOLD)

	!
	! Set TV_PROGRAM_OLD value
	!
20500	CASE OPT_SETOLD

		TV_PROGRAM_OLD = TV_PROGRAM

	!
	! Restore TV_PROGRAM_OLD value
	!
	CASE OPT_RESETOLD

		TV_PROGRAM = TV_PROGRAM_OLD

	!
	! Set default value
	!
	CASE OPT_SETDEFAULT

		TV_PROGRAM2 = TV_PROGRAM

	!
	! Restore default value
	!
	CASE OPT_RESETDEFAULT

		TV_PROGRAM = TV_PROGRAM2
		TV_PROGRAM::PRGNUM = ""

	!
	! View header
	!
	CASE OPT_VIEW

		SELECT MLOOP

		!
		! Title (One line only)
		!
		CASE 1%
			MVALUE = "  Program    " + &
				"Fr Date    To Date    " + &
				"Title                        Sour Type " + &
				"Length   Comment"

		!
		! Positions of lines
		!
		CASE 2%
			MVALUE = "013,024,035,064,069,074,083"

		!
		! Convert current record into text
		!
		CASE 3%
			MVALUE = TV_PROGRAM::PRGNUM + " " + &
				PRNT_DATE( TV_PROGRAM::FROM_DATE, 8% )+ " " + &
				PRNT_DATE( TV_PROGRAM::TO_DATE, 8% )+ " " + &
				LEFT$( TV_PROGRAM::TITLE, 28% ) + " " + &
				TV_PROGRAM::SOURCE + " " + &
				TV_PROGRAM::PTYPE + " " + &
				PRNT_TIME( TV_PROGRAM::LENGTH, 0% ) + " " + &
				LEFT$( TV_PROGRAM::COMMENT, 41% )

		END SELECT

	!
	! Find
	!
	CASE OPT_FIND
		SELECT MLOOP
		CASE 0%
			FIND #TV_PROGRAM.CH%, KEY#0 &
				GE TV_PROGRAM::PRGNUM, REGARDLESS

		CASE 1%
			FIND #TV_PROGRAM.CH%, KEY#1 &
				GE TV_PROGRAM::TITLE, REGARDLESS
		END SELECT

	!
	! Handle before doing option
	!
	CASE OPT_TESTOPT

		SELECT SCOPE::PRG_ITEM

		CASE "Add"
			GOSUB GetTrans

		END SELECT

	!
	! Handle finishing various options specially
	!
	CASE OPT_AFTEROPT

		SELECT SCOPE::PRG_ITEM

		!
		! Add records
		!
		CASE "Add"
			!
			! Add line items also
			!
			TV_MAIN_PROGRAM = MAIN_JOURNAL(TV_MAIN_BREAK.ID, "A")

		!
		! Change records
		!
		CASE "Change"
			!
			! Change line items to match new header
			! if the key was changed.
			!
			! The original record must be the one in the
			! MAP for this to be able to work.  The new
			! key is passed through the QUERY$ variable.
			!
			IF (TV_PROGRAM_OLD::PRGNUM <> TV_PROGRAM::PRGNUM)
			THEN
				TEMP$ = TV_PROGRAM::PRGNUM
				TV_PROGRAM = TV_PROGRAM_OLD
				TV_MAIN_PROGRAM = MAIN_JOURNAL(TV_MAIN_BREAK.ID, "C" + TEMP$)
			END IF

		!
		! Erase record
		!
		CASE "Erase"
			!
			! Erase any line items under the header
			!
			TV_MAIN_PROGRAM = MAIN_JOURNAL(TV_MAIN_BREAK.ID, "E")

		END SELECT

	END SELECT

 ExitFunction:
	EXIT FUNCTION

 GetTrans:
28000	!*******************************************************************
	! This subroutine will assign an PROGRAM number from the control
	! file TV_CONTROL.  It will make sure that the number it is trying
	! to assign does not already exist.
	!*******************************************************************

28020	!
	! Read in the control record
	!
	GET #TV_CONTROL.CH%, RECORD 1%

28060	!
	! We have a key to try now
	!
	TV_CONTROL::PROGNUM = FORMAT$(VAL%(TV_CONTROL::PROGNUM) + 1%, &
		"<0>#########")

	TV_PROGRAM::PRGNUM = TV_CONTROL::PROGNUM

	UPDATE #TV_CONTROL.CH%

28065	FIND #TV_PROGRAM.CH%, KEY #0 EQ TV_PROGRAM::PRGNUM

	GOTO 28020

28070	SMG_STATUS% = SMG$PUT_CHARS(SMG_WINDOW::WNUMBER, &
		TV_PROGRAM::PRGNUM,1%, 27%, ,SMG$M_BOLD)

	RETURN

	%PAGE

29000	!
	! Trap Errors
	!
	A% = ERR
	B% = ERL

	SELECT ERL

		!
		! Unable to open Read/Write
		!
		CASE 750%
			RESUME 760 IF ERR = 10%
			TV_MAIN_PROGRAM = ERR
			RESUME 770

		!
		! Unable to open Read/Only
		!
		CASE 760%
			TV_MAIN_PROGRAM = ERR
			RESUME 770

		!
		! Unable to reset
		!
		CASE 790%
			RESUME 32767

		!
		! No control file record
		!
		CASE 28020%
			TV_PROGRAM::PRGNUM = &
				FORMAT$(VAL%(TV_PROGRAM::PRGNUM) + 1%, &
				"<0>#########")
			RESUME 28065

		!
		! No record exists with that key yet, so use it.
		!
		CASE 28065%
			RESUME 28070
	END SELECT

	RESUME ExitFunction

32767	END FUNCTION
