1	%TITLE "TV_MAIN_SPOTS_FLAG- TV Traffic Spots Flag Table Maintenance"
	%SBTTL "TV system"
	%IDENT "V3.6a Calico"

	FUNCTION LONG TV_MAIN_SPOTS_FLAG(CDD_WINDOW_CDD SMG_WINDOW, &
		LONG MOPTION, LONG MLOOP, LONG MFLAG, STRING MVALUE)

	!
	! COPYRIGHT (C) 1987, 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	!
	! Abstract:
	!
	!	This program maintains the TV Traffic Spots Flag table.
	!
	! Index:
	!
	!	SPOTS_FLAG
	!	SPOTS_FLAG Table
	!
	! Input:
	!
	!
	! Output:
	!
	!
	! Example:
	!
	!
	! Environment:
	!
	!	VAX/VMS BASIC+2 V4.4
	!
	! Compile:
	!
	!	$ BAS TV_SOURCE:TV_MAIN_SPOTS_FLAG/LINE
	!	$ LIB/REP FUNC_LIB:CMCFUN TV_MAIN_SPOTS_FLAG
	!	$ DELETE TV_MAIN_SPOTS_FLAG.OBJ;*
	!
	! Author:
	!
	!	11/10/87 - B. Craig Larsen
	!
	! Modification history:
	!
	!	05/13/88 - Aaron Redd
	!		Modified to allow R/O open of file if R/W open fails.
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	08/22/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--
	%PAGE

	!
	! Set up compiling options
	!
	OPTION SIZE = (REAL GFLOAT, INTEGER LONG)

	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE


	%INCLUDE "FUNC_INCLUDE:MAIN_WINDOW.COM"

 !	%INCLUDE %FROM %CDD "CDD$TOP.CDD.CDD_WINDOW"
	%INCLUDE "SOURCE:[CDD.OPEN]CDD_WINDOW.HB"

 !	%INCLUDE %FROM %CDD "CDD$TOP.TV.TV_SPOTS_FLAG"
	%INCLUDE "SOURCE:[TV.OPEN]TV_SPOTS_FLAG.HB"
	MAP (TV_SPOTS_FLAG)	TV_SPOTS_FLAG_CDD	TV_SPOTS_FLAG
	MAP (FOO_BAR_BLAH1)	TV_SPOTS_FLAG_CDD	TV_SPOTS_FLAG_OLD, TV_SPOTS_FLAG2

	!
	! Common Areas
	!
	! These areas store information that is re-used between
	! calls to these functions.
	!
	COM (CH_TV_SPOTS_FLAG) &
		TV_SPOTS_FLAG.CH%, &
		TV_SPOTS_FLAG.READONLY%

	%PAGE

	ON ERROR GOTO 29000

	EC$(0%) = "4"
	EC$(1%) = "S - Schedule "
	EC$(2%) = "N - Not Run  "
	EC$(3%) = "R - Run      "

	SELECT MOPTION

	!
	! Initialization
	!
	! This option is used to initialize the window structure,
	! set up the default values for add, and open all files
	! necessary that have not already been opened.
	!
	CASE OPT_INIT

		!
		! Define window
		!
		SMG_WINDOW::DESCR = "TV Traffic Spots Flag Table Maintenance"
		SMG_WINDOW::NHELP = "TV_MAST_SPOTS_FLAG"
		SMG_WINDOW::HSIZE = 78%
		SMG_WINDOW::VSIZE = 18%
		SMG_WINDOW::HVIEW = 78%
		SMG_WINDOW::VVIEW = 18%
		SMG_WINDOW::HPOS  = 2%
		SMG_WINDOW::VPOS  = 2%
		SMG_WINDOW::NITEMS= 3%
		SMG_WINDOW::FLAGS = 0%

		SMG_WINDOW::NKEYS = 1%
		SMG_WINDOW::KNAME(0%) = "Spots_flag"
			SMG_WINDOW::KFIELD(0%, 0%) = 2%
			SMG_WINDOW::KFIELD(0%, 1%) = 1%
			SMG_WINDOW::KFIELD(0%, 2%) = 2%

		!
		! Load in defaults
		!
		CALL READ_DEFAULTS(SMG_WINDOW) &
			IF INSTR(1%, " QV", MVALUE) <= 1%

700		!
		! Declare channels
		!
		IF TV_SPOTS_FLAG.CH% > 0%
		THEN
			!
			! Already open, set flag to read-only if was
			! that way from last time.
			!
			SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%) &
				IF TV_SPOTS_FLAG.READONLY%
			GOTO 790
		END IF

		!
		! Open main file (existing) for modification
		!
750		%INCLUDE "SOURCE:[TV.OPEN]TV_SPOTS_FLAG.CRE"
		TV_SPOTS_FLAG.READONLY% = 0%
		GOTO 790

760		!
		! If unable to open for modify, try to open with read
		! access only.
		!
		%INCLUDE "SOURCE:[TV.OPEN]TV_SPOTS_FLAG.OPN"
		SMG_WINDOW::FLAGS = (SMG_WINDOW::FLAGS OR 2%)
		TV_SPOTS_FLAG.READONLY% = -1%

		GOTO 790

770		!
		! File not open, so reset channel
		!
		CALL ASSG_FREECHANNEL(TV_SPOTS_FLAG.CH%)

		EXIT FUNCTION

790		SMG_WINDOW::CHAN  = TV_SPOTS_FLAG.CH%
		RESET #TV_SPOTS_FLAG.CH%
		GET #TV_SPOTS_FLAG.CH%, REGARDLESS

	!
	! Display the background
	!
	! This option is used to display the background information on the
	! screen.  It must first clear any junk on the screen, and then
	! write the background onto it.
	!
20100	CASE OPT_BACKGROUND

		SMG_STATUS% = SMG$BEGIN_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

		SMG_STATUS% = SMG$ERASE_DISPLAY(SMG_WINDOW::WNUMBER)

		DATA	05, 04, "(01) Spots Flag", &
			06, 04, "(02) Spots Code", &
			07, 04, "(03) Description", &
			0,  0, ""

		RESTORE

		READ XPOS%, YPOS%, XSTR$

		WHILE (XPOS% <> 0)
			SMG_STATUS% = SMG$PUT_CHARS( &
				SMG_WINDOW::WNUMBER,XSTR$, XPOS%, YPOS%)
			READ XPOS%, YPOS%, XSTR$
		NEXT

		SMG_STATUS% = SMG$END_DISPLAY_UPDATE(SMG_WINDOW::WNUMBER)

	!
	! Enter/Display/Default
	!
	! This option is used to enter the data from the user, display data,
	! set defaults, and return the data back according to MFLAG.
	!
20200	CASE OPT_ENTRY

		TEMP$, TEMP1$ = TRM$(SCOPE::PRG_ITEM)
		TEMP$ = "View starting at" IF TEMP$ = "View"

		SCOPE::PRG_ITEM = "FLD" + FORMAT$(MLOOP, "<0>##")

		SCOPE::SCOPE_EXIT = 0%

		SELECT MLOOP

		CASE 1%
 Loop4:			TV_SPOTS_FLAG::FLAG = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, "05;30", TEMP$, &
				TV_SPOTS_FLAG::FLAG, MFLAG, "!", MVALUE )

			SELECT SCOPE::SCOPE_EXIT
				CASE SMG$K_TRM_F14
					X% = ENTR_3CHOICE(SCOPE, &
						"","",EC$(),"", 128%, &
						"List of Flags", "", 0%)
					TV_SPOTS_FLAG::FLAG = LEFT$(EC$(X%), 1%) &
						IF X% > 0%
					GOTO Loop4
			END SELECT

		CASE 2%
			TV_SPOTS_FLAG::CODE = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, "6;30", TEMP$, &
				TV_SPOTS_FLAG::CODE, MFLAG, "'E", MVALUE )

		CASE 3%
			TV_SPOTS_FLAG::DESCR = ENTR_3STRING(SCOPE, &
				SMG_WINDOW::WNUMBER, "7;30", TEMP$, &
				TV_SPOTS_FLAG::DESCR, MFLAG, &
				"'E", MVALUE )

		END SELECT

		SCOPE::PRG_ITEM = TEMP1$

	!
	! Test values
	!
20300	CASE OPT_TESTENTRY

		TV_MAIN_SPOTS_FLAG = 0%

		SELECT MLOOP

		CASE 1%
			IF TV_SPOTS_FLAG::FLAG = ""
			THEN
				TV_MAIN_SPOTS_FLAG = 1%
			ELSE
				GOOD% = 0%
				GOOD% = -1% &
					IF TV_SPOTS_FLAG::FLAG = LEFT(EC$(I%), 1%) &
					FOR I%=1% TO VAL(EC$(0%))

				IF GOOD% = 0%
				THEN
					X% = ENTR_3CHOICE(SCOPE, &
						"","",EC$(),"", 128%, &
						"List of Flags", "", 0%)

					IF X% > 0%
					THEN
						TV_SPOTS_FLAG::FLAG = LEFT(EC$(X%), 1%)
						TV_MAIN_SPOTS_FLAG = 1%
					END IF
				END IF

			END IF

		CASE 2%

			IF TV_SPOTS_FLAG::CODE = ""
			THEN
				TV_MAIN_SPOTS_FLAG = 1%
			ELSE
				IF (MVALUE = "ADD")
				THEN
					GET #TV_SPOTS_FLAG.CH%, &
						KEY#0 EQ TV_SPOTS_FLAG::FLAG + &
						TV_SPOTS_FLAG::CODE, &
						REGARDLESS
					TV_MAIN_SPOTS_FLAG = 2%
					CALL ENTR_3MESSAGE(SCOPE, &
						"Record Already Exists", 1%)
				END IF
			END IF

		END SELECT


	CASE OPT_DISPLAY

	!
	! Set TV_SPOTS_FLAG_OLD value
	!
20500	CASE OPT_SETOLD

		TV_SPOTS_FLAG_OLD = TV_SPOTS_FLAG

	!
	! Restore TV_SPOTS_FLAG_OLD value
	!
	CASE OPT_RESETOLD

		TV_SPOTS_FLAG = TV_SPOTS_FLAG_OLD

	!
	! Set default value
	!
	CASE OPT_SETDEFAULT

		TV_SPOTS_FLAG2 = TV_SPOTS_FLAG

	!
	! Restore default value
	!
	CASE OPT_RESETDEFAULT

		TV_SPOTS_FLAG = TV_SPOTS_FLAG2

	!
	! View header
	!
	CASE OPT_VIEW

		SELECT MLOOP

		!
		! Title (One line only)
		!
		CASE 1%
			MVALUE = "  Flag   Code   Description"

		!
		! Positions of lines
		!
		CASE 2%
			MVALUE = "008,015"

		!
		! Convert current record into text
		!
		CASE 3%
			!
			! View allowing only a small section of the table
			!
			IF (MID(MVALUE$, 3%, 1%) <> " ") AND &
				(MID(MVALUE$, 2%, 1%) = "X")
			THEN
				IF MID(MVALUE$, 3%, 1%) < TV_SPOTS_FLAG::FLAG
				THEN
					TV_MAIN_SPOTS_FLAG = 1%
				END IF

				IF MID(MVALUE$, 3%, 1%) > TV_SPOTS_FLAG::FLAG
				THEN
					TV_MAIN_SPOTS_FLAG = 2%
				END IF
			END IF

			MVALUE = " " + TV_SPOTS_FLAG::FLAG + "      " + &
				TV_SPOTS_FLAG::CODE + "    " + &
				TV_SPOTS_FLAG::DESCR

		END SELECT

	!
	! Find
	!
	CASE OPT_FIND
		SELECT MLOOP
		CASE 0%
			FIND #TV_SPOTS_FLAG.CH%, KEY#0 &
				GE TV_SPOTS_FLAG::FLAG + TV_SPOTS_FLAG::CODE, &
					REGARDLESS
		END SELECT
	END SELECT

 ExitFunction:
	EXIT FUNCTION

29000	!
	! Trap errors
	!
	SELECT ERL
		!
		! Unable to open Read/Write
		!
		CASE 750%
			RESUME 760 IF ERR = 10%
			TV_MAIN_SPOTS_FLAG = ERR
			RESUME 770

		!
		! Unable to open Read/Only
		!
		CASE 760%
			TV_MAIN_SPOTS_FLAG = ERR
			RESUME 770

		!
		! Unable to reset
		!
		CASE 790%
			RESUME 32767

		!
		! Record doesn't already exist
		!
		CASE 20300%
			RESUME 32767
	END SELECT

	ON ERROR GO BACK

32767	END FUNCTION
