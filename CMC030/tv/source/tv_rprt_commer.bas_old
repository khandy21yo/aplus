1	%TITLE "TV_RPRT_COMMER - TV Traffic Commercial Order File"
	%SBTTL "TV System"
	%IDENT "V3.6a Calico"

	!
	! COPYRIGHT (C) 1987, 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	!
	! Abstract:
	!
	!	This program prints the Commercial Order file.
	!
	! Index:
	!
	!
	! Input:
	!
	!
	! Output:
	!
	!
	! Example:
	!
	!
	! Environment:
	!
	!	VAX/VMS BASIC+2 V4.4
	!
	! Author:
	!
	!	12/14/87 - Kevin Handy
	!
	! Compile:
	!
	!	$ BAS TV_SOURCE:TV_RPRT_COMMER
	!	$ LINK/EXECUTABLE=TV_EXE:*.EXE TV_RPRT_COMMER, -
	!		FUNC_LIB:CMCLINK/OPTION
	!	$ DELETE TV_RPRT_COMMER.OBJ;*
	!
	! Modification history:
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	08/22/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--
	%PAGE

	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	!
	! Include files
	!
	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE


 !	%INCLUDE %FROM %CDD "CDD$TOP.UTL.UTL_REPORTX"
	%INCLUDE "SOURCE:[UTL.OPEN]UTL_REPORTX.HB"
	DECLARE UTL_REPORTX_CDD		UTL_REPORTX

 !	%INCLUDE %FROM %CDD "CDD$TOP.TV.TV_COMMER"
	%INCLUDE "SOURCE:[TV.OPEN]TV_COMMER.HB"
	MAP	(TV_COMMER)		TV_COMMER_CDD		TV_COMMER

 !	%INCLUDE %FROM %CDD "CDD$TOP.TV.TV_COMMER_SKED_INSTR"
	%INCLUDE "SOURCE:[TV.OPEN]TV_COMMER_SKED_INSTR.HB"
	MAP (TV_COMMER_SKED_INSTR)	TV_COMMER_SKED_INSTR_CDD TV_COMMER_SKED_INSTR

 !	%INCLUDE %FROM %CDD "CDD$TOP.TV.TV_COMMER_CHARGE"
	%INCLUDE "SOURCE:[TV.OPEN]TV_COMMER_CHARGE.HB"
	MAP (TV_COMMER_CHARGE)		TV_COMMER_CHARGE_CDD	TV_COMMER_CHARGE

 !	%INCLUDE %FROM %CDD "CDD$TOP.AR.AR_CUSTOM"
	%INCLUDE "SOURCE:[AR.OPEN]AR_CUSTOM.HB"
	MAP (AR_CUSTOM)		AR_CUSTOM_CDD		AR_CUSTOM

	!
	! External functions
	!
	EXTERNAL STRING  FUNCTION PRNT_DATE
	EXTERNAL STRING  FUNCTION PRNT_TIME

	%PAGE

	ON ERROR GOTO 19000

 Init:	!
	! Initilize report
	!
	CALL OUTP_INITFROMFILE(UTL_REPORTX, 132%)

	GOTO ExitProgram IF UTL_REPORTX::STAT

	!
	! Set up from user input
	!
	FROM.ITEM$	= EDIT$(UTL_REPORTX::OPTDEF(0%), 132%)
	TO.ITEM$	= EDIT$(UTL_REPORTX::OPTDEF(1%), 132%)

300	!
	! Open TV_COMMER file
	!
	%INCLUDE "SOURCE:[TV.OPEN]TV_COMMER.OPN"

	%INCLUDE "SOURCE:[TV.OPEN]TV_COMMER_SKED_INSTR.OPN"

	%INCLUDE "SOURCE:[TV.OPEN]TV_COMMER_CHARGE.OPN"

	%INCLUDE "SOURCE:[AR.OPEN]AR_CUSTOM.OPN"

 ReportTitle:
	!
	! Title
	!
	TITLE$(1%) = "Commercial Orders"
	TITLE$(2%) = "TV Traffic System"
	TITLE$(3%) = ""

	!
	! Heading
	!
	TITLE$(4%) = ""

	%PAGE

17000	!******************************************************************
	! OUTPUT REPORT
	!******************************************************************

	IF FROM.ITEM$ = ""
	THEN
		RESET #TV_COMMER.CH%, KEY#0%
	ELSE
		FIND #TV_COMMER.CH%, KEY#0% GE FROM.ITEM$, REGARDLESS
	END IF

 GetNextRec:
17020	!
	! Main loop starts here
	!
	GOTO ExitProgram IF UTL_REPORTX::STAT

	!
	! Get next record
	!
	GET #TV_COMMER.CH%, REGARDLESS

	!
	! Check current record
	!
	GOTO ExitTotal IF (TV_COMMER::FRMNUM > TO.ITEM$) AND TO.ITEM$ <> ""

	!
	! Put some seperation between forms
	!
	TEXT$ = "Order    Customer   " + &
		"Name                                               " + &
		"  Start      End      Prior Class So     Spot     Prod"

	CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), "", 8%)
	CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), "", 7%)
	CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, 6%)
	GOTO ExitProgram IF UTL_REPORTX::STAT


17021	!
	! Get customer name/address
	!
	GET #AR_CUSTOM.CH%, KEY#0 EQ TV_COMMER::CUSNUM
	CUSNAME$ = AR_CUSTOM::CUSNAM + ""


17028	!
	! Print out one line
	!
	TEXT$ = TV_COMMER::FRMNUM + " " + &
		TV_COMMER::CUSNUM + " " + &
		CUSNAME$ + " " + &
		PRNT_DATE(TV_COMMER::START_DATE, 8%) + "-" + &
		PRNT_DATE(TV_COMMER::END_DATE, 8%) + " " + &
		FORMAT$(TV_COMMER::PRIORITY, "##### ") + &
		TV_COMMER::CLASS + "  " + &
		TV_COMMER::SOURCE + " " + &
		PRNT_TIME(TV_COMMER::SPOT_SEP, 0%) + " " + &
		PRNT_TIME(TV_COMMER::PROD_SEP, 0%)

	TEXT$ = TEXT$ + "  (Canceled)" &
		IF TV_COMMER::CANCELLED = "Y"

	CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, 0%)
	GOTO ExitProgram IF UTL_REPORTX::STAT

	IF TV_COMMER::AGENCY_NUM <> "          "
	THEN
		TEXT$ = "         Agency: " + &
			TV_COMMER::AGENCY_NUM + "  " + &
			FORMAT$(TV_COMMER::AGENCY_PCT, "###.##%")

		CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, 0%)
		GOTO ExitProgram IF UTL_REPORTX::STAT
	END IF

	IF TV_COMMER::REP_NUM <> "          "
	THEN
		TEXT$ = "         Rep:    " + &
			TV_COMMER::REP_NUM + "  " + &
			FORMAT$(TV_COMMER::REP_PCT, "###.##%")

		CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, 0%)
		GOTO ExitProgram IF UTL_REPORTX::STAT
	END IF

17030	!*******************************************************************
	! Try for schedules
	!*******************************************************************

	PRINT_FLAG% = -1%

	FIND #TV_COMMER_SKED_INSTR.CH%, &
		KEY#0 GE TV_COMMER::FRMNUM, &
		REGARDLESS

17040	GET #TV_COMMER_SKED_INSTR.CH%

	IF (TV_COMMER_SKED_INSTR::FRMNUM = TV_COMMER::FRMNUM)
	THEN
		IF PRINT_FLAG%
		THEN
			PRINT_FLAG% = 0%

			TEXT$ = "      Sk Start-date End-date   Start-tm " + &
				"End-tm    In/Out  Mo Tu We Th Fr Sa Su Total " + &
				"Length       Rate"

			CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), "", 4%)
			CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, 0%)
			GOTO ExitProgram IF UTL_REPORTX::STAT
		END IF

		TEMP$ = ""
		FOR LOOP% = 0% TO 6%
			IF TV_COMMER_SKED_INSTR::SPOTS_PER_DAY(LOOP%) = 0%
			THEN
				TEMP$ = TEMP$ + "  ."
			ELSE
				TEMP$ = TEMP$ + &
				FORMAT$(TV_COMMER_SKED_INSTR::SPOTS_PER_DAY(LOOP%), "###")
			END IF
		NEXT LOOP%

		TEXT$ = "      " + &
			TV_COMMER_SKED_INSTR::SKED_NUM + " " + &
			PRNT_DATE(TV_COMMER_SKED_INSTR::START_DATE, 8%) + "-" + &
			PRNT_DATE(TV_COMMER_SKED_INSTR::END_DATE, 8%) + " " + &
			PRNT_TIME(TV_COMMER_SKED_INSTR::START_TIME, 0%) + "-" + &
			PRNT_TIME(TV_COMMER_SKED_INSTR::END_TIME, 0%) + " " + &
			FORMAT$(TV_COMMER_SKED_INSTR::IN_WEEKS, "###") + "/" + &
			FORMAT$(NUM1$(TV_COMMER_SKED_INSTR::OUT_WEEKS), "'LL ") + &
			TEMP$ + &
			FORMAT$(TV_COMMER_SKED_INSTR::TOTAL_SPOTS, "###### ") + &
			PRNT_TIME(TV_COMMER_SKED_INSTR::LENGTH, 0%) + " " + &
			FORMAT$(TV_COMMER_SKED_INSTR::RATE_PER_SPOT, "$$###.##")

		CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, 0%)
		GOTO ExitProgram IF UTL_REPORTX::STAT

		GOTO 17040
	END IF

17060	!*******************************************************************
	! Try for schedules
	!*******************************************************************

	PRINT_FLAG% = -1%

	FIND #TV_COMMER_CHARGE.CH%, &
		KEY#0 GE TV_COMMER::FRMNUM, &
		REGARDLESS

17070	GET #TV_COMMER_CHARGE.CH%

	IF (TV_COMMER_CHARGE::FRMNUM = TV_COMMER::FRMNUM)
	THEN
		IF PRINT_FLAG%
		THEN
			PRINT_FLAG% = 0%

			TEXT$ = "        Date     Description                    " + &
				"   Amount Account              Period   " + &
				"Coop      Billed"

			CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), "", 4%)
			CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, 0%)
			GOTO ExitProgram IF UTL_REPORTX::STAT
		END IF

		TEXT$ = "      " + &
			PRNT_DATE(TV_COMMER_CHARGE::BILL_DATE, 8%) + " " + &
			TV_COMMER_CHARGE::DESCR + " " + &
			FORMAT$(TV_COMMER_CHARGE::AMOUNT, "$$####.##") + " " + &
			TV_COMMER_CHARGE::ACCTNO + " " + &
			PRNT_DATE(TV_COMMER_CHARGE::PERIOD, 8%) + " " + &
			TV_COMMER_CHARGE::COOP + " " + &
			TV_COMMER_CHARGE::BILL_FLAG

		CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, 0%)
		GOTO ExitProgram IF UTL_REPORTX::STAT

		GOTO 17070
	END IF

	!
	! Try for next record
	!
17700	GOTO GetNextRec

 ExitTotal:
	!
	! Handle end of report
	!

 ExitProgram:
	CALL OUTP_FINISH(UTL_REPORTX)

	!
	! Exit to next program or menu
	!
	IF TRM$(UTL_REPORTX::NEXTRUN) = ""
	THEN
		CALL SUBR_3EXITPROGRAM(SCOPE, "", "")
	ELSE
		CALL SUBR_3EXITPROGRAM(SCOPE, "RUN " + UTL_REPORTX::NEXTRUN, "")
	END IF

	%PAGE

 HelpError:
	!******************************************************************
	! Help Message for an error
	!******************************************************************

	CALL HELP_3MESSAGE(SCOPE, ERN$ + " " + NUM1$(ERL) + " " + ERT$(ERR), &
		"ERR", PROGRAM$, "ERROR" + NUM1$(ERR))
	UTL_REPORTX::STAT = -1%
	GOTO ExitProgram

	%Page

19000	!******************************************************************
	! ERROR TRAPPING
	!******************************************************************

	SELECT ERR
	CASE 154%	! Locked record
		SLEEP 1%
		RESUME
	END SELECT

	SELECT ERL
	CASE 300%, 17000%
		PROGRAM$ = "TV_COMMER"
		RESUME HelpError

	CASE 17020%	! End of file
		RESUME ExitTotal IF ERR = 11%

	!
	! Unable to find customer record
	!
	CASE 17021%
		CUSNAME$ = "*** Undefined ***"
		RESUME 17028

	!
	! End of instructions
	!
	CASE 17030%, 17040%
		RESUME 17060

	!
	! End of charges
	!
	CASE 17060%, 17070%
		RESUME 17700

	END SELECT

	!
	! Untrapped error
	!
	PROGRAM$ = SCOPE::PRG_PROGRAM
	RESUME HelpError

32767	END
