1	%TITLE "TV_RPRT_TVL001 - TV Traffic Customer Master File"
	%SBTTL "TV System"
	%IDENT "V3.6a Calico"

	!
	! COPYRIGHT (C) 1987, 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	!
	! Abstract:
	!
	!	This program prints the Custom master file.
	!
	! Index:
	!
	!
	! Input:
	!
	!
	! Output:
	!
	!
	! Example:
	!
	!
	! Environment:
	!
	!	VAX/VMS BASIC+2 V4.4
	!
	! Author:
	!
	!	11/16/87 - B. Craig Larsen
	!
	! Compile:
	!
	!	$ BAS TV_SOURCE:TV_RPRT_TVL001
	!	$ LINK/EXECUTABLE=TV_EXE:*.EXE TV_RPRT_TVL001, -
	!		FUNC_LIB:CMCLINK/OPTION
	!	$ DELETE TV_RPRT_TVL001.OBJ;*
	!
	! Modification history:
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	08/22/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!--
	%PAGE

	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	!
	! Include files
	!
	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE


 !	%INCLUDE %FROM %CDD "CDD$TOP.UTL.UTL_REPORTX"
	%INCLUDE "SOURCE:[UTL.OPEN]UTL_REPORTX.HB"
	DECLARE		UTL_REPORTX_CDD UTL_REPORTX

 !	%INCLUDE %FROM %CDD "CDD$TOP.AR.AR_CUSTOM"
	%INCLUDE "SOURCE:[AR.OPEN]AR_CUSTOM.HB"
	MAP	(AR_CUSTOM)	AR_CUSTOM_CDD	AR_CUSTOM

	!
	! External functions
	!
	EXTERNAL STRING  FUNCTION PRNT_PHONE

	%PAGE

	ON ERROR GOTO 19000

 Init:	!
	! Initilize report
	!
	CALL OUTP_INITFROMFILE(UTL_REPORTX, 80%)

	GOTO ExitProgram IF UTL_REPORTX::STAT

	!
	! Set up from user input
	!
	FROM.ITEM$ = EDIT$(UTL_REPORTX::OPTDEF(0%), 132%)
	TO.ITEM$ = EDIT$(UTL_REPORTX::OPTDEF(1%), 132%)
	SORTBY$ = EDIT$(UTL_REPORTX::OPTDEF(2%), -1%)
	FORMTYPE$ = EDIT$(UTL_REPORTX::OPTDEF(3%), -1%)
	SELECT SORTBY$
	CASE "NU"
		K_NUM% = 0%
	CASE "NA"
		K_NUM% = 1%
	CASE ELSE
		K_NUM% = 2%
	END SELECT

300	!
	! Open AR_CUSTOM file
	!
	%INCLUDE "SOURCE:[AR.OPEN]AR_CUSTOM.OPN"

 ReportTitle:
	!
	! Title
	!
	TITLE$(1%) = "Customer Master File List"
	TITLE$(2%) = "TV Traffic System"
	TITLE$(3%) = ""

	!
	! Heading
	!
	IF FORMTYPE$ = "L"
	THEN
		TITLE1$ = "Customer                    Custom   #     "
		TITLE$(4%) = ""
	ELSE
		TITLE$(4%) = "Cus Number " + &
			"Customer Name                           "
		TITLE$(5%) = ""
	END IF

	TITLE3$ = "M Statement S Charge Alpha Sort Key"
	%PAGE

17000	!******************************************************************
	! OUTPUT REPORT
	!******************************************************************

	IF FROM.ITEM$ = ""
	THEN
		RESET #AR_CUSTOM.CH%, KEY#K_NUM%
	ELSE
		FIND #AR_CUSTOM.CH%, KEY#K_NUM% GE FROM.ITEM$, REGARDLESS
	END IF

 GetNextRec:
17020	!
	! Main loop starts here
	!
	GOTO ExitProgram IF UTL_REPORTX::STAT

	!
	! Get next record
	!
	GET #AR_CUSTOM.CH%, REGARDLESS

	!
	! Check current record
	!
	SELECT SORTBY$
	CASE "NU"
		GOTO ExitTotal IF (AR_CUSTOM::CUSNUM > TO.ITEM$) AND &
			TO.ITEM$ <> ""
	CASE "NA"
		GOTO ExitTotal IF (AR_CUSTOM::CUSNAM > TO.ITEM$) AND &
			TO.ITEM$ <> ""
	CASE ELSE
		GOTO ExitTotal IF (AR_CUSTOM::ALPSRT > TO.ITEM$) AND &
			TO.ITEM$ <> ""
	END SELECT

	!
	! Print out one line
	!
	IF FORMTYPE$ = "L"
	THEN
		CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TITLE1$, 0%)
		TEXT$ = AR_CUSTOM::CUSNAM	+ "   " + &
			AR_CUSTOM::CUSNUM

		CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, 0%)
		CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), AR_CUSTOM::ADD1, 0%)
		CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), AR_CUSTOM::ADD2, 0%)
		GOTO ExitProgram IF UTL_REPORTX::STAT

		TEXT$ = AR_CUSTOM::CITY   + " " + &
			AR_CUSTOM::STATE  + " " + &
			AR_CUSTOM::ZIP    + " " + &
			AR_CUSTOM::COUNTRY + "     " + &
			TITLE3$

		CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, 0%)
		GOTO ExitProgram IF UTL_REPORTX::STAT

		TEXT$ = PRNT_PHONE( AR_CUSTOM::PHONE, 0% ) + SPACE$(30%) + &
			AR_CUSTOM::METHOD   + "     " + &
			AR_CUSTOM::STMTFLG   + "        " + &
			AR_CUSTOM::SERCHRG   + "     " + &
			AR_CUSTOM::ALPSRT

		CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, 0%)
		CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), "", 0%)
		CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), STRING$(80%,61%), 8%)
		GOTO ExitProgram IF UTL_REPORTX::STAT

	ELSE
		TEXT$ = AR_CUSTOM::CUSNUM + " " + AR_CUSTOM::CUSNAM
		CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, 0%)
		GOTO ExitProgram IF UTL_REPORTX::STAT
	END IF

	!
	! Try for next record
	!
	GOTO GetNextRec

 ExitTotal:
	!
	! Handle end of report
	!

 ExitProgram:
	CALL OUTP_FINISH(UTL_REPORTX)

	!
	! Exit to next program or menu
	!
	IF TRM$(UTL_REPORTX::NEXTRUN) = ""
	THEN
		CALL SUBR_3EXITPROGRAM(SCOPE, "", "")
	ELSE
		CALL SUBR_3EXITPROGRAM(SCOPE, "RUN " + UTL_REPORTX::NEXTRUN, "")
	END IF

	%PAGE

 HelpError:
	!******************************************************************
	! Help Message for an error
	!******************************************************************
	CALL HELP_3MESSAGE(SCOPE, ERN$ + " " + NUM1$(ERL) + " " + ERT$(ERR), &
		"ERR", PROGRAM$, "ERROR" + NUM1$(ERR))
	UTL_REPORTX::STAT = -1%
	GOTO ExitProgram

	%Page

19000	!******************************************************************
	! ERROR TRAPPING
	!******************************************************************

	SELECT ERR
	CASE 154%	! Locked record
		SLEEP 1%
		RESUME
	END SELECT

	SELECT ERL
	CASE 300%, 17000%
		PROGRAM$ = "AR_CUSTOM"
		RESUME HelpError

	CASE 17020%	! End of file
		RESUME ExitTotal IF ERR = 11%
	END SELECT

	!
	! Untrapped error
	!
	PROGRAM$ = SCOPE::PRG_PROGRAM
	RESUME HelpError

32767	END
