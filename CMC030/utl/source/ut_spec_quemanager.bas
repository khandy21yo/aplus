1	%TITLE "Manage Print Queues"
	%SBTTL "UT_SPEC_QUEMANAGER"
	%IDENT "V3.6a Calico"

	!
	! COPYRIGHT (C) 1992 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! Abstract:HELP
	!
	! Index:
	!	Que>Manager
	!	Que>Print
	!	Print>Que Manager
	!
	! Option:
	!
	!
	! Compile:
	!
	!	$ BAS UTL_SOURCE:UT_SPEC_QUEMANAGER/LINE
	!	$ LINK/EXECUTABLE=UTL_EXE: UT_SPEC_QUEMANAGER, FUNC_LIB:CMCLINK/OPTION
	!	$ DELETE UT_SPEC_QUEMANAGER.OBJ;*
	!
	! Author:
	!
	!	08/10/92 - Kevin Handy
	!
	! Modification history:
	!
	!	08/31/92 - Kevin Handy
	!		Clean up (check)
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	09/17/96 - Kevin Handy
	!		Reformat source code.
	!
	!	08/22/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!
	!	06/09/99 - Kevin Handy
	!		Comment out FNQUEFLAGS (Dead Code)
	!
	!	10/19/2000 - Kevin Handy
	!		Lose "ON ERROR GOTO 19000/ON ERROR GOTO 0" loop
	!--
	%PAGE

	!
	! Set up compiling options
	!
	OPTION SIZE = (REAL GFLOAT, INTEGER LONG)

	!
	! Include files
	!
	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "FUNC_INCLUDE:CMC_GETQUI.TXT"
	%INCLUDE "FUNC_INCLUDE:SYS$SNDJBC.TXT"

	!
	! Maps
	!
	%INCLUDE "SOURCE:[SMG.OPEN]SMG_SCROLL.HB"
	DECLARE  SMG_SCROLL_CDD WINDOW_SCROLL
	DECLARE  SMG_SCROLL_CDD ENTRY_SCROLL

	RECORD ITEMCODE_RECORD

		WORD BUFFER_LENGTH
		WORD ITEM_CODE
		LONG BUFFER_ADDRESS
		LONG RETURN_LENGTH_ADDRESS

	END RECORD


	DIM ITEMCODE_RECORD ITEMCODES(10%)
	DIM ITEMCODE_RECORD ENTRYCODES(10%)

	RECORD IOSB_RECORD

		LONG STAT
		LONG ZERO

	END RECORD

	DECLARE IOSB_RECORD IOSB

	RECORD KEEPINFO_RECORD

		STRING	QUE_NAME = 31%
		STRING	FORM_NAME = 31%
		LONG	QUE_STATUS

	END RECORD

	DIM KEEPINFO_RECORD KEEPINFO(100%)
	DIM FILE$(100%)

	RECORD KEEPENTRY_RECORD

		STRING	USER_NAME = 12%
		STRING	FORM_NAME = 31%
		STRING	ENTRY_NAME = 31%
		STRING	QUE_NAME = 31%
		LONG	ENTRY_NUMBER

	END RECORD

	DIM ENTRY$(100%)
	DIM KEEPENTRY_RECORD KEEPENTRY(100%)

	MAP (JUNK) &
		SEARCH_NAME$ = 31%, &
		ASSIGNED_QUE_NAME$ = 32%, &
		FORM_NAME$ = 31%, &
		QUE_DESCRIPTION$ = 255%, &
		QUE_NAME$ = 31%, &
		SEARCH_USERNAME$ = 12%, &
		USERNAME$ = 12%, &
		ENTRYNAME$ = 31%


	!
	! External functions
	!
	EXTERNAL INTEGER FUNCTION DSPL_SCROLL
	EXTERNAL LONG    FUNCTION SYS$GETQUIW
	EXTERNAL LONG    FUNCTION SYS$SNDJBCW

	!
	! Declare vars
	!
	DECLARE LONG SMG_SCROLL, SYS_STATUS, ENTRY_WINDOW
	DECLARE LONG PROW, PCOL, WROWS, WCOLS

	DECLARE STRING TK_MAIN_TEXTFILEWINDOW

	%PAGE

	!
	! Initailize set and device files
	!
	CALL READ_INITIALIZE

1000	!***************************************************************
	! Select Text File
	!***************************************************************

	!
	! Create the data display
	!
	WROWS = 18%
	WCOLS = 78%

	!
	! Create the window
	!
	SMG_STATUS% = SMG$CREATE_VIRTUAL_DISPLAY(WROWS, WCOLS, &
		SMG_SCROLL, SMG$M_BORDER)

	!
	! Output the title and title line
	!
	SMG_STATUS% = SMG$LABEL_BORDER(SMG_SCROLL, " Que Manager ", &
		SMG$K_TOP,, SMG$M_BOLD)

	!
	! Define windows
	!
	WINDOW_SCROLL::WINDOW		= SMG_SCROLL
	WINDOW_SCROLL::TOP_ARRAY	= 1%
	WINDOW_SCROLL::BOT_ARRAY	= 1%
	WINDOW_SCROLL::SCROLL_TOP	= 1%
	WINDOW_SCROLL::SCROLL_BOT	= 12%
	WINDOW_SCROLL::TOP_LINE		= 1%
	WINDOW_SCROLL::BEG_ELEMENT	= 1%
	WINDOW_SCROLL::END_ELEMENT	= 1%
	WINDOW_SCROLL::CUR_LINE		= 1%
	WINDOW_SCROLL::CUR_W_ROW	= 1%
	WINDOW_SCROLL::CUR_W_COL	= 1%
	WINDOW_SCROLL::FIND_LINE	= 1%
	WINDOW_SCROLL::SMG_FLAG		= 0%
	WINDOW_SCROLL::PROMPT		= "->"
	WINDOW_SCROLL::VIDEO_COMP	= 0%
	WINDOW_SCROLL::CHARSET		= 0%
	WINDOW_SCROLL::DRAW_COLS	= ""

	OPT% = 0%

	!
	! Print the array
	!
1020	GOSUB LookUp

	TEMP = DSPL_SCROLL(WINDOW_SCROLL, FILE$(), 0%, "PAINT")
	TK_MAIN_TEXTFILEWINDOW = FILE$(WINDOW_SCROLL::CUR_LINE)

	SMG_STATUS% = SMG$PASTE_VIRTUAL_DISPLAY(SMG_SCROLL, SCOPE::SMG_PBID, 2%, 2%)

	OPLIST$ = "Stop Restart Formname reDisplay Entry Help eXit"

 Menu:
1050	SCOPE::PRG_ITEM= ""

	OPTION$ = ENTR_3OPTION(SCOPE, "COMMAND", OPLIST$, OPT%, FLAGW%)

	SELECT SCOPE::SCOPE_EXIT

	CASE SMG$K_TRM_UP, SMG$K_TRM_DOWN, &
		SMG$K_TRM_PREV_SCREEN, SMG$K_TRM_NEXT_SCREEN, &
		SMG$K_TRM_F18, SMG$K_TRM_F19

		V% = DSPL_SCROLL(WINDOW_SCROLL, FILE$(), SCOPE::SCOPE_EXIT, "")
		TK_MAIN_TEXTFILEWINDOW = FILE$(WINDOW_SCROLL::CUR_LINE)

		GOTO Menu

	CASE SMG$K_TRM_F10, SMG$K_TRM_CTRLZ, SMG$K_TRM_SELECT

		GOTO ExitProgram

	CASE 0%, 10%, 12%, 13%, SMG$K_TRM_DO

	CASE ELSE
		CALL ENTR_3BADKEY(SCOPE, SCOPE::SCOPE_EXIT)
		GOTO 1050

	END SELECT

	SELECT OPTION$

	!
	! Form Name
	!
	CASE "F"
2000		SEARCH_NAME$ = KEEPINFO(WINDOW_SCROLL::CUR_LINE)::QUE_NAME
		SMG_STATUS% = SMG$PUT_CHARS(SCOPE::SMG_OPTION, &
			"New Form Name: ", 1%, 1%, 1%)

		FORM_NAME$ = KEEPINFO(WINDOW_SCROLL::CUR_LINE)::FORM_NAME
		SELECT ENTR_3ENTER(SCOPE, SCOPE::SMG_OPTION, &
			1%, 16%, FORM_NAME$, -1%, 16% + 4096%)

		! ^C, ^Z, PF2, Exit
		CASE SMG$K_TRM_CTRLC, SMG$K_TRM_PF2, &
			SMG$K_TRM_F10,	SMG$K_TRM_CTRLZ
			GOTO 1050

		CASE 0%, 10%, 12%, 13%, SMG$K_TRM_DO
			! Good keys

		CASE ELSE
			CALL ENTR_3BADKEY(SCOPE, SCOPE::SCOPE_EXIT)
			GOTO 2000
		END SELECT

		FORM_NAME$ = EDIT$(FORM_NAME$, 4% + 8% + 128% + 256%)

		GOSUB SetForm

	!
	! Stop que
	!
	CASE "S"
		SEARCH_NAME$ = KEEPINFO(WINDOW_SCROLL::CUR_LINE)::QUE_NAME
		GOSUB StopQue

	!
	! Restart que
	!
	CASE "R"
		SEARCH_NAME$ = KEEPINFO(WINDOW_SCROLL::CUR_LINE)::QUE_NAME
		GOSUB RestartQue

	!
	! Entry's under a que
	!
	CASE "E"
		SEARCH_NAME$ = KEEPINFO(WINDOW_SCROLL::CUR_LINE)::QUE_NAME
		GOSUB ShowEntry

	!
	! reDisplay
	!
	CASE "D"
		GOTO 1020

	!
	! Exit
	!
	CASE "X"
		GOTO ExitProgram

	!
	! Help
	!
	CASE "H"
		CALL HELP_3MESSAGE(SCOPE, "", "PROG", SCOPE::PRG_PROGRAM, "HELP")
		GOTO 1050

	END SELECT

	GOTO 1020

	%PAGE

 ShowEntry:
5000	!*******************************************************************
	! Show entry's in a que
	!*******************************************************************
	!
	! Create the data display
	!
	WROWS = 18%
	WCOLS = 78%

	!
	! Create the window
	!
	SMG_STATUS% = SMG$CREATE_VIRTUAL_DISPLAY(WROWS, WCOLS, &
		ENTRY_WINDOW, SMG$M_BORDER)

	!
	! Output the title and title line
	!
	SMG_STATUS% = SMG$LABEL_BORDER(ENTRY_WINDOW, " Entrys for " + &
		TRM$(SEARCH_NAME$) + " ", &
		SMG$K_TOP,, SMG$M_BOLD)

	!
	! Define windows
	!
	ENTRY_SCROLL::WINDOW		= ENTRY_WINDOW
	ENTRY_SCROLL::TOP_ARRAY		= 1%
	ENTRY_SCROLL::BOT_ARRAY		= 1%
	ENTRY_SCROLL::SCROLL_TOP	= 1%
	ENTRY_SCROLL::SCROLL_BOT	= 12%
	ENTRY_SCROLL::TOP_LINE		= 1%
	ENTRY_SCROLL::BEG_ELEMENT	= 1%
	ENTRY_SCROLL::END_ELEMENT	= 1%
	ENTRY_SCROLL::CUR_LINE		= 1%
	ENTRY_SCROLL::CUR_W_ROW		= 1%
	ENTRY_SCROLL::CUR_W_COL		= 1%
	ENTRY_SCROLL::FIND_LINE		= 1%
	ENTRY_SCROLL::SMG_FLAG		= 0%
	ENTRY_SCROLL::PROMPT		= "->"
	ENTRY_SCROLL::VIDEO_COMP	= 0%
	ENTRY_SCROLL::CHARSET		= 0%
	ENTRY_SCROLL::DRAW_COLS		= ""

	SMG_STATUS% = SMG$PASTE_VIRTUAL_DISPLAY(ENTRY_WINDOW, SCOPE::SMG_PBID, 2%, 2%)

	OPT1% = 0%

	!
	! Print the array
	!
5020	GOSUB LookUpEntry

	TEMP = DSPL_SCROLL(ENTRY_SCROLL, ENTRY$(), 0%, "PAINT")

	OPLIST$ = "Help eXit"

 EntryMenu:
5050	SCOPE::PRG_ITEM= ""

	OPTION$ = ENTR_3OPTION(SCOPE, "COMMAND", OPLIST$, OPT%, FLAGW%)

	SELECT SCOPE::SCOPE_EXIT

	CASE SMG$K_TRM_UP, SMG$K_TRM_DOWN, &
		SMG$K_TRM_PREV_SCREEN, SMG$K_TRM_NEXT_SCREEN, &
		SMG$K_TRM_F18, SMG$K_TRM_F19

		V% = DSPL_SCROLL(ENTRY_SCROLL, ENTRY$(), SCOPE::SCOPE_EXIT, "")

		GOTO EntryMenu

	CASE SMG$K_TRM_F10, SMG$K_TRM_CTRLZ, SMG$K_TRM_SELECT

		GOTO 5090

	CASE 0%, 10%, 12%, 13%, SMG$K_TRM_DO

	CASE ELSE
		CALL ENTR_3BADKEY(SCOPE, SCOPE::SCOPE_EXIT)
		GOTO 5050

	END SELECT

	SELECT OPTION$

	!
	! Exit
	!
	CASE "X"
		GOTO 5090

	!
	! Help
	!
	CASE "H"
		CALL HELP_3MESSAGE(SCOPE, "", "PROG", SCOPE::PRG_PROGRAM, "ENTRYHELP")
		GOTO 1050

	END SELECT

	GOTO 5020

5090	!
	! Exit line entry screen
	!
	SMG_STATUS = SMG$DELETE_VIRTUAL_DISPLAY(ENTRY_WINDOW)
	RETURN


 LookUp:
12000	!*******************************************************************
	! Lookup file codes
	!*******************************************************************

	TOTALFILES% = 0%

	!
	! Input item codes
	!
	ER% = 0%
	SEARCH_NAME$ = "*"
	ITEMCODES(ER%)::BUFFER_LENGTH	= LEN(SEARCH_NAME$)
	ITEMCODES(ER%)::ITEM_CODE	= QUI$_SEARCH_NAME
	ITEMCODES(ER%)::BUFFER_ADDRESS	= LOC(SEARCH_NAME$)
	ITEMCODES(ER%)::RETURN_LENGTH_ADDRESS = 0%

	ER% = ER% + 1%
	SEARCH_FLAG% = QUI$M_SEARCH_PRINTER OR QUI$M_SEARCH_TERMINAL
	ITEMCODES(ER%)::BUFFER_LENGTH	= 4%
	ITEMCODES(ER%)::ITEM_CODE	= QUI$_SEARCH_FLAGS
	ITEMCODES(ER%)::BUFFER_ADDRESS	= LOC(SEARCH_FLAG%)
	ITEMCODES(ER%)::RETURN_LENGTH_ADDRESS = 0%

	!
	! Output Item Codes
	!
	ER% = ER% + 1%
	ASSIGNED_QUE_NAME$ = SPACE$(32%)
	ITEMCODES(ER%)::BUFFER_LENGTH	= 32%
	ITEMCODES(ER%)::ITEM_CODE	= qui$_assigned_queue_name
	ITEMCODES(ER%)::BUFFER_ADDRESS	= LOC(ASSIGNED_QUE_NAME$)
	ITEMCODES(ER%)::RETURN_LENGTH_ADDRESS = 0%

	ER% = ER% + 1%
	FORM_NAME$ = SPACE$(31%)
	ITEMCODES(ER%)::BUFFER_LENGTH	= 31%
	ITEMCODES(ER%)::ITEM_CODE	= qui$_form_name
	ITEMCODES(ER%)::BUFFER_ADDRESS	= LOC(FORM_NAME$)
	ITEMCODES(ER%)::RETURN_LENGTH_ADDRESS = 0%

	ER% = ER% + 1%
	QUE_DESCRIPTION$ = SPACE$(255%)
	ITEMCODES(ER%)::BUFFER_LENGTH	= 255%
	ITEMCODES(ER%)::ITEM_CODE	= qui$_queue_description
	ITEMCODES(ER%)::BUFFER_ADDRESS	= LOC(QUE_DESCRIPTION$)
	ITEMCODES(ER%)::RETURN_LENGTH_ADDRESS = 0%

	ER% = ER% + 1%
	QUE_FLAGS% = 0%
	ITEMCODES(ER%)::BUFFER_LENGTH	= 4%
	ITEMCODES(ER%)::ITEM_CODE	= qui$_queue_flags
	ITEMCODES(ER%)::BUFFER_ADDRESS	= LOC(QUE_FLAGS%)
	ITEMCODES(ER%)::RETURN_LENGTH_ADDRESS = 0%

	ER% = ER% + 1%
	QUE_NAME$ = SPACE$(31%)
	ITEMCODES(ER%)::BUFFER_LENGTH	= 31%
	ITEMCODES(ER%)::ITEM_CODE	= qui$_queue_name
	ITEMCODES(ER%)::BUFFER_ADDRESS	= LOC(QUE_NAME$)
	ITEMCODES(ER%)::RETURN_LENGTH_ADDRESS = 0%

	ER% = ER% + 1%
	QUE_STATUS% = 0%
	ITEMCODES(ER%)::BUFFER_LENGTH	= 4%
	ITEMCODES(ER%)::ITEM_CODE	= qui$_queue_status
	ITEMCODES(ER%)::BUFFER_ADDRESS	= LOC(QUE_STATUS%)
	ITEMCODES(ER%)::RETURN_LENGTH_ADDRESS = 0%

	ER% = ER% + 1%
	ITEMCODES(ER%)::BUFFER_LENGTH	= 0%
	ITEMCODES(ER%)::ITEM_CODE	= 0%
	ITEMCODES(ER%)::BUFFER_ADDRESS	= 0%
	ITEMCODES(ER%)::RETURN_LENGTH_ADDRESS = 0%


	!
	IOSB::STAT	= 0%
	IOSB::ZERO	= 0%

	EFN% = 0%			! Event flag
	FUNC% = QUI$_DISPLAY_QUEUE	! Function
	NULLARG% = 0%			! Null arguement
	IOSB% = LOC(IOSB)		! IO Status block
	ASTADR% = 0%			! AST address
	ASTPRM% = 0%			! AST Parameter

12050	XSTATUS% = SYS$GETQUIW(EFN% BY VALUE,FUNC% BY VALUE,NULLARG% BY VALUE, &
		ITEMCODES() BY REF,IOSB BY REF, 0% BY VALUE, 0% BY VALUE, 0% BY VALUE)

 !	PRINT
 !	PRINT "STATUS            "; XSTATUS%
 !	PRINT "IOSB              "; IOSB::STAT

	IF ((IOSB::STAT AND 1%) = 1%) AND ((XSTATUS% AND 1%) = 1%)
	THEN
		TOTALFILES% = TOTALFILES% + 1%

		KEEPINFO(TOTALFILES%)::QUE_NAME = QUE_NAME$
		KEEPINFO(TOTALFILES%)::FORM_NAME = FORM_NAME$
		KEEPINFO(TOTALFILES%)::QUE_STATUS = QUE_STATUS%

		FILE$(TOTALFILES%) = QUE_NAME$ + "  " + &
			FORM_NAME$ + " " + &
			FNQUESTATUS$(QUE_STATUS%)

 !		PRINT
 !		PRINT "ASSIGNED QUE NAME "; ASSIGNED_QUE_NAME$
 !		PRINT "FORM NAME         "; FORM_NAME$
 !		PRINT "QUE DESCRIPTION   "; QUE_DESCRIPTION$
 !		PRINT "QUE FLAGS         "; QUE_FLAGS%, FNQUEFLAGS$(QUE_FLAGS%)
 !		PRINT "QUE_NAME          "; QUE_NAME$
 !		PRINT "QUE STATUS        "; QUE_STATUS%, FNQUESTATUS$(QUE_STATUS%)

		GOTO 12050
	END IF

	WINDOW_SCROLL::BOT_ARRAY	= TOTALFILES%
	WINDOW_SCROLL::BOT_ARRAY	= 1% IF WINDOW_SCROLL::BOT_ARRAY = 0%
	WINDOW_SCROLL::END_ELEMENT	= WINDOW_SCROLL::BOT_ARRAY

	FILE$(WINDOW_SCROLL::BOT_ARRAY + 1%) = ""

	RETURN

	%PAGE

 SetForm:
	!*******************************************************************
	! Change form name
	!
	! Input SEARCH_NAME$
	!	FORM_NAME$
	!*******************************************************************

	!
	! Input item codes
	!
	ER% = 0%
	ITEMCODES(ER%)::BUFFER_LENGTH	= LEN(SEARCH_NAME$)
	ITEMCODES(ER%)::ITEM_CODE	= SJC$_QUEUE
	ITEMCODES(ER%)::BUFFER_ADDRESS	= LOC(SEARCH_NAME$)
	ITEMCODES(ER%)::RETURN_LENGTH_ADDRESS = 0%

	ER% = ER% + 1%
	ITEMCODES(ER%)::BUFFER_LENGTH	= LEN(FORM_NAME$)
	ITEMCODES(ER%)::ITEM_CODE	= SJC$_FORM_NAME
	ITEMCODES(ER%)::BUFFER_ADDRESS	= LOC(FORM_NAME$)
	ITEMCODES(ER%)::RETURN_LENGTH_ADDRESS = 0%

	ER% = ER% + 1%
	ITEMCODES(ER%)::BUFFER_LENGTH	= 0%
	ITEMCODES(ER%)::ITEM_CODE	= 0%
	ITEMCODES(ER%)::BUFFER_ADDRESS	= 0%
	ITEMCODES(ER%)::RETURN_LENGTH_ADDRESS = 0%

	!
	IOSB::STAT	= 0%
	IOSB::ZERO	= 0%

	EFN% = 0%			! Event flag
	FUNC% = SJC$_ALTER_QUEUE	! Function
	NULLARG% = 0%			! Null arguement
	IOSB% = LOC(IOSB)		! IO Status block
	ASTADR% = 0%			! AST address
	ASTPRM% = 0%			! AST Parameter

	XSTATUS% = SYS$SNDJBCW(EFN% BY VALUE,FUNC% BY VALUE,NULLARG% BY VALUE, &
		ITEMCODES() BY REF,IOSB BY REF, 0% BY VALUE, 0% BY VALUE, 0% BY VALUE)

 !	PRINT
 !	PRINT "STATUS            "; XSTATUS%
 !	PRINT "IOSB              "; IOSB::STAT

	RETURN

	%PAGE

 StopQue:
	!*******************************************************************
	! Stop que
	!
	! Input SEARCH_NAME$
	!*******************************************************************

	!
	! Input item codes
	!
	ER% = 0%
	ITEMCODES(ER%)::BUFFER_LENGTH	= LEN(SEARCH_NAME$)
	ITEMCODES(ER%)::ITEM_CODE	= SJC$_QUEUE
	ITEMCODES(ER%)::BUFFER_ADDRESS	= LOC(SEARCH_NAME$)
	ITEMCODES(ER%)::RETURN_LENGTH_ADDRESS = 0%

	ER% = ER% + 1%
	ITEMCODES(ER%)::BUFFER_LENGTH	= 0%
	ITEMCODES(ER%)::ITEM_CODE	= 0%
	ITEMCODES(ER%)::BUFFER_ADDRESS	= 0%
	ITEMCODES(ER%)::RETURN_LENGTH_ADDRESS = 0%

	!
	IOSB::STAT	= 0%
	IOSB::ZERO	= 0%

	EFN% = 0%			! Event flag
	FUNC% = SJC$_PAUSE_QUEUE	! Function
	NULLARG% = 0%			! Null arguement
	IOSB% = LOC(IOSB)		! IO Status block
	ASTADR% = 0%			! AST address
	ASTPRM% = 0%			! AST Parameter

	XSTATUS% = SYS$SNDJBCW(EFN% BY VALUE,FUNC% BY VALUE,NULLARG% BY VALUE, &
		ITEMCODES() BY REF,IOSB BY REF, 0% BY VALUE, 0% BY VALUE, 0% BY VALUE)

 !	PRINT
 !	PRINT "STATUS            "; XSTATUS%
 !	PRINT "IOSB              "; IOSB::STAT

	RETURN

	%PAGE

 RestartQue:
	!*******************************************************************
	! Restart que
	!
	! Input SEARCH_NAME$
	!*******************************************************************

	!
	! Input item codes
	!
	ER% = 0%
	ITEMCODES(ER%)::BUFFER_LENGTH	= LEN(SEARCH_NAME$)
	ITEMCODES(ER%)::ITEM_CODE	= SJC$_QUEUE
	ITEMCODES(ER%)::BUFFER_ADDRESS	= LOC(SEARCH_NAME$)
	ITEMCODES(ER%)::RETURN_LENGTH_ADDRESS = 0%

	ER% = ER% + 1%
	ITEMCODES(ER%)::BUFFER_LENGTH	= 0%
	ITEMCODES(ER%)::ITEM_CODE	= 0%
	ITEMCODES(ER%)::BUFFER_ADDRESS	= 0%
	ITEMCODES(ER%)::RETURN_LENGTH_ADDRESS = 0%

	!
	IOSB::STAT	= 0%
	IOSB::ZERO	= 0%

	EFN% = 0%			! Event flag
	FUNC% = SJC$_START_QUEUE	! Function
	NULLARG% = 0%			! Null arguement
	IOSB% = LOC(IOSB)		! IO Status block
	ASTADR% = 0%			! AST address
	ASTPRM% = 0%			! AST Parameter

	XSTATUS% = SYS$SNDJBCW(EFN% BY VALUE,FUNC% BY VALUE,NULLARG% BY VALUE, &
		ITEMCODES() BY REF,IOSB BY REF, 0% BY VALUE, 0% BY VALUE, 0% BY VALUE)

 !	PRINT
 !	PRINT "STATUS            "; XSTATUS%
 !	PRINT "IOSB              "; IOSB::STAT

	RETURN

	%PAGE

 LookUpEntry:
13000	!*******************************************************************
	!
	!*******************************************************************

	!
	! Now look for jobs on this que
	!
	TOTALENTRY% = 0%

	!
	! Lookup Queues
	!
	ER% = 0%
	SEARCH_NAME$ = "*"
	ITEMCODES(ER%)::BUFFER_LENGTH	= LEN(SEARCH_NAME$)
	ITEMCODES(ER%)::ITEM_CODE	= QUI$_SEARCH_NAME
	ITEMCODES(ER%)::BUFFER_ADDRESS	= LOC(SEARCH_NAME$)
	ITEMCODES(ER%)::RETURN_LENGTH_ADDRESS = 0%

	ER% = ER% + 1%
	SEARCH_FLAG% = QUI$M_SEARCH_PRINTER OR QUI$M_SEARCH_TERMINAL OR &
		QUI$M_SEARCH_BATCH OR QUI$M_SEARCH_SYMBIONT
 !	SEARCH_FLAG% = QUI$M_SEARCH_PRINTER OR QUI$M_SEARCH_TERMINAL OR &
 !		QUI$M_SEARCH_BATCH
	ITEMCODES(ER%)::BUFFER_LENGTH	= 4%
	ITEMCODES(ER%)::ITEM_CODE	= QUI$_SEARCH_FLAGS
	ITEMCODES(ER%)::BUFFER_ADDRESS	= LOC(SEARCH_FLAG%)
	ITEMCODES(ER%)::RETURN_LENGTH_ADDRESS = 0%

	ER% = ER% + 1%
	ITEMCODES(ER%)::BUFFER_LENGTH	= 0%
	ITEMCODES(ER%)::ITEM_CODE	= 0%
	ITEMCODES(ER%)::BUFFER_ADDRESS	= 0%
	ITEMCODES(ER%)::RETURN_LENGTH_ADDRESS = 0%


	!
13050	IOSB::STAT	= 0%
	IOSB::ZERO	= 0%

	EFN% = 0%			! Event flag
	FUNC% = QUI$_DISPLAY_QUEUE	! Function
	NULLARG% = 0%			! Null arguement
	IOSB% = LOC(IOSB)		! IO Status block
	ASTADR% = 0%			! AST address
	ASTPRM% = 0%			! AST Parameter

	XSTATUS% = SYS$GETQUIW(EFN% BY VALUE,FUNC% BY VALUE,NULLARG% BY VALUE, &
		ITEMCODES() BY REF,IOSB BY REF, 0% BY VALUE, 0% BY VALUE, 0% BY VALUE)

 !	PRINT
 !	PRINT "XXQUE STATUS            "; XSTATUS%
 !	PRINT "XXQUE IOSB              "; IOSB::STAT

	IF ((IOSB::STAT AND 1%) = 1%) AND ((XSTATUS% AND 1%) = 1%)
	THEN
		GOSUB LookUpEntryA
		GOTO 13050
	END IF

	ENTRY_SCROLL::BOT_ARRAY	= TOTALENTRY%
	ENTRY_SCROLL::BOT_ARRAY	= 1% IF ENTRY_SCROLL::BOT_ARRAY = 0%
	ENTRY_SCROLL::END_ELEMENT = ENTRY_SCROLL::BOT_ARRAY

	ENTRY$(ENTRY_SCROLL::BOT_ARRAY + 1%) = ""

	RETURN

 LookUpEntryA:
	!
	! Input codes
	!
	ER% = 0%
 !	SEARCH_USERNAME$ = "BACKUP"
 !	ENTRYCODES(ER%)::BUFFER_LENGTH	= LEN(TRM$(SEARCH_USERNAME$))
 !	ENTRYCODES(ER%)::ITEM_CODE	= QUI$_SEARCH_USERNAME
 !	ENTRYCODES(ER%)::BUFFER_ADDRESS	= LOC(SEARCH_USERNAME$)
 !	ENTRYCODES(ER%)::RETURN_LENGTH_ADDRESS = 0%
 !
 !	ER% = ER% + 1%
	SEARCH_FLAG% = QUI$M_SEARCH_PRINTER OR QUI$M_SEARCH_TERMINAL OR &
		QUI$M_SEARCH_EXECUTING_JOBS OR QUI$M_SEARCH_HOLDING_JOBS OR &
		QUI$M_SEARCH_PENDING_JOBS OR QUI$M_SEARCH_RETAINED_JOBS OR &
		QUI$M_SEARCH_TIMED_RELEASE_JOBS or &
		QUI$M_SEARCH_BATCH OR QUI$M_SEARCH_SYMBIONT OR &
		QUI$M_SEARCH_WILDCARD OR QUI$M_SEARCH_ALL_JOBS
	ENTRYCODES(ER%)::BUFFER_LENGTH	= 4%
	ENTRYCODES(ER%)::ITEM_CODE	= QUI$_SEARCH_FLAGS
	ENTRYCODES(ER%)::BUFFER_ADDRESS	= LOC(SEARCH_FLAG%)
	ENTRYCODES(ER%)::RETURN_LENGTH_ADDRESS = 0%

	!
	! Output codes
	!
	ER% = ER% + 1%
	ENTRYCODES(ER%)::BUFFER_LENGTH	= LEN(USERNAME$)
	ENTRYCODES(ER%)::ITEM_CODE	= QUI$_USERNAME
	ENTRYCODES(ER%)::BUFFER_ADDRESS	= LOC(USERNAME$)
	ENTRYCODES(ER%)::RETURN_LENGTH_ADDRESS = 0%

	ER% = ER% + 1%
	ENTRYCODES(ER%)::BUFFER_LENGTH	= LEN(FORM_NAME$)
	ENTRYCODES(ER%)::ITEM_CODE	= QUI$_FORM_NAME
	ENTRYCODES(ER%)::BUFFER_ADDRESS	= LOC(FORM_NAME$)
	ENTRYCODES(ER%)::RETURN_LENGTH_ADDRESS = 0%

	ER% = ER% + 1%
	ENTRYCODES(ER%)::BUFFER_LENGTH	= LEN(ENTRYNAME$)
	ENTRYCODES(ER%)::ITEM_CODE	= QUI$_JOB_NAME
	ENTRYCODES(ER%)::BUFFER_ADDRESS	= LOC(ENTRYNAME$)
	ENTRYCODES(ER%)::RETURN_LENGTH_ADDRESS = 0%

	ER% = ER% + 1%
	ENTRYCODES(ER%)::BUFFER_LENGTH	= 4%
	ENTRYCODES(ER%)::ITEM_CODE	= QUI$_ENTRY_NUMBER
	ENTRYCODES(ER%)::BUFFER_ADDRESS	= LOC(ENTRYNUMBER%)
	ENTRYCODES(ER%)::RETURN_LENGTH_ADDRESS = 0%

	ER% = ER% + 1%
	ENTRYCODES(ER%)::BUFFER_LENGTH	= LEN(ASSIGNED_QUE_NAME$)
	ENTRYCODES(ER%)::ITEM_CODE	= QUI$_QUEUE_NAME
	ENTRYCODES(ER%)::BUFFER_ADDRESS	= LOC(ASSIGNED_QUE_NAME$)
	ENTRYCODES(ER%)::RETURN_LENGTH_ADDRESS = 0%

	ER% = ER% + 1%
	ENTRYCODES(ER%)::BUFFER_LENGTH	= 0%
	ENTRYCODES(ER%)::ITEM_CODE	= 0%
	ENTRYCODES(ER%)::BUFFER_ADDRESS	= 0%
	ENTRYCODES(ER%)::RETURN_LENGTH_ADDRESS = 0%

	EFN% = 0%			! Event flag
	FUNC% = QUI$_DISPLAY_JOB	! Function
	NULLARG% = 0%			! Null arguement
	IOSB% = LOC(IOSB)		! IO Status block
	ASTADR% = 0%			! AST address
	ASTPRM% = 0%			! AST Parameter

13150	XSTATUS% = SYS$GETQUIW(EFN% BY VALUE,FUNC% BY VALUE,NULLARG% BY VALUE, &
		ENTRYCODES() BY REF,IOSB BY REF, 0% BY VALUE, 0% BY VALUE, 0% BY VALUE)

 !	PRINT
 !	PRINT "STATUS            "; XSTATUS%
 !	PRINT "IOSB              "; IOSB::STAT

	IF ((IOSB::STAT AND 1%) = 1%) AND ((XSTATUS% AND 1%) = 1%)
	THEN
		TOTALENTRY% = TOTALENTRY% + 1%

		KEEPENTRY(TOTALENTRY%)::USER_NAME = USERNAME$
		KEEPENTRY(TOTALENTRY%)::FORM_NAME = FORM_NAME$
		KEEPENTRY(TOTALENTRY%)::ENTRY_NAME = ENTRYNAME$
		KEEPENTRY(TOTALENTRY%)::QUE_NAME = ASSIGNED_QUE_NAME$
		KEEPENTRY(TOTALENTRY%)::ENTRY_NUMBER = ENTRYNUMBER%

		ENTRY$(TOTALENTRY%) = FORMAT$(ENTRYNUMBER%, "#### ") + &
			ASSIGNED_QUE_NAME$ + " " + &
			ENTRYNAME$ + " " + &
			FORM_NAME$ + " " + &
			USERNAME$

 !	PRINT FORMAT$(ENTRYNUMBER%, "#### ") + &
 !		ASSIGNED_QUE_NAME$ + " " + &
 !		ENTRYNAME$ + " " + &
 !		FORM_NAME$ + " " + &
 !		USERNAME$

		GOTO 13150
	END IF


	RETURN

 ExitProgram:
	!*******************************************************************
	! Exit program
	!*******************************************************************

	!
	! Delete all displays
	!
	SMG_STATUS% = SMG$POP_VIRTUAL_DISPLAY(SMG_SCROLL, SCOPE::SMG_PBID)

	CALL SUBR_3EXITPROGRAM(SCOPE, "", "")

	GOTO 32767

	%PAGE

	!*******************************************************************
	! Que Status
	!*******************************************************************

	DEF FNQUESTATUS$(QSTATUS%)
		QSTATUS$ = ""
		QSTATUS$ = QSTATUS$ + "Aligning "	IF QSTATUS% AND QUI$M_QUEUE_ALIGNING
		QSTATUS$ = QSTATUS$ + "Closed "		IF QSTATUS% and QUI$M_QUEUE_CLOSED
		QSTATUS$ = QSTATUS$ + "Idle "		IF QSTATUS% and QUI$M_QUEUE_IDLE
		QSTATUS$ = QSTATUS$ + "Lowercase "	IF QSTATUS% AND QUI$M_QUEUE_LOWERCASE
		QSTATUS$ = QSTATUS$ + "Paused "		IF QSTATUS% and QUI$M_QUEUE_PAUSED
		QSTATUS$ = QSTATUS$ + "Pausing "	IF QSTATUS% AND QUI$M_QUEUE_PAUSING
		QSTATUS$ = QSTATUS$ + "Remote "		IF QSTATUS% and QUI$M_QUEUE_REMOTE
		QSTATUS$ = QSTATUS$ + "Resetting "	IF QSTATUS% AND QUI$M_QUEUE_RESETTING
		QSTATUS$ = QSTATUS$ + "Resuming "	IF QSTATUS% AND QUI$M_QUEUE_RESUMING
		QSTATUS$ = QSTATUS$ + "Server "		IF QSTATUS% and QUI$M_QUEUE_SERVER
		QSTATUS$ = QSTATUS$ + "Stalled "	IF QSTATUS% AND QUI$M_QUEUE_STALLED
		QSTATUS$ = QSTATUS$ + "Starting "	IF QSTATUS% AND QUI$M_QUEUE_STARTING
		QSTATUS$ = QSTATUS$ + "Stopped "	IF QSTATUS% AND QUI$M_QUEUE_STOPPED
		QSTATUS$ = QSTATUS$ + "Stopping"	IF QSTATUS% AND QUI$M_QUEUE_STOPPING
		QSTATUS$ = QSTATUS$ + "Unavailable "	IF QSTATUS% AND QUI$M_QUEUE_UNAVAILABLE
		FNQUESTATUS$ = QSTATUS$
	FNEND

 !	DEF FNQUEFLAGS$(QSTATUS%)
 !		QFLAGS$ = ""
 !		QFLAGS$ = QFLAGS$ + "AclSpecified "	IF QSTATUS% AND QUI$M_QUEUE_ACL_SPECIFIED
 !		QFLAGS$ = QFLAGS$ + "Batch "		IF QSTATUS% and QUI$M_QUEUE_BATCH
 !		QFLAGS$ = QFLAGS$ + "CpuDefault "	IF QSTATUS% AND QUI$M_QUEUE_CPU_DEFAULT
 !		QFLAGS$ = QFLAGS$ + "CpuLimit "		IF QSTATUS% and QUI$M_QUEUE_CPU_LIMIT
 !		QFLAGS$ = QFLAGS$ + "FileBurst "	IF QSTATUS% AND QUI$M_QUEUE_FILE_BURST
 !		QFLAGS$ = QFLAGS$ + "FileBurstone "	IF QSTATUS% AND QUI$M_QUEUE_FILE_BURST_ONE
 !		QFLAGS$ = QFLAGS$ + "FileFlag "		IF QSTATUS% and QUI$M_QUEUE_FILE_FLAG
 !		QFLAGS$ = QFLAGS$ + "FileFlagOne "	IF QSTATUS% AND QUI$M_QUEUE_FILE_FLAG_ONE
 !		QFLAGS$ = QFLAGS$ + "FilePaginate "	IF QSTATUS% AND QUI$M_QUEUE_FILE_PAGINATE
 !		QFLAGS$ = QFLAGS$ + "FileTrailer "	IF QSTATUS% AND QUI$M_QUEUE_FILE_TRAILER
 !		QFLAGS$ = QFLAGS$ + "FileTrailerOne "	IF QSTATUS% AND QUI$M_QUEUE_FILE_TRAILER_ONE
 !		QFLAGS$ = QFLAGS$ + "Generic "		IF QSTATUS% and QUI$M_QUEUE_GENERIC
 !		QFLAGS$ = QFLAGS$ + "GenericSelection "	IF QSTATUS% AND QUI$M_QUEUE_GENERIC_SELECTION
 !		QFLAGS$ = QFLAGS$ + "JobBurst "		IF QSTATUS% and QUI$M_QUEUE_JOB_BURST
 !		QFLAGS$ = QFLAGS$ + "JobFlag "		IF QSTATUS% and QUI$M_QUEUE_JOB_FLAG
 !		QFLAGS$ = QFLAGS$ + "JobSizeSched "	IF QSTATUS% AND QUI$M_QUEUE_JOB_SIZE_SCHED
 !		QFLAGS$ = QFLAGS$ + "JobTrailer "	IF QSTATUS% AND QUI$M_QUEUE_JOB_TRAILER
 !		QFLAGS$ = QFLAGS$ + "Printer "		IF QSTATUS% and QUI$M_QUEUE_PRINTER
 !		QFLAGS$ = QFLAGS$ + "RecordBlocking "	IF QSTATUS% AND QUI$M_QUEUE_RECORD_BLOCKING
 !		QFLAGS$ = QFLAGS$ + "RetainAll "	IF QSTATUS% AND QUI$M_QUEUE_RETAIN_ALL
 !		QFLAGS$ = QFLAGS$ + "RetainError "	IF QSTATUS% AND QUI$M_QUEUE_RETAIN_ERROR
 !		QFLAGS$ = QFLAGS$ + "Swap "		IF QSTATUS% and QUI$M_QUEUE_SWAP
 !		QFLAGS$ = QFLAGS$ + "Terminal "		IF QSTATUS% and QUI$M_QUEUE_TERMINAL
 !		QFLAGS$ = QFLAGS$ + "WsDefault "	IF QSTATUS% AND QUI$M_QUEUE_WSDEFAULT
 !		QFLAGS$ = QFLAGS$ + "WsExtent "		IF QSTATUS% and QUI$M_QUEUE_WSEXTENT
 !		QFLAGS$ = QFLAGS$ + "WsQuota "		IF QSTATUS% and QUI$M_QUEUE_WSQUOTA
 !		FNQUEFLAGS$ = QFLAGS$
 !	FNEND

32767	END
