1	%TITLE "Print Transaction Account Table"
	%SBTTL "UTL_RPRT_ACCOUNT"
	%IDENT "V3.6a Calico"

	!
	! COPYRIGHT (C) 1988 BY
	!
	! Computer Management Center, Inc.
	! Idaho Falls, Idaho.
	!
	! This software is furnished under a license and may be used and
	! copied only in accordance with terms of such license and with
	! the inclusion of the above copyright notice.  This software or
	! any other copies thereof may not be provided or otherwise made
	! available to any other person.  No title to and ownership of
	! the software is hereby transferred.
	!
	! The information in this software is subject to change without
	! notice and should not be construed as a commitment by
	! Computer Management Center, Inc.
	!
	! CMC assumes no responsibility for the use or reliability of
	! its software on equipment which is not supported by CMC.
	!
	!++
	! ID:UT017
	!
	! Abstract:HELP
	!	.p
	!	The ^*Transaction Accounts\* option will provide a report
	!	which contains the following:
	!	.b
	!	.lm 15
	!	.list 0,"*"
	!	.le
	!	Location
	!	.le
	!	Name
	!	.le
	!	Type
	!	.le
	!	Description
	!	.le
	!	Account
	!	.le
	!	Description
	!	.els
	!
	! Index:
	!	.x Report>Transaction Accounts
	!	.x Transaction Accounts>Report
	!
	! Compile:
	!
	!	$ BAS UTL_SOURCE:UTL_RPRT_ACCOUNT/LINE
	!	$ LINK/EXE=UTL_EXE: UTL_RPRT_ACCOUNT, FUNC_LIB:CMCLINK/OPTION
	!	$ DELETE UTL_RPRT_ACCOUNT.OBJ;*
	!
	! AUTHOR:
	!
	!	04/11/88 - Frantisek Starman
	!
	! MODIFICATION HISTORY:
	!
	!	04/28/92 - Kevin Handy
	!		Clean up (check)
	!
	!	04/15/95 - Kevin Handy
	!		(V3.6)
	!		Update to V3.6 coding standards
	!
	!	09/16/96 - Kevin Handy
	!		Reformat source code
	!
	!	06/06/97 - Kevin Handy
	!		Use integer for #key
	!
	!	08/22/98 - Kevin Handy
	!		(V3.6a Calico)
	!		Update to V3.6a coding standards
	!
	!	06/09/2000 - Kevin Handy
	!		Use WHEN ERROR IN
	!--
	%PAGE

	OPTION SIZE = (INTEGER LONG, REAL GFLOAT)

	!
	! Include files
	!
	%INCLUDE "FUNC_INCLUDE:FUNCTION.HB"

	MAP (SCOPE) SCOPE_STRUCT SCOPE

	%INCLUDE "SOURCE:[UTL.OPEN]UTL_REPORTX.HB"
	DECLARE UTL_REPORTX_CDD UTL_REPORTX

	%INCLUDE "SOURCE:[UTL.OPEN]UTL_ACCOUNT.HB"
	MAP (UTL_ACCOUNT) UTL_ACCOUNT_CDD UTL_ACCOUNT

	%INCLUDE "SOURCE:[UTL.OPEN]UTL_TRANSTYPE.HB"
	MAP (UTL_TRANSTYPE)	UTL_TRANSTYPE_CDD	UTL_TRANSTYPE

	%INCLUDE "SOURCE:[UTL.OPEN]UTL_LOCATION.HB"
	MAP (UTL_LOCATION)	UTL_LOCATION_CDD	UTL_LOCATION

	%INCLUDE "SOURCE:[GL.OPEN]GL_CHART.HB"
	MAP (GL_CHART)	GL_CHART_CDD	GL_CHART

	MAP	(DP_OUTP_XUNSOL)				RRR_FLAG%


	!
	! External functions
	!
	EXTERNAL LONG	FUNCTION OUTP_UNSOLICITED

	%PAGE

	ON ERROR GOTO 19000

 Init:	!
	! Initilize report
	!
	CALL OUTP_INITFROMFILE(UTL_REPORTX, 132%)

	GOTO ExitProgram IF UTL_REPORTX::STAT

	FROM.ITEM$ = EDIT$(UTL_REPORTX::OPTDEF(0%), 132%)

	!++
	! Abstract:FLD01
	!
	!	^*From Location\*
	!	.p
	!	A ^*From Location\* value entered in this field will cause
	!	the printing of the report to begin with the selected transaction
	!	account.
	!	.p
	!	A blank setting will cause the report to begin with the frst
	!	transaction account in the file.
	!
	! Index:
	! Datatype:TEXT
	! Size:4
	!--

	TO.ITEM$ = EDIT$(UTL_REPORTX::OPTDEF(1%), 132%)

	!++
	! Abstract:FLD02
	!
	!	^*(02) To Location\*
	!	.p
	!	A ^*To Location\* value entered in this field will cause the
	!	printing of the report to end with the selected transaction
	!	account.
	!	.p
	!	A blank setting will cause the report to end with the last
	!	transaction account in the file.
	!
	! Index:
	! Datatype:TEXT
	! Size:4
	!--

	WLDCRD$ = EDIT$(UTL_REPORTX::OPTDEF(2%), -1%)

	!++
	! Abstract:FLD03
	!
	!	^*(03) Wildcard\*
	!	.p
	!	The ^*Wildcard\* field provides the means to select designated
	!	transaction accounts to be printed in the report by entering a
	!	"wildcard" value in this field.
	!
	! Index:
	! Datatype:TEXT
	! Size:20
	!--


300	WHEN ERROR IN
		%INCLUDE "SOURCE:[UTL.OPEN]UTL_ACCOUNT.OPN"
	USE
		FILENAME$ = "UTL_ACCOUNT"
		CONTINUE HelpError
	END WHEN

310	WHEN ERROR IN
		%INCLUDE "SOURCE:[UTL.OPEN]UTL_TRANSTYPE.OPN"
	USE
		CONTINUE 320 IF ERR = 5%
		FILENAME$ = "UTL_TRANSTYPE"
		CONTINUE HelpError
	END WHEN

320	WHEN ERROR IN
		%INCLUDE "SOURCE:[UTL.OPEN]UTL_LOCATION.OPN"
	USE
		CONTINUE 330 IF ERR = 5%
		FILENAME$ = "UTL_LOCATION"
		CONTINUE HelpError
	END WHEN

330	WHEN ERROR IN
		%INCLUDE "SOURCE:[GL.OPEN]GL_CHART.OPN"
	USE
		CONTINUE ReportTitle IF ERR = 5%
		FILENAME$ = "GL_CHART"
		CONTINUE HelpError
	END WHEN

 ReportTitle:
	!
	! Title
	!
	TITLE$(1%) = "TRANSACTION  ACCOUNTS  TABLE"
	TITLE$(2%) = "Utility system"
	TITLE$(3%) = ""

	!
	! Heading
	!
	!		'1234567890123456789012345678901234567890
	TITLE$(4%) = "Loc  Name                              "+ &
		"       Type Description          Acco"+ &
		"unt            Description"
	TITLE$(5%) = "."

	%PAGE

17000	!***************************************************************
	! OUTPUT REPORT
	!***************************************************************

	WHEN ERROR IN
		IF FROM.ITEM$ = ""
		THEN
			RESET #UTL_ACCOUNT.CH%
		ELSE
			FIND #UTL_ACCOUNT.CH%, KEY#0% GE FROM.ITEM$, REGARDLESS
		END IF
	USE
		CALL ENTR_3MESSAGE(SCOPE, &
			"Unable to find beginning record!", 0%)
		CONTINUE ExitProgram
	END WHEN

 GetNextRec:
17020	!
	! Main loop starts here
	!
	GOTO ExitProgram IF UTL_REPORTX::STAT

	!
	! Check unsolicited input
	!
	IF RRR_FLAG%
	THEN
		IF OUTP_UNSOLICITED(OPT_REPORT) <> CMC$_NORMAL
		THEN
			EXIT_STATUS = CMC$_UNTERROR
			GOTO ExitProgram
		END IF
	END IF

	!
	! Get next record
	!
	WHEN ERROR IN
		GET #UTL_ACCOUNT.CH%, REGARDLESS
	USE
		CONTINUE ExitTotal IF ERR = 11%
		FILENAME$ = "UTL_ACCOUNT"
		CONTINUE HelpError
	END WHEN

	!
	! Check current record
	!
	GOTO ExitTotal IF (UTL_ACCOUNT::LOCATION > TO.ITEM$) AND TO.ITEM$ <> ""

	GOTO GetNextRec &
		IF COMP_STRING(EDIT$(UTL_ACCOUNT::LOCATION,-1%), WLDCRD$) = 0% &
		AND WLDCRD$ <> ""

	GOSUB 18500

17300	!
	! Print out one line
	!
	ACCOUNT$ = UTL_ACCOUNT::ACCOUNT
	GOSUB 18550

	!
	! Check unsolicited input
	!
	IF RRR_FLAG%
	THEN
		IF OUTP_UNSOLICITED(OPT_REPORT) <> CMC$_NORMAL
		THEN
			EXIT_STATUS = CMC$_UNTERROR
			GOTO ExitProgram
		END IF
	END IF

	TEXT$ = UTL_ACCOUNT::LOCATION + " " + &
		UTL_LOCATION::LOCNAME + " " + &
		UTL_ACCOUNT::TRANSTYPE + "   " + &
		UTL_TRANSTYPE::DESCRIPTION + " " + &
		UTL_ACCOUNT::ACCOUNT + " " + &
		GL_CHART::DESCR

	CALL OUTP_LINE("", UTL_REPORTX, TITLE$(), TEXT$, 0%)

	GOTO ExitProgram IF UTL_REPORTX::STAT

17350	!
	! Try for next record
	!
	GOTO GetNextRec

 ExitTotal:
17400	!
	! Handle end of report
	!

 ExitProgram:
	CALL OUTP_FINISH(UTL_REPORTX)

	!
	! Exit to next program or menu
	!
	IF TRM$(UTL_REPORTX::NEXTRUN) = ""
	THEN
		CALL SUBR_3EXITPROGRAM(SCOPE, "", "")
	ELSE
		CALL SUBR_3EXITPROGRAM(SCOPE, "RUN " + UTL_REPORTX::NEXTRUN, "")
	END IF

	%PAGE

18500	!
	! Check unsolicited input
	!
	IF RRR_FLAG%
	THEN
		IF OUTP_UNSOLICITED(OPT_REPORT) <> CMC$_NORMAL
		THEN
			EXIT_STATUS = CMC$_UNTERROR
			GOTO ExitProgram
		END IF
	END IF

	WHEN ERROR IN
		GET #UTL_LOCATION.CH%, &
			KEY #0% EQ UTL_ACCOUNT::LOCATION, &
			REGARDLESS
	USE
		UTL_LOCATION::LOCNAME = &
			STRING$(LEN(UTL_LOCATION::LOCNAME), ASCII("?"))
		CONTINUE 18510 IF ERR = 155%
		FILENAME$ = "UTL_LOCATION"
		CONTINUE HelpError
	END WHEN

18510	WHEN ERROR IN
		GET #UTL_TRANSTYPE.CH%, &
			KEY# 0% EQ UTL_ACCOUNT::TRANSTYPE, &
			REGARDLESS
	USE
		UTL_TRANSTYPE::DESCRIPTION = &
			STRING$(LEN(UTL_TRANSTYPE::DESCRIPTION), ASCII("?"))
		CONTINUE EndType IF ERR = 155%
		FILENAME$ = "UTL_TRANSTYPE"
		CONTINUE HelpError
	END WHEN

 EndType:
	RETURN

18550	WHEN ERROR IN
		GET #GL_CHART.CH%, KEY #0% EQ ACCOUNT$, REGARDLESS
	USE
		GL_CHART::DESCR = STRING$(LEN(GL_CHART::DESCR), ASCII("?"))
		CONTINUE Account IF ERR = 155%
		FILENAME$ = "GL_CHART"
		CONTINUE HelpError
	END WHEN

 Account:
	RETURN

	%PAGE

 HelpError:
	!***************************************************************
	! Help Message for an error
	!***************************************************************
	CALL HELP_34MESSAGE(SCOPE, NUM1$(ERL) + " " + ERT$(ERR), &
		"E", ERN$, FILENAME$, NUM1$(ERR))
	UTL_REPORTX::STAT = -1%
	GOTO ExitProgram

	%Page

19000	!***************************************************************
	! ERROR TRAPPING
	!***************************************************************

	!
	! Untrapped error
	!
	FILENAME$ = ""
	RESUME HelpError

32767	END
